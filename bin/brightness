#!/usr/bin/perl
use strict;
use warnings;

my $lastFile = "$ENV{HOME}/.brightness";

my @brackets = (0, 34, 67, 80, 100);

my $usage = "Usage:\n"
  . "  $0            prints brightness\n"
  . "  $0 [up|down]  jump between: @brackets\n"
  . "  $0 percent    set brightness to percent, record in $lastFile\n"
  . "  $0 last       set brightness to value read from $lastFile\n"
  . "\n"
  . "xbacklight wrapper\n"
  . "\n"
  . "note: when brightness percentage is not an integer,\n"
  . "  xbacklight -set `xbacklight -get`\n"
  . "lowers the brightness one notch\n"
  . "\n"
  . "all brightnesses returned by this are rounded up:\n"
  . "  $0 `$0`\n"
  . "should NOT change the brightness\n"
  ;

sub ceil($){
  chomp $_[0];
  my $i = int($_[0]);
  my $d = $_[0] - $i;
  return $i + ($d > 0 ? 1 : 0);
}

sub getBrightness(){
  return ceil `xbacklight -get`;
}
sub setBrightness($$){
  my $target = ceil $_[0];
  my $softUpdate = $_[1];
  system "echo $target > $lastFile" if not $softUpdate;
  print getBrightness() . " => $target\n";
  system "xbacklight -time 0 -set $target";
}

my $arg = shift;
die $usage if @ARGV != 0;
if(not defined $arg){
  print getBrightness() . "\n";
}elsif($arg =~ /^(up|down)$/){
  my $b = getBrightness;
  @brackets = reverse @brackets if $arg eq 'down';
  for my $bracket(@brackets){
    if(($arg eq 'up' and $bracket > $b) or ($arg eq 'down' and $bracket < $b)){
      setBrightness $bracket, 0;
      last;
    }
  }
}elsif($arg =~ /^(last)$/){
  if(-e $lastFile){
    my $brightness = `cat $lastFile`;
    chomp $brightness;
    if($brightness =~ /^\d+$/){
      setBrightness $brightness, 1;
    }
  }
}elsif($arg =~ /^(\d+(?:\.\d+)?)$/){
  setBrightness $arg, 0;
}else{
  die $usage;
}


