#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use POSIX ":sys_wait_h";

my $DEFAULT_SCREEN_HEIGHT = 1080;
my $QTBTN_CONF_FILE = "$ENV{HOME}/.config/qtbtn-ddr.conf";

my $EXEC_STEPMANIA_CURSONG_INFO = "stepmania-cursong-info";

my $DDR_ALL_PLAYERS_FILE = "/tmp/ddr-all-players";
my $DDR_PLAYER_FILE = "/tmp/ddr-player";
my $DDR_CURSONG_FILE = "/tmp/ddr-cursong";

my $EXEC = basename $0;

my $usage = "USAGE:
  $EXEC -h|--help
    show this message

  $EXEC
  $EXEC --start-ui
    -calculated screen scaling factor, relative to 1080p
    -run qtbtn with scaling factor for $QTBTN_CONF_FILE

  $EXEC -p | -s | --set-players PLAYER [PLAYER ..]
    -write each PLAYER to $DDR_ALL_PLAYERS_FILE, separated by a space

  $EXEC --cycle-player
    -read PLAYER from $DDR_PLAYER_FILE
    -if PLAYER appears in $DDR_ALL_PLAYERS_FILE, followed by another player,
      write that following player to $DDR_PLAYER_FILE
    -otherwise, write the first PLAYER from $DDR_ALL_PLAYERS_FILE to $DDR_PLAYER_FILE
";

my $MODE_START_UI = "start-ui";
my $MODE_SET_PLAYERS = "set-players";
my $MODE_CYCLE_PLAYER = "cycle-player";

sub main(@){
  my $mode = $MODE_START_UI;
  my @players;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^(--start-ui)$/){
      $mode = $MODE_START_UI;
    }elsif($arg =~ /^(-p|-s|--set-players)$/){
      $mode = $MODE_SET_PLAYERS;
    }elsif($arg =~ /^--cycle-player$/){
      $mode = $MODE_CYCLE_PLAYER;
    }elsif($mode eq $MODE_SET_PLAYERS and $arg =~ /^([A-Z]+)$/i){
      push @players, $1;
    }else{
      die "$usage\nERROR: unknown arg $arg\n";
    }
  }

  if($mode eq $MODE_START_UI){
    system "rm /tmp/ddr-*";
    my $screenHeight = `res -h`;
    chomp $screenHeight;
    my $scale = $screenHeight / $DEFAULT_SCREEN_HEIGHT;
    $scale = 0.25 if $scale < 0.25;
    $scale = 8 if $scale > 8;

    system "$EXEC_STEPMANIA_CURSONG_INFO --kill";
    my $pid = fork();
    if(not defined $pid){
      die "ERROR: fork failed\n";
    }
    if($pid == 0){
      exec "$EXEC_STEPMANIA_CURSONG_INFO --monitor --qml --file=$DDR_CURSONG_FILE";
    }

    system "qtbtn.py --scale=$scale -w --left --bg $QTBTN_CONF_FILE >/dev/null";

    system "$EXEC_STEPMANIA_CURSONG_INFO --kill";
    waitpid(-1, WNOHANG);
  }elsif($mode eq $MODE_SET_PLAYERS){
    system "rm", "-f", $DDR_PLAYER_FILE;
    system "rm", "-f", $DDR_ALL_PLAYERS_FILE;
    system "echo @players > $DDR_ALL_PLAYERS_FILE";
  }elsif($mode eq $MODE_CYCLE_PLAYER){
    my @allPlayers = split /[ \t\r\n]+/, `cat $DDR_ALL_PLAYERS_FILE 2>/dev/null`;
    @allPlayers = grep {/\w/} @allPlayers;
    my $curPlayer = `cat $DDR_PLAYER_FILE 2>/dev/null`;
    chomp $curPlayer;
    my $index = undef;
    for(my $i=0; $i<@allPlayers; $i++){
      if($curPlayer eq $allPlayers[$i]){
        $index = $i + 1;
        last;
      }
    }
    $index = 0 if not defined $index or $index >= @allPlayers or $index < 0;
    if($index < @allPlayers){
      system "echo $allPlayers[$index] > $DDR_PLAYER_FILE";
    }
  }else{
    die "$usage\nERROR: unknown mode $mode\n";
  }
}

&main(@ARGV);
