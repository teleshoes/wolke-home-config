#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub getAllEntries();
sub getShowNames();
sub getShowEntrs();
sub getDirEntries($);
sub dirPathToName($);
sub readProcLines(@);
sub readLastUsedFile();
sub writeLastUsedFile($$);
sub bashComplete($$);

my $SHOWS_DIR = "$ENV{HOME}/Music/sheet_music/shows";
my @OTHER_DIRS = (
  "$ENV{HOME}/Music/sheet_music/trumpet",
);

my $LAST_USED_FILE = "$ENV{HOME}/.cache/last-used-sheetmusic";

my @CMD_FEH_PAGE = qw(feh-page);
my @CMD_FEH_WINDOW = qw(feh --scale-down --class sheetmusic-feh);

my $TYPE_SYNS = {
  double    => "double",
  d         => "double",
  dbl       => "double",

  single    => "single",
  sgl       => "single",
  s         => "single",

  jpeg      => "jpeg",
  jpg       => "jpeg",
  j         => "jpeg",

  image     => "image",
  png       => "image",
  feh       => "image",
  f         => "image",

  pdf       => "pdf",
  p         => "pdf",
  evince    => "pdf",
  e         => "pdf",

  musescore => "musescore",
  m         => "musescore",
  mscz      => "musescore",
};

my $EXEC = basename $0;

my $USAGE = "Usage:
   $EXEC -h|--help
     Show this message

   $EXEC [OPTS] NAME [TYPE]
     open sheetmusic for NAME with type TYPE
     (also, write NAME and TYPE to $LAST_USED_FILE)

   $EXEC [OPTS]
     if $LAST_USED_FILE exists, read NAME and TYPE from it
     run like: $EXEC NAME TYPE

   $EXEC --bash-complete COMP_LINE COMP_POINT
     print a list of words for bash completion, one per line
     COMP_LINE  - the full cmdline as a string
     COMP_POINT - the cursor position in the cmdline

   OPTS
     -w | --window
       use `feh` instead of `feh-page` with options to run windowed
         run: @CMD_FEH_WINDOW
         instead of: @CMD_FEH_PAGE

     -d | --daemon
       run feh/feh-page/evince/musescore with 'daemon -- CMD'

     -p | --player
       run commands before main command
         -run: `wmctrl -s 8`
         -run: `term play-show --show=NAME --interactive`

     --player-ipmagic=IPMAGIC_NAME
       run play-show remotely using ipmagic in --player mode:
         -run `play-show` as `ipmagic IPMAGIC_NAME --pty -s play-show`
         -get IPMAGIC_HOST with `ipmagic IPMAGIC_NAME --host`
         -add --prompt=IPMAGIC_HOST to `play-show`

     --last-file
       allow read+write of $LAST_USED_FILE
       (this is the default)
     --no-last-file
       do not read or write $LAST_USED_FILE
";

sub main(@){
  if(@_ == 3 and $_[0] =~ /^--bash-complete$/ and $_[2] =~ /^\d+$/){
    print map {"$_\n"} bashComplete($_[1], $_[2]);
    exit 0;
  }

  my $name = undef;
  my $typeArg = undef;
  my %opts = (
    window            => 0,
    daemon            => 0,
    player            => 0,
    playerIpmagicName => undef,
    readLastUsedFile  => 1,
    writeLastUsedFile => 1,
  );
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-w|--window)$/){
      $opts{window} = 1;
    }elsif($arg =~ /^(-d|--daemon)$/){
      $opts{daemon} = 1;
    }elsif($arg =~ /^(-p|--player)$/){
      $opts{player} = 1;
    }elsif($arg =~ /^--player-ipmagic=(\w+)$/){
      $opts{playerIpmagicName} = $1;
    }elsif($arg =~ /^(--last-file)$/){
      $opts{readLastUsedFile} = 1;
      $opts{writeLastUsedFile} = 1;
    }elsif($arg =~ /^(--no-last-file)$/){
      $opts{readLastUsedFile} = 0;
      $opts{writeLastUsedFile} = 0;
    }elsif(not defined $name){
      $name = $arg;
    }elsif(not defined $typeArg){
      $typeArg = $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($opts{readLastUsedFile} and not defined $name and not defined $typeArg){
    ($name, $typeArg) = readLastUsedFile();
    if(not defined $name){
      die "$USAGE\nERROR: could not read NAME from $LAST_USED_FILE\n";
    }
  }

  if(not defined $name){
    die "$USAGE\nERROR: missing NAME\n";
  }

  if(defined $typeArg){
    if(not defined $$TYPE_SYNS{$typeArg}){
      die "$USAGE\nERROR: invalid type arg: $typeArg\n";
    }
    $typeArg = $$TYPE_SYNS{$typeArg};
  }

  my $entries = getAllEntries();
  if(not defined $$entries{$name}){
    die "could not find sheet music for $name\n";
  }

  my $types = $$entries{$name}{types};
  die "no types found for $name\n" if @$types == 0;
  my $firstType = $$types[0];

  my $typeName;
  if(defined $typeArg){
    $typeName = $typeArg;
  }else{
    $typeName = $$firstType{typeName};
  }

  my $files = $$firstType{files};
  for my $type(@$types){
    if($typeName eq $$type{typeName}){
      $files = $$type{files};
      last;
    }
  }

  if($opts{writeLastUsedFile}){
    writeLastUsedFile $name, $typeArg;
  }

  my @cmd;
  if($typeName =~ /^(jpeg|image|single|double)$/){
    if($opts{window}){
      @cmd = @CMD_FEH_WINDOW;
    }else{
      @cmd = @CMD_FEH_PAGE;
      push @cmd, "--double" unless $typeName =~ /^(single)$/;
    }
  }elsif($typeName =~ /^(pdf)$/){
    @cmd = ("evince");
  }elsif($typeName =~ /^(musescore)$/){
    @cmd = ("musescore");
  }

  if($opts{daemon}){
    @cmd = ("daemon", "--", @cmd);
  }

  if($opts{player}){
    system "wmctrl", "-s", 8;

    my @playShowCmd = ("play-show", "--show=$name", "--interactive");
    if(defined $opts{playerIpmagicName}){
      my $host = `ipmagic $opts{playerIpmagicName} --host`;
      chomp $host;
      push @playShowCmd, "--prompt=$host";

      @playShowCmd = ("ipmagic", $opts{playerIpmagicName}, "--pty", "-s", @playShowCmd);
    }

    my $playShowWinTitle = "sheetmusic-play-show";
    system "term", "-t", $playShowWinTitle, @playShowCmd;
  }

  system @cmd, @$files;
  system "stty", "sane";
}

sub getAllEntries(){
  my $entries = {};
  for my $dir(@OTHER_DIRS){
    my $dirEntries = getDirEntries $dir;
    $entries = {%$entries, %$dirEntries};
  }
  my $showEntries = getShowEntries();
  $entries = {%$entries, %$showEntries};
  return $entries;
}

sub getShowNames(){
  return map {dirPathToName $_} glob "$SHOWS_DIR/sheet_double/*/";
}

sub getShowEntries(){
  my $entries = {};
  my @names = getShowNames();
  for my $name(@names){
    my $entry = {
      name => $name,
      types => [
        { typeName => "double",
          files => [glob "$SHOWS_DIR/sheet_double/$name/*.jpg"],
        },
        { typeName => "single",
          files => [glob "$SHOWS_DIR/sheet_single/$name/*.jpg"],
        },
      ]
    };
    $$entries{"$name"} = $entry;
    $$entries{"show-$name"} = $entry;
  }
  return $entries;
}

#return files in a dir before children of that dir
sub sortByDirs($$){
  my ($a, $b) = @_;
  my $aFullDir = $a;
  $aFullDir =~ s/\/+$//;
  $aFullDir =~ s/\/\/+/\//;
  $aFullDir =~ s/\/[^\/]+$//;

  my $bFullDir = $b;
  $bFullDir =~ s/\/+$//;
  $bFullDir =~ s/\/\/+/\//;
  $bFullDir =~ s/\/[^\/]+$//;

  my @aDirs = split /\//, $aFullDir;
  my @bDirs = split /\//, $bFullDir;

  my $maxSize = @aDirs > @bDirs ? @aDirs : @bDirs;

  for(my $i=0; $i<$maxSize; $i++){
    my $aDir = $i < @aDirs ? $aDirs[$i] : "";
    my $bDir = $i < @bDirs ? $bDirs[$i] : "";
    my $comp = $aDir cmp $bDir;
    if($comp != 0){
      return $comp;
    }
  }

  return $a cmp $b;
}

sub getDirEntries($){
  my ($dir) = @_;
  my $parentDirName = dirPathToName $dir;

  my $entries = {};
  for my $subDir(glob "$dir/*/"){
    my $name = dirPathToName $subDir;
    my @files = readProcLines "find", $subDir, "-type", "f";
    chomp foreach @files;

    @files = sort {sortByDirs($a, $b)} @files;
    my @types;
    my @jpeg = grep {/\.(jpg|jpeg)$/} @files;
    my @imgs = grep {/\.(jpg|jpeg|png|gif|tiff|bmp)$/} @files;
    my @pdfs = grep {/\.(pdf|ps)$/} @files;
    my @mscz = grep {/\.(mscz)$/} @files;
    if(@jpeg > 0){
      push @types, {
        typeName => "jpeg",
        files => [@jpeg],
      };
    }
    if(@imgs > 0){
      push @types, {
        typeName => "image",
        files => [@imgs],
      };
    }
    if(@pdfs > 0){
      push @types, {
        typeName => "pdf",
        files => [@pdfs],
      };
    }
    if(@mscz > 0){
      push @types, {
        typeName => "musescore",
        files => [@mscz],
      };
    }
    my $entry = {
      name => $name,
      types => [@types],
    };

    $$entries{$name} = $entry;
    $$entries{"$parentDirName-$name"} = $entry;
  }
  return $entries;
}

sub dirPathToName($){
  my ($dirPath) = @_;
  die "could not find dir: $dirPath\n" if not -d $dirPath;

  my $name = $dirPath;
  $name = "" if not defined $name;
  $name =~ s/^.*\/([^\/]+)\/?$/$1/;
  if($name =~ /\/|\r|\n|\0/){
    die "could not process dir path: $dirPath\n"
  }elsif($name =~ /\s|"|'/){
    die "name cannot have spaces or quotes: \"$name\"\n"
  }elsif($name =~ /^$/){
    die "name cannot be empty: \"$name\n";
  }elsif($name !~ /^[a-zA-Z0-9_\-]+$/){
    die "name can contain only letters/numbers/underscores/hyphens: \"$name\"\n"
  }

  return $name;
}

sub readProcLines(@){
  open CMD, "-|", @_ or die "could not run \"@_\"\n$!\n";
  my @lines = <CMD>;
  close CMD;
  return @lines;
}

sub readLastUsedFile(){
  if(-f $LAST_USED_FILE){
    open FH, "< $LAST_USED_FILE" or die "could not read $LAST_USED_FILE\n$!\n";
    my $contents = join '', <FH>;
    close FH;
    if($contents =~ /^(\w+),(\w*)$/){
      my ($name, $typeArg) = ($1, $2);
      if(defined $name and $name eq ""){
        $name = undef;
      }
      if(defined $typeArg and $typeArg eq ""){
        $typeArg = undef;
      }
      return ($name, $typeArg);
    }
  }
  return (undef, undef);
}
sub writeLastUsedFile($$){
  my ($name, $typeArg) = @_;
  $name = "" if not defined $name;
  $typeArg = "" if not defined $typeArg;

  open FH, "> $LAST_USED_FILE";
  print FH "$name,$typeArg\n";
  close FH;
}

sub bashComplete($$){
  my ($cmdLine, $pos) = @_;
  my $cmd = substr $cmdLine, 0, $pos;
  my $isNewWord = $cmd =~ /\s$/;
  $cmd =~ s/^\s+//;
  $cmd =~ s/\s+$//;

  my @words = split /\s+/, $cmd;
  shift @words;
  pop @words if not $isNewWord;

  while(@words > 0 and $words[0] =~ /^-/){
    shift @words;
  }

  my @names;
  my $entries = getAllEntries();
  for my $key(sort keys %$entries){
    push @names, $key;
  }

  if(@words == 0){
    return (
      "-h", "--help",
      "--bash-complete",
      "-w", "--window",
      "-d", "--daemon",
      "-p", "--player",
      @names,
    );
  }

  if(@words == 1){
    my @okTypes;
    my $entry = $$entries{$words[0]};
    if(defined $entry){
      for my $type(@{$$entry{types}}){
        push @okTypes, $$type{typeName};
      }
    }
    return @okTypes;
  }

  return ();
}

&main(@ARGV);
