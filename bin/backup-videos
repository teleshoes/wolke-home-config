#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub backup($$$$);
sub isMountpoint($$);

my $IPMAGIC_NAME = "raspi";
my $USER = "pi";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC
    backup videos
";

sub main(@){
  my $destLocal = {
    isRemote    => 0,
    ipmagicName => undef,
    ipmagicUser => undef,
  };
  my $destMaybeRemote= {
    isRemote    => 1,
    ipmagicName => $IPMAGIC_NAME,
    ipmagicUser => $USER,
  };

  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  backup $destLocal, $destMaybeRemote, "/media/videos", "/media/BACKUP_VIDEOS";
  backup $destLocal, $destMaybeRemote, "/media/videos_series", "/media/BACKUP_VIDEOS_SERIES";
  backup $destLocal, $destMaybeRemote, "/media/videos_movies", "/media/BACKUP_VIDEOS_MOVIES";
}

sub backup($$$$){
  my ($destLocal, $destMaybeRemote, $srcPath, $destPath) = @_;
  print "\n\nbacking up $srcPath\n";

  die "$srcPath is not a mountpoint\n" if not isMountpoint $destLocal, $srcPath;
  die "$destPath is not a mountpoint\n" if not isMountpoint $destMaybeRemote, $destPath;

  my @cmd;
  if($$destMaybeRemote{isRemote}){
    my $ipmagicName = $$destMaybeRemote{ipmagicName};
    my $user = $$destMaybeRemote{ipmagicUser};
    my $host = `ipmagic $ipmagicName --host`;
    chomp $host;

    @cmd = ("rsync", "-avP", "--del", "$srcPath/", "$user\@$host:$destPath/");
  }else{
    @cmd = ("rsync", "-avP", "--del", "$srcPath/", "$destPath/");
  }

  print "@cmd\n";
  system @cmd;
}

sub isMountpoint($$){
  my ($dest, $dir) = @_;
  my $cmd = "mountpoint '$dir'";
  if($$dest{isRemote}){
    $cmd = "ipmagic $$dest{ipmagicName} $cmd";
  }
  my $out = `$cmd 2>&1`;
  if($out =~ /^$dir is a mountpoint$/){
    return 1;
  }else{
    return 0;
  }
}

&main(@ARGV);
