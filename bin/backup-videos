#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub backup($$$$);
sub isMountpoint($$);
sub rsyncMaybeRemoteDest($$$@);
sub readProc($@);
sub run(@);

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS]
    backup videos

  OPTS
    --ipmagic-name=IPMAGIC_NAME
      use IPMAGIC_NAME for remote destination of BACKUP drives

    --ipmagic-user=IPMAGIC_USER
      use IPMAGIC_USER for user on IPMAGIC_NAME
";

sub main(@){
  my $destLocal = {
    isRemote    => 0,
    ipmagicName => undef,
    ipmagicUser => undef,
  };
  my $destMaybeRemote= {
    isRemote    => 0,
    ipmagicName => undef,
    ipmagicUser => undef,
  };

  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^--ipmagic-name=(\w+)$/){
      $$destMaybeRemote{ipmagicName} = $1;
      $$destMaybeRemote{isRemote} = 1;
    }elsif($arg =~ /^--ipmagic-user=(\w+)$/){
      $$destMaybeRemote{ipmagicUser} = $1;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  backup $destLocal, $destMaybeRemote, "/media/videos", "/media/BACKUP_VIDEOS";
  backup $destLocal, $destMaybeRemote, "/media/videos_series", "/media/BACKUP_VIDEOS_SERIES";
  backup $destLocal, $destMaybeRemote, "/media/videos_movies", "/media/BACKUP_VIDEOS_MOVIES";
}

sub backup($$$$){
  my ($destLocal, $destMaybeRemote, $srcPath, $destPath) = @_;
  print "\n\nbacking up $srcPath\n";

  die "$srcPath is not a mountpoint\n" if not isMountpoint $destLocal, $srcPath;
  die "$destPath is not a mountpoint\n" if not isMountpoint $destMaybeRemote, $destPath;

  rsyncMaybeRemoteDest($destMaybeRemote, "$srcPath/", "$destPath/",
    "-avP", "--del",
  );
}

sub isMountpoint($$){
  my ($dest, $dir) = @_;
  my $out = readProc($dest, "mountpoint", $dir);
  if($out =~ /^$dir is a mountpoint$/){
    return 1;
  }else{
    return 0;
  }
}

sub rsyncMaybeRemoteDest($$$@){
  my ($dest, $srcPath, $destPath, @rsyncArgs) = @_;
  my @cmd;
  if($$dest{isRemote}){
    @cmd = (@cmd, "ipmagic", $$dest{ipmagicName});
    @cmd = (@cmd, "-u", $$dest{ipmagicUser}) if defined $$dest{ipmagicUser};
    @cmd = (@cmd, "--rsync", @rsyncArgs, $srcPath, ":$destPath");
  }else{
    @cmd = (@cmd, "rsync", @rsyncArgs, $srcPath, $destPath);
  }
  run @cmd;
}

sub readProc($@){
  my ($dest, @cmd) = @_;
  if(defined $$dest{ipmagicName}){
    my @user = defined $$dest{ipmagicUser} ? ("-u", $$dest{ipmagicUser}) : ();
    @cmd = ("ipmagic", $$dest{ipmagicName}, @user, @cmd);
  }

  open my $cmdH, "-|", @cmd or die "ERROR: could not run @_\n$!\n";
  my @lines = <$cmdH>;
  close $cmdH;

  if(wantarray){
    return @lines;
  }else{
    return join '', @lines;
  }
}

sub run(@){
  my (@cmd) = @_;
  print "@cmd\n";
  system @cmd;
  if($? != 0){
    die "ERROR: cmd @cmd failed\n";
  }
}

&main(@ARGV);
