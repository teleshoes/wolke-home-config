#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub backup($$$$);
sub formatDest($);
sub isMountpoint($$);
sub ensureMountpoint($$);
sub rsyncMaybeRemoteDest($$$@);
sub readProc($@);
sub test($@);
sub run(@);

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS]
    backup videos

  OPTS
    --ipmagic-name=IPMAGIC_NAME
      use IPMAGIC_NAME for remote destination of BACKUP drives

    --ipmagic-user=IPMAGIC_USER
      use IPMAGIC_USER for user on IPMAGIC_NAME
";

sub main(@){
  my $destLocal = {
    isRemote    => 0,
    ipmagicName => undef,
    ipmagicUser => undef,
  };
  my $destMaybeRemote= {
    isRemote    => 0,
    ipmagicName => undef,
    ipmagicUser => undef,
  };

  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^--ipmagic-name=(\w+)$/){
      $$destMaybeRemote{ipmagicName} = $1;
      $$destMaybeRemote{isRemote} = 1;
    }elsif($arg =~ /^--ipmagic-user=(\w+)$/){
      $$destMaybeRemote{ipmagicUser} = $1;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  backup $destLocal, $destMaybeRemote, "/media/videos", "/media/BACKUP_VIDEOS";
  backup $destLocal, $destMaybeRemote, "/media/videos_series", "/media/BACKUP_VIDEOS_SERIES";
  backup $destLocal, $destMaybeRemote, "/media/videos_movies", "/media/BACKUP_VIDEOS_MOVIES";
}

sub backup($$$$){
  my ($destLocal, $destMaybeRemote, $srcPath, $destPath) = @_;
  print "\n\nbacking up $srcPath\n";

  ensureMountpoint($destLocal, $srcPath);
  ensureMountpoint($destMaybeRemote, $destPath);

  rsyncMaybeRemoteDest($destMaybeRemote, "$srcPath/", "$destPath/",
    "-avP", "--del",
  );
}

sub formatDest($){
  my ($dest) = @_;
  if($$dest{isRemote}){
    my $user = defined $$dest{ipmagicUser} ? "$$dest{ipmagicUser}@" : "";
    return "$user$$dest{ipmagicName}";
  }else{
    return "localhost";
  }
}

sub isMountpoint($$){
  my ($dest, $dir) = @_;
  my $out = readProc($dest, "mountpoint", $dir);
  if($out =~ /^$dir is a mountpoint$/){
    return 1;
  }else{
    return 0;
  }
}
sub ensureMountpoint($$){
  my ($dest, $dir) = @_;
  if(not test($dest, "-d", $dir)){
    die "ERROR: $dir is not a dir (" . formatDest($dest) . ")\n";
  }
  if(not isMountpoint($dest, $dir)){
    die "ERROR: $dir is not a mountpoint (" . formatDest($dest) . ")\n";
  }
}

sub rsyncMaybeRemoteDest($$$@){
  my ($dest, $srcPath, $destPath, @rsyncArgs) = @_;
  my @cmd;
  if($$dest{isRemote}){
    @cmd = (@cmd, "ipmagic", $$dest{ipmagicName});
    @cmd = (@cmd, "-u", $$dest{ipmagicUser}) if defined $$dest{ipmagicUser};
    @cmd = (@cmd, "--rsync", @rsyncArgs, $srcPath, ":$destPath");
  }else{
    @cmd = (@cmd, "rsync", @rsyncArgs, $srcPath, $destPath);
  }
  run @cmd;
}

sub readProc($@){
  my ($dest, @cmd) = @_;
  if(defined $$dest{ipmagicName}){
    my @user = defined $$dest{ipmagicUser} ? ("-u", $$dest{ipmagicUser}) : ();
    @cmd = ("ipmagic", $$dest{ipmagicName}, @user, @cmd);
  }

  open my $cmdH, "-|", @cmd or die "ERROR: could not run @_\n$!\n";
  my @lines = <$cmdH>;
  close $cmdH;

  if(wantarray){
    return @lines;
  }else{
    return join '', @lines;
  }
}

sub test($@){
  my ($dest, @testArgs) = @_;
  my @cmd = ("test", @testArgs);
  if(defined $$dest{ipmagicName}){
    my @user = defined $$dest{ipmagicUser} ? ("-u", $$dest{ipmagicUser}) : ();
    @cmd = ("ipmagic", $$dest{ipmagicName}, @user, @cmd);
  }

  system @cmd;
  if($? == 0){
    return 1;
  }else{
    return 0;
  }
}

sub run(@){
  my (@cmd) = @_;
  print "@cmd\n";
  system @cmd;
  if($? != 0){
    die "ERROR: cmd @cmd failed\n";
  }
}

&main(@ARGV);
