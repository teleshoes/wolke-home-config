#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub backup($$;$$);
sub isMountpoint($;$);

my $IPMAGIC_NAME = "raspi";
my $USER = "pi";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC
    backup videos
";

sub main(@){
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  backup "/media/videos", "/media/BACKUP_VIDEOS", $IPMAGIC_NAME, $USER;
  backup "/media/videos_series", "/media/BACKUP_VIDEOS_SERIES", $IPMAGIC_NAME, $USER;
  backup "/media/videos_movies", "/media/BACKUP_VIDEOS_MOVIES", $IPMAGIC_NAME, $USER;
}

sub backup($$;$$){
  my ($srcPath, $destPath, $ipmagicName, $user) = @_;
  print "\n\nbacking up $srcPath\n";

  die "$srcPath is not a mountpoint\n" if not isMountpoint $srcPath;
  die "$destPath is not a mountpoint\n" if not isMountpoint $destPath, $ipmagicName;

  my @cmd;
  if(defined $ipmagicName){
    my $host = `ipmagic $ipmagicName --host`;
    chomp $host;

    @cmd = ("rsync", "-avP", "--del", "$srcPath/", "$user\@$host:$destPath/");
  }else{
    @cmd = ("rsync", "-avP", "--del", "$srcPath/", "$destPath/");
  }

  print "@cmd\n";
  system @cmd;
}

sub isMountpoint($;$){
  my ($dir, $ipmagicName) = @_;
  my $cmd = "mountpoint '$dir'";
  $cmd = "ipmagic $ipmagicName $cmd" if defined $ipmagicName;
  my $out = `$cmd 2>&1`;
  if($out =~ /^$dir is a mountpoint$/){
    return 1;
  }else{
    return 0;
  }
}

&main(@ARGV);
