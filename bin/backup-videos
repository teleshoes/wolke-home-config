#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub formatDest($);
sub formatBackupMountpoint($);
sub isMountpoint($$);
sub ensureMountpoint($$);
sub readMountpointSizeBytes($$);
sub rsyncMaybeRemoteDest($$$@);
sub readProc($@);
sub test($@);
sub run(@);

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS]
    -get local VIDEO_MOUNTPOINT list:
      /media/videos
      /media/videos_series
      /media/videos_movies
    -for each VIDEO_MOUNTPOINT:
      -calculate BACKUP_MOUNTPOINT
        -convert last element to uppercase, prefix with BACKUP
        e.g.: /media/movies1/ => /media/BACKUP_MOVIES1/
      -ensure BACKUP_MOUNTPOINT is a mountpoint with `mountpoint`
        -use ipmagic for mountpoint if IPMAGIC_NAME is given
      -ensure VIDEO_MOUNTPOINT and BACKUP_MOUNTPOINT have the identical volume size with `df`
        -use ipmagic for 'df BACKUP_MOUNTPOINT' if IPMAGIC_NAME is given
      -run rsync
        -if IPMAGIC_NAME is given:
           run: ipmagic IPMAGIC_NAME [-u IPMAGIC_USER] --rsync -avP \\
                  VIDEO_MOUNTPOINT/ \\
                  :BACKUP_MOUNTPOINT/ \\
                  MAYBE_SIMULATE_ARG \\
                  MAYBE_DELETE_ARG \\
                ;
        -otherwise:
           run: rsync -avP \\
                  VIDEO_MOUNTPOINT/ \\
                  BACKUP_MOUNTPOINT/ \\
                  MAYBE_SIMULATE_ARG \\
                  MAYBE_DELETE_ARG \\
                ;

    MAYBE_DELETE_ARG
      '--del' if --delete is given, otherwise empty
      does not override --simulate (nothing will be deleted with --simulate)
    MAYBE_SIMULATE_ARG
      '--dry-run' if --simulate is given, otherwise empty
      (append '--dry-run' to rsync commands on --simulate)

  $EXEC [OPTS] IPMAGIC_USER\@IPMAGIC_NAME
    same as: $EXEC --ipmagic-name=IPMAGIC_NAME --ipmagic-user=IPMAGIC_USER

  OPTS
    -s | --simulate | -n | --no-act | --dry-run | --dryrun
      pass '--dry-run' to rsync
      (do not copy any files, do not delete any files)

    --del | --delete
      pass --del to rsync commands
      (if given with --simulate, will pass '--dry-run --del' and will not delete files)

    --ipmagic-name=IPMAGIC_NAME
      use IPMAGIC_NAME for remote destination of BACKUP drives

    --ipmagic-user=IPMAGIC_USER
      use IPMAGIC_USER for user on IPMAGIC_NAME
";

sub main(@){
  my $opts = {
    simulate    => 0,
    rsyncDelete => 0,
  };
  my $destLocal = {
    isRemote    => 0,
    ipmagicName => undef,
    ipmagicUser => undef,
  };
  my $destMaybeRemote= {
    isRemote    => 0,
    ipmagicName => undef,
    ipmagicUser => undef,
  };

  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-s|--simulate|-n|--no-act|--dry-run|--dryrun)$/){
      $$opts{simulate} = 1;
    }elsif($arg =~ /^(--del|--delete|--sync-del|--sync-delete)$/){
      $$opts{rsyncDelete} = 1;
    }elsif($arg =~ /^--ipmagic-name=(\w+)$/){
      $$destMaybeRemote{ipmagicName} = $1;
      $$destMaybeRemote{isRemote} = 1;
    }elsif($arg =~ /^--ipmagic-user=(\w+)$/){
      $$destMaybeRemote{ipmagicUser} = $1;
    }elsif($arg =~ /^(\w+)\@(\w+)$/){
      $$destMaybeRemote{ipmagicName} = $1;
      $$destMaybeRemote{ipmagicUser} = $2;
      $$destMaybeRemote{isRemote} = 1;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  my @mediaMountpoints = qw(
    /media/videos
    /media/videos_series
    /media/videos_movies
  );

  #ensure mountpoints and disk sizes for all up front
  for my $mediaMountpoint(@mediaMountpoints){
    my $backupMountpoint = formatBackupMountpoint($mediaMountpoint);
    ensureMountpoint($destLocal, $mediaMountpoint);
    ensureMountpoint($destMaybeRemote, $backupMountpoint);

    my $sizeMedia = readMountpointSizeBytes($destLocal, $mediaMountpoint);
    my $sizeBackup = readMountpointSizeBytes($destMaybeRemote, $backupMountpoint);
    if($sizeMedia ne $sizeBackup){
      die "ERROR: size mismatch\n"
        . "  $mediaMountpoint=$sizeMedia\n"
        . "  $backupMountpoint=$sizeBackup (" . formatDest($destMaybeRemote) . ")\n"
      ;
    }
  }

  for my $mediaMountpoint(@mediaMountpoints){
    print "\n\nbacking up $mediaMountpoint\n";
    my $backupMountpoint = formatBackupMountpoint($mediaMountpoint);

    my @maybeSimulateArg = $$opts{simulate} ? ("-n") : ();
    my @maybeDeleteArg = $$opts{rsyncDelete} ? ("--del") : ();

    rsyncMaybeRemoteDest($destMaybeRemote, "$mediaMountpoint/", "$backupMountpoint/",
      "-avP",
      @maybeSimulateArg,
      @maybeDeleteArg,
    );
  }
}

sub formatDest($){
  my ($dest) = @_;
  if($$dest{isRemote}){
    my $user = defined $$dest{ipmagicUser} ? "$$dest{ipmagicUser}@" : "";
    return "$user$$dest{ipmagicName}";
  }else{
    return "localhost";
  }
}

sub formatBackupMountpoint($){
  my ($mediaMountpoint) = @_;
  if($mediaMountpoint =~ /^\/media\/(\w+)\/?$/){
    my $name = $1;
    $name = uc $name;
    return "/media/BACKUP_$name";
  }else{
    die "ERROR: malformed MEDIA_MOUNTPOINT \"$mediaMountpoint\"\n";
  }
}

sub isMountpoint($$){
  my ($dest, $dir) = @_;
  my $out = readProc($dest, "mountpoint", $dir);
  if($out =~ /^$dir is a mountpoint$/){
    return 1;
  }else{
    return 0;
  }
}
sub ensureMountpoint($$){
  my ($dest, $dir) = @_;
  if(not test($dest, "-d", $dir)){
    die "ERROR: $dir is not a dir (" . formatDest($dest) . ")\n";
  }
  if(not isMountpoint($dest, $dir)){
    die "ERROR: $dir is not a mountpoint (" . formatDest($dest) . ")\n";
  }
}

sub readMountpointSizeBytes($$){
  my ($dest, $dir) = @_;
  my $dfOut = readProc($dest, "df", "-B", 1, "--output=size,target", $dir);

  if($dfOut !~ /^\s*1B-blocks\s+Mounted on\s*\n\s*(\d+)\s+(.+)$/){
    die "ERROR: could not read df output for $dir on " . formatDest($dest) . "\n";
  }
  my ($sizeBytes, $mountpoint) = ($1, $2);

  if($mountpoint ne $dir){
    die "ERROR: $dir is not the mountpoint ($mountpoint) on " . formatDest($dest) . "\n";
  }

  return $sizeBytes;
}

sub rsyncMaybeRemoteDest($$$@){
  my ($dest, $srcPath, $destPath, @rsyncArgs) = @_;
  my @cmd;
  if($$dest{isRemote}){
    @cmd = (@cmd, "ipmagic", $$dest{ipmagicName});
    @cmd = (@cmd, "-u", $$dest{ipmagicUser}) if defined $$dest{ipmagicUser};
    @cmd = (@cmd, "--rsync", @rsyncArgs, $srcPath, ":$destPath");
  }else{
    @cmd = (@cmd, "rsync", @rsyncArgs, $srcPath, $destPath);
  }
  run @cmd;
}

sub readProc($@){
  my ($dest, @cmd) = @_;
  if(defined $$dest{ipmagicName}){
    my @user = defined $$dest{ipmagicUser} ? ("-u", $$dest{ipmagicUser}) : ();
    @cmd = ("ipmagic", $$dest{ipmagicName}, @user, @cmd);
  }

  open my $cmdH, "-|", @cmd or die "ERROR: could not run @_\n$!\n";
  my @lines = <$cmdH>;
  close $cmdH;

  if(wantarray){
    return @lines;
  }else{
    return join '', @lines;
  }
}

sub test($@){
  my ($dest, @testArgs) = @_;
  my @cmd = ("test", @testArgs);
  if(defined $$dest{ipmagicName}){
    my @user = defined $$dest{ipmagicUser} ? ("-u", $$dest{ipmagicUser}) : ();
    @cmd = ("ipmagic", $$dest{ipmagicName}, @user, @cmd);
  }

  system @cmd;
  if($? == 0){
    return 1;
  }else{
    return 0;
  }
}

sub run(@){
  my (@cmd) = @_;
  print "@cmd\n";
  system @cmd;
  if($? != 0){
    die "ERROR: cmd @cmd failed\n";
  }
}

&main(@ARGV);
