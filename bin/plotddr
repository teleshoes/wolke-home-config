#!/usr/bin/perl
use strict;
use warnings;

sub run(@);

my $gnuplotDir = "$ENV{HOME}/.cache/gnuplot-ddr";
my $bestDir = "$ENV{HOME}/.cache/best-ddr";
my $pixmirrorDir = "$ENV{HOME}/Desktop/DCIM-pixmirror/xddr";

my @curSongs = qw(max300 legendofmax max300smmm paranoiasurvivor270 paranoiasurvivormax290 paranoiarespect maxxunlimited);
my $curSongsRegex = "^(" . join("|", @curSongs) . ")\$";

my @plotArr = (
  'cursongs_recent' =>
    ["--notes", "highscore", "--song", "$curSongsRegex", "--recent", "--singles"],
  'cursongs_recent_smooth' =>
    ["--smooth", "--notes", "highscore", "--song", "$curSongsRegex", "--recent", "--singles"],
  'hard_recent' =>
    ["--notes", "highscore", "--hard", "--recent", "--singles"],
  'hard_recent_smooth' =>
    ["--smooth", "--notes", "highscore", "--hard", "--recent", "--singles"],
  'high' =>
    ["--notes", "highscore,extra", "--score", "870000", "--singles"],
  'doubles' =>
    ["--notes", "highscore", "--recent", "--doubles"],
);
my $plots = {@plotArr};
my @plotOrder = map {$plotArr[$_]} grep {$_%2 == 0} (0 .. $#plotArr);

my $subdirVariants = {
  'fhd' => ["--size", "1920x1080"],
  'n9' => ["--size", "854x480"],
};

my $usage = "Usage: $0 [--nofeh|--non9]\n";

sub main(@){
  my $noFeh = 0;
  my $noN9 = 0;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^--nofeh/){
      $noFeh = 1;
    }elsif($arg =~ /^--non9/){
      $noN9 = 1;
    }else{
      die $usage;
    }
  }
  die $usage if @_ > 0;

  system "rm", "-rf", $gnuplotDir;
  for my $subdir(sort keys %$subdirVariants){
    system "mkdir", "-p", "$gnuplotDir/$subdir";
    my $variants = $$subdirVariants{$subdir};
    my $num = 0;
    my @order = @plotOrder;
    @order = reverse @order if $subdir eq 'n9';
    for my $name(@order){
      $num++;
      my $plot = $$plots{$name};
      my $file = "$gnuplotDir/$subdir/$num-gnuplot-$name.png";
      my @cmd = ("ddrname", "--gnuplot", "--file", $file, @$variants, @$plot);
      system @cmd;
      die "error running plot $name => @$plot\n" if $? != 0;
    }
  }

  system "rm", "-rf", $bestDir;
  my @bestNotHard;
  my @bestHard;
  my @bestDoubles;

  @bestHard = (@bestHard, `ddrname --bestfiles --hard --singles`);
  @bestHard = (@bestHard, `ddrname --bestfiles --nonstop`);

  @bestNotHard = (@bestNotHard, `ddrname --bestfiles --nothard --singles`);

  @bestDoubles = (@bestDoubles, `ddrname --bestfiles --doubles`);
  @bestDoubles = (@bestDoubles, `ddrname --bestfiles --nonstopdoubles`);

  chomp foreach @bestHard;
  chomp foreach @bestNotHard;
  chomp foreach @bestDoubles;

  @bestHard = map { "$pixmirrorDir/$_" } @bestHard;
  @bestNotHard = map { "$pixmirrorDir/$_" } @bestNotHard;
  @bestDoubles = map { "$pixmirrorDir/$_" } @bestDoubles;

  system "mkdir", "-p", "$bestDir/hard", "$bestDir/nothard", "$bestDir/doubles";
  system "cp", "-ar", @bestHard, "$bestDir/hard";
  system "cp", "-ar", @bestNotHard, "$bestDir/nothard";
  system "cp", "-ar", @bestDoubles, "$bestDir/doubles";

  if(not $noFeh){
    run "feh -ZF '$gnuplotDir/fhd' >/dev/null 2>/dev/null &";
  }
  if(not $noN9){
    my $n9Host = `n9`;
    chomp $n9Host;

    run "rsync", "-avP", "--del", "$gnuplotDir/n9/", "user\@$n9Host:~/xgnuplotddr";
    run "rsync", "-avP", "--del", "$bestDir/", "user\@$n9Host:~/xbestddr";
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
