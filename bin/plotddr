#!/usr/bin/perl
use strict;
use warnings;

sub run(@);

my $gnuplotDir = "$ENV{HOME}/.cache/gnuplot-ddr";
my $bestDir = "$ENV{HOME}/.cache/best-ddr";
my $pixmirrorDir = "$ENV{HOME}/DCIM-pixmirror/xddr";

my @curSongs = qw(
  max-300 paranoia-survivor
  gaia max-300-super-max-me-mix prana revolutionary-addict
  prana-revolutionary-addict-u1-dj-mix possession revolution
);
my $curSongsRegex = "^(" . join("|", @curSongs) . ")\$";

my $ipmagicVariantDir = "n9";
my $ipmagicName = "n9";
my $ipmagicUser = "user";
my $ipmagicPlotDir = "~/xgnuplotddr";
my $ipmagicBestScoresDir = "~/xbestddr";

my @DDRA = ("--a");
my @RCNT = ("--recent");
my @SNGL = ("--singles");
my @DBST = ("--dailybest");
my @MONO = ("--monotonic");
my @____ = ();

my @plotArr = (
  'cursongs_recent' => {
    filters => [@DDRA, @RCNT, @SNGL, @DBST, @MONO, "--song", $curSongsRegex],
    formatOpts => ["--notes", "highscore"],
    plotOpts => [],
  },
  'cursongs_recent_smooth' => {
    filters => [@DDRA, @RCNT, @SNGL, @DBST, @____, "--song", $curSongsRegex],
    formatOpts => ["--notes", "highscore"],
    plotOpts => ["--smooth"],
  },
  'hard_recent' => {
    filters => [@DDRA, @RCNT, @SNGL, @DBST, @MONO, "--minfeet", "16"],
    formatOpts => ["--notes", "highscore"],
    plotOpts => [],
  },
  'hard_recent_smooth' => {
    filters => [@DDRA, @RCNT, @SNGL, @DBST, @____, "--minfeet", "16"],
    formatOpts => ["--notes", "highscore"],
    plotOpts => ["--smooth"],
  },
  'high' => {
    filters => [@DDRA, @RCNT, @SNGL, @DBST, @____, "--score", "950000"],
    formatOpts => ["--notes", "highscore,extra"],
    plotOpts => [],
  },
  'doubles' => {
    filters => [@DDRA, @RCNT, @____, @DBST, @MONO, "--doubles"],
    formatOpts => ["--notes", "highscore"],
    plotOpts => [],
  },
);
my $plots = {@plotArr};
my @plotOrder = map {$plotArr[$_]} grep {$_%2 == 0} (0 .. $#plotArr);

my $subdirVariants = {
  'fhd' => {
    extraFilters => [],
    extraPlotOpts => ["--size", "1920x1080"],
    extraFormatOpts => [],
    reverse => 0,
  },
  'n9' => {
    extraFilters => [],
    extraPlotOpts => ["--size", "854x480"],
    extraFormatOpts => ["--abbrev"],
    reverse => 1,
  },
};

my $usage = "Usage: $0 [--nofeh|--noipmagic]\n";

sub main(@){
  my $noFeh = 0;
  my $noIpmagic = 0;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^--nofeh/){
      $noFeh = 1;
    }elsif($arg =~ /^--noIpmagic/){
      $noIpmagic = 1;
    }else{
      die $usage;
    }
  }
  die $usage if @_ > 0;

  system "rm", "-rf", $gnuplotDir;
  for my $subdir(sort keys %$subdirVariants){
    system "mkdir", "-p", "$gnuplotDir/$subdir";
    my $variant = $$subdirVariants{$subdir};
    my $num = 0;
    my @order = @plotOrder;

    my $isReverse = $$variant{reverse};
    $isReverse = 0 if not defined $isReverse;
    @order = reverse @order if $$variant{reverse};

    for my $name(@order){
      $num++;
      my $plot = $$plots{$name};
      my @filters = (@{$$plot{filters}}, @{$$variant{extraFilters}});
      my @formatOpts = (@{$$plot{formatOpts}}, @{$$variant{extraFormatOpts}});
      my @plotOpts = (@{$$plot{plotOpts}}, @{$$variant{extraPlotOpts}});

      my $file = "$gnuplotDir/$subdir/$num-gnuplot-$name.png";
      @plotOpts = ("--file", $file, @plotOpts);
      my @cmd = ("ddrname", "--gnuplot", @plotOpts, @formatOpts, @filters);

      run @cmd;
      die "error running plot $name\n" if $? != 0;
    }
  }

  system "rm", "-rf", $bestDir;
  my @bestNotHard;
  my @bestHard;
  my @bestDoubles;

  @bestHard = (@bestHard, `ddrname --bestfiles --hard --singles`);
  @bestHard = (@bestHard, `ddrname --bestfiles --nonstop`);

  @bestNotHard = (@bestNotHard, `ddrname --bestfiles --nothard --singles`);

  @bestDoubles = (@bestDoubles, `ddrname --bestfiles --doubles`);
  @bestDoubles = (@bestDoubles, `ddrname --bestfiles --nonstopdoubles`);

  chomp foreach @bestHard;
  chomp foreach @bestNotHard;
  chomp foreach @bestDoubles;

  @bestHard = map { "$pixmirrorDir/$_" } @bestHard;
  @bestNotHard = map { "$pixmirrorDir/$_" } @bestNotHard;
  @bestDoubles = map { "$pixmirrorDir/$_" } @bestDoubles;

  system "mkdir", "-p", "$bestDir/hard", "$bestDir/nothard", "$bestDir/doubles";
  system "cp", "-ar", @bestHard, "$bestDir/hard";
  system "cp", "-ar", @bestNotHard, "$bestDir/nothard";
  system "cp", "-ar", @bestDoubles, "$bestDir/doubles";
  for my $dir(glob "$bestDir/*"){
    for my $file(glob "$dir/*.jpg"){
      my $newFile = $file;
      #rename <DATE>_<PIC#>_<INFO>.jpg => <INFO>_<DATE>_<PIC#>.jpg
      $newFile =~ s/\/(\d{8})_(\d+)_([^\/]+)\.jpg$/\/$3_$2_$1.jpg/;
      if(not -e $newFile){
        system "mv", $file, $newFile;
      }
    }
  }

  if(not $noFeh){
    run "feh -ZF '$gnuplotDir/fhd' >/dev/null 2>/dev/null &";
  }
  if(not $noIpmagic){
    my $ipmagicHost = `ipmagic $ipmagicName`;
    chomp $ipmagicHost;

    run "rsync", "-avP", "--del", "$gnuplotDir/$ipmagicVariantDir/", "$ipmagicUser\@$ipmagicHost:$ipmagicPlotDir";
    run "rsync", "-avP", "--del", "$bestDir/", "$ipmagicUser\@$ipmagicHost:$ipmagicBestScoresDir";
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
