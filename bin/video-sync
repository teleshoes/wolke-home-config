#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use Time::HiRes qw(time);
use Digest::MD5 qw();
my $USE_LIB_TEXT_CSV = eval {require Text::CSV};

sub syncRemote($);
sub parseCsvRow($);
sub readIncludeFile();
sub buildLocalSyncDir($$$);
sub findAllFiles();
sub readInfoCache($);
sub writeInfoCache($$);
sub formatInfoLine($$);
sub updateInfoCache($$$);
sub readMd5sumCache($);
sub writeMd5sumCache($$);
sub formatMd5sumLine($$);
sub updateMd5sumCache($$$);
sub gitCommitFile($$$);
sub isGitFileUnstaged($$);
sub parseDuration($);
sub formatDuration($);
sub md5sumFile($);
sub readFile($);
sub writeFile($@);
sub appendFile($@);
sub run(@);
sub runQuiet(@);
sub readProc(@);
sub nowMillis();

my $BASE_DIR = "/media/videos";
my $INFO_DIR = "$BASE_DIR/zzz_info";

my $FILE_INCLUDE_SYNC = "$INFO_DIR/include-sync";
my $FILE_REMOTE_MIRROR_IPMAGIC_NAME = "$BASE_DIR/videos_mirror_ipmagic_name";
my $TMP_SYNC_SYMLINKS_DIR_PREFIX = "/tmp/video-sync-tmp-symlinks-dir";

my @DEFAULT_SYNC_EXCLUDE_ARGS = qw(extras/ highres/);

my @SYNC_CATEGORIES = qw(
  anime
  clips_shorts_trailers
  concerts
  courses
  movies
  operas
  series
  specials
);
my @NONSYNC_CATEGORIES = qw(
  camera
  events
);
my @ALL_CATEGORIES = sort(@SYNC_CATEGORIES, @NONSYNC_CATEGORIES);
my $OK_CATS = join "|", @ALL_CATEGORIES;

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC [OPTS] --sync
  $EXEC [OPTS] --sync-remote
    -read IPMAGIC_NAME from $FILE_REMOTE_MIRROR_IPMAGIC_NAME
      -if none exists, fail
    -use SYNC_NAME=sync-<IPMAGIC_NAME>-<NOW_MILLIS>
    -use TMP_SYMLINKS_DIR=$TMP_SYNC_SYMLINKS_DIR_PREFIX-<SYNC_NAME>
    -build IPMAGIC_NAME:TMP_SYNC_DIR remotely
      -run: ipmagic IPMAGIC_NAME -s $EXEC --quiet --build-sync-dir SYNC_NAME_ARG
    -get list of CATEGORY entries to sync
      -default is: @SYNC_CATEGORIES
      -see: --sync-category
    -rsync each CATEGORY from remote TMP_SYMLINKS_DIR to $BASE_DIR
      -follow remote symlinks with '-L', copying actual files, not links
      -run: ipmagic IPMAGIC_NAME --rsync -L -avP \\
              :TMP_SYMLINKS_DIR/CATEGORY/ \\
              $BASE_DIR/CATEGORY/ \\
              EXCLUDE_ARGS \\
              MAYBE_DELETE_ARG \\
              MAYBE_SIMULATE_ARG \\
            ;
    EXCLUDE_ARGS
      default is: " . join(" ", map{"--exclude $_"} @DEFAULT_SYNC_EXCLUDE_ARGS) . "
      see: --sync-excludes
    MAYBE_DELETE_ARG
      '--del' if --sync-del is given, otherwise empty
      does not override --simulate (nothing will be deleted with --simulate)
    MAYBE_SIMULATE_ARG
      if --simulate is given '--dry-run', otherwise ''
      (append '--dry-run' to rsync commands on --simulate)

  $EXEC [OPTS] --sync-simulate
    same as: $EXEC --sync-remote --simulate

  $EXEC [OPTS] --build-sync-dir SYNC_NAME
  $EXEC [OPTS] --build-local-sync-dir SYNC_NAME
    -use TMP_SYMLINKS_DIR=$TMP_SYNC_SYMLINKS_DIR_PREFIX-<SYNC_NAME>
    -delete TMP_SYMLINKS_DIR if it exists
      -run: rm -rf TMP_SYMLINKS_DIR
    -read list of <CATEGORY>/<MEDIA_NAME> pairs, one per line, from:
      $FILE_INCLUDE_SYNC
    -for each <CATEGORY>/<MEDIA_NAME> pair in $FILE_INCLUDE_SYNC:
      -create symlink for each <MEDIA_NAME> in category subdir of sync dir:
        -run: mkdir -p TMP_SYNC_DIR/CATEGORY/
        -run: ln -s $BASE_DIR/CATEGORY/MEDIA_NAME/ TMP_SYNC_DIR/CATEGORY/MEDIA_NAME

    SYNC_NAME
      must start with 'sync' and contain only:
        lowercase letters, numbers, underscores, and dashes

  $EXEC [OPTS] --cache
    same as: $EXEC --cache-info && $EXEC --cache-md5sum

  $EXEC [OPTS] --cache-info
    fetch stat and duration info for all files in $BASE_DIR

  $EXEC [OPTS] --cache-md5sum
    calculate MD5SUM for each file in $BASE_DIR

  $EXEC [OPTS] --list-files
    list all regular files in all categories, following symlinks
      as in: find -L $BASE_DIR/!(<CATEGORY>|<CATEGORY>..) \\
               -type f
    -get ALL_FILES:
      -for each CATEGORY:
        -find every file in $BASE_DIR/<CATEGORY>
        -follow and dereference symlinks
        -omit non-regular files like directories and broken symlinks
    -print each file in ALL_FILES, one per line

  CATEGORY
    one of: $OK_CATS

  OPTS
    -q | --quiet
      do no print each symlink command in --build-local-sync-dir

    -s | --simulate | -n | --no-act | --dry-run | --dryrun
      do not actually sync media files in --sync-remote

    --no-git | --skip-git
      do not automatically git commit info cache files in --info

    -c CATEGORY_CSV
    --sync-categories=CATEGORY_CSV
      in sync-remote, only sync the indicated CATEGORY entries
        -parse CATEGORY_CSV into CATEGORY entries
        -skip any CATEGORY not given in --sync-remote
      CATEGORY_CSV
        comma-separated list of CATEGORY entries
          separator=','    quote_char='\"'    escape_char='\\'    quote_literal='\"\"'
          e.g.: -c movies,series,operas
        NOTE: simple csv parsing without quotes/escapes is used if Text::CSV is not found
        the default is:
          " . join(",", @SYNC_CATEGORIES) . "

    -x EXCLUDE_CSV
    --sync-excludes=EXCLUDE_CSV
      in sync-remote, override the default rsync EXCLUDE args
        -parse EXCLUDE_CSV into EXCLUDE args
        -pass '--exclude EXCLUDE' to each rsync command in --sync-remote
      EXCLUDE_CSV
        comma-separated list of EXCLUDE args
          separator=','    quote_char='\"'    escape_char='\\'    quote_literal='\"\"'
          e.g.: -x 'bigFiles1/,\"really,reallyBigFiles2/\",\"a\"\"good\"\"dirName3/'
        NOTE: simple csv parsing without quotes/escapes is used if Text::CSV is not found
        the default is:
          " . join(",", @DEFAULT_SYNC_EXCLUDE_ARGS) . "

    --del | --delete | --sync-del | --sync-delete
      pass '--del' to rsync commands in --sync-remote
";

my $CMD_SYNC_REMOTE = "sync-remote";
my $CMD_BUILD_LOCAL_SYNC_DIR = "build-local-sync-dir";
my $CMD_CACHE = "cache";

my $CMD_LIST_FILES = "list-files";

my $CACHE_TYPE_INFO = "info";
my $CACHE_TYPE_MD5SUM = "md5sum";

sub main(@){
  my $cmd = $CMD_SYNC_REMOTE;
  my $syncName = undef;
  my @cacheTypes = ($CACHE_TYPE_INFO, $CACHE_TYPE_MD5SUM);
  my $opts = {
    quiet           => 0,
    simulate        => 0,
    skipGit         => 0,
    syncCategories  => [@SYNC_CATEGORIES],
    syncExcludeArgs => [@DEFAULT_SYNC_EXCLUDE_ARGS],
    syncDelete      => 0,
  };

  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--sync|--sync-remote)$/){
      $cmd = $CMD_SYNC_REMOTE;
    }elsif($arg =~ /^(--sync-simulate)$/){
      $cmd = $CMD_SYNC_REMOTE;
      $$opts{simulate} = 1;
    }elsif($arg =~ /^(--build-sync-dir|--build-local-sync-dir)$/){
      $cmd = $CMD_BUILD_LOCAL_SYNC_DIR;
    }elsif($arg =~ /^(--cache)$/){
      $cmd = $CMD_CACHE;
      @cacheTypes = ($CACHE_TYPE_INFO, $CACHE_TYPE_MD5SUM);
    }elsif($arg =~ /^(--cache-info)$/){
      $cmd = $CMD_CACHE;
      @cacheTypes = ($CACHE_TYPE_INFO);
    }elsif($arg =~ /^(--cache-md5sum)$/){
      $cmd = $CMD_CACHE;
      @cacheTypes = ($CACHE_TYPE_MD5SUM);
    }elsif($arg =~ /^(--list-files)$/){
      $cmd = $CMD_LIST_FILES;
    }elsif($arg =~ /^(-q|--quiet)$/){
      $$opts{quiet} = 1;
    }elsif($arg =~ /^(-s|--simulate|-n|--no-act|--dry-run|--dryrun)$/){
      $$opts{simulate} = 1;
    }elsif($arg =~ /^(--no-git|--skip-git)$/){
      $$opts{skipGit} = 1;
    }elsif($arg =~ /^(-c)$/ and @_ > 0){
      my $csv = shift @_;
      $$opts{syncCategories} = [parseCsvRow($csv)],
    }elsif($arg =~ /^--sync-categories=(.+)$/){
      my $csv = $1;
      $$opts{syncCategories} = [parseCsvRow($csv)],
    }elsif($arg =~ /^(-x)$/ and @_ > 0){
      my $csv = shift @_;
      $$opts{syncExcludeArgs} = [parseCsvRow($csv)],
    }elsif($arg =~ /^--sync-excludes=(.+)$/){
      my $csv = $1;
      $$opts{syncExcludeArgs} = [parseCsvRow($csv)],
    }elsif($arg =~ /^(--del|--delete|--sync-del|--sync-delete)$/){
      $$opts{syncDelete} = 1;
    }elsif($cmd eq $CMD_BUILD_LOCAL_SYNC_DIR
             and not defined $syncName and $arg =~ /^(sync[a-z0-9_\-]*)$/){
      $syncName = $arg
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($cmd eq $CMD_SYNC_REMOTE){
    syncRemote($opts);
  }elsif($cmd eq $CMD_BUILD_LOCAL_SYNC_DIR){
    die "ERROR: missing SYNC_NAME\n" if not defined $syncName;
    my $mediaNamesByCategory = readIncludeFile();
    buildLocalSyncDir($opts, $syncName, $mediaNamesByCategory);
  }elsif($cmd eq $CMD_CACHE){
    for my $cacheType(@cacheTypes){
      if($cacheType eq $CACHE_TYPE_INFO){
        print "=====\n";
        print "Info cache\n";
        updateInfoCache $opts, $BASE_DIR, "$BASE_DIR/info-cache";
        print "=====\n\n";
      }elsif($cacheType eq $CACHE_TYPE_MD5SUM){
        print "=====\n";
        print "Md5sum cache\n";
        updateMd5sumCache $opts, $BASE_DIR, "$BASE_DIR/md5sum-cache";
        print "=====\n\n";
      }
    }
  }elsif($cmd eq $CMD_LIST_FILES){
    my @allFiles = findAllFiles();
    @allFiles = sort @allFiles;
    print "$_\n" foreach @allFiles;
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub syncRemote($){
  my ($opts) = @_;
  my $ipmagicName = readFile $FILE_REMOTE_MIRROR_IPMAGIC_NAME;
  chomp $ipmagicName;
  if($ipmagicName !~ /^(\w+)$/){
    die "ERROR: invalid/missing IPMAGIC_NAME\n";
  }

  my $syncName = "sync-$ipmagicName-" . nowMillis();

  my $tmpSymlinksDir = "$TMP_SYNC_SYMLINKS_DIR_PREFIX-$syncName";

  run "ipmagic", $ipmagicName, "-s", $EXEC, "--quiet", "--build-sync-dir", $syncName;
  print "built symlinks remotely at $ipmagicName:$tmpSymlinksDir\n";

  my @maybeSimulateArg = $$opts{simulate} ? ("-n") : ();
  my @excludeArgs = map {("--exclude", $_)} @{$$opts{syncExcludeArgs}};
  my @maybeDeleteArg = $$opts{syncDelete} ? ("--del") : ();

  for my $cat(@{$$opts{syncCategories}}){
    print "\n\n$cat:\n";
    run "ipmagic", $ipmagicName, "--rsync",
      "-L",
      "-avP",
      ":$tmpSymlinksDir/$cat/",
      "$BASE_DIR/$cat/",
      @excludeArgs,
      @maybeDeleteArg,
      @maybeSimulateArg,
    ;
  }
}

sub parseCsvRow($){
  my ($csvRow) = @_;

  my @vals;
  if($USE_LIB_TEXT_CSV){
    require Text::CSV;
    Text::CSV->import;

    my $csv = Text::CSV->new({
      sep_char => ",",
    });
    $csv->parse($csvRow);

    @vals = $csv->fields();
  }else{
    @vals = split /,/, $csvRow;
  }

  return @vals;
}

sub readIncludeFile(){
  my $mediaNamesByCategory = {};
  my @lines = readFile($FILE_INCLUDE_SYNC);
  for my $line(@lines){
    chomp $line;
    next if $line =~ /^\s*$/;

    my $namePtrnRegex = '[a-zA-Z0-9_\-\*]+';
    if($line !~ /^(\w+)\/($namePtrnRegex)\s*$/){
      die "ERROR: malformed include-sync line $line\n";
    }
    my ($cat, $namePtrn) = ($1, $2);

    my @dirs = grep {-d $_} glob "/media/videos/$cat/$namePtrn/";
    if(@dirs == 0){
      die "ERROR: unknown include dir $line\n";
    }
    for my $dir(@dirs){
      if($dir !~ /^\/media\/videos\/$cat\/([a-z0-9_\-]+)\/?$/){
        die "ERROR: malformed media dir $dir\n";
      }
      my $name = $1;

      $$mediaNamesByCategory{$cat} = [] if not defined $$mediaNamesByCategory{$cat};
      push @{$$mediaNamesByCategory{$cat}}, $name;
    }
  }

  return $mediaNamesByCategory;
}

sub buildLocalSyncDir($$$){
  my ($opts, $syncName, $mediaNamesByCategory) = @_;

  my $syncDir = "$TMP_SYNC_SYMLINKS_DIR_PREFIX-$syncName";
  if(-e $syncDir){
    die "ERROR: $syncDir already exists\n";
  }

  my @cmds;
  push @cmds, ["mkdir", "-p", "$syncDir/"];
  for my $cat(sort keys %$mediaNamesByCategory){
    my @names = @{$$mediaNamesByCategory{$cat}};
    push @cmds, ["mkdir", "-p", "$syncDir/$cat"];
    for my $name(@names){
      push @cmds, ["ln", "-s",          "/media/videos/$cat/$name/", "$syncDir/$cat/$name"];
      push @cmds, ["touch", "-h", "-r", "/media/videos/$cat/$name/", "$syncDir/$cat/$name"];
    }
    push @cmds, ["touch", "$syncDir/$cat", "-r", "/media/videos/$cat/"];
  }

  for my $cmdArr(@cmds){
    my @cmd = @$cmdArr;
    if($$opts{simulate}){
      print "#@cmd\n";
    }else{
      if($$opts{quiet}){
        runQuiet(@cmd);
      }else{
        run @cmd;
      }
    }
  }
}

sub findAllFiles(){
  my @findCmd = ("find",
    "-L",
    $BASE_DIR,
    "-type", "f",
    "-not", "-path", "*/zzz_info/*",
  );

  my @files = readProc @findCmd;
  chomp foreach @files;

  s/^$BASE_DIR\/+// foreach @files;
  return @files;
}

sub readInfoCache($){
  my ($cacheFile) = @_;
  my $cache = {};
  if(-e $cacheFile){
    my @lines = readFile $cacheFile;
    for my $line(@lines){
      next if $line =~ /^\s*$/;
      chomp $line;
      if($line =~ /^
        \s*        (\d+|\?)k
        \s* \| \s* (\d+:\d\d:\d\d\.\d\d|\?)
        \s* \| \s* (.+)
      $/x){
        my ($size, $dur, $file) = ($1, $2, $3);
        $size = undef if $size eq "?";
        $dur = parseDuration $dur;
        $$cache{$file} = [$size, $dur];
      }else{
        die "Malformed info cache line: $line\n";
      }
    }
  }
  return $cache;
}
sub writeInfoCache($$){
  my ($cacheFile, $cache) = @_;
  my @lines = map{formatInfoLine($cache, $_)} sort keys %$cache;
  writeFile $cacheFile, @lines;
}
sub formatInfoLine($$){
  my ($cache, $file) = @_;
  die "missing $file\n" if not defined $$cache{$file};
  my ($size, $dur) = @{$$cache{$file}};

  my $sizeFmt = defined $size ? sprintf "%9d", $size : sprintf "%9s", "?";
  my $durFmt = formatDuration $dur;
  return "${sizeFmt}k | ${durFmt} | $file\n";
}
sub updateInfoCache($$$){
  my ($opts, $dir, $cacheFile) = @_;
  my $cache = readInfoCache $cacheFile;

  my @files = readProc "find",
    "-L",
    $dir,
    "-not", "-path", "$dir/.git*",
    "-mindepth", "2",
    "-type", "f";
  chomp foreach @files;
  s/^$dir\/// foreach @files;

  my %okFiles = map {$_ => 1} @files;

  for my $file(sort keys %$cache){
    if(not defined $okFiles{$file}){
      delete $$cache{$file};
      print "Remove: $file\n";
      writeInfoCache $cacheFile, $cache;
    }
  }

  for my $file(sort @files){
    if(not defined $$cache{$file}){
      my $size = readProc "du", "-s", "$dir/$file";
      chomp $size;
      $size = $size =~ /^(\d+)\s/ ? $1 : undef;

      my $dur = readProc "duration", "-s", "$dir/$file";
      chomp $dur;
      $dur = $dur =~ /^(\d+\.\d+)\s/ ? $1 : undef;

      $$cache{$file} = [$size, $dur];
      print "Add: " . formatInfoLine($cache, $file);
      writeInfoCache $cacheFile, $cache;
    }
  }

  if(isGitFileUnstaged($INFO_DIR, $cacheFile)){
    if($$opts{skipGit}){
      print "#SKIPPING GIT COMMIT FOR $cacheFile\n";
    }else{
      gitCommitFile($INFO_DIR, $cacheFile, "cache-info: automatic commit");
    }
  }
}

sub readMd5sumCache($){
  my ($cacheFile) = @_;
  my $cache = {};
  if(-e $cacheFile){
    my @lines = readFile $cacheFile;
    for my $line(@lines){
      next if $line =~ /^\s*$/;
      chomp $line;
      if($line =~ /^
        \s* ([0-9a-f]{32})
        \s* \| \s* (\d+)
        \s* \| \s* (\d+)b
        \s* \| \s* (.+)
      $/x){
        my ($md5sum, $mtime, $size, $file) = ($1, $2, $3, $4);
        $$cache{$file} = {md5sum=>$md5sum, size=>$size, mtime=>$mtime};
      }else{
        die "Malformed md5sum cache line: $line\n";
      }
    }
  }
  return $cache;
}
sub writeMd5sumCache($$){
  my ($cacheFile, $cache) = @_;
  my @lines = map{formatMd5sumLine($cache, $_)} sort keys %$cache;
  writeFile $cacheFile, @lines;
}
sub formatMd5sumLine($$){
  my ($cache, $file) = @_;
  my $f = $$cache{$file};
  die "missing $file\n" if not defined $f;
  return sprintf "%s|%10d|%12db|%s\n", $$f{md5sum}, $$f{mtime}, $$f{size}, $file;
}
sub updateMd5sumCache($$$){
  my ($opts, $dir, $md5sumCacheFile) = @_;
  my $md5sums = readMd5sumCache $md5sumCacheFile;

  print "fetching all mtimes and filesizes\n";
  my @fileStats = readProc "find",
    "-L",
    $dir,
    "-not", "-path", "$dir/.git*",
    "-mindepth", "2",
    "-type", "f",
    "-exec", "stat", "-c%s-%Y-%n", "{}", ";",
    ;

  my @files;
  my %sizes;
  my %mtimes;
  for my $fileStat(@fileStats){
    if($fileStat !~ /^(\d+)-(\d+)-(.*)\n$/){
      die "invalid stat output: $fileStat";
    }
    my ($size, $mtime, $file) = ($1, $2, $3);
    $file =~ s/^$dir\///;

    push @files, $file;
    $sizes{$file} = $size;
    $mtimes{$file} = $mtime;
  }

  my %okFiles = map {$_ => 1} @files;

  for my $file(sort keys %$md5sums){
    my $info = $$md5sums{$file};
    if(not defined $okFiles{$file}){
      print "$file is not present, deleting md5 cache entry\n";
      delete $$md5sums{$file};
    }elsif($mtimes{$file} != $$info{mtime} or $sizes{$file} != $$info{size}){
      print "$file is out of date, deleting md5 cache entry\n";
      delete $$md5sums{$file};
    }
  }

  my @missingFiles = sort grep {not defined $$md5sums{$_}} @files;

  print "missing:\n";
  print map {"$_\n"} @missingFiles;
  print "\n\n";
  print "(" . (0+@missingFiles) . " missing files)\n";

  for my $file(@missingFiles){
    print "fetching md5sum for: $file\n";
    my $md5 = md5sumFile "$dir/$file";
    $$md5sums{$file} = {md5sum=>$md5, size=>$sizes{$file}, mtime=>$mtimes{$file}};
    writeMd5sumCache $md5sumCacheFile, $md5sums;
  }

  if(isGitFileUnstaged($INFO_DIR, $md5sumCacheFile)){
    if($$opts{skipGit}){
      print "#SKIPPING GIT COMMIT FOR $md5sumCacheFile\n";
    }else{
      gitCommitFile($INFO_DIR, $md5sumCacheFile, "cache-md5sum: automatic commit");
    }
  }
}

sub gitCommitFile($$$){
  my ($repo, $file, $msg) = @_;
  run "git", "-C", $repo, "add", $file;
  run "git", "-C", $repo, "commit", "-m", $msg;
}
sub isGitFileUnstaged($$){
  my ($repo, $file) = @_;

  my @gitDiffCmd = ("git", "-C", $repo, "diff", "--quiet", "--exit-code", $file);
  system @gitDiffCmd;
  my $exitCode = $? >> 8;

  if($exitCode == 1){
    #file has differences
    return 1;
  }elsif($exitCode == 0){
    #file has no differences
    return 0;
  }else{
    die "ERROR: @gitDiffCmd failed with exitCode=$exitCode\n$!\n";
  }
}

sub parseDuration($){
  my $dur = shift;
  return undef if $dur =~ /^\s*\?\s*$/;
  if($dur !~ /^(\d+):(\d+):(\d+|\d+.\d+)$/){
    die "Malformed duration: $dur\n";
  }
  return $1*60*60 + $2*60 + $3;
}

sub formatDuration($){
  my $dur = shift;
  return sprintf "%11s", "?" if not defined $dur;

  my $h = int($dur/60/60);
  my $m = int($dur/60) - ($h*60);
  my $s = $dur - ($h*60*60) - ($m*60);
  return sprintf '%02d:%02d:%05.2f', $h, $m, $s;
}

sub md5sumFile($){
  my ($file) = @_;
  my $d = Digest::MD5->new();
  open my $fh, "<", $file or die "ERROR: could not read $file\n$!\n";
  $d->addfile($fh);
  my $digest = $d->hexdigest();
  close $fh;
  return $digest;
}

sub readFile($){
  my ($file) = @_;
  open my $fh, "< $file" or die "ERROR: could not read $file\n$!\n";
  my @lines = <$fh>;
  close $fh;

  if(wantarray){
    return @lines;
  }else{
    return join "", @lines;
  }
}
sub writeFile($@){
  my ($file, @lines) = @_;
  open FH, "> $file" or die "Could not write to $file\n$!\n";
  print FH @lines;
  close FH;
}
sub appendFile($@){
  my ($file, @lines) = @_;
  open FH, ">> $file" or die "Could not append to $file\n$!\n";
  print FH @lines;
  close FH;
}

sub run(@){
  print "@_\n";
  runQuiet(@_);
}
sub runQuiet(@){
  system @_;
  if($? != 0){
    die "ERROR: command \"@_\" failed\n$!\n";
  }
}

sub readProc(@){
  my @cmd = @_;
  open my $cmdH, "-|", @cmd or die "ERROR: could not run @_\n$!\n";
  my @lines = <$cmdH>;
  close $cmdH;
  if(wantarray){
    return @lines;
  }else{
    return join '', @lines;
  }
}

sub nowMillis(){
  return int(time() * 1000.0 + 0.5);
}

&main(@ARGV);
