#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use Time::HiRes qw(sleep);
use IPC::Open3;
use Symbol qw(gensym);
my $USE_LIB_TEXT_CSV = eval {require Text::CSV};

sub syncRemote($);
sub parseCsvRow($);
sub readIncludeFile();
sub buildLocalSyncDir($$$);
sub updateCache($);
sub readInfoCache($$);
sub writeInfoCache($$$);
sub formatInfoLine($$);
sub updateInfoCache($$$);
sub readMd5sumCache($$);
sub writeMd5sumCache($$$);
sub formatMd5sumLine($$);
sub updateMd5sumCache($$$$);
sub parseDuration($);
sub formatDuration($);
sub getSizes($);
sub readExcludeFile($);
sub checkExcludes($$);
sub getRsyncExcludes($);
sub md5($$);
sub fmtSize($);
sub isMountPoint($$);
sub test($@);
sub getIpmagicCmd($@);
sub getIpmagicFile($$);
sub readFile($$);
sub writeFile($$@);
sub appendFile($$@);
sub gitCmd($@);
sub printCmd(@);
sub run(@);
sub tryrunQuiet(@);
sub runQuiet(@);
sub runPrompt(@);
sub readProc($@);
sub readProcSilent($@);
sub readProcStdout($$$);
sub nowMillis();

my $REMOTE_VIDEOS_DIR = "/media/videos";
my $MIRROR_VIDEOS_DIR = "/media/stuff/Videos";
my $IPMAGIC_NAME = "tv";

my $BASE_DIR = "/media/videos";
my $INFO_DIR = "$BASE_DIR/zzz_info";

my $FILE_INCLUDE_SYNC = "$INFO_DIR/include-sync";
my $FILE_REMOTE_MIRROR_IPMAGIC_NAME = "$BASE_DIR/videos_mirror_ipmagic_name";
my $TMP_SYNC_SYMLINKS_DIR_PREFIX = "/tmp/video-sync-tmp-symlinks-dir";

my @DEFAULT_SYNC_EXCLUDE_ARGS = qw(extras/ highres/);

my @SYNC_CATEGORIES = qw(
  anime
  clips_shorts_trailers
  concerts
  courses
  movies
  operas
  series
  specials
);
my @NONSYNC_CATEGORIES = qw(
  camera
  events
);
my @ALL_CATEGORIES = sort(@SYNC_CATEGORIES, @NONSYNC_CATEGORIES);
my $OK_CATS = join "|", @ALL_CATEGORIES;

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC [OPTS] --sync
  $EXEC [OPTS] --sync-remote
    -read IPMAGIC_NAME from $FILE_REMOTE_MIRROR_IPMAGIC_NAME
      -if none exists, fail
    -use SYNC_NAME=sync-<IPMAGIC_NAME>-<NOW_MILLIS>
    -use TMP_SYMLINKS_DIR=$TMP_SYNC_SYMLINKS_DIR_PREFIX-<SYNC_NAME>
    -build IPMAGIC_NAME:TMP_SYNC_DIR remotely
      -run: ipmagic IPMAGIC_NAME -s $EXEC --build-sync-dir SYNC_NAME_ARG
    -get list of CATEGORY entries to sync
      -default is: @SYNC_CATEGORIES
      -see: --sync-category
    -rsync each CATEGORY from remote TMP_SYMLINKS_DIR to $BASE_DIR
      -follow remote symlinks with '-L', copying actual files, not links
      -run: ipmagic IPMAGIC_NAME --rsync -L -avP \\
              :TMP_SYMLINKS_DIR/CATEGORY/ \\
              $BASE_DIR/CATEGORY/ \\
              EXCLUDE_ARGS \\
              MAYBE_DELETE_ARG \\
              MAYBE_SIMULATE_ARG \\
            ;
    EXCLUDE_ARGS
      default is: " . join(" ", map{"--exclude $_"} @DEFAULT_SYNC_EXCLUDE_ARGS) . "
      see: --sync-excludes
    MAYBE_DELETE_ARG
      '--del' if --sync-del is given, otherwise empty
      does not override --simulate (nothing will be deleted with --simulate)
    MAYBE_SIMULATE_ARG
      if --simulate is given '--dry-run', otherwise ''
      (append '--dry-run' to rsync commands on --simulate)

  $EXEC [OPTS] --sync-simulate
    same as: $EXEC --sync-remote --simulate

  $EXEC [OPTS] --build-sync-dir SYNC_NAME
  $EXEC [OPTS] --build-local-sync-dir SYNC_NAME
    -use TMP_SYMLINKS_DIR=$TMP_SYNC_SYMLINKS_DIR_PREFIX-<SYNC_NAME>
    -delete TMP_SYMLINKS_DIR if it exists
      -run: rm -rf TMP_SYMLINKS_DIR
    -read list of <CATEGORY>/<MEDIA_NAME> pairs, one per line, from:
      $FILE_INCLUDE_SYNC
    -for each <CATEGORY>/<MEDIA_NAME> pair in $FILE_INCLUDE_SYNC:
      -create symlink for each <MEDIA_NAME> in category subdir of sync dir:
        -run: mkdir -p TMP_SYNC_DIR/CATEGORY/
        -run: ln -s $BASE_DIR/CATEGORY/MEDIA_NAME/ TMP_SYNC_DIR/CATEGORY/MEDIA_NAME

    SYNC_NAME
      must start with 'sync' and contain only:
        lowercase letters, numbers, underscores, and dashes

  $EXEC [OPTS] --cache
    perform info/md5sum updates of $REMOTE_VIDEOS_DIR only

  OPTS
    -s | --simulate | -n | --no-act | --dry-run | --dryrun
      only run rsync commands with --dry-run in --sync-remote
      do not actually copy files from and to remote
        localhost:$MIRROR_VIDEOS_DIR <=> $IPMAGIC_NAME:$REMOTE_VIDEOS_DIR

    --no-git
      do not perform any git commands

    --local
      only affect the local filesystem
      fail if $REMOTE_VIDEOS_DIR is not a mountpoint

    -c CATEGORY_CSV
    --sync-categories=CATEGORY_CSV
      in sync-remote, only sync the indicated CATEGORY entries
        -parse CATEGORY_CSV into CATEGORY entries
        -skip any CATEGORY not given in --sync-remote
      CATEGORY_CSV
        comma-separated list of CATEGORY entries
          separator=','    quote_char='\"'    escape_char='\\'    quote_literal='\"\"'
          e.g.: -c movies,series,operas
        NOTE: simple csv parsing without quotes/escapes is used if Text::CSV is not found
        the default is:
          " . join(",", @SYNC_CATEGORIES) . "

    -x EXCLUDE_CSV
    --sync-excludes=EXCLUDE_CSV
      in sync-remote, override the default rsync EXCLUDE args
        -parse EXCLUDE_CSV into EXCLUDE args
        -pass '--exclude EXCLUDE' to each rsync command in --sync-remote
      EXCLUDE_CSV
        comma-separated list of EXCLUDE args
          separator=','    quote_char='\"'    escape_char='\\'    quote_literal='\"\"'
          e.g.: -x 'bigFiles1/,\"really,reallyBigFiles2/\",\"a\"\"good\"\"dirName3/'
        NOTE: simple csv parsing without quotes/escapes is used if Text::CSV is not found
        the default is:
          " . join(",", @DEFAULT_SYNC_EXCLUDE_ARGS) . "

    --del | --delete | --sync-del | --sync-delete
      pass '--del' to rsync commands in --sync-remote
";

my $CMD_SYNC_REMOTE = "sync-remote";
my $CMD_BUILD_LOCAL_SYNC_DIR = "build-local-sync-dir";
my $CMD_CACHE = "cache";

sub main(@){
  my $cmd = $CMD_SYNC_REMOTE;
  my $syncName = undef;
  my $opts = {
    simulate        => 0,
    useGit          => 1,
    localOnly       => 0,
    syncCategories  => [@SYNC_CATEGORIES],
    syncExcludeArgs => [@DEFAULT_SYNC_EXCLUDE_ARGS],
    syncDelete      => 0,
  };

  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--sync|--sync-remote)$/){
      $cmd = $CMD_SYNC_REMOTE;
    }elsif($arg =~ /^(--sync-simulate)$/){
      $cmd = $CMD_SYNC_REMOTE;
      $$opts{simulate} = 1;
    }elsif($arg =~ /^(--build-sync-dir|--build-local-sync-dir)$/){
      $cmd = $CMD_BUILD_LOCAL_SYNC_DIR;
    }elsif($arg =~ /^(--cache)$/){
      $cmd= $CMD_CACHE;
    }elsif($arg =~ /^(-s|--simulate|-n|--no-act|--dry-run|--dryrun)$/){
      $$opts{simulate} = 1;
    }elsif($arg =~ /^(--local)$/){
      $$opts{localOnly} = 1;
    }elsif($arg =~ /^(--no-git)$/){
      $$opts{useGit} = 0;
    }elsif($arg =~ /^(-c)$/ and @_ > 0){
      my $csv = shift @_;
      $$opts{syncCategories} = [parseCsvRow($csv)],
    }elsif($arg =~ /^--sync-categories=(.+)$/){
      my $csv = $1;
      $$opts{syncCategories} = [parseCsvRow($csv)],
    }elsif($arg =~ /^(-x)$/ and @_ > 0){
      my $csv = shift @_;
      $$opts{syncExcludeArgs} = [parseCsvRow($csv)],
    }elsif($arg =~ /^--sync-excludes=(.+)$/){
      my $csv = $1;
      $$opts{syncExcludeArgs} = [parseCsvRow($csv)],
    }elsif($arg =~ /^(--del|--delete|--sync-del|--sync-delete)$/){
      $$opts{syncDelete} = 1;
    }elsif($cmd eq $CMD_BUILD_LOCAL_SYNC_DIR
             and not defined $syncName and $arg =~ /^(sync[a-z0-9_\-]*)$/){
      $syncName = $arg
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($cmd eq $CMD_SYNC_REMOTE){
    syncRemote($opts);
  }elsif($cmd eq $CMD_BUILD_LOCAL_SYNC_DIR){
    die "ERROR: missing SYNC_NAME\n" if not defined $syncName;
    my $mediaNamesByCategory = readIncludeFile();
    buildLocalSyncDir($opts, $syncName, $mediaNamesByCategory);
  }elsif($cmd eq $CMD_CACHE){
    updateCache($opts);
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub syncRemote($){
  my ($opts) = @_;
  my $ipmagicName = readFile undef, $FILE_REMOTE_MIRROR_IPMAGIC_NAME;
  chomp $ipmagicName;
  if($ipmagicName !~ /^(\w+)$/){
    die "ERROR: invalid/missing IPMAGIC_NAME\n";
  }

  my $syncName = "sync-$ipmagicName-" . nowMillis();

  my $tmpSymlinksDir = "$TMP_SYNC_SYMLINKS_DIR_PREFIX-$syncName";

  run "ipmagic", $ipmagicName, "-s", $EXEC, "--build-sync-dir", $syncName;
  print "built symlinks remotely at $ipmagicName:$tmpSymlinksDir\n";

  my @maybeSimulateArg = $$opts{simulate} ? ("-n") : ();
  my @excludeArgs = map {("--exclude", $_)} @{$$opts{syncExcludeArgs}};
  my @maybeDeleteArg = $$opts{syncDelete} ? ("--del") : ();

  for my $cat(@{$$opts{syncCategories}}){
    print "\n\n$cat:\n";
    run "ipmagic", $ipmagicName, "--rsync",
      "-L",
      "-avP",
      ":$tmpSymlinksDir/$cat/",
      "$BASE_DIR/$cat/",
      @excludeArgs,
      @maybeDeleteArg,
      @maybeSimulateArg,
    ;
  }
}

sub parseCsvRow($){
  my ($csvRow) = @_;

  my @vals;
  if($USE_LIB_TEXT_CSV){
    require Text::CSV;
    Text::CSV->import;

    my $csv = Text::CSV->new({
      sep_char => ",",
    });
    $csv->parse($csvRow);

    @vals = $csv->fields();
  }else{
    @vals = split /,/, $csvRow;
  }

  return @vals;
}

sub readIncludeFile(){
  my $mediaNamesByCategory = {};
  my @lines = readFile(undef, $FILE_INCLUDE_SYNC);
  for my $line(@lines){
    chomp $line;
    next if $line =~ /^\s*$/;

    my $namePtrnRegex = '[a-zA-Z0-9_\-\*]+';
    if($line !~ /^(\w+)\/($namePtrnRegex)\s*$/){
      die "ERROR: malformed include-sync line $line\n";
    }
    my ($cat, $namePtrn) = ($1, $2);

    my @dirs = grep {-d $_} glob "/media/videos/$cat/$namePtrn/";
    if(@dirs == 0){
      die "ERROR: unknown include dir $line\n";
    }
    for my $dir(@dirs){
      if($dir !~ /^\/media\/videos\/$cat\/([a-z0-9_\-]+)\/?$/){
        die "ERROR: malformed media dir $dir\n";
      }
      my $name = $1;

      $$mediaNamesByCategory{$cat} = [] if not defined $$mediaNamesByCategory{$cat};
      push @{$$mediaNamesByCategory{$cat}}, $name;
    }
  }

  return $mediaNamesByCategory;
}

sub buildLocalSyncDir($$$){
  my ($opts, $syncName, $mediaNamesByCategory) = @_;

  my $syncDir = "$TMP_SYNC_SYMLINKS_DIR_PREFIX-$syncName";
  if(-e $syncDir){
    die "ERROR: $syncDir already exists\n";
  }

  my @cmds;
  push @cmds, ["mkdir", "-p", "$syncDir/"];
  for my $cat(sort keys %$mediaNamesByCategory){
    my @names = @{$$mediaNamesByCategory{$cat}};
    push @cmds, ["mkdir", "-p", "$syncDir/$cat"];
    for my $name(@names){
      push @cmds, ["ln", "-s",          "/media/videos/$cat/$name/", "$syncDir/$cat/$name"];
      push @cmds, ["touch", "-h", "-r", "/media/videos/$cat/$name/", "$syncDir/$cat/$name"];
    }
    push @cmds, ["touch", "$syncDir/$cat", "-r", "/media/videos/$cat/"];
  }

  for my $cmdArr(@cmds){
    my @cmd = @$cmdArr;
    if($$opts{simulate}){
      print "#@cmd\n";
    }else{
      run @cmd;
    }
  }
}

sub updateCache($){
  my $opts = @_;

  my $ipmagic;
  my $mainIpmagic;
  if(isMountPoint undef, $REMOTE_VIDEOS_DIR){
    $ipmagic = undef;
  }elsif(not $$opts{localOnly} and isMountPoint $IPMAGIC_NAME, $REMOTE_VIDEOS_DIR){
    $ipmagic = $IPMAGIC_NAME;
  }else{
    die "\"$REMOTE_VIDEOS_DIR\" not found locally or with \"ipmagic $IPMAGIC_NAME\"\n";
  }
  $mainIpmagic = getIpmagicFile $ipmagic, $REMOTE_VIDEOS_DIR;

  die "could not find \"main\" locally\n" if $$opts{localOnly} and defined $ipmagic;

  my $sizes = getSizes($ipmagic);
  print "\n";

  print "=====\n";
  print "Info cache\n";
  updateInfoCache $ipmagic, $REMOTE_VIDEOS_DIR, "$REMOTE_VIDEOS_DIR/info-cache";
  print "=====\n\n";

  print "=====\n";
  print "Md5sum cache\n";
  updateMd5sumCache $ipmagic, $REMOTE_VIDEOS_DIR, "$REMOTE_VIDEOS_DIR/md5sum-cache", "$REMOTE_VIDEOS_DIR/md5sum-extra";
  print "=====\n\n";

  if($$opts{useGit}){
    print "=====\n";
    print "Git repo\n";
    gitCmd $ipmagic, "commit", "-a", "-m", "automatic commit";
    print "=====\n\n";
  }
}

sub readInfoCache($$){
  my ($ipmagic, $cacheFile) = @_;
  my $cache = {};
  if(test $ipmagic, "-e", $cacheFile){
    my @lines = readFile $ipmagic, $cacheFile;
    for my $line(@lines){
      next if $line =~ /^\s*$/;
      chomp $line;
      if($line =~ /^
        \s*        (\d+|\?)k
        \s* \| \s* (\d+:\d\d:\d\d\.\d\d|\?)
        \s* \| \s* (.+)
      $/x){
        my ($size, $dur, $file) = ($1, $2, $3);
        $size = undef if $size eq "?";
        $dur = parseDuration $dur;
        $$cache{$file} = [$size, $dur];
      }else{
        die "Malformed info cache line: $line\n";
      }
    }
  }
  return $cache;
}
sub writeInfoCache($$$){
  my ($ipmagic, $cacheFile, $cache) = @_;
  my @lines = map{formatInfoLine($cache, $_)} sort keys %$cache;
  writeFile $ipmagic, $cacheFile, @lines;
}
sub formatInfoLine($$){
  my ($cache, $file) = @_;
  die "missing $file\n" if not defined $$cache{$file};
  my ($size, $dur) = @{$$cache{$file}};

  my $sizeFmt = defined $size ? sprintf "%9d", $size : sprintf "%9s", "?";
  my $durFmt = formatDuration $dur;
  return "${sizeFmt}k | ${durFmt} | $file\n";
}
sub updateInfoCache($$$){
  my ($ipmagic, $dir, $cacheFile) = @_;
  my $cache = readInfoCache $ipmagic, $cacheFile;

  my @files = readProc $ipmagic, "find",
    "-L",
    $dir,
    "-not", "-path", "$dir/.git*",
    "-mindepth", "2",
    "-type", "f";
  chomp foreach @files;
  s/^$dir\/// foreach @files;

  my %okFiles = map {$_ => 1} @files;

  for my $file(sort keys %$cache){
    if(not defined $okFiles{$file}){
      delete $$cache{$file};
      print "Remove: $file\n";
      writeInfoCache $ipmagic, $cacheFile, $cache;
    }
  }

  for my $file(sort @files){
    if(not defined $$cache{$file}){
      my $size = readProc $ipmagic, "du", "-s", "$dir/$file";
      chomp $size;
      $size = $size =~ /^(\d+)\s/ ? $1 : undef;

      my $dur = readProcSilent $ipmagic, "duration", "-s", "$dir/$file";
      chomp $dur;
      $dur = $dur =~ /^(\d+\.\d+)\s/ ? $1 : undef;

      $$cache{$file} = [$size, $dur];
      print "Add: " . formatInfoLine($cache, $file);
      writeInfoCache $ipmagic, $cacheFile, $cache;
    }
  }
}

sub readMd5sumCache($$){
  my ($ipmagic, $cacheFile) = @_;
  my $cache = {};
  if(test $ipmagic, "-e", $cacheFile){
    my @lines = readFile $ipmagic, $cacheFile;
    for my $line(@lines){
      next if $line =~ /^\s*$/;
      chomp $line;
      if($line =~ /^
        \s* ([0-9a-f]{32})
        \s* \| \s* (\d+)
        \s* \| \s* (\d+)b
        \s* \| \s* (.+)
      $/x){
        my ($md5sum, $mtime, $size, $file) = ($1, $2, $3, $4);
        $$cache{$file} = {md5sum=>$md5sum, size=>$size, mtime=>$mtime};
      }else{
        die "Malformed md5sum cache line: $line\n";
      }
    }
  }
  return $cache;
}
sub writeMd5sumCache($$$){
  my ($ipmagic, $cacheFile, $cache) = @_;
  my @lines = map{formatMd5sumLine($cache, $_)} sort keys %$cache;
  writeFile $ipmagic, $cacheFile, @lines;
}
sub formatMd5sumLine($$){
  my ($cache, $file) = @_;
  my $f = $$cache{$file};
  die "missing $file\n" if not defined $f;
  return sprintf "%s|%10d|%12db|%s\n", $$f{md5sum}, $$f{mtime}, $$f{size}, $file;
}
sub updateMd5sumCache($$$$){
  my ($ipmagic, $dir, $md5sumCacheFile, $md5sumExtraFile) = @_;
  my $md5sums = readMd5sumCache $ipmagic, $md5sumCacheFile;

  print "fetching all mtimes and filesizes\n";
  my @fileStats = readProc $ipmagic, "find",
    "-L",
    $dir,
    "-not", "-path", "$dir/.git*",
    "-mindepth", "2",
    "-type", "f",
    "-exec", "stat", "-c%s-%Y-%n", "{}", ";",
    ;

  my @files;
  my %sizes;
  my %mtimes;
  for my $fileStat(@fileStats){
    if($fileStat !~ /^(\d+)-(\d+)-(.*)\n$/){
      die "invalid stat output: $fileStat";
    }
    my ($size, $mtime, $file) = ($1, $2, $3);
    $file =~ s/^$dir\///;

    push @files, $file;
    $sizes{$file} = $size;
    $mtimes{$file} = $mtime;
  }

  my %okFiles = map {$_ => 1} @files;

  my @extraMd5sums;
  for my $file(sort keys %$md5sums){
    my $info = $$md5sums{$file};
    my $isExtra = 0;
    if(not defined $okFiles{$file}){
      print "$file is not present, moving cache entry to extra\n";
      $isExtra = 1;
    }elsif($mtimes{$file} != $$info{mtime} or $sizes{$file} != $$info{size}){
      print "$file is out of date, moving cache entry to extra\n";
      $isExtra = 1;
    }

    if($isExtra){
      push @extraMd5sums, formatMd5sumLine $md5sums, $file;
      delete $$md5sums{$file};
    }
  }

  if(@extraMd5sums > 0){
    print "appending " . (0+@extraMd5sums) . " extras to $md5sumExtraFile\n";
    appendFile $ipmagic, $md5sumExtraFile, @extraMd5sums;
    writeMd5sumCache $ipmagic, $md5sumCacheFile, $md5sums;
  }

  my @missingFiles = sort grep {not defined $$md5sums{$_}} @files;

  print "missing:\n";
  print map {"$_\n"} @missingFiles;
  print "\n\n";
  print "(" . (0+@missingFiles) . " missing files)\n";

  for my $file(@missingFiles){
    print "fetching md5sum for: $file\n";
    my $md5 = md5 $ipmagic, "$dir/$file";
    $$md5sums{$file} = {md5sum=>$md5, size=>$sizes{$file}, mtime=>$mtimes{$file}};
    writeMd5sumCache $ipmagic, $md5sumCacheFile, $md5sums;
  }
}

sub parseDuration($){
  my $dur = shift;
  return undef if $dur =~ /^\s*\?\s*$/;
  if($dur !~ /^(\d+):(\d+):(\d+|\d+.\d+)$/){
    die "Malformed duration: $dur\n";
  }
  return $1*60*60 + $2*60 + $3;
}

sub formatDuration($){
  my $dur = shift;
  return sprintf "%11s", "?" if not defined $dur;

  my $h = int($dur/60/60);
  my $m = int($dur/60) - ($h*60);
  my $s = $dur - ($h*60*60) - ($m*60);
  return sprintf '%02d:%02d:%05.2f', $h, $m, $s;
}

sub getSizes($){
  my $ipmagic = shift;
  my $sizes = {};
  for my $cat(@ALL_CATEGORIES){
    print "getting sizes for: $cat\n";
    my $catSizes = {};
    my @files = readProc $ipmagic, "ls", "$REMOTE_VIDEOS_DIR/$cat";
    chomp foreach @files;
    @files = map {"$REMOTE_VIDEOS_DIR/$cat/$_"} @files;

    if(@files > 0){
      my @lines = readProc $ipmagic, "du", "-s", @files;

      for my $line(@lines){
        if($line !~ /^(\d+)\t(.*)$/){
          die "Failed du: $line\n";
        }
        my ($size, $file) = ($1, $2);
        if($file !~ /^$REMOTE_VIDEOS_DIR\/$cat\/(.+)$/){
          die "Malformed filename from du: $line\n";
        }
        my $fileName = $1;
        $$catSizes{$fileName} = $size;
      }
    }
    $$sizes{$cat} = $catSizes;
  }
  return $sizes;
}

sub readExcludeFile($){
  my $ipmagic = shift;
  my $excludeFile = "$REMOTE_VIDEOS_DIR/exclude";
  my $excludeFileIpmagic = getIpmagicFile $ipmagic, $excludeFile;
  die "Not found: $excludeFile\n" if not test $ipmagic, "-f", $excludeFile;
  my $excludes = {};
  $$excludes{$_} = {} foreach @ALL_CATEGORIES;
  my $cat;
  for my $line(readFile $ipmagic, $excludeFile){
    chomp $line;
    $line =~ s/#.*//;
    next if $line =~ /^\s*$/;
    if($line =~ /^($OK_CATS)$/){
      $cat = $line;
    }elsif($line =~ /^-/){
      $line =~ s/^-//;
      die "Missing category\n" if not defined $cat;
      die "Unknown category\n" if not defined $$excludes{$cat};
      $$excludes{$cat}{$line} = 1;
    }else{
      die "Malformed line: $line\n";
    }
  }
  return $excludes;
}

sub checkExcludes($$){
  my ($excludes, $sizes) = @_;
  my $unexcluded = [];
  for my $cat(sort keys %$excludes){
    for my $file(sort keys %{$$excludes{$cat}}){
      die "Unknown exclude: $cat/$file\n" if not defined $$sizes{$cat}{$file};
      my $mirrorFile = "$MIRROR_VIDEOS_DIR/$cat/$file";
      push @$unexcluded, $mirrorFile if test undef, "-e", $mirrorFile;
    }
  }
  return $unexcluded;
}

sub getRsyncExcludes($){
  my ($excludes) = @_;
  my $rsyncExcludes = [];
  for my $cat(sort keys %$excludes){
    for my $file(sort keys %{$$excludes{$cat}}){
      my $ex = "$cat/$file";
      $ex =~ s/\[/\\\[/g;
      $ex =~ s/\]/\\\]/g;
      push @$rsyncExcludes, "--exclude=$ex";
    }
  }
  push @$rsyncExcludes, "--exclude=.git*";
  return $rsyncExcludes;
}

sub md5($$){
  my ($ipmagic, $file) = @_;
  my $md5 = readProcSilent $ipmagic, "md5sum", $file;
  if($md5 =~ /^([0-9a-f]{32})\s*/){
    return $1;
  }else{
    die "Error finding md5sum for: $file\n";
  }
}

sub fmtSize($){
  return sprintf "%5.1fG", $_[0] / 1024 / 1024;
}

sub isMountPoint($$){
  my ($ipmagic, $dir) = @_;
  readProcSilent $ipmagic, "mountpoint", $dir;
  return $? == 0;
}

sub test($@){
  my $ipmagic = shift;
  my @test = getIpmagicCmd($ipmagic, "test", @_);
  tryrunQuiet @test;
  return $? == 0 ? 1 : 0;
}

sub getIpmagicCmd($@){
  my ($ipmagic, @cmd) = @_;
  if(defined $ipmagic){
    s/"/\\\\"/g foreach @cmd;
    s/^(.*)$/"$1"/ foreach @cmd;
    @cmd = ("ipmagic", $ipmagic, "@cmd");
  }

  if(wantarray){
    return @cmd;
  }else{
    return "@cmd";
  }
}

sub getIpmagicFile($$){
  my ($ipmagic, $file) = @_;

  if(defined $ipmagic){
    my $host = `ipmagic $ipmagic`;
    chomp $host;
    $file = "$host:$file";
  }
  return $file;
}

sub readFile($$){
  my ($ipmagic, $file) = @_;
  my @lines = readProc $ipmagic, "cat", $file;
  if(wantarray){
    return @lines;
  }else{
    return "@lines";
  }
}
sub writeFile($$@){
  my ($ipmagic, $file, @lines) = @_;
  my $tmpFile = "/tmp/video-sync-" . time . ".tmp";
  open FH, "> $tmpFile" or die "Could not write to $tmpFile ($file)\n";
  print FH @lines;
  close FH;

  runQuiet "rsync", "-aqP", $tmpFile, getIpmagicFile($ipmagic, $file);
}
sub appendFile($$@){
  my ($ipmagic, $file, @lines) = @_;
  my @prevLines = readFile $ipmagic, $file;
  writeFile $ipmagic, $file, (@prevLines, @lines);
}

sub gitCmd($@){
  my ($ipmagic, @cmd) = @_;
  tryrunQuiet getIpmagicCmd $ipmagic, "git", "-C", $REMOTE_VIDEOS_DIR, @cmd;
}

sub printCmd(@){
  my @escArgs;
  for my $arg(@_){
    my $escArg = $arg;
    if($escArg =~ /[ '"\\\$!]/){
      $escArg =~ s/'/'\\''/g;
      $escArg = "'$escArg'";
    }
    push @escArgs, $escArg;
  }
  print "@escArgs\n";
}

sub run(@){
  printCmd @_;
  runQuiet @_;
}
sub tryrunQuiet(@){
  system @_;
}
sub runQuiet(@){
  system @_;
  die "Error running @_\n" if $? != 0;
}
sub runPrompt(@){
  print "@_\nRun above [y/N]? ";
  my $ok = <STDIN>;
  if($ok eq "y\n"){
    run @_;
  }
}

sub readProc($@){
  my ($ipmagic, @cmd) = @_;
  return readProcStdout $ipmagic, \@cmd, ">&STDERR";
}
sub readProcSilent($@){
  my ($ipmagic, @cmd) = @_;
  return readProcStdout $ipmagic, \@cmd, ">/dev/null";
}
sub readProcStdout($$$){
  my ($ipmagic, $cmd, $stderr) = @_;
  my @cmdArr = getIpmagicCmd $ipmagic, @$cmd;
  my $pid = open3(gensym, \*OUT, $stderr, @cmdArr);
  my @lines = <OUT>;
  waitpid($pid, 0);
  if(wantarray){
    return @lines;
  }else{
    return join '', @lines;
  }
}

sub nowMillis(){
  return int(time() * 1000.0 + 0.5);
}

&main(@ARGV);
