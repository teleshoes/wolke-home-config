#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $EXEC = basename $0;

my $IPMAGIC_NAME = "pi";
my $MIDEA_RUBY_EXEC = "midea-ruby";
my $MIDEA_IR_CMD_EXEC = "midea-ir-cmd";

my $IR_CMD_REGEX = "
        on | off | cold |
        cool | fan
        low | med | high |
        led | eco | swingOn | swingOff |
        16 | 17 | 18 | 19 | 20 | 21 | 22 | 23 |
        24 | 25 | 26 | 27 | 28 | 29 | 30 |
        60 | 61 | 62 | 63 | 64 | 65 | 66 | 67 |
        68 | 69 | 70 | 71 | 72 | 73 | 74 | 75 |
        76 | 77 | 78 | 79 | 80 | 81 | 82 | 83 |
        84 | 85 | 86
";

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC
  $EXEC -t | --toggle
    -check $MIDEA_RUBY_EXEC --read
    -if POWER=on:
      -run: $EXEC --set off
    -if POWER=off:
      -run: $EXEC --set on

  $EXEC -g|--get|-f|--fetch
    -run $MIDEA_RUBY_EXEC --fetch-and-read

  $EXEC -r|--read
    -run $MIDEA_RUBY_EXEC --read

  $EXEC -s|--set [ARG ARG ..]
    -run $MIDEA_IR_CMD_EXEC ARG ARG ..
    -run $MIDEA_RUBY_EXEC --fetch

  $EXEC IR_CMD_ARG [ARG ARG ..]
    -same as: $EXEC --set IR_CMD_ARG ARG ARG
      IR_CMD_ARG" . $IR_CMD_REGEX . "
";

my $COMMAND_TOGGLE = "toggle";
my $COMMAND_FETCH = "fetch";
my $COMMAND_READ = "read";
my $COMMAND_SET = "set";

sub isACOn();

sub main(@){
  my $cmd = $COMMAND_TOGGLE;
  my @irCmdArgs;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-t|--toggle)$/){
      $cmd = $COMMAND_TOGGLE;
    }elsif($arg =~ /^(-g|--get|-f|--fetch)$/){
      $cmd = $COMMAND_FETCH;
    }elsif($arg =~ /^(-r|--read)$/){
      $cmd = $COMMAND_READ;
    }elsif($arg =~ /^(-s|--set)$/){
      $cmd = $COMMAND_SET;
    }elsif($arg =~ /^($IR_CMD_REGEX)$/x){
      $cmd = $COMMAND_SET;
      push @irCmdArgs, $arg;
    }else{
      push @irCmdArgs, $arg;
    }
  }

  if(@irCmdArgs > 0 and $cmd ne $COMMAND_SET){
    die "ERROR: unknown args @irCmdArgs\n";
  }

  if($cmd eq $COMMAND_TOGGLE){
    my $toggleArg = isACOn() ? "off" : "on";
    system $MIDEA_IR_CMD_EXEC, $toggleArg;
    system $MIDEA_RUBY_EXEC, "--fetch";
  }elsif($cmd eq $COMMAND_FETCH){
    system $MIDEA_RUBY_EXEC, "--fetch-and-read";
  }elsif($cmd eq $COMMAND_READ){
    system $MIDEA_RUBY_EXEC, "--read";
  }elsif($cmd eq $COMMAND_SET){
    system $MIDEA_IR_CMD_EXEC, @irCmdArgs;
    system $MIDEA_RUBY_EXEC, "--fetch";
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub isACOn(){
  my $status = `$MIDEA_RUBY_EXEC --read`;
  if($status =~ /^on/){
    return 1;
  }elsif($status =~ /^off/){
    return 0;
  }else{
    die "ERROR: could not parse $MIDEA_RUBY_EXEC output: $status\n";
  }
}

&main(@ARGV);
