#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $EXEC = basename $0;

my $IPMAGIC_NAME = "pi";
my $MIDEA_RUBY_EXEC = "midea-ruby";
my $MIDEA_IGCMD_EXEC = "midea-igcmd";

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC
  $EXEC -t | --toggle
    -check $MIDEA_RUBY_EXEC --read
    -if POWER=on:
      -run $MIDEA_IGCMD_EXEC off
    -if POWER=off:
      -run $MIDEA_IGCMD_EXEC cool 22 low

  $EXEC -g|--get|-f|--fetch
    -run $MIDEA_RUBY_EXEC --fetch-and-read

  $EXEC -r|--read
    -run $MIDEA_RUBY_EXEC --read

  $EXEC -s|--set [ARG ARG ..]
    -run $MIDEA_IGCMD_EXEC ARG ARG ..
    -run $MIDEA_RUBY_EXEC --fetch
";

my $COMMAND_TOGGLE = "toggle";
my $COMMAND_FETCH = "fetch";
my $COMMAND_READ = "read";
my $COMMAND_SET = "set";

sub isACOn();

sub main(@){
  my $cmd = $COMMAND_TOGGLE;
  my @igcmdArgs;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-t|--toggle)$/){
      $cmd = $COMMAND_TOGGLE;
    }elsif($arg =~ /^(-g|--get|-f|--fetch)$/){
      $cmd = $COMMAND_FETCH;
    }elsif($arg =~ /^(-r|--read)$/){
      $cmd = $COMMAND_READ;
    }elsif($arg =~ /^(-s|--set)$/){
      $cmd = $COMMAND_SET;
    }else{
      push @igcmdArgs, $arg;
    }
  }

  if(@igcmdArgs > 0 and $cmd ne $COMMAND_SET){
    die "ERROR: unknown args @igcmdArgs\n";
  }

  if($cmd eq $COMMAND_TOGGLE){
    my $toggleArg = isACOn() ? "off" : "on";
    system $MIDEA_IGCMD_EXEC, $toggleArg;
    system $MIDEA_RUBY_EXEC, "--fetch";
  }elsif($cmd eq $COMMAND_FETCH){
    system $MIDEA_RUBY_EXEC, "--fetch-and-read";
  }elsif($cmd eq $COMMAND_READ){
    system $MIDEA_RUBY_EXEC, "--read";
  }elsif($cmd eq $COMMAND_SET){
    system $MIDEA_IGCMD_EXEC, @igcmdArgs;
    system $MIDEA_RUBY_EXEC, "--fetch";
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub isACOn(){
  my $status = `$MIDEA_RUBY_EXEC --read`;
  if($status =~ /^on/){
    return 1;
  }elsif($status =~ /^off/){
    return 0;
  }else{
    die "ERROR: could not parse $MIDEA_RUBY_EXEC output: $status\n";
  }
}

&main(@ARGV);
