#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use Cwd qw(abs_path);

my $EXEC = basename $0;

my @CODEC_COPY_ARGS = qw(-c copy);

my $usage = "Usage:
  $EXEC -h | -help

  $EXEC [OPTS] FILE [FILE FILE ..]
    use -concat filter

  OPTS
    -o OUTPUT_FILE | --output=OUTPUT_FILE
      use OUTPUT_FILE instead of out.<EXT>
      where EXT is the extension of the first input file

    --arg=FFMPEG_ARG
      append FFMPEG_ARG to FFMPEG_ARGS

    --args=FFMPEG_ARG_LIST
      split FFMPEG_ARG_LIST at spaces and append to FFMPEG_ARGS

    --no-copy
      do not prepend '@CODEC_COPY_ARGS' to FFMPEG_ARGS
";

sub main(@){
  my @inputFiles;
  my $outputFile = undef;
  my @ffmpegArgs;
  my $includeCopy = 1;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^(-o)$/ and @_ > 0){
      $outputFile = shift @_;
    }elsif($arg =~ /^--output=(.+)$/){
      $outputFile = $1;
    }elsif($arg =~ /^(--no-copy)$/){
      $includeCopy = 0;
    }elsif($arg =~ /^--arg=(.+)$/){
      push @ffmpegArgs, $1;
    }elsif($arg =~ /^--args=(.+)$/){
      @ffmpegArgs = (@ffmpegArgs, split /\s+/, $1);
    }elsif(-f $arg){
      push @inputFiles, $arg;
    }else{
      die "$usage\nERROR: unknown arg $arg\n";
    }
  }

  die "$usage\nERROR: no input files\n" if @inputFiles == 0;

  if($includeCopy){
    @ffmpegArgs = (@CODEC_COPY_ARGS, @ffmpegArgs);
  }

  if(not defined $outputFile){
    my $ext = $1 if $inputFiles[0] =~ /\.(\w+)$/;
    die "ERROR: could not read extension from $inputFiles[0]\n" if not defined $ext;
    $outputFile = "out.$ext";
  }

  my $tmpList = "/tmp/$EXEC-tmp-file-list.txt";
  open FH, "> $tmpList" or die "ERROR: could not write $tmpList\n$!\n";
  for my $inputFile(@inputFiles){
    my $absPath = abs_path $inputFile;
    $absPath =~ s/'/'\\''/g;
    print FH "file '$absPath'\n";
  }
  close FH;

  my @cmd = (
    "ffmpeg",
      "-f", "concat",
      "-safe", "0",
      "-i", $tmpList,
      @ffmpegArgs,
      $outputFile,
  );
  system @cmd;
}

&main(@ARGV);
