#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(time);

my $katUrl = "kat.cr";
my $magnetCommand = "seedbox";
my $defaultSortField = "size";
my $defaultSortDir = "asc";
my %sortFields = (
  size        => ['size'],
  time_add    => ['time_add', 'age', 'date'],
  seeders     => ['seeders', 'seed'],
  leechers    => ['leechers', 'leech'],
);
my %sortFieldSyns = map {my $key=$_; map {$_ => $key} @{$sortFields{$key}}} keys %sortFields;
my @allSortFieldSyns = join "|", sort ( map {@{$_}} values %sortFields );
my $sortFieldFmt = join "\n        ", map {"$_ => @{$sortFields{$_}}"} sort keys %sortFields;
my $okSortField = join '|', sort keys %sortFieldSyns;

my $usage = "Usage:
  $0 [--sort=SORT] WORD [WORD ..]
    fetch web page at \"$katUrl/usearch/WORD WORD..\"
    parse the resulting HTML and display each torrent with an index
      (last torrent shown has index \"#1\")
    prompt the user for indices to run '$magnetCommand <magnet_link>'

    WORD = query word
      first WORD cannot start with hyphen

    SORT
      one of: @allSortFieldSyns
        $sortFieldFmt
      torrents are sorted locally before displaying
      you can also add \"-asc\" or \"-desc\" to sort ascending or descending
        \"-asc\" index number #1 (at the bottom) has the highest value
        \"-desc\" index number #1 (at the bottom) has the lowest value
      e.g.: --sort=size-asc
      default field is \"$defaultSortField\"
      default dir is \"$defaultSortDir\"
";

sub getKatHtml($$);
sub parseAgeS($);
sub formatTorrent($);
sub parseTorrents($);
sub run(@);

sub main(@){
  my $sortField = $defaultSortField;
  my $sortDir = $defaultSortDir;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^--sort=($okSortField)(?:-(asc|desc))?$/){
      my ($sField, $sDir) = ($1, $2);
      $sField = $sortFieldSyns{$sField};
      $sDir = 'desc' if not $sDir;
      ($sortField, $sortDir) = ($sField, $sDir);
    }else{
      die $usage;
    }
  }

  my $html = getKatHtml "@_", "";

  my @torrents = parseTorrents($html);

  @torrents = sort {
    my ($A, $B);
    if($sortDir eq "asc"){
      ($A, $B) = ($b, $a);
    }elsif($sortDir eq "desc"){
      ($A, $B) = ($a, $b);
    }

    if($sortField eq "size"){
      return $$A{sizeMB} <=> $$B{sizeMB};
    }elsif($sortField eq "time_add"){
      return parseAgeS($$A{age}) <=> parseAgeS($$B{age});
    }elsif($sortField eq "seeders"){
      return $$A{seeders} <=> $$B{seeders};
    }elsif($sortField eq "leechers"){
      return $$A{leechers} <=> $$B{leechers};
    }
  } @torrents;

  my $index = 1;
  for my $torrent(@torrents){
    $$torrent{index} = $index++;
  }

  if(@torrents == 0){
    print "no torrents found for query: \"@_\"\n";
    exit;
  }

  my %magnets;
  for my $t(reverse @torrents){
    print formatTorrent $t;
    $magnets{$$t{index}} = $$t{magnet} if $$t{magnet} =~ /^magnet/;
  }

  print "\nrun '$magnetCommand' on torrents INDEX INDEX ...: ";
  my $response = <STDIN>;
  my @magnetsToDownload;
  for my $idx($response =~ /(\d+)/g){
    if(not defined $magnets{$idx}){
      print STDERR "WARNING: no magnet for $idx\n";
    }else{
      push @magnetsToDownload, $magnets{$idx};
    }
  }
  run $magnetCommand, @magnetsToDownload if @magnetsToDownload > 0;
}

sub getKatHtml($$){
  my ($searchQuery, $postQuery) = @_;

  my $url = "$katUrl/usearch/$searchQuery?$postQuery";
  my $tmpFile = "/tmp/kickasstorrents-" . int(time*1000);
  run "wget", $url, "--no-check-certificate", "-O", $tmpFile;

  if(-f $tmpFile){
    run "mv", $tmpFile, "$tmpFile.gz";
    run "gunzip $tmpFile.gz >/dev/null 2>/dev/null";
    if(-f "$tmpFile.gz"){
      run "mv", "$tmpFile.gz", $tmpFile;
    }
  }

  my $html = `cat $tmpFile`;
  run "rm", $tmpFile;

  print "="x30 . "\n";

  return $html;
}

sub parseAgeS($){
  my $ageVal = shift;
  my $ageSex = 0;
  if($ageVal =~ /^\s*(\d+(?:\.\d+)?)\s*(year|month|week|day|hour)s?\s*$/){
    my ($val, $unit) = ($1, $2);
    if($unit eq "year"){
      $ageSex = $val * 365 * 24 * 60 * 60;
    }elsif($unit eq "month"){
      $ageSex = $val * 30 * 24 * 60 * 60;
    }elsif($unit eq "week"){
      $ageSex = $val * 7 * 24 * 60 * 60;
    }elsif($unit eq "day"){
      $ageSex = $val * 24 * 60 * 60;
    }elsif($unit eq "hour"){
      $ageSex = $val * 60 * 60;
    }
  }
  return $ageSex;
}

sub formatTorrent($){
  my ($t) = @_;
  my $verified = $$t{verified} =~ /verified/ ? "V" : " ";
  my $error = $$t{magnet} =~ /^magnet/ ? "" : "!!! NO MAGNET !!!";
  return sprintf "%s\n#%-3d %s\n  %s | %9.3fMB | %s | %sC | %s in %s | %s | %sS | %sL\n",
      '-'x30,
      $$t{index},
      $$t{title},
      $error,
      $$t{sizeMB},
      $verified,
      $$t{comments},
      $$t{postedBy},
      $$t{postedIn},
      $$t{age},
      $$t{seeders},
      $$t{leechers},
      ;
}

sub parseTorrents($){
  my $html = shift;

  my $tag = "[^<>]*";
  my $otag = "[^<>\\/]*";
  my $ws = "[ \\t\\n]*";

  my @trs;
  for(my $i=0; $i<length $html; $i++){
    my $substr = substr $html, $i, (length $html) - $i;
    if($substr =~ /^<tr\W/){
      my $tr = $substr;
      $tr =~ s/(<\s*\/\s*tr\s*>).*/$1/s;
      push @trs, $tr;
    }
  }

  my $index = 1;

  my @torrents;
  for my $tr(@trs){
    if($tr =~ /^               $ws
       <tr$tag>                $ws
         <td$otag> (.*) <\/td> $ws
         <td$otag> (.*) <\/td> $ws
         <td$otag> (.*) <\/td> $ws
         <td$otag> (.*) <\/td> $ws
         <td$otag> (.*) <\/td> $ws
         <td$otag> (.*) <\/td> $ws
       <\/tr>                  $ws
       $/xs){
       my ($nameTD, $sizeTD, $filesTD, $ageTD, $seedTD, $leechTD) =
         ($1, $2, $3, $4, $5, $6);

       my $magnet = "?";
       if($nameTD =~ /<a $tag href="(magnet:[^"]+)"/){
         $magnet = $1;
       }

       my $title = $nameTD;
       $title =~ s/>\s*\d+\s*</></g; # remove index
       $title =~ s/<a $tag class="[^"]*icommentjs[^"]*"$tag>.*?<\/a>//xs;
       $title =~ s/<$tag>$ws Posted $ws by $ws <$tag>.*//sx;
       $title =~ s/<$tag >$ws in $ws <$tag>.*//sx;
       $title =~ s/<$tag>//g;
       $title =~ s/^$ws//;
       $title =~ s/$ws$//;
       $title =~ s/\&quot;/"/g;
       $title =~ s/[\n\r]//g;
       $title =~ s/^\s*//;
       $title =~ s/\s*$//;

       my $verified = "?";
       if($nameTD =~ /<a $tag title="Verified\ Torrent"$tag>/xs){
         $verified = "verified";
       }

       my $comments = "?";
       if($nameTD =~ /<a $tag class="[^"]*icommentjs[^"]*"$tag>(?:<$tag>)*(\d+)/xs){
         $comments = $1;
       }

       my $postedBy = "?";
       if($nameTD =~ /<$tag>$ws Posted $ws by $ws <$tag> .* href="\/user\/([^"\/]+)\/"/sx){
         $postedBy = $1;
       }

       my $postedIn = "?";
       if($nameTD =~ /<$tag> $ws in $ws <$tag> (.*)/sx){
         $postedIn = $1;
         $postedIn =~ s/<$tag>//g;
         $postedIn =~ s/^$ws//;
         $postedIn =~ s/$ws$//;
       }

       my $sizeMB = $sizeTD;
       $sizeMB =~ s/<$tag>//g;
       if($sizeMB =~ /^(\d+(?:\.\d+)?)\s*(GB|MB|KB)/){
         my ($val, $unit) = ($1, $2);
         $sizeMB = $val if $unit eq "MB";
         $sizeMB = $val * 1000 if $unit eq "GB";
         $sizeMB = $val / 1000 if $unit eq "KB";
       }else{
         $sizeMB = "?";
       }

       my $fileCount = $filesTD;
       $fileCount =~ s/<$tag>//g;
       $fileCount = $fileCount =~ /(\d+)/ ? $1 : "?";

       my $age = $ageTD;
       $age =~ s/<$tag>//g;
       $age =~ s/\&nbsp;/ /g;

       my $seeders = $seedTD;
       $seeders =~ s/<$tag>//g;
       $seeders = $seeders =~ /(\d+)/ ? $1 : "?";

       my $leechers = $leechTD;
       $leechers =~ s/<$tag>//g;
       $leechers = $leechers =~ /(\d+)/ ? $1 : "?";

       push @torrents, {
         magnet => $magnet,
         title => $title,
         verified => $verified,
         comments => $comments,
         postedBy => $postedBy,
         postedIn => $postedIn,
         sizeMB => $sizeMB,
         fileCount => $fileCount,
         age => $age,
         seeders => $seeders,
         leechers => $leechers,
       };
    }
  }

  return @torrents;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
