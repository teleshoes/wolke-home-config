#!/usr/bin/perl
use strict;
use warnings;

my $usage ="Usage:
  $0 -h|--help
    show this message

  $0 [OPTS] APK_FILE [APK_FILE APK_FILE ..]
    show apk info with aapt

  OPTS
    --short
      (this is the default when exactly one APK_FILE is passed in)
      format each APK_FILE as:
        <PACKAGE_NAME>  <VERSION>  sdk<SDK_VERSION> (act=<LAUNCHABLE_ACTIVITY)
    --grid
      (this is the default when two ore more APK_FILES are passed in)
      format each APK_FILE as:
        | <PACKAGE_NAME> | <VERSION> | sdk<SDK_VERSION> | (act=<LAUNCHABLE_ACTIVITY) |
    --long
      print all parsed values one per line, as <KEY>=<VAL>

    --prefix
      add 'APK_FILE ' before each line
    --no-prefix
      (this is the default)
      do not add 'APK_FILE ' before each line
";

sub parseApk($);

my $FORMAT_SHORT = "short";
my $FORMAT_GRID = "grid";
my $FORMAT_LONG = "long";

sub main(@){
  my @apkFiles;
  my $format = undef;
  my $prefix = 0;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif(-f $arg){
      push @apkFiles, $arg;
    }elsif($arg =~ /^(--short)$/){
      $format = $FORMAT_SHORT;
    }elsif($arg =~ /^(--long)$/){
      $format = $FORMAT_LONG;
    }elsif($arg =~ /^(--prefix)$/){
      $prefix = 1;
    }elsif($arg =~ /^(--no-prefix)$/){
      $prefix = 0;
    }else{
      die "ERROR: unknown arg $arg\n";
    }
  }

  die "ERROR: at least one APK_FILE is required\n" if @apkFiles == 0;

  if(not defined $format){
    $format = @apkFiles > 1 ? $FORMAT_GRID : $FORMAT_SHORT;
  }

  my %apkInfos = map {$_ => parseApk($_)} @apkFiles;

  if($format eq $FORMAT_SHORT){
    for my $apkFile(sort keys %apkInfos){
      my $info = $apkInfos{$apkFile};
      my $linePrefix = $prefix ? "$apkFile " : "";
      my $fmt = ""
        . "$linePrefix$$info{packageName}"
        . "  $$info{version}"
        . "  sdk=$$info{sdkVersion}"
        . "  (act=$$info{activity})"
        . "\n";
      print $fmt;
    }
  }elsif($format eq $FORMAT_GRID){
    my @rows;
    for my $apkFile(sort keys %apkInfos){
      my $info = $apkInfos{$apkFile};
      my $linePrefix = $prefix ? "$apkFile " : "";
      my $row = [];
      push @$row, $apkFile if $prefix;
      push @$row, $$info{packageName};
      push @$row, $$info{version};
      push @$row, "sdk=$$info{sdkVersion}";
      push @$row, "(act=$$info{activity})";

      push @rows, $row;
    }
    my @maxLens = map{0} @{$rows[0]};
    for my $row(@rows){
      for(my $col=0; $col<@$row; $col++){
        my $len = length $$row[$col];
        if($len > $maxLens[$col]){
          $maxLens[$col] = $len;
        }
      }
    }
    for my $row(@rows){
      my $rowFmt = "|";
      for(my $col=0; $col<@$row; $col++){
        my $val = $$row[$col];
        $val .= ' ' x ($maxLens[$col] - length $val);
        $rowFmt .= " $val |";
      }
      print "$rowFmt\n";
    }
  }elsif($format eq $FORMAT_LONG){
    for my $apkFile(sort keys %apkInfos){
      my $info = $apkInfos{$apkFile};
      my $linePrefix = $prefix ? "$apkFile " : "";
      for my $key(sort keys %$info){
        print "$linePrefix$key = $$info{$key}\n";
      }
    }
  }else{
    die "ERROR: unknown format $format\n";
  }
}

sub parseApk($){
  my ($apkFile) = @_;
  my $out = `aapt dump badging '$apkFile'`;
  my $info = {};

  my %requiredVarSynonyms = (
    "package.name"             => "packageName",
    "package.versionName"      => "version",
    "package.versionCode"      => "versionCode",
    "application-label"        => "label",
    "sdkVersion"               => "sdk",
  );
  my %optionalVarSynonyms = (
    "launchable-activity.name" => "activity",
  );

  my @packageVars = qw(
    name versionName versionCode
    compileSdkVersion compileSdkVersionCodeName platformBuildVersionName
  );
  my @launchableActivityVars = qw(
    name label
  );
  my @lineVars = qw(application-label sdkVersion targetSdkVersion native-code);

  for my $var(@packageVars){
    $$info{"package.$var"} = $1 if $out =~ /^package: .*? \s+ $var='([^']+)'/msxi;
  }
  for my $var(@packageVars){
    $$info{"launchable-activity.$var"} = $1
      if $out =~ /^launchable-activity: .*? \s+ $var='([^']+)'/msxi;
  }
  for my $var(@lineVars){
    $$info{$var} = $1 if $out =~ /^$var\s*:\s*'([^']+)'$/msxi;
  }
  $$info{perms} = join(',', $out =~ /^uses-permission: \s* name='([^']+)'$/gmsxi);

  for my $var(sort keys %requiredVarSynonyms){
    my $syn = $requiredVarSynonyms{$var};
    if(not defined $$info{$var}){
      die "ERROR: could not find $var in $apkFile\n$out\n";
    }
    $$info{$syn} = $$info{$var};
  }

  for my $var(sort keys %optionalVarSynonyms){
    my $syn = $optionalVarSynonyms{$var};
    $$info{$syn} = defined $$info{$var} ? $$info{$var} : "";
  }

  return $info;
}

&main(@ARGV);
