#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub getACByName($);
sub formatACInfo($);
sub readAC($);
sub writeAC($$);
sub readMideaCube($);
sub writeMideaCube($$);
sub readGEAC($);
sub writeGEAC($$);
sub readMideaRuby($);
sub writeMideaIR($$);
sub readTasmota($);
sub writeTasmota($$);
sub run(@);

my $ACS = [
  {
    names    => [qw(1 ac1 liv liv1 midea)],
    readDev  => "ac",
    writeDev => "ac",
    readSub  => \&readMideaCube,
    writeSub => \&writeMideaCube,
  },
  {
    names    => [qw(2 ac2 bed bed1 ge)],
    readDev  => "bed1",
    writeDev => "bed1",
    readSub  => \&readGEAC,
    writeSub => \&writeGEAC,
  },
  {
    names    => [qw(midea_ir_ruby)],
    readDev  => 1,
    readSub  => \&readMideaRuby,
    writeDev => "/dev/ttyUSB0",
    writeSub => \&writeMideaIR,
  },
  {
    names    => [qw(fan1)],
    readDev  => "fan1",
    readSub  => \&readTasmota,
    writeDev => "fan1",
    writeSub => \&writeTasmota,
  },
];
my @ALL_AC_NAMES = (map {@{$$_{names}}} @$ACS);
my %OK_AC_NAMES = map {$_ => 1} @ALL_AC_NAMES;
my $DEFAULT_AC_NAME = "liv1";

my $IPMAGIC_NAME = "raspi";

my $MODE_AUTO = "mode-auto";
my $MODE_COOL = "mode-cool";
my $MODE_DRY = "mode-dry";
my $MODE_FAN = "mode-fan";

my $FAN_AUTO = "fan-auto";
my $FAN_HIGH = "fan-high";
my $FAN_MED = "fan-med";
my $FAN_LOW = "fan-low";

my $CMD_TOGGLE = "toggle";
my $CMD_GET = "get";
my $CMD_SET = "set";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC [AC_NAME]
  $EXEC [AC_NAME] -t | --toggle | toggle
    -get power info for AC as in `$EXEC AC_NAME --get`
    -if AC is off:
      same as: $EXEC AC_NAME --set default
    -if AC is on:
      same as: $EXEC AC_NAME --set off

  $EXEC [AC_NAME] -g | --get | get
    read POWER/MODE/TEMP/FAN/INSIDE_TEMP/OUTSIDE_TEMP from AC
    print as: <POWER> mode:<MODE> <TEMP>F fan:<FAN> (<INSIDE_TEMP>F / <OUTSIDE_TEMP>F)
      -any missing info is formatted as '??'

  $EXEC [AC_NAME] AC_SET_ARG [AC_SET_ARG AC_SET_ARG ..]
  $EXEC [AC_NAME] -s | --set | set AC_SET_ARG [AC_SET_ARG AC_SET_ARG ..]

  AC_NAME
    one of: @ALL_AC_NAMES
    default is: $DEFAULT_AC_NAME

  AC_SET_ARG = AC_PRESET | AC_POWER_OFF | AC_MODE | AC_FAN | AC_TEMP | AC_ECO
    set power+mode+temp+fan for AC
    NOTE: any arg, except 'off', will implicitly set power=on
          'off' must be the last arg to turn AC off

  AC_PRESET
    on | default | normal
      same as: mode-cool fan-low 72F
    cold
      same as: mode-cool fan-high 70F

  AC_POWER_OFF
    off
      set power=off

  AC_MODE
    mode-auto
      set mode to auto
    cool | mode-cool
      set mode to cool
    dry | mode-dry
      set mode to dry
    fan | mode-fan
      set mode to fan

  AC_TEMP
    <TEMP_F>F | <TEMP_F>
      set temp to TEMP_F
    TEMP_F
      an integer between 50 and 99, inclusive

  AC_FAN
    fan-auto
      set fan to auto
    low | fan-low
      set fan to low
    med | fan-med
      set fan to med
    high | fan-high
      set fan to high

  AC_ECO
    eco
      set eco to true, or use mode eco
";

sub main(@){
  my $cmd = undef;
  my $acName = $DEFAULT_AC_NAME;
  my $acSetInfo = {};
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif(defined $OK_AC_NAMES{$arg}){
      $acName = $arg;
    }elsif($arg =~ /^(-t|--toggle|toggle)$/){
      $cmd = $CMD_TOGGLE;
    }elsif($arg =~ /^(-g|--get|get)$/){
      $cmd = $CMD_GET;
    }elsif($arg =~ /^(-s|--set|set)$/){
      $cmd = $CMD_SET;
    }elsif($arg =~ /^(on|default|normal)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{mode} = $MODE_COOL;
      $$acSetInfo{temp} = 72;
      $$acSetInfo{fan} = $FAN_LOW;
    }elsif($arg =~ /^(cold)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{mode} = $MODE_COOL;
      $$acSetInfo{temp} = 70;
      $$acSetInfo{fan} = $FAN_HIGH;
    }elsif($arg =~ /^(off)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 0;
    }elsif($arg =~ /^(mode-auto)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{mode} = $MODE_AUTO;
    }elsif($arg =~ /^(cool|mode-cool)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{mode} = $MODE_COOL;
    }elsif($arg =~ /^(dry|mode-dry)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{mode} = $MODE_DRY;
    }elsif($arg =~ /^(fan|mode-fan)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{mode} = $MODE_FAN;
    }elsif($arg =~ /^([56789]\d)F?$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{temp} = $1;
    }elsif($arg =~ /^(fan-auto)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{fan} = $FAN_AUTO;
    }elsif($arg =~ /^(low|fan-low)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{fan} = $FAN_LOW;
    }elsif($arg =~ /^(med|fan-med)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{fan} = $FAN_MED;
    }elsif($arg =~ /^(high|fan-high)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{fan} = $FAN_HIGH;
    }elsif($arg =~ /^(eco)$/i and (not defined $cmd or $cmd eq $CMD_SET)){
      $cmd = $CMD_SET;
      $$acSetInfo{power} = 1;
      $$acSetInfo{eco} = 1;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  $cmd = $CMD_TOGGLE if not defined $cmd;

  my $ac = getACByName($acName);
  if(not defined $ac){
    die "ERROR: could not find ac $acName\n";
  }

  if($cmd eq $CMD_TOGGLE){
    my $curInfo = readAC($ac);
    if(defined $curInfo and $$curInfo{power}){
      writeAC($ac, {
        power => 0,
      });
    }else{
      writeAC($ac, {
        power => 1,
        mode  => $MODE_COOL,
        temp  => 72,
        fan   => $FAN_LOW,
      });
    }
    $curInfo = readAC($ac);
    print formatACInfo($curInfo) . "\n";
  }elsif($cmd eq $CMD_GET){
    my $curInfo = readAC($ac);
    print formatACInfo($curInfo) . "\n";
  }elsif($cmd eq $CMD_SET){
    writeAC($ac, $acSetInfo);
    my $curInfo = readAC($ac);
    print formatACInfo($curInfo) . "\n";
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub getACByName($){
  my ($targetName) = @_;
  for my $ac(@$ACS){
    for my $name(sort @{$$ac{names}}){
      if($name eq $targetName){
        return $ac;
      }
    }
  }
  return undef;
}

sub formatACInfo($){
  my ($info) = @_;
  $info = {} if not defined $info;

  my $modeFmts = {
    $MODE_AUTO => "auto",
    $MODE_COOL => "cool",
    $MODE_DRY  => "dry",
    $MODE_FAN  => "fan",
  };

  my $fanFmts = {
    $FAN_AUTO => "auto",
    $FAN_HIGH => "high",
    $FAN_MED  => "med",
    $FAN_LOW  => "low",
  };

  return sprintf("%-3s %2sF mode:%-4s fan:%-4s (%3sF / %3sF)%s",
    (defined $$info{power} ? ($$info{power} ? "on" : "off") : "???"),
    (defined $$info{temp} ? $$info{temp} : "???"),
    (defined $$info{mode} ? $$modeFmts{$$info{mode}} : "???"),
    (defined $$info{fan} ? $$fanFmts{$$info{fan}} : "???"),
    (defined $$info{insideTemp} ? $$info{insideTemp} : "???"),
    (defined $$info{outsideTemp} ? $$info{outsideTemp} : "???"),
    (defined $$info{eco} and $$info{eco} ? " (eco)" : ""),
  );
}

sub readAC($){
  my ($ac) = @_;
  return &{$$ac{readSub}}($$ac{readDev});
}

sub writeAC($$){
  my ($ac, $info) = @_;
  &{$$ac{writeSub}}($$ac{writeDev}, $info);
}

sub readMideaCube($){
  my ($dev) = @_;

  my $modes = {
    AUTO => $MODE_AUTO,
    COOL => $MODE_COOL,
    DRY => $MODE_DRY,
    FAN => $MODE_FAN,
  };
  my $okModes = join "|", sort keys %$modes;

  my $fans = {
    102 => $FAN_AUTO,
    100 => $FAN_HIGH,
    60  => $FAN_MED,
    40  => $FAN_LOW,
  };

  my $numRe = '\d+|\d*\.\d+';

  my $out = `midea-cube --dev='$dev' --get`;
  if($out =~ /
    ^
    \w+:
    \s* (on|off) \s+
    \s* => \s*
    \s* ($numRe)F\s*
    \(
      \s* ($numRe)F \s*
      \/
      \s* ($numRe)F \s*
    \)
    \s* mode=($okModes) \s*
    \s* fan=(\d+) \s*
    \s* eco=(True|False) \s*
  $/x){
    my ($powerStr, $temp, $insideTemp, $outsideTemp, $modeStr, $fanNum, $isEco) = (
      $1, $2, $3, $4, $5, $6, $7);
    my $fanName = "?";
    for my $namedFanSpeed(sort {$a <=> $b} keys %$fans){
      if($namedFanSpeed >= $fanNum){
        $fanName = $$fans{$namedFanSpeed};
        last;
      }
    }

    return {
      power       => ($powerStr =~ /on/i ? 1 : 0),
      mode        => $$modes{$modeStr},
      temp        => int($temp + 0.5),
      fan         => $fanName,
      eco         => ($isEco =~ /True/i ? 1 : 0),
      insideTemp  => int($insideTemp + 0.5),
      outsideTemp => int($outsideTemp + 0.5),
    };
  }else{
    return undef;
  }
}

sub writeMideaCube($$){
  my ($dev, $info) = @_;
  my @cmd = ("midea-cube");

  @cmd = (@cmd, "--dev=$dev") if defined $dev;

  if(defined $$info{power}){
    if($$info{power}){
      @cmd = (@cmd, "--on");
    }else{
      @cmd = (@cmd, "--off");
    }
  }

  if(defined $$info{mode}){
    if($$info{mode} eq $MODE_AUTO){
      @cmd = (@cmd, "--mode-auto");
    }elsif($$info{mode} eq $MODE_COOL){
      @cmd = (@cmd, "--mode-cool");
    }elsif($$info{mode} eq $MODE_DRY){
      @cmd = (@cmd, "--mode-dry");
    }elsif($$info{mode} eq $MODE_FAN){
      @cmd = (@cmd, "--mode-fan");
    }else{
      die "ERROR: unknown mode $$info{mode}\n";
    }
  }

  if(defined $$info{temp}){
    @cmd = (@cmd, "--temp", $$info{temp});
  }

  if(defined $$info{fan}){
    if($$info{fan} eq $FAN_AUTO){
      @cmd = (@cmd, "--fan-auto");
    }elsif($$info{fan} eq $FAN_HIGH){
      @cmd = (@cmd, "--fan-high");
    }elsif($$info{fan} eq $FAN_MED){
      @cmd = (@cmd, "--fan-med");
    }elsif($$info{fan} eq $FAN_LOW){
      @cmd = (@cmd, "--fan-low");
    }else{
      die "ERROR: unknown fan $$info{fan}\n";
    }
  }

  if(defined $$info{eco} and $$info{eco}){
    @cmd = (@cmd, "--set", "--eco-mode", "on");
  }else{
    @cmd = (@cmd, "--set", "--eco-mode", "off");
  }

  system @cmd;
}

sub readGEAC($){
  my ($dev) = @_;
  my $modes = {
    AUTO         => $MODE_AUTO,
    COOL         => $MODE_COOL,
    DRY          => $MODE_DRY,
    FAN_ONLY     => $MODE_FAN,
    ENERGY_SAVER => $MODE_AUTO, #ENERGY_SAVER => mode=auto + eco=True
  };
  my $okModes = join "|", sort keys %$modes;
  my $fans = {
    AUTO => $FAN_AUTO,
    HIGH => $FAN_HIGH,
    MED  => $FAN_MED,
    LOW  => $FAN_LOW,
  };
  my $okFans = join "|", sort keys %$fans;

  my $out = `ge-ac --dev '$dev' --get`;
  if($out =~ /^(OFF\s*-\s*)?(\d+)°F\s*\(cur=(\d+)°F\)\s+($okModes)\s+($okFans)$/){
    my ($powerStr, $temp, $curTemp, $modeStr, $fanStr) = ($1, $2, $3, $4, $5);
    $powerStr = "" if not defined $powerStr;

    return {
      power       => ($powerStr =~ /OFF/i ? 0 : 1),
      mode        => $$modes{$modeStr},
      temp        => $temp,
      fan         => $$fans{$fanStr},
      eco         => ($modeStr =~ /ENERGY_SAVER/ ? 1 : 0),
      insideTemp  => $curTemp,
      outsideTemp => undef,
    };
  }else{
    return undef;
  }
}

sub writeGEAC($$){
  my ($dev, $info) = @_;
  my @cmd = ("ge-ac");

  @cmd = (@cmd, "--device", $dev) if defined $dev;

  if(defined $$info{power}){
    if($$info{power}){
      @cmd = (@cmd, "--power", "on");
    }else{
      @cmd = (@cmd, "--power", "off");
    }
  }

  if(defined $$info{eco} and $$info{eco} and not defined $$info{mode}){
    $$info{mode} = $MODE_AUTO;
  }

  if(defined $$info{mode}){
    if($$info{mode} eq $MODE_AUTO){
      if(defined $$info{eco} and $$info{eco}){
        @cmd = (@cmd, "--mode", "eco");
      }else{
        @cmd = (@cmd, "--mode", "auto");
      }
    }elsif($$info{mode} eq $MODE_COOL){
      @cmd = (@cmd, "--mode", "cool");
    }elsif($$info{mode} eq $MODE_DRY){
      @cmd = (@cmd, "--mode", "dry");
    }elsif($$info{mode} eq $MODE_FAN){
      @cmd = (@cmd, "--mode", "fan");
    }else{
      die "ERROR: unknown mode $$info{mode}\n";
    }
  }

  if(defined $$info{temp}){
    @cmd = (@cmd, "--temp", $$info{temp});
  }

  if(defined $$info{fan}){
    if($$info{fan} eq $FAN_AUTO){
      @cmd = (@cmd, "--fan", "auto");
    }elsif($$info{fan} eq $FAN_HIGH){
      @cmd = (@cmd, "--fan", "high");
    }elsif($$info{fan} eq $FAN_MED){
      @cmd = (@cmd, "--fan", "med");
    }elsif($$info{fan} eq $FAN_LOW){
      @cmd = (@cmd, "--fan", "low");
    }else{
      die "ERROR: unknown fan $$info{fan}\n";
    }
  }

  system @cmd;
}

sub readMideaRuby($){
  my ($dev) = @_; #dev == acNum

  my $modes = {
    auto => $MODE_AUTO,
    cool => $MODE_COOL,
    dry => $MODE_DRY,
    fan => $MODE_FAN,
  };
  my $okModes = join "|", sort keys %$modes;

  my $fans = {
    102 => $FAN_AUTO,
    100 => $FAN_HIGH,
    60  => $FAN_MED,
    40  => $FAN_LOW,
  };

  my $out = `midea-ruby --ac=$dev --read`;
  if($out =~ /
    ^
    \s* (on|off) \s*
    \s* ($okModes) \s*
    \s* (\d+)% \s*
    \s* (\d+)F \s*
    \(
      \s* (\d+)F \s*
      \/
      \s* (\d+)F \s*
    \)
    .*
  $/x){
    my ($powerStr, $modeStr, $fanNum, $temp, $insideTemp, $outsideTemp) = (
      $1, $2, $3, $4, $5, $6);
    my $fanName = "?";
    for my $namedFanSpeed(sort keys %$fans){
      if($namedFanSpeed >= $fanNum){
        $fanName = $$fans{$namedFanSpeed};
        last;
      }
    }

    return {
      power       => $powerStr =~ /on/i ? 1 : 0,
      mode        => $$modes{$modeStr},
      temp        => $temp,
      fan         => $fanName,
      eco         => 0,
      insideTemp  => $insideTemp,
      outsideTemp => $outsideTemp,
    };
  }else{
    return undef;
  }
}

sub writeMideaIR($$){
  my ($dev, $info) = @_; #dev == /dev/ttyUSB0

  my @cmd = ("ipmagic", $IPMAGIC_NAME, "--sshc", "midea-ir-cmd");

  if(defined $$info{power}){
    if(not $$info{power}){
      @cmd = (@cmd, "off");
    }
  }

  if(defined $$info{mode}){
    if($$info{mode} eq $MODE_AUTO){
      @cmd = (@cmd, "mode-auto");
    }elsif($$info{mode} eq $MODE_COOL){
      @cmd = (@cmd, "cool");
    }elsif($$info{mode} eq $MODE_DRY){
      @cmd = (@cmd, "dry");
    }elsif($$info{mode} eq $MODE_FAN){
      @cmd = (@cmd, "fan-only");
    }else{
      die "ERROR: unknown mode $$info{mode}\n";
    }
  }

  if(defined $$info{temp}){
    @cmd = (@cmd, $$info{temp});
  }

  if(defined $$info{fan}){
    if($$info{fan} eq $FAN_AUTO){
      @cmd = (@cmd, "fan-auto");
    }elsif($$info{fan} eq $FAN_HIGH){
      @cmd = (@cmd, "high");
    }elsif($$info{fan} eq $FAN_MED){
      @cmd = (@cmd, "med");
    }elsif($$info{fan} eq $FAN_LOW){
      @cmd = (@cmd, "low");
    }else{
      die "ERROR: unknown fan $$info{fan}\n";
    }
  }

  if(defined $$info{eco} and $$info{eco}){
    print STDERR "WARNING: not setting eco with midea-ir-cmd, can only toggle with IR\n";
  }

  system @cmd;
}

sub readTasmota($){
  my ($dev) = @_;
  my $info = `tasmota --info $dev`;
  if($info =~ /^\w+:\s*(on|off)\s*/){
    my ($powerStr) = ($1);
    return {
      power       => ($powerStr =~ /on/ ? 1 : 0),
      mode        => undef,
      temp        => undef,
      fan         => undef,
      eco         => undef,
      insideTemp  => undef,
      outsideTemp => undef,
    };
  }else{
    return undef;
  }
}
sub writeTasmota($$){
  my ($dev, $info) = @_;

  my @cmd = ("tasmota", $dev);
  if(defined $$info{power} and $$info{power}){
    @cmd = (@cmd, "on");
  }else{
    @cmd = (@cmd, "off");
  }

  system @cmd;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
