#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use JSON qw(from_json);
use POSIX qw(ceil);
use Time::HiRes qw(time);

my $EXEC = basename $0;
my $JSON_ORDERS_CACHE_DIR = "$ENV{HOME}/.cache/robinhood/orders-json";
my $JSON_QUOTES_CACHE_DIR = "$ENV{HOME}/.cache/robinhood/quotes-json";
my $BEARER_TOKEN_READONLY_CACHE = "$ENV{HOME}/.cache/robinhood/bearer-token-readonly";
my $INSTRUMENT_CACHE = "$ENV{HOME}/.cache/robinhood/instrument-cache";
my $EXTRA_ORDERS_CACHE = "$ENV{HOME}/.cache/robinhood/extra-orders";

my $API_INSTRUMENT_URL_PREFIX = "https://api.robinhood.com/instruments";
my $API_CURRENCY_PAIRS_URL_PREFIX = "https://nummus.robinhood.com/currency_pairs";
my $API_ORDERS_STOCKS_URL = "https://api.robinhood.com/orders/?page_size=100";
my $API_ORDERS_CRYPTO_URL = "https://nummus.robinhood.com/orders/?page_size=100";
my $API_QUOTES_CRYPTO_URL = "https://api.robinhood.com/marketdata/forex/quotes";
my $API_QUOTES_STOCKS_URL = "https://api.robinhood.com/quotes";

my $READONLY_BEARER_TOKEN_FETCH_URL = "https://robinhood.com/us/en/stocks/IBM/";
my $TEST_BEARER_TOKEN_URL = "$API_QUOTES_STOCKS_URL/IBM/";

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS]
  $EXEC [OPTS] --fetch-summary
    same as: $EXEC --fetch-quotes >/dev/null && $EXEC --summary

  $EXEC [OPTS] -s | --summary
    show all instruments total cost and total quantity

  $EXEC [OPTS] -o | --orders
    show all orders

  $EXEC [OPTS] --fetch-orders BEARER_TOKEN_FULL_AUTH
    fetch stocks+crypto orders JSON and place them in:
      $JSON_ORDERS_CACHE_DIR

    to get BEARER_TOKEN_FULL_AUTH:
      -log in with username/password/mfa/etc in a browser
      -open network tab
      -filter on '/orders'
      -copy the api.robinhood.com authorization header with 'Copy value'
      -paste between single quotes as arg to $EXEC
      -CLEAR YOUR TERMINAL HISTORY

    BEARER_TOKEN_FULL_AUTH
      -the fully authorized robinhood auth token
      -leading/trailing whitespace is removed
      -additionally, the following prefixes are removed if present:
        \"Authorization: Bearer \"
        \"Bearer \"
      NOTE: protect this BEARER_TOKEN_FULL_AUTH closer than a password

  $EXEC [OPTS] --fetch-quotes
    -obtain a readonly auth token
    -for each instrument in any cached order
      -fetch a quote from the api (last_trade_price or ask_price)
      -store the quote in:
        $JSON_QUOTES_CACHE_DIR/YYYYMMDD_HHMMSS_MILLIS_SYMBOL.json

  $EXEC [OPTS] --cleanup-quotes
    -get all QUOTE_FILEs in $JSON_QUOTES_CACHE_DIR
    -get the date DATE_YYYYMMDD for each QUOTE_FILE, formatted as YYYY-mm-dd
    -group QUOTE_FILEs by DATE_YYYYMMDD
    -for each DATE_YYYYMMDD:
      -for each QUOTE_FILE with DATE_YYYYMMDD:
        -get quote INSTRUMENT for the QUOTE_FILE
        -get the LATEST_QUOTE_FILE for that INSTRUMENT
        -skip QUOTE_FILE if QUOTE_FILE is the LATEST_QUOTE_FILE
        -if the LATEST_QUOTE_FILE has a valid price:
          -include QUOTE_FILE as OBSOLETE_QUOTE_FILE
      -select the OLDEST_OBSOLETE_QUOTE_FILE
      -obtain mtime in millis TIME_MILLIS of OLDEST_OBSOLETE_QUOTE_FILE
      -create a tar file named <DATE_YYYYMMDD>_<TIMEMILLIS>_quotes.json
        -add each OBSOLETE_QUOTE_FILE
        -compress with xz
        -delete each OBSOLETE_QUOTE_FILE

   OPTS
    --color
      use Term::ANSIColor to output text formatted with ANSI color escapes
      used for \$USD profit/loss in summary (green/red)
    --no-color
      do not output any ANSI color escapes
    --color-term-only
      (this is the default)
      if STDOUT is a TTY, same as --color
      if STDOUT is not a TTY, same as --no-color
";

sub apiCallCurl($$);
sub runCurl($@);
sub getInstrumentSummary(@);
sub getReadonlyBearerToken();
sub fetchReadonlyBearerToken();
sub testBearerToken($);
sub fetchAndCacheQuote($$$$$$);
sub readCachedQuote($);
sub readCachedQuoteFile($$);
sub parseJsonOrders();
sub readInstrumentCache();
sub writeInstrumentCache($);
sub getInstrumentSymbol($);
sub getCryptoCode($);
sub readUrl($);
sub readFile($);
sub writeFile($$);
sub maybeColorText($$$);
sub colorText($$);
sub usdFmt($$$$);
sub dateFmt($$);
sub nowMillis();
sub mtime($);
sub run(@);
sub runQuiet(@);

my $COMMAND_FETCH_SUMMARY = "fetch-summary";
my $COMMAND_SUMMARY = "summary";
my $COMMAND_ORDERS = "orders";
my $COMMAND_FETCH_ORDERS = "fetch-orders";
my $COMMAND_FETCH_QUOTES = "fetch-quotes";
my $COMMAND_CLEANUP_QUOTES = "cleanup-quotes";

my $IS_TERM = -t STDOUT;

sub main(@){
  my $cmd = $COMMAND_FETCH_SUMMARY;
  my $opts = {};
  my @cmdArgs;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--fetch-summary)$/){
      $cmd = $COMMAND_FETCH_SUMMARY;
    }elsif($arg =~ /^(-s|--summary)$/){
      $cmd = $COMMAND_SUMMARY;
    }elsif($arg =~ /^(-o|--orders)$/){
      $cmd = $COMMAND_ORDERS;
    }elsif($arg =~ /^(--fetch-orders)$/){
      $cmd = $COMMAND_FETCH_ORDERS;
    }elsif($arg =~ /^(--fetch-quotes)$/){
      $cmd = $COMMAND_FETCH_QUOTES;
    }elsif($arg =~ /^(--cleanup-quotes)$/){
      $cmd = $COMMAND_CLEANUP_QUOTES;
    }elsif($arg =~ /^(--color)$/){
      $$opts{useColor} = 1;
    }elsif($arg =~ /^(--no-color)$/){
      $$opts{useColor} = 0;
    }elsif($arg =~ /^(--color-term-only)$/){
      $$opts{useColor} = $IS_TERM;
    }else{
      push @cmdArgs, $arg;
    }
  }

  $$opts{quiet} = $cmd eq $COMMAND_FETCH_SUMMARY ? 1 : 0 if not defined $$opts{quiet};
  $$opts{useColor} = $IS_TERM if not defined $$opts{useColor};

  if($cmd eq $COMMAND_FETCH_SUMMARY){
    die "$USAGE\nERROR: unknown args @cmdArgs\n" if @cmdArgs > 0;
    cmdFetchQuotes($opts);
    cmdSummary($opts);
  }elsif($cmd eq $COMMAND_SUMMARY){
    die "$USAGE\nERROR: unknown args @cmdArgs\n" if @cmdArgs > 0;
    cmdSummary($opts);
  }elsif($cmd eq $COMMAND_ORDERS){
    die "$USAGE\nERROR: unknown args @cmdArgs\n" if @cmdArgs > 0;
    cmdOrders($opts);
  }elsif($cmd eq $COMMAND_FETCH_ORDERS){
    die "$USAGE\nERROR: missing BEARER_TOKEN_FULL_AUTH\n" if @cmdArgs == 0;
    die "$USAGE\nERROR: unknown args @cmdArgs\n" if @cmdArgs > 1;
    my ($bearerTokenFullAuth) = @cmdArgs;
    cmdFetchOrders($opts, $bearerTokenFullAuth);
  }elsif($cmd eq $COMMAND_FETCH_QUOTES){
    die "$USAGE\nERROR: unknown args @cmdArgs\n" if @cmdArgs > 0;
    cmdFetchQuotes($opts);
  }elsif($cmd eq $COMMAND_CLEANUP_QUOTES){
    die "$USAGE\nERROR: unknown args @cmdArgs\n" if @cmdArgs > 0;
    cmdCleanupQuotes($opts);
  }else{
    die $USAGE;
  }
}

sub cmdSummary($){
  my ($opts) = @_;
  my @orders = parseJsonOrders();
  my $summary = getInstrumentSummary(@orders);
  my $totalBuyUSD = 0;
  my $totalSellUSD = 0;
  my $totalValue = 0;
  for my $sym(sort keys %$summary){
    my $quote = readCachedQuote($sym);
    my $amount = $$summary{$sym}{netAmount};
    my $cost = $$summary{$sym}{netUSD};
    my $value = $amount * $quote;
    my $profit = $value - $cost;

    $totalBuyUSD += $$summary{$sym}{buyUSD};
    $totalSellUSD += $$summary{$sym}{sellUSD};
    $totalValue += $value;

    my $profitSign = $profit > 0 ? "+" : "-";
    my $absProfit = $profit >= 0 ? $profit : 0-$profit;
    my $colorProfit = $profitSign eq "+" ? "green" : "red";

    if($value == 0 and $amount == 0 and $cost == 0 and $profit == 0){
      #skip rows that are just zero
      next;
    }

    printf "%-5s | %s | %11.6f @ %s | %s%s\n",
      $sym,
      usdFmt($opts, 9, undef, $value),
      $amount,
      usdFmt($opts, 9, undef, $quote),
      $profitSign,
      usdFmt($opts, 9, $colorProfit, $absProfit),
    ;
  }
  my $totalProfit = $totalValue - $totalBuyUSD + $totalSellUSD;
  my $totalProfitSign = $totalProfit > 0 ? "+" : "-";
  my $totalAbsProfit = $totalProfit >= 0 ? $totalProfit : 0-$totalProfit;
  my $colorTotalProfit = $totalProfitSign eq "+" ? "green" : "red";

  printf "%-5s : %s -%s +%s     = %s%s\n",
    "TOTAL",
    usdFmt($opts, 9, undef, $totalValue),
    usdFmt($opts, 9, undef, $totalBuyUSD),
    usdFmt($opts, 9, undef, $totalSellUSD),
    $totalProfitSign,
    usdFmt($opts, 9, $colorTotalProfit, $totalAbsProfit),
  ;
}
sub cmdOrders($){
  my ($opts) = @_;
  my @orders = parseJsonOrders();
  for my $order(@orders){
    my $timeFmt = dateFmt $$order{time}, '%Y-%m-%d_%H:%M:%S';
    chomp $timeFmt;
    my ($symbol, $price, $amount, $buySell) = (
      $$order{symbol}, $$order{price}, $$order{amount}, $$order{buySell});
    my $cost = defined $price && defined $amount ? $price*$amount : undef;

    $price = 0 if not defined $price;
    $amount = 0 if not defined $amount;
    $cost = 0 if not defined $cost;

    my $plusMinus = $buySell eq "buy" ? "-" : "+";

    printf "%s | %-5s | %4s | %8.3f @ %8.2f\$ | %s %8.2f\$\n",
      $timeFmt, $symbol, $buySell, $amount, $price, $plusMinus, $cost;
  }
}
sub cmdFetchOrders($$){
  my ($opts, $bearerTokenFullAuth) = @_;
  $bearerTokenFullAuth =~ s/^\s*//;
  $bearerTokenFullAuth =~ s/\s*$//;
  $bearerTokenFullAuth =~ s/^\s*Authorization:\s*Bearer\s*//;
  $bearerTokenFullAuth =~ s/^\s*Bearer\s*//;

  if($bearerTokenFullAuth !~ /^[a-zA-Z0-9_\-\+\/\.]+$/){
    die "ERROR: malformed BEARER_TOKEN_FULL_AUTH\n";
  }

  my $nowMillis = nowMillis();
  my $nowFmt = dateFmt $nowMillis/1000, "%Y%m%d_%H%M%S";
  my $filePrefix = "${nowFmt}_${nowMillis}";

  runQuiet "mkdir", "-p", $JSON_ORDERS_CACHE_DIR;

  my $stocksJSON = apiCallCurl($API_ORDERS_STOCKS_URL, $bearerTokenFullAuth);
  if($stocksJSON =~ /^{"detail":".*"}$/i or $stocksJSON !~ /"results":/){
    die "\nERROR: invalid response fetching stocks orders:\n$stocksJSON\n";
  }


  my $cryptoJSON = apiCallCurl($API_ORDERS_CRYPTO_URL, $bearerTokenFullAuth);
  if($cryptoJSON =~ /^{"detail":".*"}$/i or $cryptoJSON !~ /"results":/){
    die "\nERROR: invalid response fetching crypto orders:\n$cryptoJSON\n";
  }

  writeFile "${JSON_ORDERS_CACHE_DIR}/${filePrefix}_stocks.json", "$stocksJSON\n";
  writeFile "${JSON_ORDERS_CACHE_DIR}/${filePrefix}_crypto.json", "$cryptoJSON\n";
}
sub cmdFetchQuotes($){
  my ($opts) = @_;
  my @orders = parseJsonOrders();

  my $nowMillis = nowMillis();
  my $nowFmt = dateFmt $nowMillis/1000, "%Y%m%d_%H%M%S";
  my $filePrefix = "${nowFmt}_${nowMillis}";

  print "fetching read-only bearer token\n" unless $$opts{quiet};
  my $bearerTokenReadOnly = getReadonlyBearerToken();

  my %seenSymbols;
  for my $order(@orders){
    next if defined $seenSymbols{$$order{symbol}};
    $seenSymbols{$$order{symbol}} = 1;
    fetchAndCacheQuote($$opts{quiet}, $filePrefix, $bearerTokenReadOnly,
      $$order{symbol}, $$order{instrumentId}, $$order{isCrypto});
  }

  if(not $$opts{quiet}){
    print "\n";
    for my $symbol(sort keys %seenSymbols){
      printf "%-5s = %s\n", $symbol, readCachedQuote($symbol);
    }
  }
}
sub cmdCleanupQuotes($){
  my ($opts) = @_;
  my @quoteFiles = sort glob "$JSON_QUOTES_CACHE_DIR/*.json";
  my $latestQuoteByInstrument = {};
  my $quotesByDate = {};
  my $maxDate;
  for my $quoteFile(@quoteFiles){
    if($quoteFile =~ /^(?:.*\/)?(\d\d\d\d\d\d\d\d)_(\d\d\d\d\d\d)_(\d+)_(\w+).json$/){
      my ($date, $time, $millis, $instrument) = ($1, $2, $3, $4);
      my $quote = {
        date       => $date,
        time       => $time,
        millis     => $millis,
        instrument => $instrument,
        file       => $quoteFile,
      };

      $maxDate = $date if not defined $maxDate or $date >= $maxDate;

      my $prevMillis = 0;
      if(defined $$latestQuoteByInstrument{$instrument}){
        $prevMillis = $$latestQuoteByInstrument{$instrument}{millis};
      }
      if($millis > $prevMillis){
        $$latestQuoteByInstrument{$instrument} = $quote;
      }
      $$quotesByDate{$date} = [] if not defined $$quotesByDate{$date};
      push @{$$quotesByDate{$date}}, $quote;
    }
  }

  for my $date(sort keys %$quotesByDate){
    next if $date eq $maxDate;
    my @archiveFiles;
    my @quotes = @{$$quotesByDate{$date}};
    my $oldestMillis = ${$quotes[0]}{millis};
    my $newestMtime = mtime ${$quotes[-1]}{file};
    for my $quote(@quotes){
      my $instrument = $$quote{instrument};
      my $latestQuote = $$latestQuoteByInstrument{$instrument};
      if($$latestQuote{file} eq $$quote{file}){
        next;
      }

      my $latestPrice = readCachedQuoteFile($instrument, $$latestQuote{file});
      if(defined $latestPrice){
        print "archiving: $$quote{file}\n";
        push @archiveFiles, $$quote{file};
      }else{
        print "NOT archiving $$quote{file}\n";
      }
    }

    my $tarFile = "$JSON_QUOTES_CACHE_DIR/${date}_${oldestMillis}_quotes.tar.xz";

    my @cmd = ("tar",
      "--remove-files",
      "--xform", "s:^.*/::", #flatten dirs
      "-cJf", $tarFile,
      @archiveFiles,
    );
    print "@cmd\n";
    system @cmd;
    system "touch", $tarFile, "--date=\@$newestMtime";
    print "\n";
  }
}

sub apiCallCurl($$){
  my ($url, $bearerToken) = @_;
  return runCurl($url, "-H", "Authorization: Bearer $bearerToken");
}
sub runCurl($@){
  my ($url, @curlArgs) = @_;

  my @cmd = ("curl",
    "--silent",
    @curlArgs,
    $url,
  );
  open CMD, "-|", @cmd or die "ERROR: curl failed\n$!\n";
  my $out = join '', <CMD>;
  close CMD;
  return $out;
}

sub getInstrumentSummary(@){
  my @orders = @_;
  my $summary = {};
  for my $order(@orders){
    my ($sym, $price, $amount) = ($$order{symbol}, $$order{price}, $$order{amount});
    if(not defined $$summary{$sym}){
      $$summary{$sym} = {
        buyUSD       => 0,
        buyAmount    => 0,
        sellUSD      => 0,
        sellAmount   => 0,
        netAmount    => 0,
        netUSD       => 0,
        symbol       => $sym,
        instrumentId => $$order{instrumentId},
      };
    }

    if(defined $amount and defined $price){
      if($$order{buySell} =~ /sell/i){
        $$summary{$sym}{sellUSD} += $price*$amount;
        $$summary{$sym}{sellAmount} += $amount;
        $$summary{$sym}{netUSD} -= $price*$amount;
        $$summary{$sym}{netAmount} -= $amount;
      }else{
        $$summary{$sym}{buyUSD} += $price*$amount;
        $$summary{$sym}{buyAmount} += $amount;
        $$summary{$sym}{netUSD} += $price*$amount;
        $$summary{$sym}{netAmount} += $amount;
      }
    }
  }
  return $summary;
}

sub readCachedQuote($){
  my ($symbol) = @_;
  my @quoteFiles = sort glob "$JSON_QUOTES_CACHE_DIR/*_$symbol.json";
  return undef if @quoteFiles == 0;
  my $quoteFile = $quoteFiles[-1];
  return readCachedQuoteFile($symbol, $quoteFile);
}
sub readCachedQuoteFile($$){
  my ($symbol, $quoteFile) = @_;
  my $json = readFile $quoteFile;
  my $obj = from_json $json;

  my @priceKeyOrder = qw(
    mark_price
    last_extended_hours_trade_price
    last_trade_price
    ask_price
    bid_price
    adjusted_previous_close
    previous_close
  );

  if(defined $$obj{symbol} and $$obj{symbol} =~ /^$symbol(USD)?$/){
    my $quote = undef;
    for my $key(@priceKeyOrder){
      my $price = $$obj{$key};
      if(defined $price){
        $quote = $price;
        last;
      }
    }

    return $quote;
  }elsif(defined $$obj{inactive_instruments}){
    return 0;
  }else{
    die "ERROR: malformed quote $quoteFile\n";
  }
}

sub fetchAndCacheQuote($$$$$$){
  my ($quiet, $filePrefix, $bearerTokenReadOnly, $symbol, $instrumentId, $isCrypto) = @_;
  my $url;
  if($isCrypto){
    $url = "$API_QUOTES_CRYPTO_URL/$instrumentId/";
  }else{
    $url = "$API_QUOTES_STOCKS_URL/$symbol/";
  }

  runQuiet "mkdir", "-p", $JSON_QUOTES_CACHE_DIR;
  print "fetching quote for $symbol\n" unless $quiet;
  writeFile "${JSON_QUOTES_CACHE_DIR}/${filePrefix}_$symbol.json",
    apiCallCurl($url, $bearerTokenReadOnly) . "\n";
}

sub getReadonlyBearerToken(){
  my $bearerTokenReadOnly = undef;
  if(-e $BEARER_TOKEN_READONLY_CACHE){
    $bearerTokenReadOnly = readFile $BEARER_TOKEN_READONLY_CACHE;
    chomp $bearerTokenReadOnly;
    if(testBearerToken($bearerTokenReadOnly)){
      return $bearerTokenReadOnly;
    }
  }
  $bearerTokenReadOnly = fetchReadonlyBearerToken();
  writeFile $BEARER_TOKEN_READONLY_CACHE, "$bearerTokenReadOnly\n";
  return $bearerTokenReadOnly;
}

sub fetchReadonlyBearerToken(){
  my $cmd = "curl -L --silent $READONLY_BEARER_TOKEN_FETCH_URL";
  my $attempts = 5;
  while($attempts > 0){
    my $html = `$cmd`;
    if($html =~ /"accessToken":"([^"]*)"/){
      return $1;
    }
    if($html =~ /"access_token":"([^"]*)"/){
      return $1;
    }
    $attempts--;
  }
  die "ERROR: failed to fetch read-only bearer token with $cmd\n";
}

sub testBearerToken($){
  my ($bearerToken) = @_;
  my $out = apiCallCurl($TEST_BEARER_TOKEN_URL, $bearerToken);
  if($out =~ /ask_price/){
    return 1;
  }else{
    return 0;
  }
}

sub parseJsonOrders(){
  my $instrumentCache = readInstrumentCache();
  my $updateInstrumentCache = 0;
  my @orders;
  my %seenOrderIds;
  my @ordersFiles = sort glob "$JSON_ORDERS_CACHE_DIR/*";
  for my $file(reverse @ordersFiles){
    my $json = readFile $file;
    my $obj = from_json $json;
    my @results = @{$$obj{results}};
    for my $res(@results){
      my $orderId = $$res{id};
      next if defined $seenOrderIds{$orderId};
      $seenOrderIds{$orderId} = 1;

      my $isCrypto = defined $$res{currency_pair_id};

      my $instId = $isCrypto ? $$res{currency_pair_id} : $$res{instrument_id};
      my $price = $$res{average_price};
      my $amount = $$res{cumulative_quantity};
      my $createdAt = $$res{created_at};
      my $buySell = $$res{side};

      my $time = `date --date="$createdAt" +%s.%N`;
      chomp $time;

      if(defined $instId and not defined $$instrumentCache{$instId}){
        my $sym = $isCrypto ? getCryptoCode($instId) : getInstrumentSymbol($instId);
        $$instrumentCache{$instId} = $sym;
        $updateInstrumentCache = 1;
      }

      my $symbol = $$instrumentCache{$instId} if defined $instId;

      my $order = {
        symbol       => $symbol,
        instrumentId => $instId,
        isCrypto     => $isCrypto,
        price        => $price,
        amount       => $amount,
        time         => $time,
        buySell      => $buySell,
      };
      push @orders, $order;
    }
  }

  if(-e $EXTRA_ORDERS_CACHE){
    my @extraOrderLines = split /\n/, readFile $EXTRA_ORDERS_CACHE;
    for my $line(@extraOrderLines){
      chomp $line;
      $line =~ s/#.*//;
      next if $line =~ /^\s*$/;

      if($line !~ /
        ^
        \s*       ([a-z]+)
        \s*,\s*   ([0-9a-f]{8}-[0-9a-f]{4}-[0-9a-f]{4}-[0-9a-f]{4}-[0-9a-f]{12})
        \s*,\s*   (true|false)
        \s*,\s*   \$(\d+\.\d+)
        \s*,\s*   (\d+\.\d+)
        \s*,\s*   (\d\d\d\d-\d\d-\d\d_\d\d:\d\d:\d\d)
        \s*,\s*   (buy|sell)
        $
      /xi){
        die "ERROR: malformed line in $EXTRA_ORDERS_CACHE:\n$line\n";
      }
      my ($symbol, $instId, $isCrypto, $price, $amount, $time, $buySell) =
        ($1, $2, $3, $4, $5, $6, $7);

      $isCrypto = $isCrypto =~ /true/i ? 1 : 0;

      $buySell = lc $buySell;

      $time =~ s/_/T/;
      $time = `date --date="$time" +%s.%N`;
      chomp $time;

      my $order = {
        symbol       => $symbol,
        instrumentId => $instId,
        isCrypto     => $isCrypto,
        price        => $price,
        amount       => $amount,
        time         => $time,
        buySell      => $buySell,
      };
      push @orders, $order;
    }
  }

  if($updateInstrumentCache){
    writeInstrumentCache $instrumentCache;
  }

  @orders = sort { $$a{time} <=> $$b{time} } @orders;

  return @orders;
}

sub readInstrumentCache(){
  return {} if not -f $INSTRUMENT_CACHE;

  open FH, "< $INSTRUMENT_CACHE" or die "ERROR: could not read $INSTRUMENT_CACHE\n$!\n";
  my @lines = <FH>;
  close FH;
  my $instruments = { map {$_ =~ /^(\w+) (.+)$/; ($2, $1)} @lines };
  return $instruments;
}
sub writeInstrumentCache($){
  my ($instruments) = @_;
  open FH, "> $INSTRUMENT_CACHE" or die "ERROR: could not write $INSTRUMENT_CACHE\n$!\n";
  my @idOrder = sort {$$instruments{$a} cmp $$instruments{$b}} keys %$instruments;
  for my $id(@idOrder){
    my $sym = $$instruments{$id};
    print FH "$sym $id\n";
  }
  close FH;
}

sub getInstrumentSymbol($){
  my ($instrumentId) = @_;
  my $url = "$API_INSTRUMENT_URL_PREFIX/$instrumentId/";
  my $json = readUrl($url);
  my $obj = from_json($json);
  my $sym = $$obj{symbol};
  die "ERROR: invalid instrument symbol $sym\n" if $sym !~ /^\w+$/;
  return $sym;
}
sub getCryptoCode($){
  my ($currencyPairId) = @_;
  my $url = "$API_CURRENCY_PAIRS_URL_PREFIX/$currencyPairId/";
  my $json = readUrl($url);
  my $obj = from_json($json);
  my $code = $$obj{asset_currency}{code};
  die "ERROR: invalid crypto code $code\n" if $code !~ /^\w+$/;
  return $code;
}

sub readUrl($){
  my ($url) = @_;
  return `curl --silent -L "$url"`;
}

sub readFile($){
  my ($file) = @_;
  open FH, "< $file" or die "ERROR: could not read $file\n$!\n";
  my $content = join '', <FH>;
  close FH;
  return $content;
}

sub writeFile($$){
  my ($file, $content) = @_;
  open FH, "> $file" or die "ERROR: could not write $file\n$!\n";
  print FH $content;
  close FH;
}

sub maybeColorText($$$){
  my ($useColor, $text, $color) = @_;
  if($useColor and defined $color){
    return colorText($text, $color);
  }else{
    return $text;
  }
}
sub colorText($$){
  my ($text, $color) = @_;
  require Term::ANSIColor;
  return Term::ANSIColor::colored($text, $color);
}

sub usdFmt($$$$){
  my ($opts, $len, $color, $usd) = @_;
  my $sign = $usd >= 0 ? "" : "-";
  $usd = $usd*-1 if $usd < 0;
  my $fmt = sprintf "\$%.2f", ceil($usd * 100.0) / 100.0;
  $fmt = "$sign$fmt";
  $fmt = sprintf "%${len}s", $fmt;
  return maybeColorText($$opts{useColor}, $fmt, $color);
}

sub dateFmt($$){
  my ($epoch, $fmt) = @_;
  my $dateFmt = `date --date=\@$epoch +'$fmt'`;
  chomp $dateFmt;
  return $dateFmt;
}

sub nowMillis(){
  return int(time*1000.0 + 0.5)
}

sub mtime($){
  my ($file) = @_;
  my @stat = stat $file;
  return $stat[9];
}

sub run(@){
  print "@_\n";
  runQuiet(@_);
}

sub runQuiet(@){
  system @_;
}

&main(@ARGV);
