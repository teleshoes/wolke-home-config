#!/usr/bin/perl
use strict;
use warnings;
use Digest::SHA qw(hmac_sha256_hex);
use File::Basename qw(basename);
use JSON qw(from_json);
use POSIX qw(ceil);

my $EXEC = basename $0;
my $API_KEY_NAME = "coinbase_readonly";
my $SECRETS_FILE = "$ENV{HOME}/.secrets";
my $API_HOST = "api.coinbase.com";
my $API_VERSION = "2017-12-12";
my $CACHE_DIR = "$ENV{HOME}/.cache/coinbase";
my $CACHE_FILE_BTC_BALANCE= "$CACHE_DIR/btc-balance";
my $CACHE_FILE_BTC_TRANSACTIONS_JSON = "$CACHE_DIR/transactions.json";
my $URL_EXCHANGE_RATE = "https://$API_HOST/v2/exchange-rates?currency=BTC";

sub fetchBTCToUSDExchangeRate();
sub fetchBTCBalance($$);
sub fetchBTCAccountID($$);
sub callCoinbaseAPI($$$);
sub createJWT($$$);
sub readBTCBalanceCache();
sub writeBTCBalanceCache($);
sub readBTCTransactionsJSONCache();
sub writeBTCTransactionsJSONCache($);
sub readCoinbaseKeyIDAndSecret();
sub readSecrets();
sub decrypt($);

my $usage = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS]
  $EXEC [OPTS] -b
  $EXEC [OPTS] --balance
    -fetch BTC=>USD conversion rate using API
    -fetch BTC balance if not cached using API
    -print balance in USD and BTC

  $EXEC [OPTS] -t
  $EXEC [OPTS] --transactions
    -fetch all BTC transactions if not cached using API
    -format and print all transactions, one per line

  OPTS
    --fetch-default
      fetch BTC balance/transactions only if there is no cached value
      (this is the default)
    -f | --fetch-always | --fetch
      always fetch BTC balance/transactions, never use cache
    --fetch-never
      never fetch BTC balance/transactions, always use cache
";

my $COMMAND_BALANCE = "balance";
my $COMMAND_TRANSACTIONS = "transactions";

my $FETCH_MODE_DEFAULT = "default";
my $FETCH_MODE_ALWAYS = "always";
my $FETCH_MODE_NEVER = "never";

sub main(@){
  my $command = $COMMAND_BALANCE;
  my $fetchMode = $FETCH_MODE_DEFAULT;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^(-b|--balance)$/){
      $command = $COMMAND_BALANCE;
    }elsif($arg =~ /^(-t|--transactions)$/){
      $command = $COMMAND_TRANSACTIONS;
    }elsif($arg =~ /^(--fetch-default)$/){
      $fetchMode = $FETCH_MODE_DEFAULT;
    }elsif($arg =~ /^(-f|--fetch-always|--fetch)$/){
      $fetchMode = $FETCH_MODE_ALWAYS;
    }elsif($arg =~ /^(--fetch-never)$/){
      $fetchMode = $FETCH_MODE_NEVER;
    }else{
      die "$usage\nERROR: unknown arg $arg\n";
    }
  }

  if($command eq $COMMAND_BALANCE){
    my $btcBalance = undef;
    my $cachedBTCBalance = readBTCBalanceCache();

    my $fetch = 0;
    $fetch = 1 if $fetchMode eq $FETCH_MODE_DEFAULT and not defined $cachedBTCBalance;
    $fetch = 1 if $fetchMode eq $FETCH_MODE_ALWAYS;
    $fetch = 0 if $fetchMode eq $FETCH_MODE_NEVER;

    if($fetch){
      my ($keyID, $keySecret) = readCoinbaseKeyIDAndSecret();
      $btcBalance = fetchBTCBalance($keyID, $keySecret);
      writeBTCBalanceCache($btcBalance);
    }else{
      $btcBalance = $cachedBTCBalance;
    }

    die "ERROR: unable to get BTC balance\n" if not defined $btcBalance;

    my $exRate = fetchBTCToUSDExchangeRate();
    my $usdBalance = $btcBalance*$exRate;

    printf ""
      . "%.2fUSD  (%.8fBTC)\n"
      . "  (1 BTC = %.2f USD)\n",
      $usdBalance,
      $btcBalance,
      $exRate;
  }elsif($command eq $COMMAND_TRANSACTIONS){
    my $transactionsJSON = undef;
    my $cachedTransactionsJSON = readBTCTransactionsJSONCache();

    my $fetch = 0;
    $fetch = 1 if $fetchMode eq $FETCH_MODE_DEFAULT and not defined $cachedTransactionsJSON;
    $fetch = 1 if $fetchMode eq $FETCH_MODE_ALWAYS;
    $fetch = 0 if $fetchMode eq $FETCH_MODE_NEVER;

    if($fetch){
      my ($keyID, $keySecret) = readCoinbaseKeyIDAndSecret();
      $transactionsJSON = fetchBTCTransactions($keyID, $keySecret);
      writeBTCTransactionsJSONCache($transactionsJSON);
    }else{
      $transactionsJSON = $cachedTransactionsJSON
    }

    my @transactions = parseTransactionsJSON($transactionsJSON);
    for my $t(sort {$$b{date} <=> $$a{date}} @transactions){
      my $dateFmt = `date --date=\@$$t{date} +'%Y-%m-%d %H:%M:%S'`;
      chomp $dateFmt;
      my $desc = $$t{desc};
      $desc = "" if not defined $desc;
      printf "%s | %4s | %s | %s | @%-6d | %s\n",
        $dateFmt, $$t{type}, btcFmt(15, $$t{btc}), usdFmt(9, $$t{usd}), $$t{usd_per_btc}, $desc;
    }
  }else{
    die "ERROR: unknown command $command\n";
  }
}

sub fetchBTCToUSDExchangeRate(){
  my $out = `curl --silent $URL_EXCHANGE_RATE`;
  if($out =~ /"currency":"BTC","rates":\{[^}]*"USD":"(\d*\.\d*)"/){
    return $1;
  }else{
    die "ERROR: could not get exchange rate from $URL_EXCHANGE_RATE\n";
  }
}

sub fetchBTCBalance($$){
  my ($keyID, $keySecret) = @_;

  my $out = callCoinbaseAPI($keyID, $keySecret, "/v2/accounts");
  my $btcBalance;
  if($out =~ /"balance":\{"amount":"(\d*\.\d*)","currency":"BTC"}/){
    $btcBalance = $1;
  }else{
    die "ERROR: could not parse BTC balance from coinbase output:\n$out\n";
  }
  return $btcBalance;
}

sub fetchBTCAccountID($$){
  my ($keyID, $keySecret) = @_;
  my $out = callCoinbaseAPI($keyID, $keySecret, "/v2/accounts");
  my $uuidRegex = "[0-9a-f]{8}-[0-9a-f]{4}-[0-9a-f]{4}-[0-9a-f]{4}-[0-9a-f]{12}";
  if($out =~ /"id":"($uuidRegex)","name":"BTC Wallet"/){
    return $1;
  }else{
    die "ERROR: could not parse BTC account ID from coinbase output:\n$out\n";
  }
}

sub fetchBTCTransactions($$){
  my ($keyID, $keySecret) = @_;
  my $accId = fetchBTCAccountID($keyID, $keySecret);
  my $out = callCoinbaseAPI($keyID, $keySecret, "/v2/accounts/$accId/transactions");
  return $out;
}

sub callCoinbaseAPI($$$){
  my ($keyID, $keySecret, $path) = @_;
  my $jwt = createJWT($keyID, $keySecret, $path);

  my @cmd = ("curl",
    "https://$API_HOST$path",
    "--silent",
    "--header", "Authorization: Bearer $jwt",
  );
  open CMD, "-|", @cmd or die "error running curl\n";
  my $out = join '', <CMD>;
  close CMD;

  return $out;
}

sub createJWT($$$){
  my ($keyID, $keySecret, $path) = @_;
  my $uri = "GET $API_HOST$path";
  open my $cmdH, "-|", "jwt-gen", $keyID, $keySecret, $uri
    or die "ERROR: cmd `jwt-gen` failed\n$!\n";
  my $jwt = join '', <$cmdH>;
  close $cmdH;
  chomp $jwt;

  if($jwt =~ /^[a-zA-Z0-9\-_\.]+$/){
    return $jwt;
  }else{
    die "ERROR: could not obtain jwt with `jwt-gen`\n";
  }
}

sub parseTransactionsJSON($){
  my ($json) = @_;
  my $obj = from_json $json;
  my @jsonTransactions = @{$$obj{data}};
  my @transactions;
  for my $j(@jsonTransactions){
    my $date = `date --date="$$j{created_at}" +%s`;
    chomp $date;

    die "ERROR: currency is not BTC\n$json" if $$j{amount}{currency} ne "BTC";
    die "ERROR: native currency is not USD\n$json" if $$j{native_amount}{currency} ne "USD";
    die "ERROR: could not parse date\n$json" if $date !~ /^\d+$/;

    my $t = {
      type => $$j{type},
      btc  => $$j{amount}{amount},
      usd  => $$j{native_amount}{amount},
      date => $date,
      desc => $$j{description},
    };

    if($$t{type} =~ /^advanced_trade_fill$/){
      if($$t{btc} < 0){
        $$t{type} = "sell";
      }else{
        $$t{type} = "buy";
      }
      my $desc = $$t{desc};
      $desc = "" if not defined $desc;
      $desc = " $desc" if length $desc > 0;
      $desc = "adv-fill$desc";
      $$t{desc} = $desc;
    }

    my $usdAbs = $$t{usd} > 0 ? $$t{usd} : 0 - $$t{usd};
    my $btcAbs = $$t{btc} > 0 ? $$t{btc} : 0 - $$t{btc};

    #guess fee using coinbase's stated 1.49% bank xfer fee
    my $bankBtcFee = $btcAbs - ($btcAbs * (1 - 0.0149));

    my $btcFee;
    if($$t{type} =~ /^(buy)$/){
      $btcFee = $bankBtcFee
    }elsif($$t{type} =~ /^(sell)$/){
      $btcFee = 0 - $bankBtcFee;
    }elsif($$t{type} eq "send"){
      $btcFee = 0;
    }else{
      $btcFee = 0;
    }

    $$t{usd_per_btc} = $usdAbs / ($btcAbs + $btcFee);

    push @transactions, $t;
  }
  return @transactions;
}

sub usdFmt($$){
  my ($len, $usd) = @_;
  my $sign = $usd >= 0 ? "" : "-";
  $usd = $usd*-1 if $usd < 0;
  my $fmt = sprintf "\$%.2f", ceil($usd * 100.0) / 100.0;
  $fmt = "$sign$fmt";
  $fmt = sprintf "%${len}s", $fmt;
  return $fmt;
}
sub btcFmt($$){
  my ($len, $btc) = @_;
  my $sign = $btc >= 0 ? "" : "-";
  $btc = $btc*-1 if $btc < 0;
  my $fmt = sprintf "%.8f BTC", $btc;
  $fmt = "$sign$fmt";
  $fmt = sprintf "%${len}s", $fmt;
  return $fmt;
}

sub readBTCBalanceCache(){
  if(not -f $CACHE_FILE_BTC_BALANCE){
    return undef;
  }else{
    my $btcBalance = `cat $CACHE_FILE_BTC_BALANCE`;
    chomp $btcBalance;
    if($btcBalance =~ /^(\d+|\d*\.\d+)$/){
      return $btcBalance;
    }else{
      die "ERROR: malformed BTC balance cache: $btcBalance\n";
    }
  }
}
sub writeBTCBalanceCache($){
  my ($btcBalance) = @_;
  system "mkdir", "-p", $CACHE_DIR;

  if(defined $btcBalance){
    open FH, "> $CACHE_FILE_BTC_BALANCE"
      or die "ERROR: could not write $CACHE_FILE_BTC_BALANCE\n$!\n";
    print FH "$btcBalance\n";
    close FH;
  }else{
    system "rm", "-f", $CACHE_FILE_BTC_BALANCE;
  }
}

sub readBTCTransactionsJSONCache(){
  if(not -f $CACHE_FILE_BTC_TRANSACTIONS_JSON){
    return undef;
  }else{
    return `cat $CACHE_FILE_BTC_TRANSACTIONS_JSON`;
  }
}
sub writeBTCTransactionsJSONCache($){
  my ($transactionsJSON) = @_;
  system "mkdir", "-p", $CACHE_DIR;

  if(defined $transactionsJSON){
    open FH, "> $CACHE_FILE_BTC_TRANSACTIONS_JSON"
      or die "ERROR: could not write $CACHE_FILE_BTC_TRANSACTIONS_JSON\n$!\n";
    print FH $transactionsJSON;
    close FH;
  }else{
    system "rm", "-f", $CACHE_FILE_BTC_TRANSACTIONS_JSON;
  }
}

sub readCoinbaseKeyIDAndSecret(){
  my $conf = readSecrets();
  my ($keyID, $keySecret);
  $keyID = $$conf{"$API_KEY_NAME.key_id"};
  $keySecret = $$conf{"$API_KEY_NAME.key_secret"};

  if(not defined $keyID){
    die "could not find 'coinbase.api.$API_KEY_NAME.key_id\n";
  }
  if(not defined $keySecret){
    die "could not find 'coinbase.api.$API_KEY_NAME.key_secret\n";
  }

  return ($keyID, $keySecret);
}

sub readSecrets(){
  my @lines = `cat $SECRETS_FILE 2>/dev/null`;
  my $cfg = {};
  for my $line(@lines){
    if($line =~ /^coinbase\.api\.(.+\.(?:key_id|key_secret))=(.+)$/){
      $$cfg{$1} = $2;
    }
  }
  for my $key(sort keys %$cfg){
    $$cfg{$key} = decrypt $$cfg{$key};
  }
  return $cfg;
}

sub decrypt($){
  my ($s) = @_;
  open GPG, "-|", "gpg-sym", $s or die "could not run gpg-sym\n";
  my @contents = <GPG>;
  close GPG;
  die "error running gpg-sym\n$!\n" if $? != 0;
  my $value = join '', @contents;
  chomp $value;
  return $value;
}

&main(@ARGV);
