#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub copyPubKeysForTarget($$$);
sub run(@);
sub bashComplete($$);

my $EXEC = basename $0;

my $MODE_RSYNC = "rsync";
my $MODE_SCP = "scp";

my $SUDO_ROOT = "sudo-root";

my @PUBKEY_TARGETS_ARR = (
  "main"          => ["ipmagic main",         undef,  undef,      $MODE_RSYNC],
  "aux"           => ["ipmagic aux",          undef,  undef,      $MODE_RSYNC],
  "aux2"          => ["ipmagic aux2",         undef,  undef,      $MODE_RSYNC],
  "bed"           => ["ipmagic bed",          undef,  undef,      $MODE_RSYNC],
  "tv"            => ["ipmagic tv",           undef,  undef,      $MODE_RSYNC],
  "tv-root"       => ["ipmagic tv",           "root", undef,      $MODE_RSYNC],
# "nuc"           => ["ipmagic nuc",          undef,  undef,      $MODE_RSYNC],
# "nuc-root"      => ["ipmagic nuc",          "root", undef,      $MODE_RSYNC],
  "ser"           => ["ipmagic ser",          undef,  undef,      $MODE_RSYNC],
  "ser-root"      => ["ipmagic ser",          "root", undef,      $MODE_RSYNC],
  "sb"            => ["seedbox --host",       undef,  undef,      $MODE_RSYNC],
  "ddwrt-68u"     => ["ddwrt 68u --ip",       "root", undef,      $MODE_SCP],
# "ddwrt-buffalo" => ["ddwrt buffalo --ip",   "root", undef,      $MODE_SCP],
  "sx"            => ["ipmagic sx",           undef,  undef,      $MODE_RSYNC],
  "sx-root"       => ["ipmagic sx",           "root", undef,      $MODE_RSYNC],
  "pi"            => ["ipmagic raspi",        undef,  undef,      $MODE_RSYNC],
  "pi-root"       => ["ipmagic raspi",        "root", undef,      $MODE_RSYNC],
);

my %PUBKEY_TARGETS_BY_NAME = @PUBKEY_TARGETS_ARR;
my @PUBKEY_TARGET_NAMES = map {$PUBKEY_TARGETS_ARR[$_]}
                          grep {$_ % 2 == 0} (0..$#PUBKEY_TARGETS_ARR);
my $OK_PUBKEY_TARGET_NAMES = join "|", @PUBKEY_TARGET_NAMES;

my $USAGE = "Usage:
  $EXEC [OPTS] [TARGET TARGET ..]
    copy pubkeys/authkeys to the hosts returned by the above commands

  $EXEC -h|--help
    print this message

  $EXEC --bash-complete COMP_LINE COMP_POINT
    print a list of words for bash completion, one per line
    COMP_LINE  - the full cmdline as a string
    COMP_POINT - the cursor position in the cmdline

  TARGET
    one of: $OK_PUBKEY_TARGET_NAMES
    copy for just the named targets (default is all)

  OPTS
    --scp | scp
      use `scp` to copy keys regardless of the default for the named TARGET

    --rsync | rsync
      use `rsync` to copy keys regardless of the default for the named TARGET
";

sub main(@){
  if(@_ == 3 and $_[0] =~ /^--bash-complete$/ and $_[2] =~ /^\d+$/){
    print map {"$_\n"} bashComplete($_[1], $_[2]);
    exit 0;
  }

  my @names;
  my $opts = {
    modeOverride => undef,
  };
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^($OK_PUBKEY_TARGET_NAMES)$/){
      push @names, $arg;
    }elsif($arg =~ /^(--scp|scp)$/){
      $$opts{modeOverride} = $MODE_SCP;
    }elsif($arg =~ /^(--rsync|rsync)$/){
      $$opts{modeOverride} = $MODE_RSYNC;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";;
    }
  }

  @names = @PUBKEY_TARGET_NAMES if @names == 0;

  my (@entryNamesSuccess, @entryNamesFailure);

  my $pubkeyFiles = [grep {-f $_} glob "$ENV{HOME}/.ssh/*.pub"];
  print "Copying these pubkeys:\n" . (join "", map {"  $_\n"} @$pubkeyFiles) . "\n\n";

  for my $pubkeyTargetName(@names){
    if(copyPubKeysForTarget($opts, $pubkeyTargetName, $pubkeyFiles)){
      push @entryNamesSuccess, $pubkeyTargetName;
    }else{
      push @entryNamesFailure, $pubkeyTargetName;
    }
  }

  print "\n\n\n"
    . (@entryNamesSuccess+0) . " successful entries\n"
    . (@entryNamesFailure+0) . " failed entries\n"
    ;
  print "\n";
  print "SUCCESS: @entryNamesSuccess\n" if @entryNamesSuccess > 0;
  print "FAILURE: @entryNamesFailure\n" if @entryNamesFailure > 0;
}

sub copyPubKeysForTarget($$$){
  my ($opts, $pubkeyTargetName, $pubkeyFiles) = @_;

  my $entry = $PUBKEY_TARGETS_BY_NAME{$pubkeyTargetName};
  if(not defined $entry){
    die "ERROR: unknown pubkey TARGET '$pubkeyTargetName'\n";
  }

  my ($ipCmd, $user, $isSudoRoot, $mode) = @$entry;
  my $userFmt = defined $user ? "USER=$user" : "default-user";
  $userFmt = "sudo as root for $userFmt" if $isSudoRoot;
  print "\n\n$pubkeyTargetName: copying pubkeys for $ipCmd ($userFmt)\n";

  my $hostname = `$ipCmd`;
  chomp $hostname;
  if($? != 0){
    print STDERR "WARNING: error running '$ipCmd'\n";
    return 0;
  }elsif($hostname !~ /\w/){
    print STDERR "WARNING: malformed hostname from '$ipCmd': $hostname\n";
    return 0;
  }

  $mode = $$opts{modeOverride} if defined $$opts{modeOverride};

  my $host = $hostname;
  $host = "$user\@$host" if defined $user;
  if($mode eq $MODE_RSYNC){
    run "rsync", "-avP", @$pubkeyFiles, "$host:~/.ssh/";
  }elsif($mode eq $MODE_SCP){
    run "scp", "-O", @$pubkeyFiles, "$host:~/.ssh/";
  }else{
    die "unknown mode: $mode\n";
  }

  if($isSudoRoot){
    run "ssh", $host, "sudo cp ~/.ssh/authorized_keys ~/.ssh/*.pub /root/.ssh/";
    run "ssh", $host, "sudo chmod 700 /root/.ssh; sudo chmod 600 /root/.ssh/authorized_keys";
  }else{
    run "ssh", $host, "cat ~/.ssh/*.pub > ~/.ssh/authorized_keys";
    run "ssh", $host, "chmod 700 ~/.ssh; chmod 600 ~/.ssh/authorized_keys";
  }

  return 1;
}

sub run(@){
  print "@_\n";
  system @_;
  die "Error running \"@_\"\n" if $? != 0;
}

sub bashComplete($$){
  my ($cmdLine, $pos) = @_;
  my $cmd = substr $cmdLine, 0, $pos;
  my $isNewWord = $cmd =~ /\s$/;
  $cmd =~ s/^\s+//;
  $cmd =~ s/\s+$//;

  my @words = split /\s+/, $cmd;
  shift @words;
  my $curWord = pop @words if not $isNewWord;

  my @opts = qw(
    -h --help
    --scp --rsync
    --bash-complete
  );
  my $optWord = $isNewWord ? $words[-1] : $curWord;

  if(defined $optWord and $optWord =~ /^-/){
    return @opts;
  }elsif(@words == 0){
    return sort @PUBKEY_TARGET_NAMES;
  }

  return ();
}

&main(@ARGV);
