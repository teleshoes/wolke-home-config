#!/usr/bin/perl
use strict;
use warnings;

sub run(@);
sub bashComplete($$);

my $MODE_RSYNC = "rsync";
my $MODE_SCP = "scp";

my @hostIpCmdEntries = (
  ["main"          => "ipmagic main",         undef,  $MODE_RSYNC],
  ["aux"           => "ipmagic aux",          undef,  $MODE_RSYNC],
  ["aux2"          => "ipmagic aux2",         undef,  $MODE_RSYNC],
  ["bed"           => "ipmagic bed",          undef,  $MODE_RSYNC],
  ["nuc"           => "ipmagic nuc",          undef,  $MODE_RSYNC],
  ["nuc-root"      => "ipmagic nuc",          "root", $MODE_RSYNC],
  ["sb"            => "seedbox --host",       undef,  $MODE_RSYNC],
  ["ddwrt-68u"     => "ddwrt 68u --ip",       "root", $MODE_SCP],
  ["ddwrt-buffalo" => "ddwrt buffalo --ip",   "root", $MODE_SCP],
  ["sx"            => "ipmagic sx",           "nemo", $MODE_RSYNC],
  ["sx-root"       => "ipmagic sx",           "root", $MODE_RSYNC],
  ["pi"            => "ipmagic raspi",        "pi",   $MODE_RSYNC],
  ["pi-root"       => "ipmagic raspi",        "root", $MODE_RSYNC],
);
my @allNames = map {${$_}[0]} @hostIpCmdEntries;
my $okNames = join "|", @allNames;

my $usage = "Usage:
  $0 [TARGET [MODE]]
    copy pubkeys/authkeys to the hosts returned by the above commands

  $0 -h|--help
    print this message

  $0 --bash-complete COMP_LINE COMP_POINT
    print a list of words for bash completion, one per line
    COMP_LINE  - the full cmdline as a string
    COMP_POINT - the cursor position in the cmdline

  TARGET
    one of: $okNames
    copy for just the named command

  MODE
    one of: $MODE_RSYNC | $MODE_SCP
";

sub main(@){
  my $targetName = undef;
  my $modeOverride = undef;
  if(@_ == 3 and $_[0] =~ /^--bash-complete$/ and $_[2] =~ /^\d+$/){
    print map {"$_\n"} bashComplete($_[1], $_[2]);
    exit 0;
  }elsif(@_ == 1 and $_[0] =~ /^(-h|--help)$/){
    print $usage;
    exit 0;
  }elsif(@_ == 1 and $_[0] =~ /^($okNames)$/){
    $targetName = $_[0];
  }elsif(@_ == 2 and $_[0] =~ /^($okNames)$/ and $_[1] =~ /^($MODE_RSYNC|$MODE_SCP)$/){
    $targetName = $_[0];
    $modeOverride = $_[1];
  }elsif(@_ > 0){
    die $usage;
  }

  my (@entryNamesSuccess, @entryNamesFailure);

  my @pubkeyFiles = `ls $ENV{HOME}/.ssh/*.pub`;
  chomp foreach @pubkeyFiles;
  print "Copying these pubkeys:\n" . (join "\n", @pubkeyFiles) . "\n\n";
  for my $entry(@hostIpCmdEntries){
    my ($name, $ipCmd, $user, $mode) = @$entry;
    next if defined $targetName and $name ne $targetName;
    print "\n\n$name: copying pubkeys for $ipCmd" . (defined $user ? " ($user)" : "") . "\n";
    my $hostname = `$ipCmd`;
    chomp $hostname;
    if($? != 0){
      print STDERR "WARNING: error running '$ipCmd'\n";
      push @entryNamesFailure, $name;
      next;
    }elsif($hostname !~ /\w/){
      print STDERR "WARNING: malformed hostname from '$ipCmd': $hostname\n";
      push @entryNamesFailure, $name;
      next;
    }

    $mode = $modeOverride if defined $modeOverride;

    my $host = $hostname;
    $host = "$user\@$host" if defined $user;
    if($mode eq $MODE_RSYNC){
      run "rsync", "-avP", @pubkeyFiles, "$host:~/.ssh/";
    }elsif($mode eq $MODE_SCP){
      run "scp", @pubkeyFiles, "$host:~/.ssh/";
    }else{
      die "unknown mode: $mode\n";
    }
    run "ssh", $host, "cat ~/.ssh/*.pub > ~/.ssh/authorized_keys";
    run "ssh", $host, "chmod 700 ~/.ssh; chmod 600 ~/.ssh/authorized_keys";

    push @entryNamesSuccess, $name;
  }

  print "\n\n\n"
    . (@entryNamesSuccess+0) . " successful entries\n"
    . (@entryNamesFailure+0) . " failed entries\n"
    ;
  print "\n";
  print "SUCCESS: @entryNamesSuccess\n" if @entryNamesSuccess > 0;
  print "FAILURE: @entryNamesFailure\n" if @entryNamesFailure > 0;
}

sub run(@){
  print "@_\n";
  system @_;
  die "Error running \"@_\"\n" if $? != 0;
}

sub bashComplete($$){
  my ($cmdLine, $pos) = @_;
  my $cmd = substr $cmdLine, 0, $pos;
  my $isNewWord = $cmd =~ /\s$/;
  $cmd =~ s/^\s+//;
  $cmd =~ s/\s+$//;

  my @words = split /\s+/, $cmd;
  shift @words;
  my $curWord = pop @words if not $isNewWord;

  my @opts = qw(
    -h --help
    --bash-complete
  );
  my $optWord = $isNewWord ? $words[-1] : $curWord;

  if(defined $optWord and $optWord =~ /^-/){
    return @opts;
  }elsif(@words == 0){
    return sort @allNames;
  }

  return ();
}
&main(@ARGV);
