#!/usr/bin/perl
use strict;
use warnings;

sub run(@);

my $MODE_RSYNC = "rsync";
my $MODE_SCP = "scp";

my @hostIpCmdEntries = (
  ["ipmagic t430s",        undef,  $MODE_RSYNC],
  ["ipmagic w520",         undef,  $MODE_RSYNC],
  ["ipmagic nuc",          undef,  $MODE_RSYNC],
  ["ipmagic nuc",          "root", $MODE_RSYNC],
  ["seedbox --host",       undef,  $MODE_RSYNC],
  ["ddwrt 68u --ip",       "root", $MODE_SCP],
  ["ddwrt buffalo --ip",   "root", $MODE_SCP],
  ["ipmagic sx",           "nemo", $MODE_RSYNC],
  ["ipmagic sx",           "root", $MODE_RSYNC],
  ["ipmagic raspi",        "pi",   $MODE_RSYNC],
  ["ipmagic raspi",        "root", $MODE_RSYNC],
);

sub main(@){
  die "Usage: $0\n" if @_ > 0;
  my @pubkeyFiles = `ls $ENV{HOME}/.ssh/*.pub`;
  chomp foreach @pubkeyFiles;
  print "Copying these pubkeys:\n" . (join "\n", @pubkeyFiles) . "\n\n";
  for my $entry(@hostIpCmdEntries){
    my ($ipCmd, $user, $mode) = @$entry;
    print "\n\ncopying pubkeys for $ipCmd" . (defined $user ? " ($user)" : "") . "\n";
    my $hostname = `$ipCmd`;
    chomp $hostname;
    die "Error running '$ipCmd'\n" if $? != 0;
    die "Malformed hostname from '$ipCmd': $hostname\n" if $hostname !~ /\w/;

    my $host = $hostname;
    $host = "$user\@$host" if defined $user;
    if($mode eq $MODE_RSYNC){
      run "rsync", "-avP", @pubkeyFiles, "$host:~/.ssh/";
    }elsif($mode eq $MODE_SCP){
      run "scp", @pubkeyFiles, "$host:~/.ssh/";
    }else{
      die "unknown mode: $mode\n";
    }
    run "ssh", $host, "cat ~/.ssh/*.pub > ~/.ssh/authorized_keys";
    run "ssh", $host, "chmod 700 ~/.ssh; chmod 600 ~/.ssh/authorized_keys";
  }
}

sub run(@){
  print "@_\n";
  system @_;
  die "Error running \"@_\"\n" if $? != 0;
}

&main(@ARGV);
