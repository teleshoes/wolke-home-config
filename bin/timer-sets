#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use Time::HiRes qw(sleep time);

my $DEFAULT_DUR_MILLIS = 30000;
my $SET_SEP_MILLIS = 3000;
my $GROUP_SEP_MILLIS = 10000;

my $DEFAULT_GROUP_SET_COUNT = 3;

my $CMD_SET_BEGIN = "alarm -s success";
my $CMD_SET_SEP = "alarm -s failure";
my $CMD_GROUP_COMPLETE = "alarm -s ff7-menuselect";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC [OPTS] [DURATION_SECONDS] [GROUP_SET_COUNT]
    -set CURRENT_SET to 0
    -run this forever:
      -increment CURRENT_SET
      -print CURRENT_SET to terminal and notify/notify-send
      -run $CMD_SET_SEP
      -sleep " . $SET_SEP_MILLIS/1000.0 . "s
      -run $CMD_SET_BEGIN
      -sleep DURATION_SECONDS (default is " . $DEFAULT_DUR_MILLIS/1000.0 . "s)
        -sleep in 1s chunks, printing time remaining
      -if CURRENT_SET = GROUP_SET_COUNT (default is $DEFAULT_GROUP_SET_COUNT)
        -set CURRENT_SET to 0
        -run $CMD_GROUP_COMPLETE
        -sleep " . $GROUP_SEP_MILLIS/1000.0 . "s
";

sub execExists($);
sub nowMillis();

sub main(@){
  my $durMillis = $DEFAULT_DUR_MILLIS;
  my $groupSetCount = $DEFAULT_GROUP_SET_COUNT;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(\d+)$/){
      $durMillis = $1 * 1000;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  $| = 1;
  my $count = 0;
  while(1){
    $count++;

    system "alarm -s failure";
    print "SET: $count\n";
    if(execExists("notify")){
      system "notify", "SET: $count\n";
    }elsif(execExists("notify-send")){
      system "notify-send", "-t", 1000, "SET: $count";
    }

    sleep $SET_SEP_MILLIS/1000.0;
    system "alarm -s success";

    my $startMillis = nowMillis();
    my $endMillis = $startMillis + $durMillis;
    while(1){
      my $remMillis = $endMillis - nowMillis();
      last if $remMillis <= 0;

      my $sleepMillis = $remMillis > 1000 ? 1000 : $remMillis;
      sleep $sleepMillis/1000.0;

      my $elapsedMillis = nowMillis() - $startMillis;
      my $elapsedS = int($elapsedMillis/1000.0 + 0.5);
      print " $elapsedS";
    }
    print "\n";

    if($count >= $groupSetCount){
      $count = 0;
      system $CMD_GROUP_COMPLETE;
      sleep $GROUP_SEP_MILLIS/1000.0;
    }
  }
}

sub execExists($){
  my ($execName) = @_;
  system "which \"$execName\" >/dev/null 2>/dev/null";
  if($? == 0){
    return 1;
  }
  return 0;
}

sub nowMillis(){
  return int(time() * 1000.0 + 0.5);
}

&main(@ARGV);
