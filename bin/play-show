#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use Term::ReadLine;

sub play($$$$);
sub getShowDirs();
sub getFilesForShowName($);
sub bashComplete($$);

my @MUSIC_BASE_DIRS = (
  "$ENV{HOME}/Music/sheet_music/shows/music",
  "$ENV{HOME}/Music/sheet_music/trumpet",
);
my $PLAY_DIR = "$ENV{HOME}/play";
my $SHOW_FILE = "$ENV{HOME}/show";

my @EXTS = qw(
  flac ogg opus wav mp3 m4a wma
  mkv avi m2ts mts mov mpeg mpg mp4 webm
);
my $OK_EXTS = join "|", @EXTS;

my @DEFAULT_MPV_ARGS = qw(--no-video --af=@rb:rubberband);

my $REGEX_SHOW_NAME = '[a-zA-Z]\w*';
my $REGEX_SONG_NUM = '\d+[a-z]?';
my $REGEX_REAL_NUM = "(?:\\d+|\\d*\\.\\d+)";
my $REGEX_TIME_HMS_MS_S = "(\\d+:)?(\\d+:)?$REGEX_REAL_NUM";
my $REGEX_POS_ARG = '(?:'
  . "(?:[+-]?$REGEX_TIME_HMS_MS_S)"
  . "|"
  . "(?:$REGEX_REAL_NUM\%)"
  . ")"
;

my $MODE_PLAY = "play";
my $MODE_INTERACTIVE = "interactive";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC --bash-complete COMP_LINE COMP_POINT
    print a list of words for bash completion, one per line
    COMP_LINE  - the full cmdline as a string
    COMP_POINT - the cursor position in the cmdline

  $EXEC [OPTS]
    -collect a list of SONG_DIRS to check:
      -if --show=SHOW_NAME given, or SHOW_NAME defined in $SHOW_FILE:
        " . join("\n        ", map {"-include: $_/<SHOW_NAME>"} @MUSIC_BASE_DIRS) . "
      -include: $PLAY_DIR/
      -include: \$PWD/
    -for each SONG_DIR in SONG_DIRS:
      -collect SONG_FILES in SONG_DIR/FILENAME such that:
        -FILENAME ends with '.' followed by one of the allowed EXTS
        -if --song=SONG_NUM is given:
          -FILENAME starts with any number of leading 0s followed by SONG_NUM followed by '_'
      -i.e.: <SONG_DIR>/00000<SONG_NUM>_*.EXTS
    -play song `mpv <MPV_ARGS> <SONG_FILES>`

  $EXEC [OPTS] [SHOW_NAME] [SONG_NUM [START_POS [END_POS]]]
    same as: $EXEC OPTS --show=SHOW_NAME --song=SONG_NAME --start=START_POS --end=END_POS

  EXTS
    one of: @EXTS

  MPV_ARGS
    default options to mpv:
      -default: '@DEFAULT_MPV_ARGS'
      -plus: '--start=START_POS' if START_POS given
      -plus: '---ab-loop-a=START_POS --ab-loop-b=END_POS' if START_POS + END_POS given

  POSITION_ARG
    one of:
      -a '+' sign followed by a <POSITION_ARG>
      -a '-' sign followed by a <POSITION_ARG>
      -<POSITION_HOUR_ARG>
      -<POSITION_MINUTE_ARG>
      -<POSITION_SECOND_ARG>
      -<POSITION_PERCENT_ARG>
    POSITION_HOUR_ARG
      -integer number of hours, followed by ':', followed by <POSITION_MINUTE_ARG>
    POSITION_MINUTE_ARG
      -integer number of minutes, followed by ':', followed by <POSITION_SECOND_ARG>
    POSITION_SECOND_ARG
      -fractional number of seconds
    POSITION_PERCENT_ARG
      -fractional percent, followed by a '%' sign

  OPTS
    -i | --interactive
      -repeatedly:
        -reset SONG_NUM, START_POS, and END_POS, but preserve SHOW_NAME
        -read a line of input
          -if line is q | quit | exit:
            -exit
          -if line matches: [SHOW_NAME] [SONG_NUM [START_POS [END_POS]]]
            -update SHOW_NAME, SONG_NUM, START_POS, and END_POS, if given
        -play with mpv as in `$EXEC --play`, unless line was malformed
    --play
      play mpv once and exit (this is the default)

    --show=SHOW_NAME | --show SHOW_NAME
      -override the value in $SHOW_FILE, look in sub dir SHOW_NAME of music dirs
      -SHOW_NAME must start with a letter, followed by any letters/numbers/underscores

    --song=SONG_NUM | --song SONG_NUM
    --num=SONG_NUM | --num SONG_NUM
      -if given, include only SONG_FILES that start with SONG_NUM
        -SONG_NUM is any number of digits, optionally followed by a single letter
        -leading zeros are ignored, in both SONG_NUM and SONG_FILES

    --start=START_POS | --start START_POS | -ss START_POS
      -run mpv starting at START_POS
        -must be a valid POSITION_ARG
        -if given:
          -append to MPV_ARGS '--start=START_POS'

    --end=END_POS | --end END_POS | -endpos END_POS
      -run mpv with ab-loop from START_POS to END_POS
        -must be a valid POSITION_ARG
        -if START_POS and END_POS are both given:
          -append to MPV_ARGS '--ab-loop-a=START_POS'
          -append to MPV_ARGS '--ab-loop-b=END_POS'
";

sub main(@){
  if(@_ == 3 and $_[0] =~ /^--bash-complete$/ and $_[2] =~ /^\d+$/){
    print map {"$_\n"} bashComplete($_[1], $_[2]);
    exit 0;
  }

  my $mode = $MODE_PLAY;
  my $showName = undef;
  my $songNum = undef;
  my $startPos = undef;
  my $endPos = undef;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(?:--play)$/){
      $mode = $MODE_PLAY;
    }elsif($arg =~ /^(?:-i|--interactive)$/){
      $mode = $MODE_INTERACTIVE;
    }elsif($arg =~ /^(?:--show)=($REGEX_SHOW_NAME)$/){
      $showName = $1;
    }elsif($arg =~ /^(?:--show)$/ and @_ > 0 and $_[0] =~ /^($REGEX_SHOW_NAME)$/){
      $showName = shift @_;
    }elsif($arg =~ /^(?:--song|--num)=($REGEX_SONG_NUM)$/){
      $songNum = $1;
    }elsif($arg =~ /^(?:--song|--num)$/ and @_ > 0 and $_[0] =~ /^($REGEX_SONG_NUM)$/){
      $songNum = shift @_;
    }elsif($arg =~ /^(?:--start)=($REGEX_POS_ARG)$/){
      $startPos = $1;
    }elsif($arg =~ /^(?:--start|-ss)$/ and @_ > 0 and $_[0] =~ /^($REGEX_POS_ARG)$/){
      $startPos = shift @_;
    }elsif($arg =~ /^(?:--end)=($REGEX_POS_ARG)$/){
      $endPos = $1;
    }elsif($arg =~ /^(?:--end|-endpos)$/ and @_ > 0 and $_[0] =~ /^($REGEX_POS_ARG)$/){
      $endPos = shift @_;
    }elsif($arg =~ /^($REGEX_SHOW_NAME)$/ and not defined $showName){
      $showName = $arg;
    }elsif($arg =~ /^($REGEX_SONG_NUM)$/ and not defined $songNum){
      $songNum = $arg;
    }elsif($arg =~ /^($REGEX_POS_ARG)$/ and not defined $startPos){
      $startPos = $arg;
    }elsif($arg =~ /^($REGEX_POS_ARG)$/ and not defined $endPos){
      $endPos = $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if(not defined $showName and -e $SHOW_FILE){
    $showName = `cat $SHOW_FILE 2>/dev/null`;
    chomp $showName;
    $showName =~ s/^\s*//;
    $showName =~ s/\s*$//;
    if($showName !~ /^($REGEX_SHOW_NAME)$/){
      die "$USAGE\nERROR: invalid SHOW_NAME in $SHOW_FILE: $showName\n";
    }
  }

  if($mode eq $MODE_PLAY){
    play($showName, $songNum, $startPos, $endPos);
  }elsif($mode eq $MODE_INTERACTIVE){
    my @showNames = getShowDirs();
    my %okShowNames = map {$_ => 1} @showNames;

    my $term = Term::ReadLine->new($EXEC);

    while(1){
      $songNum = undef;
      $startPos = undef;
      $endPos = undef;

      my @showNameFiles = getFilesForShowName($showName);
      @showNameFiles = map {basename $_} @showNameFiles;
      printf "\n%s\n", join("", map {"  $_\n"} @showNameFiles);

      printf "\nshow: %s\n", defined $showName ? $showName : "<none>";
      print "  switch show:             SHOW_NAME\n";
      print "  play song:               SONG_NUM [START_POS [END_POS]]\n";
      print "  switch show + play song: SHOW_NAME SONG_NUM [START_POS [END_POS]]\n";
      print "  quit:                    q | quit | exit\n";

      my $line = $term->readline("> ");
      if(not defined $line){
        exit 0;
      }

      chomp $line;
      $line =~ s/^\s*//;
      $line =~ s/\s*$//;

      if($line =~ /^(q|quit|exit)$/){
        exit 0;
      }elsif(defined $okShowNames{$line}){
        $showName = $line;
      }else{
        my @words = split /\s+/, $line;
        for my $word(@words){
          if(defined $okShowNames{$word}){
            $showName = $word;
          }elsif($word =~ /^($REGEX_SONG_NUM)$/ and not defined $songNum){
            $songNum = $word;
          }elsif($word =~ /^($REGEX_POS_ARG)$/ and not defined $startPos){
            $startPos = $word;
          }elsif($word =~ /^($REGEX_POS_ARG)$/ and not defined $endPos){
            $endPos = $word;
          }else{
            print "ERROR: unknown arg $word\n";
            $songNum = undef;
            $startPos = undef;
            $endPos = undef;
            last;
          }
        }
      }
      if(defined $songNum){
        play($showName, $songNum, $startPos, $endPos);
      }
    }
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub play($$$$){
  my ($showName, $songNum, $startPos, $endPos) = @_;
  if(defined $songNum){
    $songNum =~ s/^0+//;
  }

  my @showNameDirs;
  if(defined $showName){
    for my $baseDir(@MUSIC_BASE_DIRS){
      @showNameDirs = (@showNameDirs, grep {-d $_} glob "$baseDir/$showName/");
    }
    if(@showNameDirs == 0){
      die "$USAGE\nERROR: unknown SHOW_NAME $showName\n";
    }
  }

  my @dirs = (@showNameDirs, $PLAY_DIR, $ENV{PWD});
  @dirs = grep {defined $_ and -d $_} @dirs;

  my @songFiles;
  for my $dir(@dirs){
    my @files = grep {-f $_} glob "$dir/*";
    @files = grep {$_ =~ /\.($OK_EXTS)$/i} @files;
    if(defined $songNum){
      @files = grep {$_ =~ /^(.*\/)0*${songNum}_[^\/]*$/} @files;
    }
    @songFiles = (@songFiles, @files);
  }

  if(@songFiles == 0){
    my $errorMsg = "$USAGE\nERROR: no matching song";
    if(defined $songNum){
      $errorMsg .= " with SONG_NUM=$songNum";
    }
    $errorMsg .= " found in SONG_DIRS:";
    $errorMsg .= join("", map {"\n  $_"} @dirs);
    $errorMsg .= "\n";
    die $errorMsg;
  }

  my @mpvArgs = @DEFAULT_MPV_ARGS;
  if(defined $startPos){
    push @mpvArgs, "--start=$startPos";
  }
  if(defined $startPos and defined $endPos){
    push @mpvArgs, "--ab-loop-a=$startPos";
    push @mpvArgs, "--ab-loop-b=$endPos";
  }

  my @cmd = ("mpv", @mpvArgs, @songFiles);
  print "@cmd\n";
  system @cmd;
}

sub getShowDirs(){
  my @showDirs;
  for my $baseDir(@MUSIC_BASE_DIRS){
    my @dirs = `cd $baseDir/ && ls`;
    chomp foreach @dirs;
    @dirs = grep {-d "$baseDir/$_/"} @dirs;
    @showDirs = (@showDirs, @dirs);
  }
  return @showDirs;
}

sub getFilesForShowName($){
  my ($showName) = @_;
  my @files;
  if(defined $showName and $showName ne ""){
    for my $baseDir(@MUSIC_BASE_DIRS){
      @files = (@files, glob "$baseDir/$showName/*");
      @files = grep {-f $_} @files;
      @files = grep {$_ =~ /\.($OK_EXTS)$/i} @files;
    }
  }
  @files = sort @files;
  return @files;
}

sub bashComplete($$){
  my ($cmdLine, $pos) = @_;
  my $cmd = substr $cmdLine, 0, $pos;
  my $isNewWord = $cmd =~ /\s$/;
  $cmd =~ s/^\s+//;
  $cmd =~ s/\s+$//;

  my @words = split /\s+/, $cmd;
  shift @words;
  pop @words if not $isNewWord;

  my @showNames = getShowDirs();

  my %okShowNames = map {$_ => 1} @showNames;

  my $showFound = 0;
  for my $word(@words){
    if(defined $okShowNames{$word}){
      $showFound = 1;
      last;
    }
  }

  my @suggestions = (
    "-h", "--help",
    "--bash-complete",
    "--show",
    "--song", "--num",
    "--start", "-ss",
    "--end", "-endpos",
  );

  @suggestions = (@suggestions, @showNames) if not $showFound;

  return @suggestions;
}

&main(@ARGV);
