#!/usr/bin/perl
use strict;
use warnings;

sub run(@);

my @homeFiles = (
  ".secrets",
  "wifi.conf",
  "wifi-auto.conf",
  "wvdial.conf",

  ".config/taffybar/icons/",
  "install/root-files/usr/share/sounds/custom/",
  "Backgrounds/",
  "Music/sheet_music/",
);

my @ipmagicNames = qw(aux bed nuc);
my %okIpmagicNames = map {$_ => 1} @ipmagicNames;

my $usage = "Usage:
  $0 [OPTS]
    sync to each IPMAGIC_NAME of: @ipmagicNames

  $0 IPMAGIC_NAME
    sync to IPMAGIC_NAME

  OPTS
    -h | --help
      show this message
    --del
      delete: pass '--del' to rsync
    -n
      simulate: pass '-n' to rsync
";

sub main(@){
  my @rsyncArgs;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^(--del)$/){
      push @rsyncArgs, "--del";
    }elsif($arg =~ /^(-n)$/){
      push @rsyncArgs, "-n";
    }else{
      die "unknown arg: $arg\n";
    }
  }

  if(@_ == 1 and defined $okIpmagicNames{$_[0]}){
    @ipmagicNames = ($_[0]);
  }elsif(@_ > 0){
    die $usage;
  }

  for my $ipmagicName(@ipmagicNames){
    my $host = `ipmagic $ipmagicName`;
    chomp $host;

    for my $file(@homeFiles){
      run "rsync", "-avP", @rsyncArgs, "-s", "$ENV{HOME}/$file", "$host:$file";
    }
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
