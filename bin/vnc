#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(sleep);

sub startVNC($$$$$$$$$$);
sub getSSHControlPath($);
sub startSSHTunnel($$$@);
sub stopSSHTunnel($);
sub getDesiredLocalRes($);
sub getRemoteRes($);
sub runRemoteCmd($$@);
sub guessRemoteDisplay($);

my $DELAY_MILLIS = 300;

my $LOCALHOST = "127.0.0.1";
my $DEFAULT_VNC_PORT = 5900;
my $DEFAULT_REMOTE_DISPLAY = ":0";
my $SSH_CONTROL_PATH_DIR = "/tmp/vnc-ssh-ctl";

my %VNC_SERVER_ARGS = (
  x11vnc       => [qw(-safer -nopw -once -noncache -bg)],
);

my %VNC_CLIENT_ARGS = (
  xtigervncviewer => [qw(DotWhenNoCursor=1)],
  xtightvncviewer => [qw(-quality 0         -compresslevel 5)],
  x2vnc           => [qw(-hotkey a-F9       -edgewidth 0)],
);

my $DEFAULT_VNC_SERVER = "x11vnc";
my $DEFAULT_VNC_CLIENT = "xtigervncviewer";
my $OK_VNC_CLIENTS = join "|", sort keys %VNC_CLIENT_ARGS;

my $ROTATE_ARG_SYNS = {
  "0"   => [qw(0    +0    -0    none)],
  "90"  => [qw(90   +90   -270)],
  "180" => [qw(180  +180  -180  xy  yx)],
  "270" => [qw(270  +270  -90)],
  "x"   => [qw(x)],
  "y"   => [qw(y)],
  "90x" => [qw(90x)],
  "90y" => [qw(90y)],
};
my $ROTATE_ARG_BY_SYN = {
  map {
    my $rotArg = $_;
    map {$_ => $rotArg} @{$$ROTATE_ARG_SYNS{$rotArg}};
  } sort keys %$ROTATE_ARG_SYNS
};
my $OK_ROTATE_ARG_REGEX = join("|", map {quotemeta($_)} sort keys %$ROTATE_ARG_BY_SYN);
my $ROTATE_ARG_COUNTER_CLOCKWISE = {
  "0"   => "270",
  "90"  => "0",
  "180" => "90",
  "270" => "180",
  "x"   => "90y",
  "y"   => "90x",
  "90x" => "y",
  "90y" => "x",
};

my $SCALE_AUTO = "auto";

my $USAGE = "Usage:
  $0 [OPTS] HOST | IPMAGIC_NAME
    -if <IPMAGIC_NAME> is given, get <HOST> using `ipmagic IPMAGIC_NAME`
    -choose a <LOCAL_PORT> based on the number of existing SSH sessions
    -start vnc server on <HOST> with port=$DEFAULT_VNC_PORT
    -setup an ssh tunnel to bind $LOCALHOST:<LOCAL_PORT> to <HOST>:$DEFAULT_VNC_PORT
    -run vnc client to connect to $LOCALHOST:<LOCAL_PORT>

  $0 -h | --help
    show this message

  $0 off
    disconnect ssh tunnel

  OPTS
    --client=VNC_CLIENT   (default is $DEFAULT_VNC_CLIENT)
      one of: $OK_VNC_CLIENTS

    -u USER | --user=USER
      -use <USER> for all ssh/ipmagic commands

    --no-ssh-tunnel
      -do not select a <LOCAL_PORT>
      -do not create an SSH tunnel
      -connect directly to the remote vnc server on <HOST>:$DEFAULT_VNC_PORT

    --no-server
      -do not start vnc server, assume already running on <HOST>:$DEFAULT_VNC_PORT

    -r | --remote
      same as: --no-ssh-tunnel --no-server


    --geometry=<WIDTH>x<HEIGHT>
    --res=<WIDTH>x<HEIGHT>
      pass '-geometry <WIDTH>x<HEIGHT>' to vnc server
      e.g.: --geometry=1152x648

    -s SCALE_RATIO | --scale SCALE_RATIO | --scale=SCALE_RATIO
      set resolution of vnc server with -geometry to the remote resolution times SCALE_RATIO
       (preserving aspect ratio)
      e.g.: if remote is 1280x720
              --scale=0.9  =>  --geometry=1152x648

    --auto-scale
      calculate a SCALE_RATIO to fit the remote resolution within the local resolution
        (preserving aspect ratio)
      e.g.: if remote is 1080x1920 and local is 1920x1080
             --auto-scale  =>  --scale=0.5625  =>  --geometry=1080x607

    --auto-scale-bar
      same as: --auto-scale --local-height-scale=0.95

    --local-height-scale=LOCAL_HEIGHT_SCALE | --local-height-scale LOCAL_HEIGHT_SCALE
      -scale local screen height before calculating --scale-auto
      -this can be used to accommodate a system bar without having to scroll
        e.g.: if remote is 1920x1080 and local is 1280x720 and system bar is 72px
                --auto-scale --local-height-scale=0.9  =>  --scale=0.6  =>  --geometry=1152x648
      (default: 1.0)


    --rotate=ROTATION | -rotate=ROTATION
      pass -rotate arg to x11vnc
        ROTATION
          0   | +0   | -0   | none      do not pass any rotation arg  (default)
          90  | +90  | -270             pass '-rotate 90'   to x11vnc (rotate clockwise)
          180 | +180 | -180 | xy | yx   pass '-rotate 180'  to x11vnc (flip x-axis, flip y-axis)
          270 | +270 | -90              pass '-rotate 270'  to x11vnc (rot counter-clockwise)
          x                             pass '-rotate x'    to x11vnc (flip x-axis)
          y                             pass '-rotate y'    to x11vnc (flip y-axis)
          +90x | 90x                    pass '-rotate 90x'  to x11vnc (rotate CW, flip x-axis)
          +90y | 90y                    pass '-rotate 90y'  to x11vnc (rotate CW, fiip y-axis)
    --rotate-from-landscape
      if HOST resolution is portrait, add an additional left rotation to --rotate
        -get ROTATION from --rotate arg
        -fetch resolution <WIDTH>x<HEIGHT> from target HOST
         -if <HEIGHT> is greater than <WIDTH>:
            --rotate=0   => --rotate=270
            --rotate=90  => --rotate=0
            --rotate=180 => --rotate=90
            --rotate=270 => --rotate=180
            --rotate=x   => --rotate=90y
            --rotate=y   => --rotate=90x
            --rotate=90x => --rotate=y
            --rotate=90y => --rotate=x

    -l | --landscape        same as: --rotate-from-landscape --rotate=0
    --landscape-inverted    same as: --rotate-from-landscape --rotate=180
    -p | --portrait         same as: --rotate-from-landscape --rotate=90
    --portrait-inverted     same as: --rotate-from-landscape --rotate=270
";

my $CMD_START_VNC = "start";
my $CMD_SSH_OFF = "off";

sub main(@){
  my $cmd = $CMD_START_VNC;
  my $opts = {
    remoteInfo    => {
      ipmagicName => undef,
      host        => undef,
      user        => undef,
    },
    vncClient     => $DEFAULT_VNC_CLIENT,
    vncServer     => $DEFAULT_VNC_SERVER,
    geometry      => undef,
    scale         => undef,
    localHeightScale => 1.0,
    rotateArg     => 0,
    rotateFromLandscape => 0,
    isStartServer => 1,
    isSSHTunnel   => 1,
  };
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(off)$/){
      $cmd = $CMD_SSH_OFF;
    }elsif($arg =~ /^--client=($OK_VNC_CLIENTS)$/){
      $$opts{vncClient} = $1;
    }elsif($arg =~ /^(--no-server)$/){
      $$opts{isStartServer} = 0;
    }elsif($arg =~ /^(--no-ssh-tunnel)$/){
      $$opts{isSSHTunnel} = 0;
    }elsif($arg =~ /^(-r|--remote)$/){
      $$opts{isStartServer} = 0;
      $$opts{isSSHTunnel} = 0;
    }elsif($arg =~ /^--geometry=(\d+x\d+)$/){
      $$opts{geometry} = $1;
    }elsif($arg =~ /^--res=(\d+x\d+)$/){
      $$opts{geometry} = $1;
    }elsif($arg =~ /^--scale=(\d+|\d*\.\d+)$/){
      $$opts{scale} = $1;
    }elsif($arg =~ /^(-s|--scale)$/ and @_ > 0 and $_[0] =~ /^(\d+|\d*\.\d+)$/){
      $$opts{scale} = shift @_;
    }elsif($arg =~ /^(--auto-scale)$/){
      $$opts{scale} = $SCALE_AUTO;
    }elsif($arg =~ /^(--auto-scale-bar)$/){
      $$opts{scale} = $SCALE_AUTO;
      $$opts{localHeightScale} = 0.95;
    }elsif($arg =~ /^--local-height-scale=(\d+|\d*\.\d+)$/){
      $$opts{localHeightScale} = $1;
    }elsif($arg =~ /^(--local-height-scale)$/ and @_ > 0 and $_[0] =~ /^(\d+|\d*\.\d+)$/){
      $$opts{localHeightScale} = shift @_;
    }elsif($arg =~ /^--rotate=($OK_ROTATE_ARG_REGEX)$/){
      $$opts{rotateArg} = $1;
    }elsif($arg =~ /^(--rotate)$/ and @_ > 0 and $_[0] =~ /^($OK_ROTATE_ARG_REGEX)$/){
      $$opts{rotateArg} = shift @_;
    }elsif($arg =~ /^--rotate-from-landscape/){
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^(-l|--landscape)$/){
      $$opts{rotateArg} = 0;
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^(--landscape-inverted)$/){
      $$opts{rotateArg} = 180;
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^(-p|--portrait)$/){
      $$opts{rotateArg} = 90;
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^(--portrait-inverted)$/){
      $$opts{rotateArg} = 270;
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^--user=(.+)$/){
      $$opts{remoteInfo}{user} = $1;
    }elsif($arg =~ /^(-u)$/ and @_ > 0){
      $$opts{remoteInfo}{user} = shift @_;
    }elsif(not defined $$opts{remoteInfo}{host}){
      $$opts{remoteInfo}{host} = $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if(defined $$opts{remoteInfo}{host}){
    my %okIpmagicNames = map{$_ => 1} split /\s+/, `ipmagic --list`;
    if(defined $okIpmagicNames{$$opts{remoteInfo}{host}}){
      my $ipmagicName = $$opts{remoteInfo}{host};
      my $host = `ipmagic $ipmagicName --host`;
      chomp $host;
      if($host !~ /\w/){
        die "ERROR: could not get host for IPMAGIC_NAME=$ipmagicName\n";
      }
      $$opts{remoteInfo}{host} = $host;
      $$opts{remoteInfo}{ipmagicName} = $ipmagicName;
    }
  }

  if($cmd eq $CMD_START_VNC){
    die "$USAGE\nERROR: missing HOST\n" if not defined $$opts{remoteInfo}{host};
    startVNC($$opts{remoteInfo}, $$opts{vncClient}, $$opts{vncServer},
      $$opts{geometry}, $$opts{scale}, $$opts{localHeightScale},
      $$opts{rotateArg}, $$opts{rotateFromLandscape},
      $$opts{isStartServer}, $$opts{isSSHTunnel});
  }elsif($cmd eq $CMD_SSH_OFF){
    runSSHExit($$opts{host});
  }else{
    die "ERROR: unknown cmd $cmd\n";
  }
}

sub startVNC($$$$$$$$$$){
  my ($remoteInfo, $vncClient, $vncServer,
      $geometry, $scale, $localHeightScale,
      $rotateArg, $rotateFromLandscape,
      $isStartServer, $isSSHTunnel) = @_;

  my @serverArgs;

  my ($remoteW, $remoteH);
  if($rotateFromLandscape or defined $scale){
    #only fetch remote res when needed
    ($remoteW, $remoteH) = getRemoteRes($remoteInfo);
  }

  if($rotateFromLandscape){
    if($remoteH > $remoteW){
      $rotateArg = $$ROTATE_ARG_COUNTER_CLOCKWISE{$rotateArg};
    }
  }

  if(defined $rotateArg and $rotateArg ne "0"){
    @serverArgs = (@serverArgs, "-rotate", $rotateArg);
  }

  if(defined $scale and $scale eq $SCALE_AUTO){
    my ($desiredLocalW, $desiredLocalH) = getDesiredLocalRes($localHeightScale);

    my $scaleW = $desiredLocalW/$remoteW;
    my $scaleH = $desiredLocalH/$remoteH;

    #minimum of width scale and height scale
    $scale = $scaleW < $scaleH ? $scaleW : $scaleH;
  }

  if(defined $geometry){
    @serverArgs = (@serverArgs, "-geometry", $geometry);
  }elsif(defined $scale){
    @serverArgs = (@serverArgs, "-geometry", int($remoteW*$scale) . "x" . int($remoteH*$scale));
  }

  if($isSSHTunnel){
    push @serverArgs, "-localhost";
  }

  my @vncServerCmd = ($vncServer,
    '-display', guessRemoteDisplay($remoteInfo),
    @serverArgs,
    @{$VNC_SERVER_ARGS{$vncServer}},
  );

  my $sshCtlFileCount = 0 + grep {-f $_} glob("$SSH_CONTROL_PATH_DIR/*.ctl");

  if($isStartServer){
    runRemoteCmd($remoteInfo, {}, "pkill", $vncServer);
    runRemoteCmd($remoteInfo, {background=>1}, @vncServerCmd);
    sleep($DELAY_MILLIS/1000.0);
  }

  my $remotePort = $DEFAULT_VNC_PORT;

  my @vncClientCmd = ($vncClient, @{$VNC_CLIENT_ARGS{$vncClient}});
  if($isSSHTunnel){
    #5900 + 10*otherVNCSessions + random first digit
    my $localPort = $DEFAULT_VNC_PORT + 10*($sshCtlFileCount) + int(rand(10));

    stopSSHTunnel($remoteInfo);
    startSSHTunnel($remoteInfo, $localPort, $remotePort);

    push @vncClientCmd, sprintf("%s::%s", $LOCALHOST, $localPort);
  }else{
    push @vncClientCmd, sprintf("%s::%s", $$remoteInfo{host}, $remotePort);
  }

  system @vncClientCmd;

  if($isStartServer){
    runRemoteCmd($remoteInfo, {}, "pkill", $vncServer);
  }
  if($isSSHTunnel){
    stopSSHTunnel($remoteInfo);
  }
}

sub getSSHControlPath($){
  my ($remoteInfo) = @_;
  if(not -d $SSH_CONTROL_PATH_DIR){
    system "mkdir", "-p", $SSH_CONTROL_PATH_DIR;
  }
  if(not -d $SSH_CONTROL_PATH_DIR){
    die "ERROR: $SSH_CONTROL_PATH_DIR does not exist\n";
  }

  my $baseName = $$remoteInfo{host};
  $baseName =~ s/\W+/_/g;
  $baseName =~ s/^_+//;
  $baseName =~ s/_+$//;

  return "$SSH_CONTROL_PATH_DIR/$baseName.ctl";
}

sub startSSHTunnel($$$@){
  my ($remoteInfo, $localPort, $remotePort) = @_;
  my @remoteCmd = (); #ssh -N instead of a command
  runRemoteCmd($remoteInfo, {
    sshArgs => [
      "-o", ("ControlPath=" . getSSHControlPath($remoteInfo)),
      "-o", "ControlMaster=yes",
      "-f",
      "-N",
      "-n",
      "-L", "$localPort:$LOCALHOST:$remotePort",
    ],
  }, @remoteCmd);
}

sub stopSSHTunnel($){
  my ($remoteInfo) = @_;
  my @sshCmd = ("ssh",
    "-o", ("ControlPath=" . getSSHControlPath($remoteInfo)),
    "-O", "exit",
    $LOCALHOST,
  );
  system @sshCmd;
}

sub getDesiredLocalRes($){
  my ($localHeightScale) = @_;

  my $localRes = `res`;
  my ($localWidth, $localHeight);
  if($localRes =~ /^(\d+)x(\d+)$/){
    $localWidth = $1;
    $localHeight = $2;
  }
  if($localWidth !~ /^\d+$/ or $localHeight !~ /^\d+$/){
    die "ERROR: could not obtain local resolution\n";
  }

  return ($localWidth, $localHeight*$localHeightScale);
}

sub getRemoteRes($){
  my ($remoteInfo) = @_;
  my $remoteRes = runRemoteCmd($remoteInfo, {readProc=>1}, qw(res));
  if($remoteRes =~ /(\d+)x(\d+)/){
    return ($1, $2);
  }else{
    die "ERROR: could not get remote res from $$remoteInfo{host}\n";
  }
}

sub runRemoteCmd($$@){
  my ($remoteInfo, $opts, @remoteCmd) = @_;

  if(not defined $$opts{sshArgs}){
    $$opts{sshArgs} = [];
  }
  if($$opts{background}){
    push @{$$opts{sshArgs}}, "-f";
    push @{$$opts{sshArgs}}, "-n";
  }

  my @cmd;
  if(defined $$remoteInfo{ipmagicName}){
    @cmd = ("ipmagic", $$remoteInfo{ipmagicName});
    @cmd = (@cmd, "-u", $$remoteInfo{user}) if defined $$remoteInfo{user};
    @cmd = (@cmd, "--ssh-args", join(" ", @{$$opts{sshArgs}}));
    @cmd = (@cmd, "-s", @remoteCmd);
  }else{
    @cmd = ("ssh");
    if(defined $$remoteInfo{user}){
      @cmd = (@cmd, $$remoteInfo{user} . "@" . $$remoteInfo{host});
    }else{
      @cmd = (@cmd, $$remoteInfo{host});
    }
    @cmd = (@cmd, @{$$opts{sshArgs}});
    @cmd = (@cmd, @remoteCmd);
  }

  if($$opts{readProc}){
    open my $cmdH, "-|", @cmd or die "ERROR: command failed @cmd\n$!\n";
    my @lines = <$cmdH>;
    close $cmdH;
    if(wantarray){
      return @lines;
    }else{
      return join "", @lines;
    }
  }else{
    system @cmd;
  }
}

sub guessRemoteDisplay($){
  my ($remoteInfo) = @_;
  my $who = runRemoteCmd($remoteInfo, {readProc=>1}, qw(who));
  if($who =~ /\s*\((:\d+)\)$/m){
    return $1;
  }
  return $DEFAULT_REMOTE_DISPLAY;
}

&main(@ARGV);
