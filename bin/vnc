#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(sleep);

sub startVNC($$$$$$$$$$$);
sub runVNCServer($$$$$$$);
sub killVNCServer($$);
sub getSSHControlPath($);
sub startSSHTunnel($$$@);
sub stopSSHTunnel($);
sub getDesiredLocalRes($);
sub getRemoteRes($$);
sub getOrientationArg($$);
sub runRemoteCmd($$@);
sub guessRemoteDisplay($);

my $DELAY_MILLIS = 300;

my $LOCALHOST = "127.0.0.1";
my $DEFAULT_VNC_PORT = 5900;
my $DEFAULT_REMOTE_DISPLAY = ":0";
my $SSH_CONTROL_PATH_DIR = "/tmp/vnc-ssh-ctl";

my $VNC_SERVER_X11VNC = "x11vnc";
my $VNC_SERVER_LIPSTICK2VNC = "lipstick2vnc";

my $REMOTE_RES_CACHE_DIR = "$ENV{HOME}/.cache/vnc-remote-res";

my %VNC_SERVER_ARGS = (
  $VNC_SERVER_X11VNC       => [qw(-safer -nopw -once -noncache -bg)],
  $VNC_SERVER_LIPSTICK2VNC => [qw(-k)],
);

my %VNC_CLIENT_ARGS = (
  xtigervncviewer => [qw(DotWhenNoCursor=1)],
  xtightvncviewer => [qw(-quality 0         -compresslevel 5)],
  x2vnc           => [qw(-hotkey a-F9       -edgewidth 0)],
);

my $DEFAULT_VNC_SERVER = $VNC_SERVER_X11VNC;
my $DEFAULT_VNC_CLIENT = "xtigervncviewer";
my $OK_VNC_CLIENTS = join "|", sort keys %VNC_CLIENT_ARGS;

my $ROTATE_ARG_TYPES = {
  "0"   => {syns=>[qw(0    +0    -0    none)],   left=>"270", right=>"90"},
  "90"  => {syns=>[qw(90   +90   -270)],         left=>"0",   right=>"180"},
  "180" => {syns=>[qw(180  +180  -180  xy  yx)], left=>"90",  right=>"270"},
  "270" => {syns=>[qw(270  +270  -90)],          left=>"180", right=>"0"},
  "x"   => {syns=>[qw(x)],                       left=>"90x", right=>"90y"},
  "y"   => {syns=>[qw(y)],                       left=>"90y", right=>"90x"},
  "90x" => {syns=>[qw(90x)],                     left=>"y",   right=>"x"},
  "90y" => {syns=>[qw(90y)],                     left=>"x",   right=>"y"},
};
my $ROTATE_ARG_BY_SYN = {
  map {
    my $rotArg = $_;
    map {$_ => $rotArg} @{$$ROTATE_ARG_TYPES{$rotArg}{syns}};
  } sort keys %$ROTATE_ARG_TYPES
};
my $OK_ROTATE_ARG_REGEX = join("|", map {quotemeta($_)} sort keys %$ROTATE_ARG_BY_SYN);

my $SCALE_AUTO = "auto";

my $USAGE = "Usage:
  $0 [OPTS] HOST | IPMAGIC_NAME
    -if <IPMAGIC_NAME> is given, get <HOST> using `ipmagic IPMAGIC_NAME`
    -choose a <LOCAL_PORT> based on the number of existing SSH sessions
    -start vnc server on <HOST> with port=$DEFAULT_VNC_PORT
    -setup an ssh tunnel to bind $LOCALHOST:<LOCAL_PORT> to <HOST>:$DEFAULT_VNC_PORT
    -run vnc client to connect to $LOCALHOST:<LOCAL_PORT>

  $0 -h | --help
    show this message

  $0 off
    disconnect ssh tunnel

  OPTS
    --client=VNC_CLIENT   (default is $DEFAULT_VNC_CLIENT)
      one of: $OK_VNC_CLIENTS

    --lipstick2vnc
      use vnc server $VNC_SERVER_LIPSTICK2VNC instead of $VNC_SERVER_X11VNC

    -u USER | --user USER | --user=USER
      -use <USER> for all ssh/ipmagic commands

    --no-ssh-tunnel
      -do not select a <LOCAL_PORT>
      -do not create an SSH tunnel
      -connect directly to the remote vnc server on <HOST>:$DEFAULT_VNC_PORT

    --no-server
      -do not start vnc server, assume already running on <HOST>:$DEFAULT_VNC_PORT

    -r | --remote
      same as: --no-ssh-tunnel --no-server

    --cache
      -before fetching <REMOTE_WIDTH>x<REMOTE_HEIGHT>
        -read <REMOTE_RES_CACHE_FILE> if present instead of fetching
      -after fetching <REMOTE_WIDTH>x<REMOTE_HEIGHT
        -write <REMOTE_RES_CACHE_FILE>
    --no-cache
      -always fetch <REMOTE_WIDTH>x<REMOTE_HEIGHT> remotely
      -never write <REMOTE_RES_CACHE_FILE>
      (this is the default)

        REMOTE_RES_CACHE_FILE
          $REMOTE_RES_CACHE_DIR/<REMOTE_CACHE_NAME>
        REMOTE_CACHE_NAME
          -same as <HOST>, with only letters, numbers, '_', '-', and '-'
          -replace all characters except letters/numbers/underscores/dashes/periods with '_'

    --geometry=<WIDTH>x<HEIGHT>
    --res=<WIDTH>x<HEIGHT>
      -pass '-geometry <WIDTH>x<HEIGHT>' to vnc server
      -if --rotate is given and <ROTATION> is 90, 270, 90x, or 90y:
        -swap <WIDTH> and <HEIGHT> in --geometry
      e.g.: '--res=500x1000 --rotate=90'
              will pass '-geometry 1000x500 -rotate 90' to $VNC_SERVER_X11VNC,
              and will therefore be 500x1000 in the vnc client

    -s SCALE_RATIO | --scale SCALE_RATIO | --scale=SCALE_RATIO
      -scale remote resolution, preserving aspect ratio
        -fetch <REMOTE_WIDTH>x<REMOVE_HEIGHT> with `res`
        -calculate <SCALED_WIDTH> as <SCALE_RATIO>*<REMOTE_WIDTH>
        -calculate <SCALED_HEIGHT> as <SCALE_RATIO>*<REMOTE_HEIGHT>
      -set resolution as in --geometry=<SCALED_WIDTH>x<SCALED_HEIGHT>
      e.g.: if remote is 1280x720
        '--scale=0.9'             => '--geometry=1152x648'
        '--scale=0.9 --rotate=90' => '--geometry=648x1152 --rotate=90'

    --auto-scale
      -calculate <SCALE_RATIO> to fit remote screen in local screen, preserving aspect ratio
        -read <LOCAL_WIDTH>x<LOCAL_HEIGHT> with `res`
        -fetch <REMOTE_WIDTH>x<REMOTE_HEIGHT> with `res`
        -swap <REMOTE_WIDTH> and <REMOTE_HEIGHT> if --rotate is given and <ROTATION> is portrait
        -calculate <WIDTH_SCALE_RATIO> as <LOCAL_WIDTH>/<REMOTE_WIDTH>
        -calculate <HEIGHT_SCALE_RATIO> as <LOCAL_HEIGHT>/<REMOTE_HEIGHT>
        -select <SCALE_RATIO> as the least of <WIDTH_SCALE_RATIO> and <HEIGHT_SCALE_RATIO>
      -if --rotate is given and <ROTATION> is 90, 270, 90x, or 90y:
        -fit the rotated remote resolution within the local resolution
          (i.e.: compare remote width to local height, and vice-versa)
      e.g.: if remote is 1080x1920 and local is 1920x1080
        '--auto-scale'             => '--scale=0.5625'          => '--res=1080x607'
        '--auto-scale --rotate=90' => '--scale=1.0 --rotate=90' => '--res=1920x1080 --rotate=90'

    --auto-scale-bar
      same as: --auto-scale --local-height-scale=0.95

    --local-height-scale=LOCAL_HEIGHT_SCALE | --local-height-scale LOCAL_HEIGHT_SCALE
      -scale local screen height before calculating --scale-auto
      -this can be used to accommodate a system bar without having to scroll
        e.g.: if remote is 1920x1080 and local is 1280x720 and system bar is 72px
          '--auto-scale --local-height-scale=0.9' => '--scale=0.6' => '--res=1152x648'
      (default: 1.0)


    --rotate=ROTATION | -rotate=ROTATION
      -pass -rotate arg to $VNC_SERVER_X11VNC
      -if <ROTATION> swaps aspect ratio (i.e.: for 90, 270, 90x, or 90y):
        -swap <WIDTH> and <HEIGHT> in --geometry
        -swap <REMOTE_WIDTH> and <REMOTE_HEIGHT> is --auto-scale

        ROTATION
          0   | +0   | -0   | none      do not pass any rotation arg  (default)
          90  | +90  | -270             pass '-rotate 90'   to x11vnc (rotate clockwise)
          180 | +180 | -180 | xy | yx   pass '-rotate 180'  to x11vnc (flip x-axis, flip y-axis)
          270 | +270 | -90              pass '-rotate 270'  to x11vnc (rot counter-clockwise)
          x                             pass '-rotate x'    to x11vnc (flip x-axis)
          y                             pass '-rotate y'    to x11vnc (flip y-axis)
          90x | +90x                    pass '-rotate 90x'  to x11vnc (rotate CW, flip x-axis)
          90y | +90y                    pass '-rotate 90y'  to x11vnc (rotate CW, fiip y-axis)

    --rotate-from-landscape
      if HOST resolution is portrait, add an additional left rotation to --rotate
        -get ROTATION from --rotate arg (assume '0' if none given)
        -fetch <REMOTE_WIDTH>x<REMOTE_HEIGHT> with `res`
        -if <REMOTE_WIDTH> is greater than or equal to <REMOTE_HEIGHT>:
          -do nothing (leave --rotate=<ROTATION> arg unmodified)
          e.g.:
            '--rotate=0   --rotate-from-landscape' => '--rotate=0'
            '--rotate=90  --rotate-from-landscape' => '--rotate=90'
        -if <REMOTE_HEIGHT> is greater than <REMOTE_WIDTH>:
          -add an additional counter-clockwise turn to --rotate:
          e.g.:
            '--rotate=0   --rotate-from-landscape' => '--rotate=270'
            '--rotate=90  --rotate-from-landscape' => '--rotate=0'
            '--rotate=180 --rotate-from-landscape' => '--rotate=90'
            '--rotate=270 --rotate-from-landscape' => '--rotate=180'
            '--rotate=x   --rotate-from-landscape' => '--rotate=90x'
            '--rotate=y   --rotate-from-landscape' => '--rotate=90y'
            '--rotate=90x --rotate-from-landscape' => '--rotate=y'
            '--rotate=90y --rotate-from-landscape' => '--rotate=x'

    -l | --landscape        same as:  '--rotate=0   --rotate-from-landscape'
    --landscape-inverted    same as:  '--rotate=180 --rotate-from-landscape'
    -p | --portrait         same as:  '--rotate=90  --rotate-from-landscape'
    --portrait-inverted     same as:  '--rotate=270 --rotate-from-landscape'
";

my $CMD_START_VNC = "start";
my $CMD_SSH_OFF = "off";

sub main(@){
  my $cmd = $CMD_START_VNC;
  my $opts = {
    remoteInfo    => {
      ipmagicName => undef,
      host        => undef,
      user        => undef,
    },
    vncClient     => $DEFAULT_VNC_CLIENT,
    vncServer     => $DEFAULT_VNC_SERVER,
    useCache      => 0,
    geometry      => undef,
    scale         => undef,
    localHeightScale => 1.0,
    rotateArg     => 0,
    rotateFromLandscape => 0,
    isStartServer => 1,
    isSSHTunnel   => 1,
  };
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(off)$/){
      $cmd = $CMD_SSH_OFF;
    }elsif($arg =~ /^--client=($OK_VNC_CLIENTS)$/){
      $$opts{vncClient} = $1;
    }elsif($arg =~ /^(--lipstick2vnc)$/){
      $$opts{vncServer} = $VNC_SERVER_LIPSTICK2VNC;
    }elsif($arg =~ /^(--no-server)$/){
      $$opts{isStartServer} = 0;
    }elsif($arg =~ /^(--no-ssh-tunnel)$/){
      $$opts{isSSHTunnel} = 0;
    }elsif($arg =~ /^(-r|--remote)$/){
      $$opts{isStartServer} = 0;
      $$opts{isSSHTunnel} = 0;
    }elsif($arg =~ /^(--cache)$/){
      $$opts{useCache} = 1;
    }elsif($arg =~ /^(--no-cache)$/){
      $$opts{useCache} = 0;
    }elsif($arg =~ /^--geometry=(\d+x\d+)$/){
      $$opts{geometry} = $1;
    }elsif($arg =~ /^--res=(\d+x\d+)$/){
      $$opts{geometry} = $1;
    }elsif($arg =~ /^--scale=(\d+|\d*\.\d+)$/){
      $$opts{scale} = $1;
    }elsif($arg =~ /^(-s|--scale)$/ and @_ > 0 and $_[0] =~ /^(\d+|\d*\.\d+)$/){
      $$opts{scale} = shift @_;
    }elsif($arg =~ /^(--auto-scale)$/){
      $$opts{scale} = $SCALE_AUTO;
    }elsif($arg =~ /^(--auto-scale-bar)$/){
      $$opts{scale} = $SCALE_AUTO;
      $$opts{localHeightScale} = 0.95;
    }elsif($arg =~ /^--local-height-scale=(\d+|\d*\.\d+)$/){
      $$opts{localHeightScale} = $1;
    }elsif($arg =~ /^(--local-height-scale)$/ and @_ > 0 and $_[0] =~ /^(\d+|\d*\.\d+)$/){
      $$opts{localHeightScale} = shift @_;
    }elsif($arg =~ /^--rotate=($OK_ROTATE_ARG_REGEX)$/){
      my $rotateSyn = $1;
      $$opts{rotateArg} = $$ROTATE_ARG_BY_SYN{$rotateSyn};
    }elsif($arg =~ /^(--rotate)$/ and @_ > 0 and $_[0] =~ /^($OK_ROTATE_ARG_REGEX)$/){
      my $rotateSyn = shift @_;
      $$opts{rotateArg} = $$ROTATE_ARG_BY_SYN{$rotateSyn};
    }elsif($arg =~ /^--rotate-from-landscape/){
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^(-l|--landscape)$/){
      $$opts{rotateArg} = 0;
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^(--landscape-inverted)$/){
      $$opts{rotateArg} = 180;
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^(-p|--portrait)$/){
      $$opts{rotateArg} = 90;
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^(--portrait-inverted)$/){
      $$opts{rotateArg} = 270;
      $$opts{rotateFromLandscape} = 1;
    }elsif($arg =~ /^--user=(.+)$/){
      $$opts{remoteInfo}{user} = $1;
    }elsif($arg =~ /^(-u|--user)$/ and @_ > 0){
      $$opts{remoteInfo}{user} = shift @_;
    }elsif(not defined $$opts{remoteInfo}{host}){
      $$opts{remoteInfo}{host} = $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if(defined $$opts{remoteInfo}{host}){
    my %okIpmagicNames = map{$_ => 1} split /\s+/, `ipmagic --list`;
    if(defined $okIpmagicNames{$$opts{remoteInfo}{host}}){
      my $ipmagicName = $$opts{remoteInfo}{host};
      my $host = `ipmagic $ipmagicName --host`;
      chomp $host;
      if($host !~ /\w/){
        die "ERROR: could not get host for IPMAGIC_NAME=$ipmagicName\n";
      }
      $$opts{remoteInfo}{host} = $host;
      $$opts{remoteInfo}{ipmagicName} = $ipmagicName;
    }
  }

  if($cmd eq $CMD_START_VNC){
    die "$USAGE\nERROR: missing HOST\n" if not defined $$opts{remoteInfo}{host};
    startVNC($$opts{remoteInfo}, $$opts{vncClient}, $$opts{vncServer}, $$opts{useCache},
      $$opts{geometry}, $$opts{scale}, $$opts{localHeightScale},
      $$opts{rotateArg}, $$opts{rotateFromLandscape},
      $$opts{isStartServer}, $$opts{isSSHTunnel});
  }elsif($cmd eq $CMD_SSH_OFF){
    runSSHExit($$opts{host});
  }else{
    die "ERROR: unknown cmd $cmd\n";
  }
}

sub startVNC($$$$$$$$$$$){
  my ($remoteInfo, $vncClient, $vncServer, $useCache,
      $geometry, $scale, $localHeightScale,
      $rotateArg, $rotateFromLandscape,
      $isStartServer, $isSSHTunnel) = @_;

  my $isRemotePortrait = 0;

  my ($remoteW, $remoteH);
  if($isStartServer){
    ($remoteW, $remoteH) = getRemoteRes($remoteInfo, $useCache);
  }else{
    ($remoteW, $remoteH) = (0, 0);
  }

  if($remoteH > $remoteW){
    $isRemotePortrait = 1;
  }

  if($rotateFromLandscape and $isRemotePortrait){
    #rotate additional counter-clockwise turn if remote is portrait
    $rotateArg = $$ROTATE_ARG_TYPES{$rotateArg}{left};
  }

  my $dimensionSwap = 0;
  if($rotateArg =~ /^(90|270|90x|90y)$/){
    $dimensionSwap = 1;
  }

  if($dimensionSwap){
    ($remoteW, $remoteH) = ($remoteH, $remoteW);
  }

  if(defined $scale and $scale eq $SCALE_AUTO){
    my ($desiredLocalW, $desiredLocalH) = getDesiredLocalRes($localHeightScale);

    my $scaleW = $desiredLocalW/$remoteW;
    my $scaleH = $desiredLocalH/$remoteH;

    #minimum of width scale and height scale
    $scale = $scaleW < $scaleH ? $scaleW : $scaleH;
  }

  if(defined $scale and not defined $geometry){
    $geometry = int($remoteW*$scale) . "x" . int($remoteH*$scale);
  }
  if(defined $geometry and not defined $scale and $geometry =~ /^(\d+)x(\d+)$/){
    my ($w, $h) = ($1, $2);
    my $scaleW = $w/$remoteW;
    my $scaleH = $h/$remoteH;

    #minimum of width scale and height scale
    $scale = $scaleW < $scaleH ? $scaleW : $scaleH;
  }

  if($dimensionSwap and defined $geometry and $geometry =~ /^(\d+)x(\d+)$/){
    $geometry = "${2}x${1}";
  }

  my $sshCtlFileCount = 0 + grep {-f $_} glob("$SSH_CONTROL_PATH_DIR/*.ctl");

  if($isStartServer){
    runVNCServer($remoteInfo, $vncServer, $geometry, $scale, $rotateArg, 
      $isRemotePortrait, $isSSHTunnel);
    sleep($DELAY_MILLIS/1000.0);
  }

  my $remotePort = $DEFAULT_VNC_PORT;

  my @vncClientCmd = ($vncClient, @{$VNC_CLIENT_ARGS{$vncClient}});
  if($isSSHTunnel){
    #5900 + 10*otherVNCSessions + random first digit
    my $localPort = $DEFAULT_VNC_PORT + 10*($sshCtlFileCount) + int(rand(10));

    stopSSHTunnel($remoteInfo);
    startSSHTunnel($remoteInfo, $localPort, $remotePort);

    push @vncClientCmd, sprintf("%s::%s", $LOCALHOST, $localPort);
  }else{
    push @vncClientCmd, sprintf("%s::%s", $$remoteInfo{host}, $remotePort);
  }

  system @vncClientCmd;

  if($isStartServer){
    killVNCServer($remoteInfo, $vncServer);
  }
  if($isSSHTunnel){
    stopSSHTunnel($remoteInfo);
  }
}

sub runVNCServer($$$$$$$){
  my ($remoteInfo, $vncServer, $geometry, $scale, $rotateArg,
    $isRemotePortrait, $isSSHTunnel) = @_;

  my @serverArgs = @{$VNC_SERVER_ARGS{$vncServer}};

  if($vncServer eq $VNC_SERVER_X11VNC){
    if(defined $rotateArg and $rotateArg ne "0"){
      @serverArgs = (@serverArgs, "-rotate", $rotateArg);
    }
    if(defined $geometry){
      @serverArgs = (@serverArgs, "-geometry", $geometry);
    }
    if($isSSHTunnel){
      push @serverArgs, "-localhost";
    }

    my @vncServerCmd = ($vncServer,
      '-display', guessRemoteDisplay($remoteInfo),
      @serverArgs,
    );

    killVNCServer($remoteInfo, $vncServer);
    runRemoteCmd($remoteInfo, {background=>1}, @vncServerCmd);
  }elsif($vncServer eq $VNC_SERVER_LIPSTICK2VNC){
    if(defined $scale){
      #minimum scale is 10%
      $scale = 0.1 if $scale <= 0.1;

      #scale must be in increments of 10%
      #  weird display bugs happen at finer granularity like 0.41
      $scale = sprintf "%.1f", int($scale*10)/10.0;

      @serverArgs = (@serverArgs, "-s", $scale);
    }

    push @serverArgs, getOrientationArg($rotateArg, $isRemotePortrait);

    if(not $isSSHTunnel){
      die "ERROR: --allow-all option removed from lipstick2vnc, must use SSH tunnel\n";
    }

    my $lipstick2vncConfFile =
      "/var/lib/environment/$$remoteInfo{user}/70-lipstick2vnc.conf";
    runRemoteCmd($remoteInfo, {backgroup=>1}, "
      echo $lipstick2vncConfFile
      echo \"LIPSTICK2VNC_OPTS=\\\"@serverArgs\\\"\" \\
        | sudo tee $lipstick2vncConfFile
      lock --unlock &

      #/dev/uinput is normally owned by system.net_bt_stack
      #  /usr/bin/lipstick2vnc has setgid bit and is owned by group privileged
      sudo chown system.privileged /dev/uinput

      systemctl --user stop vnc.socket
      systemctl --user stop vnc
      systemctl --user start vnc
    ");
  }else{
    die "ERROR: unknown VNC server $vncServer\n";
  }
}
sub killVNCServer($$){
  my ($remoteInfo, $vncServer) = @_;
  if($vncServer eq $VNC_SERVER_X11VNC){
    runRemoteCmd($remoteInfo, {}, "pkill", $vncServer);
  }elsif($vncServer eq $VNC_SERVER_LIPSTICK2VNC){
    runRemoteCmd($remoteInfo, {}, "systemctl --user stop vnc");
  }else{
    die "ERROR: unknown VNC server $vncServer\n";
  }
}

sub getSSHControlPath($){
  my ($remoteInfo) = @_;
  if(not -d $SSH_CONTROL_PATH_DIR){
    system "mkdir", "-p", $SSH_CONTROL_PATH_DIR;
  }
  if(not -d $SSH_CONTROL_PATH_DIR){
    die "ERROR: $SSH_CONTROL_PATH_DIR does not exist\n";
  }

  my $baseName = $$remoteInfo{host};
  $baseName =~ s/\W+/_/g;
  $baseName =~ s/^_+//;
  $baseName =~ s/_+$//;

  return "$SSH_CONTROL_PATH_DIR/$baseName.ctl";
}

sub startSSHTunnel($$$@){
  my ($remoteInfo, $localPort, $remotePort) = @_;
  my @remoteCmd = (); #ssh -N instead of a command
  runRemoteCmd($remoteInfo, {
    sshArgs => [
      "-o", ("ControlPath=" . getSSHControlPath($remoteInfo)),
      "-o", "ControlMaster=yes",
      "-f",
      "-N",
      "-n",
      "-L", "$localPort:$LOCALHOST:$remotePort",
    ],
  }, @remoteCmd);
}

sub stopSSHTunnel($){
  my ($remoteInfo) = @_;
  my @sshCmd = ("ssh",
    "-o", ("ControlPath=" . getSSHControlPath($remoteInfo)),
    "-O", "exit",
    $LOCALHOST,
  );
  system @sshCmd;
}

sub getDesiredLocalRes($){
  my ($localHeightScale) = @_;

  my $localRes = `res`;
  my ($localWidth, $localHeight);
  if($localRes =~ /^(\d+)x(\d+)$/){
    $localWidth = $1;
    $localHeight = $2;
  }
  if($localWidth !~ /^\d+$/ or $localHeight !~ /^\d+$/){
    die "ERROR: could not obtain local resolution\n";
  }

  return ($localWidth, $localHeight*$localHeightScale);
}

sub getRemoteRes($$){
  my ($remoteInfo, $useCache) = @_;

  my $cacheFile;
  if($useCache){
    my $cacheName = $$remoteInfo{host};
    $cacheName =~ s/[^a-zA-Z0-9\-_\.]/_/g;
    $cacheFile = "$REMOTE_RES_CACHE_DIR/$cacheName";
  }

  if($useCache and -f $cacheFile){
    my $res = `cat $cacheFile`;
    if($res =~ /^(\d+)x(\d+)$/){
      return ($1, $2);
    }
  }

  my $remoteRes = runRemoteCmd($remoteInfo, {readProc=>1}, qw(res));
  if($remoteRes =~ /(\d+)x(\d+)/){
    my ($w, $h) = ($1, $2);
    if($useCache){
      system "mkdir", "-p", $REMOTE_RES_CACHE_DIR if not -d $REMOTE_RES_CACHE_DIR;
      system "echo '${w}x${h}' > $cacheFile";
    }
    return ($w, $h);
  }else{
    die "ERROR: could not get remote res from $$remoteInfo{host}\n";
  }
}

sub getOrientationArg($$){
  my ($rotateArg, $isRemotePortrait) = @_;

  if($isRemotePortrait){
    #if remote is already portrait, then '0' means 'portrait' and 270 means 'landscape'
    $rotateArg = $$ROTATE_ARG_TYPES{$rotateArg}{right};
  }

  if($rotateArg eq "0"){
    return "--landscape";
  }elsif($rotateArg eq "180"){
    return "--landscape-inverted";
  }elsif($rotateArg eq "90"){
    return "--portrait";
  }elsif($rotateArg eq "270"){
    return "--portrait-inverted";
  }else{
    die "ERROR: cannot parse $rotateArg as an orientation arg for target VNC server\n";
  }
}

sub runRemoteCmd($$@){
  my ($remoteInfo, $opts, @remoteCmd) = @_;

  if(not defined $$opts{sshArgs}){
    $$opts{sshArgs} = [];
  }
  if($$opts{background}){
    push @{$$opts{sshArgs}}, "-f";
    push @{$$opts{sshArgs}}, "-n";
  }

  my @cmd;
  if(defined $$remoteInfo{ipmagicName}){
    @cmd = ("ipmagic", $$remoteInfo{ipmagicName});
    @cmd = (@cmd, "-u", $$remoteInfo{user}) if defined $$remoteInfo{user};
    @cmd = (@cmd, "--ssh-args", join(" ", @{$$opts{sshArgs}}));
    @cmd = (@cmd, "-s", @remoteCmd);
  }else{
    @cmd = ("ssh");
    if(defined $$remoteInfo{user}){
      @cmd = (@cmd, $$remoteInfo{user} . "@" . $$remoteInfo{host});
    }else{
      @cmd = (@cmd, $$remoteInfo{host});
    }
    @cmd = (@cmd, @{$$opts{sshArgs}});
    @cmd = (@cmd, @remoteCmd);
  }

  if($$opts{readProc}){
    open my $cmdH, "-|", @cmd or die "ERROR: command failed @cmd\n$!\n";
    my @lines = <$cmdH>;
    close $cmdH;
    if(wantarray){
      return @lines;
    }else{
      return join "", @lines;
    }
  }else{
    system @cmd;
  }
}

sub guessRemoteDisplay($){
  my ($remoteInfo) = @_;
  my $who = runRemoteCmd($remoteInfo, {readProc=>1}, qw(who));
  if($who =~ /\s*\((:\d+)\)$/m){
    return $1;
  }
  return $DEFAULT_REMOTE_DISPLAY;
}

&main(@ARGV);
