#!/usr/bin/perl
use strict;
use warnings;
use Term::ReadKey;

$SIG{'INT'} = sub{ReadMode 0; exit 0};
$SIG{'TERM'} = sub{ReadMode 0; exit 0};

our $keyDelay = 0.1;

sub readKeys();
sub readKeyBytes();
sub arrContainsAt($$$);
sub isRunning($);

my $seqByte = 0x1b;

my $keyBytes = {
  0x09 => 'Tab',
  0x0A => 'KP_Enter',
  0x7F => 'BackSpace',
};

my $keyByteSeqs = [
  ['Up',        ['[', 'A']],
  ['Down',      ['[', 'B']],
  ['Right',     ['[', 'C']],
  ['Left',      ['[', 'D']],
  ['Home',      ['[', '7', '~']],
  ['End',       ['[', '8', '~']],
  ['Insert',    ['[', '2', '~']],
  ['Delete',    ['[', '3', '~']],
  ['Page_Up',   ['[', '5', '~']],
  ['Page_Down', ['[', '6', '~']],
  ['F1',        ['[', '1', '1', '~']],
  ['F2',        ['[', '1', '2', '~']],
  ['F3',        ['[', '1', '3', '~']],
  ['F4',        ['[', '1', '4', '~']],
  ['F5',        ['[', '1', '5', '~']],
  ['F6',        ['[', '1', '7', '~']],
  ['F7',        ['[', '1', '8', '~']],
  ['F8',        ['[', '1', '9', '~']],
  ['F9',        ['[', '2', '0', '~']],
  ['F10',       ['[', '2', '1', '~']],
  ['F11',       ['[', '2', '3', '~']],
  ['F12',       ['[', '2', '4', '~']],
  ['Escape',    []],
];

my $usage = "Usage:
  $0 -h | --help
    print this message

  $0 [--check-pid=PID]
    read keystrokes with Term::ReadKey and print them to STDOUT
    quit if process PIDs is not alive (checked with `kill -0 PID`)
";

sub main(@){
  my $checkPid = undef;
  if(@_ == 1 and $_[0] =~ /^(-h|--help)$/){
    print $usage;
    exit 0;
  }elsif(@_ == 1 and $_[0] =~ /^--check-pid=(\d+)$/){
    $checkPid = $1;
  }elsif(@_ > 0){
    die $usage;
  }

  select((select(STDOUT), $|=1)[0]);
  my %keys;
  while(1){
    for my $key(@{readKeys()}){
      print "$key\n";
    }
    if(defined $checkPid and not isRunning $checkPid){
      ReadMode 0;
      exit 0;
    }
    if(not -t STDIN){
      #input is not from a terminal, assume there is no more input
      exit 0;
    }
  }
}

#assumes utf8
sub readKeys(){
  my @keys;
  my $bytes = readKeyBytes();
  for(my $i=0; $i<@$bytes; $i++){
    my $b = ord $$bytes[$i];
    if(defined $$keyBytes{$b}){
      push @keys, $$keyBytes{$b};
    }elsif($b == $seqByte){
      for my $keyByteSeq(@$keyByteSeqs){
        my ($key, $byteSeq) = @$keyByteSeq;
        my $seq = [@$bytes];
        shift @$seq;
        if(arrContainsAt $seq, $byteSeq, $i){
          $i+=@$byteSeq; #mark the next few bytes as read
          push @keys, $key;
          last;
        }
      }
    }elsif($b >= 0xc2 and $b <= 0xdf){
      my $b1 = $$bytes[$i];
      my $b2 = $i+1<@$bytes ? $$bytes[$i+1] : '';
      my $key = "$b1$b2";
      $i+=1;
      utf8::decode($key);
      push @keys, $key;
    }elsif($b >= 0xe0 and $b <= 0xef){
      my $b1 = $$bytes[$i];
      my $b2 = $i+1<@$bytes ? $$bytes[$i+1] : '';
      my $b3 = $i+2<@$bytes ? $$bytes[$i+2] : '';
      my $key = "$b1$b2$b3";
      $i+=2;
      utf8::decode($key);
      push @keys, $key;
    }else{
      push @keys, $$bytes[$i];
    }
  }
  return \@keys;
}

sub readKeyBytes(){
  ReadMode 3;
  my @bytes;
  my $start = time;

  while(1){
    my $byte = ReadKey($keyDelay);
    last if not defined $byte and time - $start > $keyDelay;
    push @bytes, $byte if defined $byte;
  }
  ReadMode 0;
  return \@bytes;
}

sub arrContainsAt($$$){
  my ($arr, $subArr, $offset) = @_;

  for(my $i=0; $i<@$subArr; $i++){
    my $pos = $i + $offset;
    return 0 if $pos >= @$arr;
    if($$arr[$pos] ne $$subArr[$i]){
      return 0;
    }
  }
  return 1;
}

sub isRunning($){
  my ($pid) = @_;
  waitpid $pid, 1;
  system "kill -0 $pid 2>/dev/null";
  if($? == 0){
    return 1;
  }else{
    return 0;
  }
}

&main(@ARGV);
