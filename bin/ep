#!/usr/bin/perl
use strict;
use warnings;
use Cwd qw(abs_path);

my $BITRATE_GUESS = 2.0 * 1000.0 * 1000.0; #2 Mbps
my $DURATION_MIN_GUESS = 60.0;

my $HIST_FILE = "$ENV{HOME}/.cache/ep/hist";

sub mpvPos(@);
sub histEntryDone($);
sub parseHistory();
sub readHistEntries();
sub appendHist($);
sub mtime($);
sub filesize($);
sub duration($);
sub durationGuess($);
sub durationOrGuess($);
sub nowMillis();
sub run(@);

sub main(@){
  my $dir = $_[0];
  $dir = abs_path($dir);
  die "Usage: $0 DIR\n" if not -d $dir;

  my @files = `find $dir -maxdepth 2 -type f`;
  chomp foreach @files;

  my %fileHist;

  my $hist = readHistEntries();
  my %okPaths = map {$_ => 1} @files;
  for my $entry(@$hist){
    my $path = $$entry{path};
    if(defined $okPaths{$path}){
      $fileHist{$path} = $entry;
    }
  }

  my $nextFileIndex = undef;
  my $nextFilePos = undef;
  for(my $i=$#files; $i>=0; $i--){
    my $file = $files[$i];
    my $entry = $fileHist{$file};
    if(histEntryDone $entry){
      last;
    }else{
      $nextFileIndex = $i;
      $nextFilePos = defined $entry ? $$entry{pos} : 0;
    }
  }

  my $playlistFile = "/tmp/ep-playlist-" . nowMillis();
  open FH, "> $playlistFile" or die "could not write $playlistFile\n$!\n";
  print FH "$_\n" foreach @files;
  close FH;

  mpvPos
    $nextFilePos,
    "--playlist=$playlistFile",
    "--playlist-start=$nextFileIndex",
    ;

  system "rm", $playlistFile;
}

sub mpvPos($@){
  my ($seekPos, @mpvArgs) = @_;
  my $posFile = "/tmp/ep-pos-file-" . nowMillis();
  system "mpv-pos", "--seek=$seekPos", "--pos-file=$posFile", @mpvArgs;
  my $posLine = `tail -1 $posFile`;
  system "rm", $posFile;
  if($posLine =~ /^(\d+|\d*\.\d+) - (.+)$/){
    my ($pos, $path) = ($1, $2);
    my $entry = {
      date  => nowMillis(),
      mtime => mtime($path),
      size  => filesize($path),
      pos   => $pos,
      dur   => durationOrGuess($path),
      path  => $path,
    };

    appendHist $entry;
  }
}

sub histEntryDone($){
  my ($histEntry) = @_;
  return 0 if not defined $histEntry;
  my $pos = $$histEntry{pos};
  my $dur = $$histEntry{dur};
  if($dur > 0 and $pos/$dur > 0.93){
    return 1;
  }else{
    return 0;
  }
}

sub readHistEntries(){
  return [] if not -f $HIST_FILE;

  open FH, "< $HIST_FILE" or die "could not read $HIST_FILE\n";
  my @lines = <FH>;
  close FH;

  my $entries = [];

  for my $line(@lines){
    if($line =~ /^
      (\d+),
      (\d+)mod,
      (\d+)b,
      (\d+|\d*\.\d+)s \s* \/ \s* (\d+|\d*\.\d+)s,
      (.+)
    $/x){
      my $entry = {
        date   => $1,
        mtime  => $2,
        size   => $3,
        pos    => $4,
        dur    => $5,
        path   => $6,
      };
      push @$entries, $entry;
    }else{
      die "invalid history line in $HIST_FILE: $line";
    }
  }

  return $entries;
}

sub appendHist($){
  my $entry = shift;
  my $line = ""
    . "$$entry{date},"
    . "$$entry{mtime}mod,"
    . "$$entry{size}b,"
    . "$$entry{pos}s / $$entry{dur}s,"
    . "$$entry{path}\n"
    ;

  my $dir = $HIST_FILE;
  $dir =~ s/\/[^\/]+$//;
  run "mkdir", "-p", $dir if not -d $dir;

  open FH, ">> $HIST_FILE" or die "could not write $HIST_FILE\n";
  print FH $line;
  close FH;
}

sub mtime($){
  my @stat = stat $_[0];
  return $stat[9];
}
sub filesize($){
  my @stat = stat $_[0];
  return $stat[7];
}
sub duration($){
  my @durCmd = ("duration", "-s", "-n", $_[0]);
  open FH, "-|", @durCmd or die "could not run @durCmd\n$!\n";
  my $out = join '', <FH>;
  close FH;

  if($out =~ /^(\d+|\d*\.\d+)$/){
    return $1;
  }else{
    return undef;
  }
}

sub durationGuess($){
  my $filesize = filesize $_[0];
  my $bits = $filesize * 8;
  my $dur = $bits / $BITRATE_GUESS;
  if($dur < $DURATION_MIN_GUESS){
    return $DURATION_MIN_GUESS;
  }else{
    return sprintf "%.2f", $dur;
  }
}
sub durationOrGuess($){
  my $dur = duration $_[0];
  $dur = durationGuess $_[0] if not defined $dur;
  return $dur;
}

sub nowMillis(){
  return int(time * 1000.0 + 0.5);
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
