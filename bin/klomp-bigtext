#!/usr/bin/perl
use strict;
use warnings;
use Text::CSV;
use Time::HiRes qw(sleep time);
use utf8;

sub formatKlompInfoLine($$$);
sub getIpmagicCommand($$@);
sub klompInfo(@);
sub offset($$$);
sub run(@);

my $extraCmdsByExec = {
  "xscreensaver-command" => ["xscreensaver-command", "-deactivate"],
};
my $extraCmdsFmt = join('',
  map {"\n      $_ => $$extraCmdsByExec{$_}"} sort keys %$extraCmdsByExec
);

my $defaultDelayMillis = 1000;

my $usage = "Usage:
  $0 [OPTS]
    -run bigtext with info from klomp-info
    -if any of the following execs exist, run each associated cmd:$extraCmdsFmt

    OPTS
       --ipmagic=CONFNAME
         runs bigtext with ipmagic (a la `ipmagic CONFNAME bigtext`)

       --ipmagic-user=USERNAME
         adds -u USERNAME to ipmagic (a la `ipmagic CONFNAME -u USERNAME bigtext`)

       --delay=MILLIS
         time to sleep in milliseconds after subsequent klomp displays
         default is $defaultDelayMillis

       --rhythmbox
         use rhythmbox-client instead of klomp-info

       --once
         run once and quit

       -w|--window
         include --fullScreen=false in bigtext opts

       --height=HEIGHT
         include --forceHeight=HEIGHT in bigtext opts

       --width=WIDTH
         include --forceWidth=WIDTH in bigtext opts

       --off
         run `pkill ^bigtext\$` and `bigtext -k` (local or with ipmagic)
         ignore options: --once --delay --height --width

       --toggle
         check if bigtext is running with:
           `pgrep ^bigtext\$` (local or with ipmagic)
         if bigtext is running:
           run `pkill ^bigtext\$` and `bigtext -k` (same as --off)
         otherwise:
           start bigtext normally (this option has no effect if not running)
";

sub main(@){
  my $ipmagic;
  my $ipmagicUser;
  my $delay = $defaultDelayMillis;
  my $useRhythmbox = 0;
  my $once = 0;
  my $window = 0;
  my $height;
  my $width;
  my $off = 0;
  my $toggle = 0;

  while(@_ > 0){
    my $arg=shift;
    if($arg =~ /^--ipmagic=(.*)$/){
      $ipmagic = $1;
    }elsif($arg =~ /^--ipmagic-user=(.*)$/){
      $ipmagicUser = $1;
    }elsif($arg =~ /^--delay=(\d+)$/){
      $delay = $1;
    }elsif($arg =~ /^(--rhythmbox)$/){
      $useRhythmbox = 1;
    }elsif($arg =~ /^--once$/){
      $once = 1;
    }elsif($arg =~ /^(-w|--window)$/){
      $window = 1;
    }elsif($arg =~ /^--height=(\d+)$/){
      $height = $1;
    }elsif($arg =~ /^--width=(\d+)$/){
      $width = $1;
    }elsif($arg =~ /^--off$/){
      $off = 1;
    }elsif($arg =~ /^--toggle$/){
      $toggle = 1;
    }else{
      die $usage;
    }
  }

  if($toggle and not $off){
    my @pgrepCmd = getIpmagicCommand($ipmagic, $ipmagicUser, 'pgrep', '^bigtext$');
    open FH, "-|", @pgrepCmd
      or die "could not run @pgrepCmd\n$!\n";
    my @procs = <FH>;
    close FH;
    if(@procs > 0 and $procs[0] =~ /^\d+$/){
      $off = 1;
    }
  }

  if($off){
    system getIpmagicCommand($ipmagic, $ipmagicUser, 'pkill', '^bigtext$');
    system getIpmagicCommand($ipmagic, $ipmagicUser, 'bigtext', '-k');
    exit;
  }

  my @bigtextOpts;
  push @bigtextOpts, "--fullScreen=false" if $window;
  push @bigtextOpts, "--forceHeight=$height" if defined $height;
  push @bigtextOpts, "--forceWidth=$width" if defined $width;

  my @bigtextCmd = getIpmagicCommand($ipmagic, $ipmagicUser,
    "bigtext",
      "--lineSeparator=true",
      @bigtextOpts,
      "--stdin");

  my @extraCmds;
  for my $exec(sort keys %$extraCmdsByExec){
    my @whichCmd = getIpmagicCommand($ipmagic, $ipmagicUser, "which $exec");
    my $res = `@whichCmd 2>/dev/null`;
    chomp $res;
    if($res =~ /\/$exec$/){
      push @extraCmds, $$extraCmdsByExec{$exec};
    }
  }

  print "@bigtextCmd\n";
  open FH, "|-", @bigtextCmd;

  while(1){
    my $now = time;

    for my $extraCmd(@extraCmds){
      run getIpmagicCommand($ipmagic, $ipmagicUser, @$extraCmd);
    }
    print FH formatKlompInfoLine($ipmagic, $ipmagicUser, $useRhythmbox);
    last if $once;

    my $toWait = ($delay/1000) - (time - $now);
    sleep $toWait if $toWait > 0;
    print "\n";
  }

  close FH;
}

sub formatKlompInfoLine($$$){
  my $ipmagic = shift;
  my $ipmagicUser = shift;
  my $useRhythmbox = shift;
  my $info;
  if($useRhythmbox){
    $info = rhythmboxInfo(qw(pos len title artist album number));
  }else{
    $info = klompInfo(qw(pos len title artist album number));
  }

  my $fmt = ''
    . "$$info{pos} / $$info{len}  #$$info{number}\n"
    . "$$info{artist}\n"
    . "$$info{album}\n"
    . "$$info{title}\n"
    ;
  $fmt =~ s/\\/\\\\/g;
  $fmt =~ s/\n/\\n/g;
  $fmt =~ s/\t/\\t/g;
  $fmt =~ s/\r//g;
  $fmt .= "\n";
  utf8::encode($fmt);
  print $fmt;
  return $fmt;
}

sub getIpmagicCommand($$@){
  my $ipmagic = shift;
  my $ipmagicUser = shift;
  my @cmd = @_;
  if(defined $ipmagic){
    @cmd = map {$_ =~ s/'/'\\''/g; "'$_'"} @cmd;
    if(defined $ipmagicUser){
      @cmd = ("ipmagic", $ipmagic, "-u", $ipmagicUser, @cmd);
    }else{
      @cmd = ("ipmagic", $ipmagic, @cmd);
    }
  }
  return @cmd;
}

sub klompInfo(@){
  my @cols = @_;

  my $csv = Text::CSV->new ( { binary => 1 } );
  $csv->parse(`klomp-info -c @cols`);

  my @vals = $csv->fields();
  die "Error parsing klomp-info CSV\n" if @cols != @vals;

  my $info = {};
  for(my $i=0; $i<@cols; $i++){
    my $col = $cols[$i];
    my $val = $vals[$i];
    $$info{$col} = $val;
  }
  return $info;
}

sub rhythmboxInfo(@){
  my @cols = @_;

  #    %at    album title
  #    %aa    album artist
  #    %aA    album artist (lowercase)
  #    %as    album artist sortname
  #    %aS    album artist sortname (lowercase)
  #    %ay    album year
  #    %ag    album genre
  #    %aG    album genre (lowercase)
  #    %an    album disc number
  #    %aN    album disc number, zero padded
  #    %st    stream title
  #    %tn    track number (i.e 8)
  #    %tN    track number, zero padded (i.e 08)
  #    %tt    track title
  #    %ta    track artist
  #    %tA    track artist (lowercase)
  #    %ts    track artist sortname
  #    %tS    track artist sortname (lowercase)
  #    %td    track duration
  #    %te    track elapsed time

  my %klompToRboxCols = (
    pos    => '%te',
    len    => '%td',
    title  => '%tt',
    artist => '%ta',
    album  => '%tt',
    number => '%tn',
  );

  my $fmt = join "###", map {$klompToRboxCols{$_}} @cols;

  my $infoStr = `rhythmbox-client --print-playing-format '$fmt'`;
  chomp $infoStr;

  my @vals = split /###/, $infoStr;

  if(@cols != @vals){
    die "ERROR: could not parse rhythmbox-client output\n";
  }

  my $info = {};
  for(my $i=0; $i<@cols; $i++){
    my $col = $cols[$i];
    my $val = $vals[$i];
    $$info{$col} = $val;
  }
  return $info;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
