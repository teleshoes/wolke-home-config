#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC [OPTS] TEXT
    copy TEXT to clipboard using `xsel`

  $EXEC [OPTS]
    copy STDIN to clipboard using `xsel`

  OPTS
    -c
      use Clipboard (same as `xsel --clipboard`)
      (this is the default)
    -s
      use X selection (same as `xsel --primary`)
";

my $CMD_XSEL = "xsel";

my $MODE_CLIPBOARD = "clipboard";
my $MODE_SELECTION = "selection";

sub checkExec($);

sub main(@){
  my $text = undef;
  my $mode = $MODE_CLIPBOARD;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-c)$/){
      $mode = $MODE_CLIPBOARD;
    }elsif($arg =~ /^(-s)$/){
      $mode = $MODE_SELECTION;
    }elsif(not defined $text){
      $text = $arg;
    }else{
      die $USAGE;
    }
  }

  my @cmd;

  if(checkExec($CMD_XSEL)){
    if($mode eq $MODE_CLIPBOARD){
      @cmd = ($CMD_XSEL, "--input", "--clipboard");
    }elsif($mode eq $MODE_SELECTION){
      @cmd = ($CMD_XSEL, "--input", "--primary");
    }else{
      die "ERROR: unknown mode $mode\n";
    }
  }else{
    die "ERROR: could not find command $CMD_XSEL\n";
  }

  if(defined $text){
    open FH, "|-", @cmd or die "ERROR: `@cmd` failed\n$!\n";
    print FH $text;
    close FH;
  }else{
    exec @cmd;
  }
}

sub checkExec($){
  my ($exec) = @_;
  system "command -v '$exec' >/dev/null 2>/dev/null";
  return $? == 0 ? 1 : 0;
}

&main(@ARGV);
