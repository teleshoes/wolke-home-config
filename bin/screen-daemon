#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use IO::Handle;
use Time::HiRes qw(time sleep);

my $SCREEN_DAEMON_CONF_FILE = "$ENV{HOME}/.config/screen-daemon.conf";
my $LOG_BASE_DIR = "$ENV{HOME}/.cache/logs";

my $EXEC = basename $0;

my $PRINT_TO_STDOUT = 1;

my @INTERVAL_TIME_UNITS = qw(ms s m min h d);
my $INTERVAL_TIME_UNIT_REGEX = join("|", @INTERVAL_TIME_UNITS);
my $INTERVAL_REGEX = "(0|\\d+($INTERVAL_TIME_UNIT_REGEX))";

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC
  $EXEC --list
    -read entries from config file $SCREEN_DAEMON_CONF_FILE
    -read screen sessions from `screen -list`
    -for each entry in the config:
      -print \"NAME on\" if a screen session named NAME exists
      -print \"NAME off\" otherwise

  $EXEC --list-names
    -read entries from config file $SCREEN_DAEMON_CONF_FILE
    -print \"NAME\" for each entry in the config

  $EXEC NAME
  $EXEC NAME --ensure-attach
    -$EXEC NAME --ensure
    -$EXEC NAME --attach

  $EXEC NAME --status
    -read screen sessions from `screen -list`
    -if a screen session named NAME exists, print \"on\". otherwise print \"off\"

  $EXEC NAME --toggle
    -if `$EXEC NAME --status` is \"off\"
      -run `$EXEC NAME --start
    -if `$EXEC NAME --status` is \"on\"
      -run `$EXEC NAME --stop

  $EXEC NAME --start
    read $SCREEN_DAEMON_CONF_FILE and find a line of the form:
      NAME DELAY_INTERVAL TIMEOUT_INTERVAL CMD [ARG ARG ..]
    invoke $EXEC NAME --start DELAY_INTERVAL TIMEOUT_INTERVAL CMD [ARG ARG ..]

  $EXEC NAME --start DELAY_INTERVAL TIMEOUT_INTERVAL CMD [ARG ARG ..]
  $EXEC NAME --start-run DELAY_INTERVAL TIMEOUT_INTERVAL CMD [ARG ARG ..]
    -quit screen session 'NAME'
      screen -S NAME -X quit
    -start a screen session named 'NAME' that runs `$EXEC --run`
      screen -S NAME -d -m \\
        $EXEC NAME --run DELAY_INTERVAL TIMEOUT_INTERVAL \\
          CMD [ARG ARG ..]

  $EXEC NAME --run DELAY_INTERVAL TIMEOUT_INTERVAL CMD [ARG ARG ..]
    -generate <LOG_FILE>
    -invoke `daemon-run` with CMD, TIMEOUT_INTERVAL, NAME, and LOG_FILE
      -create symlink to LOG_FILE at $LOG_BASE_DIR/<NAME>/latest
      -run 'CMD [ARG ARG ..]', appending STDOUT/STDERR to LOG_FILE
      -if CMD takes longer than TIMEOUT_INTERVAl, kill it (never kill it if TIMEOUT_INTERVAL is 0)
    -wait DELAY_INTERVAL and re-run `daemon-run`, forever
      (if DELAY_INTERVAL == 0, exit after first run)

      LOG_FILE = $LOG_BASE_DIR/<NAME>/<START_MILLIS>.log
      START_MILLIS = time in milliseconds at initial start

  $EXEC NAME --ensure
    -if `$EXEC NAME --status` is \"off\"
      -run `$EXEC NAME --start
    -otherwise, do nothing

  $EXEC NAME --attach
    -attach to screen session 'NAME'
      screen -S NAME -d r

  $EXEC NAME --stop|--exit|--quit|--kill
    -quit screen session 'NAME'
      screen -S NAME -X quit

  $EXEC NAME --tail
    -tail the 'latest' log file symlink for NAME
      tail -F $LOG_BASE_DIR/NAME/latest

  $EXEC --bash-complete COMP_LINE COMP_POINT
    print a list of words for bash completion, one per line
    COMP_LINE  - the full cmdline as a string
    COMP_POINT - the cursor position in the cmdline

  NAME
    screen session name, must be letters, numbers, underscores, or hyphens only

  DELAY_INTERVAL = 0 | <POSITIVE_INT><TIME_UNIT>
    time to wait between commands (e.g.: 30s, 5m, 1500ms),
      or '0' for run only once
    if delay and timeout are non-zero, the maximum delay between commands is:
      DELAY_INTERVAL + TIMEOUT_INTERVAL

  TIMEOUT_INTERVAL = 0 | <POSITIVE_INT><TIME_UNIT>
    time to wait before killing command (e.g.: 30s, 5m, 1500ms),
      or '0' for never kill command
    if delay and timeout are non-zero, the maximum delay between commands is:
      DELAY_INTERVAL + TIMEOUT_INTERVAL

  POSITIVE_INT
    a positive (non-zero, non-negative) integer

  TIME_UNIT
    ms  => milliseconds
    s   => seconds
    m   => minutes
    min => minutes
    h   => hours
    d   => days (24 hours)
";

my $CMD_LIST = "list";
my $CMD_LIST_NAMES = "list-names";
my $CMD_ENSURE_ATTACH = "ensure-attach";
my $CMD_STATUS = "status";
my $CMD_TOGGLE = "toggle";
my $CMD_START = "start";
my $CMD_START_RUN = "start-run";
my $CMD_RUN = "run";
my $CMD_ENSURE = "ensure";
my $CMD_ATTACH = "attach";
my $CMD_STOP = "stop";
my $CMD_TAIL = "tail";

sub getAllStatusesByName();
sub getStatus($);
sub getNames();
sub toggle($);
sub ensure($);
sub parseScreenDaemonConf();
sub getRunningScreenNames();
sub getIntervalMillis($);
sub checkRunArgs($$$@);
sub daemonRunCommandForever($$$@);
sub cmdTimeout($$@);
sub logMsg($$);
sub shellParse($);
sub bashComplete($$);

sub main(@){
  if(@_ == 3 and $_[0] =~ /^--bash-complete$/ and $_[2] =~ /^\d+$/){
    print map {"$_\n"} bashComplete($_[1], $_[2]);
    exit 0;
  }

  my $cmd = undef;
  my $name = undef;
  my $runDelayInterval = undef;
  my $runTimeoutInterval = undef;
  my @runCmd;
  while(@_ > 0){
    my $arg = shift @_;
    if(not defined $cmd and $arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif(not defined $cmd and $arg =~ /^(--list)$/){
      $cmd = $CMD_LIST;
    }elsif(not defined $cmd and $arg =~ /^(--list-names)$/){
      $cmd = $CMD_LIST_NAMES;
    }elsif(not defined $cmd and $arg =~ /^(--ensure-attach)$/){
      $cmd = $CMD_ENSURE_ATTACH;
    }elsif(not defined $cmd and $arg =~ /^(--status)$/){
      $cmd = $CMD_STATUS;
    }elsif(not defined $cmd and $arg =~ /^(--toggle)$/){
      $cmd = $CMD_TOGGLE;
    }elsif(not defined $cmd and $arg =~ /^(--start)$/){
      $cmd = $CMD_START;
    }elsif(not defined $cmd and $arg =~ /^(--start-run)$/){
      $cmd = $CMD_START_RUN;
    }elsif(not defined $cmd and $arg =~ /^(--run)$/){
      $cmd = $CMD_RUN;
    }elsif(not defined $cmd and $arg =~ /^(--ensure)$/){
      $cmd = $CMD_ENSURE;
    }elsif(not defined $cmd and $arg =~ /^(--attach)$/){
      $cmd = $CMD_ATTACH;
    }elsif(not defined $cmd and $arg =~ /^(--stop|--exit|--quit|--kill)$/){
      $cmd = $CMD_STOP;
    }elsif(not defined $cmd and $arg =~ /^(--tail)$/){
      $cmd = $CMD_TAIL;
    }elsif(not defined $name and $arg =~ /^[a-zA-Z0-9_\-]+$/){
      $name = $arg;
    }elsif(not defined $runDelayInterval and $arg =~ /^($INTERVAL_REGEX)$/){
      $runDelayInterval = $arg;
      $cmd = $CMD_START_RUN if defined $cmd and $cmd eq $CMD_START;
    }elsif(not defined $runTimeoutInterval and $arg =~ /^($INTERVAL_REGEX)$/){
      $runTimeoutInterval = $arg;
      $cmd = $CMD_START_RUN if defined $cmd and $cmd eq $CMD_START;
    }elsif(defined $runDelayInterval and defined $runTimeoutInterval){
      push @runCmd, $arg;
      $cmd = $CMD_START_RUN if defined $cmd and $cmd eq $CMD_START;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if(not defined $cmd){
    if(defined $name){
      $cmd = $CMD_ENSURE_ATTACH;
    }else{
      $cmd = $CMD_LIST;
    }
  }

  if($cmd =~ /^($CMD_LIST|$CMD_LIST_NAMES)$/){
    die "$USAGE\nERROR: cannot give NAME for cmd $cmd\n" if defined $name;
  }else{
    die "$USAGE\nERROR: missing NAME for cmd $cmd\n" if not defined $name;
  }

  if(defined $runDelayInterval or defined $runTimeoutInterval or @runCmd > 0){
    if($cmd !~ /^($CMD_START_RUN|$CMD_RUN)$/){
      die "$USAGE\nERROR: cannot specify DELAY_INTERVAL/TIMEOUT_INTERVAL/CMD for $cmd\n";
    }elsif(not defined $runDelayInterval){
      die "$USAGE\nERROR: missing DELAY_INTERVAL for $cmd\n";
    }elsif(not defined $runTimeoutInterval){
      die "$USAGE\nERROR: missing TIMEOUT_INTERVAL for $cmd\n";
    }elsif(@runCmd == 0){
      die "$USAGE\nERROR: missing CMD for $cmd\n";
    }
  }

  if($cmd eq $CMD_LIST){
    my $allStatusesByName = getAllStatusesByName();
    for my $name(sort keys %$allStatusesByName){
      print "$name $$allStatusesByName{$name}\n";
    }
  }elsif($cmd eq $CMD_LIST_NAMES){
    print "$_\n" foreach getNames();
  }elsif($cmd eq $CMD_STATUS){
    print getStatus($name) . "\n";
  }elsif($cmd eq $CMD_ENSURE_ATTACH){
    system $0, $name, "--ensure";
    exec $0, $name, "--attach";
  }elsif($cmd eq $CMD_ENSURE){
    ensure($name);
  }elsif($cmd eq $CMD_TOGGLE){
    toggle($name);
  }elsif($cmd eq $CMD_START){
    my $conf = ${parseScreenDaemonConf()}{$name};
    if(not defined $conf){
      die "no entry in $SCREEN_DAEMON_CONF_FILE found for $name\n";
    }
    my $delayInterval = $$conf{delayInterval};
    my $timeoutInterval = $$conf{timeoutInterval};
    my @cmd = shellParse $$conf{cmdStr};
    print "$name --start $delayInterval $timeoutInterval @cmd\n";

    checkRunArgs $name, $delayInterval, $timeoutInterval, @cmd;
    system "screen -S $name -X quit >/dev/null";
    system "screen", "-S", $name, "-d", "-m",
      $0, $name, "--run", $delayInterval, $timeoutInterval, @cmd;
  }elsif($cmd eq $CMD_START_RUN){
    checkRunArgs $name, $runDelayInterval, $runTimeoutInterval, @runCmd;
    system "screen -S $name -X quit >/dev/null";
    system "screen", "-S", $name, "-d", "-m",
      $0, $name, "--run", $runDelayInterval, $runTimeoutInterval, @runCmd;
  }elsif($cmd eq $CMD_RUN){
    checkRunArgs $name, $runDelayInterval, $runTimeoutInterval, @runCmd;

    my $delayMillis = getIntervalMillis $runDelayInterval;
    my $timeoutMillis = getIntervalMillis $runTimeoutInterval;
    daemonRunCommandForever $name, $delayMillis, $timeoutMillis, @runCmd;
  }elsif($cmd eq $CMD_TAIL){
    exec "tail", "-F", "$LOG_BASE_DIR/$name/latest";
  }elsif($cmd eq $CMD_STOP){
    system "screen", "-S", $name, "-X", "quit";
  }elsif($cmd eq $CMD_ATTACH){
    system "screen", "-S", $name, "-d", "-r"
  }else{
    die "$USAGE\nERROR: unknown command $cmd\n";
  }
}

sub getAllStatusesByName(){
  my @configNames = sort keys %{parseScreenDaemonConf()};
  my @screenNames = getRunningScreenNames();
  my %okScreenNames = map {$_ => 1} @screenNames;
  my $statusesByName = {};
  for my $name(@configNames){
    my $status = defined $okScreenNames{$name} ? "on" : "off";
    $$statusesByName{$name} = $status;
  }
  return $statusesByName;
}

sub getStatus($){
  my ($name) = @_;
  my @screenNames = getRunningScreenNames();
  my %okScreenNames = map {$_ => 1} @screenNames;
  return defined $okScreenNames{$name} ? "on" : "off";
}

sub getNames(){
  return sort keys %{parseScreenDaemonConf()};
}

sub toggle($){
  my ($name) = @_;
  my $status = `$0 $name --status`;
  if($status =~ /off/){
    print "starting\n";
    exec $0, $name, "--start";
  }elsif($status =~ /on/){
    print "stopping\n";
    exec $0, $name, "--stop";
  }
}

sub ensure($){
  my ($name) = @_;
  my $status = `$0 $name --status`;
  if($status =~ /off/){
    print "starting\n";
    exec $0, $name, "--start";
  }else{
    print "already running\n";
  }
}

sub parseScreenDaemonConf(){
  my $confs = {};
  return $confs if not -f $SCREEN_DAEMON_CONF_FILE;
  for my $line(`cat $SCREEN_DAEMON_CONF_FILE`){
    chomp $line;
    next if $line =~ /^\s*(#.*)?$/; #skip empty or comment-only lines
    if($line =~ /^([a-zA-Z0-9_\-]+)\s+(\d+\w*)\s+(\d+\w*)\s+(.+)$/){
      my ($name, $delayInterval, $timeoutInterval, $cmdStr) = ($1, $2, $3, $4);
      if(not defined getIntervalMillis($delayInterval)){
        die "invalid delay interval: $delayInterval\n";
      }
      if(not defined getIntervalMillis($timeoutInterval)){
        die "invalid delay interval: $timeoutInterval\n";
      }
      $$confs{$name} = {
        name => $name,
        delayInterval => $delayInterval,
        timeoutInterval => $timeoutInterval,
        cmdStr => $cmdStr,
      };
    }else{
      die "malformed conf line: $line\n";
    }
  }
  return $confs;
}

sub getRunningScreenNames(){
  my @screenLines = `screen -list`;
  my @names;
  for my $line(@screenLines){
    if($line =~ /^\s+\d+\.([a-zA-Z0-9_\-]+)\s+/){
      push @names, $1;
    }
  }
  return @names;
}

sub getIntervalMillis($){
  my ($interval) = @_;
  if($interval =~ /^0$/){
    return 0;
  }elsif($interval =~ /^(\d+)($INTERVAL_TIME_UNIT_REGEX)$/){
    my ($val, $unit) = ($1, $2);

    if($unit eq "ms"){
      return int($val + 0.5);
    }elsif($unit eq "s"){
      return int($val*1000.0 + 0.5);
    }elsif($unit eq "m" or $unit eq "min"){
      return int($val*1000.0*60.0 + 0.5);
    }elsif($unit eq "h"){
      return int($val*1000.0*60.0*60.0 + 0.5);
    }elsif($unit eq "d"){
      return int($val*1000.0*60.0*60.0*24.0 + 0.5);
    }else{
      return undef;
    }
  }else{
    return undef;
  }
}

sub checkRunArgs($$$@){
  my ($name, $delayInterval, $timeoutInterval, @cmd) = @_;
  if($name !~ /^[a-zA-Z0-9_\-]+$/){
    die "$USAGE\ninvalid name: $name\n";
  }elsif(not defined getIntervalMillis($delayInterval)){
    die "$USAGE\ninvalid delayInterval: $delayInterval\n"
  }elsif(not defined getIntervalMillis($delayInterval)){
    die "$USAGE\ninvalid timeoutInterval: $timeoutInterval\n"
  }elsif(@cmd == 0){
    die "$USAGE\nmissing CMD\n";
  }
}

sub daemonRunCommandForever($$$@){
  my ($name, $delayMillis, $timeoutMillis, @cmd) = @_;
  my $nowMillis = int(time * 1000.0);
  my $logFileName = "$name-$nowMillis.log";

  while(1){
    system "daemon-run",
      "--name=$name",
      $timeoutMillis > 0 ? ("--timeout=${timeoutMillis}ms") : (),
      "--logfile-name=$logFileName",
      "--verbose",
      @cmd;


    if($delayMillis > 0){
      sleep $delayMillis/1000.0;
    }else{
      last;
    }
  }
}

sub shellParse($){
  my ($str) = @_;
  my @words = `for x in $str; do echo \$x; done`;
  chomp foreach @words;
  return @words;
}

sub bashComplete($$){
  my ($cmdLine, $pos) = @_;
  my $cmd = substr $cmdLine, 0, $pos;
  my $isNewWord = $cmd =~ /\s$/;
  $cmd =~ s/^\s+//;
  $cmd =~ s/\s+$//;

  my @words = split /\s+/, $cmd;
  shift @words;
  my $curWord = pop @words if not $isNewWord;

  my @rootOpts = qw(
    -h --help
    --list
    --list-names
    --bash-complete
  );

  my @nameOpts = qw(
    --status
    --toggle
    --start
    --run
    --ensure
    --attach
    --stop --exit --quit --kill
    --tail
  );

  if(@words == 0){
    my @configNames = sort keys %{parseScreenDaemonConf()};
    return (@rootOpts, @configNames);
  }elsif(@words >= 1 and $words[0] =~ /^[a-zA-Z0-9_\-]+$/){
    if(@words == 1){
      return @nameOpts;
    }else{
      my $cmd = $words[1];
      if($cmd =~ /^(--start|--run)$/){
        return qw( 0 ) if @words == 2;    # DELAY_INTERVAL
        return qw( 0 ) if @words == 3;    # TIMEOUT_INTERVAL
        return qw( echo ) if @words == 4; # COMMAND
        return qw( hello world ) if @words == 5; # COMMAND
      }
    }
  }

  return ();
}

&main(@ARGV);
