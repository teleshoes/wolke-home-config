#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $XFT_TYPEFACE = "Inconsolata";
my $DEFAULT_PX = 20;

my $EXEC = basename $0;

my $USAGE = "USAGE:
  $EXEC -h | --help
    show this message

  $EXEC FONT_SIZE_PX
    set the font size in pixels using typeface: $XFT_TYPEFACE
      prints this <FONT_STRING> to the console:
        xft:$XFT_TYPEFACE:pixelsize=<FONT_SIZE_PX>
      using:
        echo -ne '\\033]710;<FONT_STRING>\\033\\\\'

  $EXEC
    same as: $EXEC $DEFAULT_PX

  $EXEC --scale=BASE_FONT_SIZE_PX
    -use `resconfig` to fetch scaling factors: <SCALE_PX>, <SCALE_SIZE>, <SCALE_DIST>
      -use '0' for unknown scaling factors
      -use '0' for negative scaling factors (do not decrease base font)
    -calculate <TARGET_FONT_SIZE>:
      <BASE_FONT_SIZE_PX> + 6*(<SCALE_PX) + 2*(SCALE_SIZE) + 2*(SCALE_DIST)
    -set font size as in:
      $EXEC <TARGET_FONT_SIZE>
";

sub scaleFontSize($);
sub getResconfigNonNegativeScale($);

sub main(@){
  my $px = $DEFAULT_PX;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(\d+|\d*\.\d+)$/){
      $px = $1;
    }elsif($arg =~ /^--scale=(\d+|\d*\.\d+)$/){
      $px = scaleFontSize($1);
    }else{
      die $USAGE;
    }
  }
  my $font = "xft:$XFT_TYPEFACE:pixelsize=$px";
  my @cmd = ("echo", "-ne", "\\033]710;$font\\033\\\\");
  print "@cmd\n";
  system @cmd;
}

sub scaleFontSize($){
  my ($baseFontSizePx) = @_;
  my $scalePx = getResconfigNonNegativeScale("px");
  my $scaleSize = getResconfigNonNegativeScale("size");
  my $scaleDist = getResconfigNonNegativeScale("dist");

  return $baseFontSizePx + 6*$scalePx + 2*$scaleSize + 2*$scaleDist;
}

sub getResconfigNonNegativeScale($){
  my ($scaleType) = @_;
  my $val = `resconfig --get-scale-$scaleType 2>/dev/null`;
  chomp $val;
  if($val =~ /^(\d+|\d*\.\d+)$/){
    return $1;
  }else{
    return 0;
  }
}

&main(@ARGV);
