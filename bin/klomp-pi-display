#!/usr/bin/perl
use strict;
use warnings;

sub isKlompRunning();
sub isRhythmboxRunning();
sub checkPlayerRunning($);
sub checkBigtextRunning();
sub checkIpmagic();
sub run(@);

my $IPMAGIC_NAME = "raspi";
my $IPMAGIC_USER = "pi";

my @QDBUS_RHYTHMBOX_CMD = qw(qdbus
  org.mpris.MediaPlayer2.rhythmbox
  /org/mpris/MediaPlayer2
  org.freedesktop.DBus.Properties.Get
  org.mpris.MediaPlayer2.Player
  PlaybackStatus
);

my $USAGE = "Usage:
  $0 -h|--help
    show this message

  $0 [OPTS] --on
    -stop klomp-bigtext
    -ensure `ipmagic $IPMAGIC_NAME` is reachable
    -on $IPMAGIC_NAME:
      -run: tv-gpio on
      -kill bigtext
      -run: xscreensaver-command -activate
    -start klomp-bigtext with --ipmagic=$IPMAGIC_NAME

  $0 [OPTS] --off
    -stop klomp-bigtext
    -ensure `ipmagic $IPMAGIC_NAME` is reachable
    -on $IPMAGIC_NAME:
      -run: tv-gpio off
      -kill bigtext

  $0 [OPTS]
  $0 [OPTS] --ensure
    -if klomplayer is running:
      -same as $0 --on
    -otherwise:
      -same as $0 --off

  OPTS
    --player=rhythmbox | --rhythmbox
    --player=rb | --rb
      -for --on, pass --rhythmbox to klomp-bigtext
      -for --ensure, check if rhythmbox is playing:
        -use MediaPlayer2 dbus interface with `qdbus`
        @QDBUS_RHYTHMBOX_CMD
    --player=klomp | --klomp
      do not use rhythmbox (this is the default)
    --player=auto | --auto-player | --player-auto
      -if klomplayer is running:
        same as --klomp
      -if rhythmbox is running:
        same as --rhythmbox
      -otherwise:
        same as --klomp

    --no-restart
      for --on (or --ensure when klomplayer is running):
        if bigtext is already running, do nothing
      for --off:
        if bigtext is NOT running, do nothing (skip tv-gpio and screensaver)
    --restart
      allow restart if bigtext already running (this is the default)
";

my $MODE_ON = "on";
my $MODE_OFF = "off";
my $MODE_ENSURE = "ensure";

my $PLAYER_KLOMP = "klomp";
my $PLAYER_RHYTHMBOX = "rhythmbox";
my $PLAYER_AUTO = "auto";

sub main(@){
  my $mode = $MODE_ENSURE;
  my $player = $PLAYER_KLOMP;
  my $allowRestart = 1;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--on)$/){
      $mode = $MODE_ON;
    }elsif($arg =~ /^(--off)$/){
      $mode = $MODE_OFF;
    }elsif($arg =~ /^(--ensure)$/){
      $mode = $MODE_ENSURE;
    }elsif($arg =~ /^(--player=rhythmbox|--player=rb|--rhythmbox|--rb)$/){
      $player = $PLAYER_RHYTHMBOX;
    }elsif($arg =~ /^(--player=klomp|--klomp)$/){
      $player = $PLAYER_KLOMP;
    }elsif($arg =~ /^(--player=auto|--auto-player|--player-auto)$/){
      $player = $PLAYER_AUTO;
    }elsif($arg =~ /^(--no-restart)$/){
      $allowRestart = 0;
    }elsif($arg =~ /^(--restart)$/){
      $allowRestart = 1;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($player eq $PLAYER_AUTO){
    if(isKlompRunning()){
      $player = $PLAYER_KLOMP;
    }elsif(isRhythmboxRunning()){
      $player = $PLAYER_RHYTHMBOX;
    }else{
      $player = $PLAYER_KLOMP;
    }
  }

  if($mode eq $MODE_ENSURE){
    if(checkPlayerRunning($player)){
      my $msg = "";
      $msg = "klomplayer running" if $player eq $PLAYER_KLOMP;
      $msg = "rhythmbox playing" if $player eq $PLAYER_RHYTHMBOX;
      print "#$msg: $0 --on\n";
      $mode = $MODE_ON;
    }else{
      my $msg = "";
      $msg = "klomplayer not running" if $player eq $PLAYER_KLOMP;
      $msg = "rhythmbox not playing" if $player eq $PLAYER_RHYTHMBOX;
      print "#$msg: $0 --off\n";
      $mode = $MODE_OFF;
    }
  }

  if($mode eq $MODE_OFF){
    run "pkill", "-9", "-f", "klomp-bigtext";
    if(checkIpmagic()){
      if(not $allowRestart and not checkBigtextRunning()){
        print "bigtext already off\n";
        exit 0;
      }
      system "ipmagic", $IPMAGIC_NAME, "-u", $IPMAGIC_USER, "
        set -x
        tv-gpio off
        pkill '^/usr/bin/perl /usr/bin/bigtext' -f
        bigtext -k
        xscreensaver-command -activate
      ";
    }
  }elsif($mode eq $MODE_ON){
    if(checkIpmagic()){
      if(not $allowRestart and checkBigtextRunning()){
        print "bigtext already running\n";
        exit 0;
      }
      run "pkill", "-9", "-f", "klomp-bigtext";
      system "ipmagic", $IPMAGIC_NAME, "-u", $IPMAGIC_USER, "
        set -x
        tv-gpio on
        pkill '^/usr/bin/perl /usr/bin/bigtext' -f
        bigtext -k
      ";
      my $playerArg = "";
      $playerArg = "--klomp" if $player eq $PLAYER_KLOMP;
      $playerArg = "--rhythmbox" if $player eq $PLAYER_RHYTHMBOX;
      my $cmd = ""
        . "nohup klomp-bigtext"
        . " $playerArg"
        . " --ipmagic=$IPMAGIC_NAME --ipmagic-user=$IPMAGIC_USER"
        . " >/dev/null 2>/dev/null"
        . " &";
      run $cmd;
    }
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub isKlompRunning(){
  system "pkill", "-0", "-x", "klomplayer";
  return ($? == 0) ? 1 : 0;
}

sub isRhythmboxRunning(){
  system "pkill", "-0", "-x", "rhythmbox";
  return ($? == 0) ? 1 : 0;
}

sub checkPlayerRunning($){
  my ($player) = @_;
  if($player eq $PLAYER_KLOMP){
    return isKlompRunning();
  }elsif($player eq $PLAYER_RHYTHMBOX){
    if(not isRhythmboxRunning()){
      return 0;
    }

    my $status = `@QDBUS_RHYTHMBOX_CMD 2>&1`;
    my $exitCode = $? >> 8;
    chomp $status;
    if($status =~ /Cannot find.*in object/){
      return 0;
    }elsif($exitCode != 0){
      return 0;
    }elsif($status =~ /(Playing)/){
      return 1;
    }elsif($status =~ /(Paused)/){
      return 0;
    }elsif($status =~ /(Stopped)/){
      return 0;
    }else{
      return 0;
    }
  }else{
    die "ERROR: unknown player $player\n";
  }
}

sub checkBigtextRunning(){
  system "ipmagic", $IPMAGIC_NAME, "-u", $IPMAGIC_USER,
    "pgrep -f '^\\S*python\\S*\\s+\\S*qtbigtext.py\\S*' >/dev/null 2>/dev/null";
  my $exitCode = $? >> 8;
  return $exitCode == 0 ? 1 : 0;
}

sub checkIpmagic(){
  run "execPing --timeout=5 --ipmagic=$IPMAGIC_NAME";
  return $? == 0;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
