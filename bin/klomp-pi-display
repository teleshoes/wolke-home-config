#!/usr/bin/perl
use strict;
use warnings;

sub checkRunning();
sub checkIpmagic();
sub run(@);

my $IPMAGIC_NAME = "raspi";
my $IPMAGIC_USER = "pi";

my $USAGE = "Usage:
  $0 -h|--help
    show this message

  $0 --on
    -stop klomp-bigtext
    -ensure `ipmagic $IPMAGIC_NAME` is reachable
    -on $IPMAGIC_NAME:
      -run: tv-gpio on
      -kill bigtext
      -run: xscreensaver-command -activate
    -start klomp-bigtext with --ipmagic=$IPMAGIC_NAME

  $0 --off
    -stop klomp-bigtext
    -ensure `ipmagic $IPMAGIC_NAME` is reachable
    -on $IPMAGIC_NAME:
      -run: tv-gpio off
      -kill bigtext

  $0
  $0 --ensure
    -if klomplayer is running:
      -same as $0 --on
    -otherwise:
      -same as $0 --off
";

my $MODE_ON = "on";
my $MODE_OFF = "off";
my $MODE_ENSURE = "ensure";

sub main(@){
  my $mode = $MODE_ENSURE;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--on)$/){
      $mode = $MODE_ON;
    }elsif($arg =~ /^(--off)$/){
      $mode = $MODE_OFF;
    }elsif($arg =~ /^(--ensure)$/){
      $mode = $MODE_ENSURE;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($mode eq $MODE_ENSURE){
    if(checkRunning()){
      print "#klomplayer running: $0 --on\n";
      $mode = $MODE_ON;
    }else{
      print "#klomplayer not running: $0 --off\n";
      $mode = $MODE_OFF;
    }
  }

  if($mode eq $MODE_OFF){
    run "pkill", "-9", "-f", "klomp-bigtext";
    if(checkIpmagic()){
      system "ipmagic", $IPMAGIC_NAME, "-u", $IPMAGIC_USER, "
        set -x
        tv-gpio off
        pkill '^/usr/bin/perl /usr/bin/bigtext' -f
        bigtext -k
        xscreensaver-command -activate
      ";
    }
  }elsif($mode eq $MODE_ON){
    run "pkill", "-9", "-f", "klomp-bigtext";
    if(checkIpmagic()){
      system "ipmagic", $IPMAGIC_NAME, "-u", $IPMAGIC_USER, "
        set -x
        tv-gpio on
        pkill '^/usr/bin/perl /usr/bin/bigtext' -f
        bigtext -k
      ";
      run "nohup klomp-bigtext --ipmagic=$IPMAGIC_NAME --ipmagic-user=$IPMAGIC_USER >/dev/null 2>/dev/null &";
    }
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub checkRunning(){
  system "pkill", "-0", "klomplayer";
  if($? == 0){
    return 1;
  }else{
    return 0;
  }
}

sub checkIpmagic(){
  run "execPing --timeout=5 --ipmagic=$IPMAGIC_NAME";
  return $? == 0;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
