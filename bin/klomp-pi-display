#!/usr/bin/perl
use strict;
use warnings;

sub checkRunning($);
sub checkIpmagic();
sub run(@);

my $IPMAGIC_NAME = "raspi";
my $IPMAGIC_USER = "pi";

my @QDBUS_RHYTHMBOX_CMD = qw(qdbus
  org.mpris.MediaPlayer2.rhythmbox
  /org/mpris/MediaPlayer2
  org.freedesktop.DBus.Properties.Get
  org.mpris.MediaPlayer2.Player
  PlaybackStatus
);

my $USAGE = "Usage:
  $0 -h|--help
    show this message

  $0 [OPTS] --on
    -stop klomp-bigtext
    -ensure `ipmagic $IPMAGIC_NAME` is reachable
    -on $IPMAGIC_NAME:
      -run: tv-gpio on
      -kill bigtext
      -run: xscreensaver-command -activate
    -start klomp-bigtext with --ipmagic=$IPMAGIC_NAME

  $0 [OPTS] --off
    -stop klomp-bigtext
    -ensure `ipmagic $IPMAGIC_NAME` is reachable
    -on $IPMAGIC_NAME:
      -run: tv-gpio off
      -kill bigtext

  $0 [OPTS]
  $0 [OPTS] --ensure
    -if klomplayer is running:
      -same as $0 --on
    -otherwise:
      -same as $0 --off

  OPTS
    --rhythmbox
      -for --on, pass --rhythmbox to klomp-bigtext
      -for --ensure, check if rhythmbox is playing:
        -use MediaPlayer2 dbus interface with `qdbus`
        @QDBUS_RHYTHMBOX_CMD

    --klomp
      do not use rhythmbox (this is the default)
";

my $MODE_ON = "on";
my $MODE_OFF = "off";
my $MODE_ENSURE = "ensure";

sub main(@){
  my $mode = $MODE_ENSURE;
  my $useRhythmbox = 0;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--on)$/){
      $mode = $MODE_ON;
    }elsif($arg =~ /^(--off)$/){
      $mode = $MODE_OFF;
    }elsif($arg =~ /^(--ensure)$/){
      $mode = $MODE_ENSURE;
    }elsif($arg =~ /^(--rhythmbox)$/){
      $useRhythmbox = 1;
    }elsif($arg =~ /^(--klomp)$/){
      $useRhythmbox = 0;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($mode eq $MODE_ENSURE){
    if(checkRunning($useRhythmbox)){
      my $msg = $useRhythmbox ? "rhythmbox playing" : "klomplayer running";
      print "#$msg: $0 --on\n";
      $mode = $MODE_ON;
    }else{
      my $msg = $useRhythmbox ? "rhythmbox not playing" : "klomplayer not running";
      print "#$msg: $0 --off\n";
      $mode = $MODE_OFF;
    }
  }

  if($mode eq $MODE_OFF){
    run "pkill", "-9", "-f", "klomp-bigtext";
    if(checkIpmagic()){
      system "ipmagic", $IPMAGIC_NAME, "-u", $IPMAGIC_USER, "
        set -x
        tv-gpio off
        pkill '^/usr/bin/perl /usr/bin/bigtext' -f
        bigtext -k
        xscreensaver-command -activate
      ";
    }
  }elsif($mode eq $MODE_ON){
    run "pkill", "-9", "-f", "klomp-bigtext";
    if(checkIpmagic()){
      system "ipmagic", $IPMAGIC_NAME, "-u", $IPMAGIC_USER, "
        set -x
        tv-gpio on
        pkill '^/usr/bin/perl /usr/bin/bigtext' -f
        bigtext -k
      ";
      my $cmd = ""
        . "nohup klomp-bigtext"
        . " " . ($useRhythmbox ? "--rhythmbox" : "--klomp")
        . " --ipmagic=$IPMAGIC_NAME --ipmagic-user=$IPMAGIC_USER"
        . " >/dev/null 2>/dev/null"
        . " &";
      run $cmd;
    }
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub checkRunning($){
  my ($useRhythmbox) = @_;
  if($useRhythmbox){
    my $status = `@QDBUS_RHYTHMBOX_CMD 2>&1`;
    my $exitCode = $? >> 8;
    chomp $status;
    if($status =~ /Cannot find.*in object/){
      return 0;
    }elsif($exitCode != 0){
      return 0;
    }elsif($status =~ /(Playing)/){
      return 1;
    }elsif($status =~ /(Paused)/){
      return 0;
    }elsif($status =~ /(Stopped)/){
      return 0;
    }else{
      return 0;
    }
  }else{
    system "pkill", "-0", "klomplayer";
    if($? == 0){
      return 1;
    }else{
      return 0;
    }
  }
}

sub checkIpmagic(){
  run "execPing --timeout=5 --ipmagic=$IPMAGIC_NAME";
  return $? == 0;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
