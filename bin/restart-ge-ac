#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $EXEC = basename $0;

my $TS_DEV = "x3";

my $TARGET_TEMP = 68;
my @GE_AC_COLD_ARGS = ("-p", "on", "-m", "cool", "-f", "high", "-t", $TARGET_TEMP);

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC
    monitor for GE AC to be offline (presumably because of P1 error)
      -check power usage of tasmota dev $TS_DEV
      -if >= 5W, do nothing
      -otherwise:
        -toggle $TS_DEV
        -wait up to 2min
          -toggle AC off and on every 10s or so
          -stop waiting if power usage exceeds 5W
        -run 'ge-ac @GE_AC_COLD_ARGS'
";

sub run(@);

sub main(@){
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }else{
      die "ERROR: unknown arg $arg\n";
    }
  }

  system "date";
  print "checking outlet power usage\n";
  my $out = `tasmota $TS_DEV --active`;
  chomp $out;
  if($out =~ /idle/){
     print "RESTARTING GE AC\n";
     run "tasmota", $TS_DEV, "off";
     run "sleep", 1;
     run "tasmota", $TS_DEV, "on";
     my $start = time;
     while(time - $start < 120){
       run "timeout 3 ge-ac off";
       run "timeout 3 ge-ac on";
       print "\n";
       run "sleep", 4;
       $out = `tasmota $TS_DEV --active`;
       chomp $out;
       if($out =~ /active/){
         print "  ac appears active\n";
         last;
       }
     }
     run "sleep", 1;
     run "timeout 60 ge-ac @GE_AC_COLD_ARGS";
     system "date";
     run "tasmota", $TS_DEV, "-i";
  }else{
    print "  skipping restart, $TS_DEV is not idle\n";
  }

  print "checking ac temp/mode/fan\n";
  my $acInfo = `timeout 60 ge-ac -g`;
  my ($temp, $mode, $fan);
  if($acInfo =~ /^(\d+)\S*F.*\s+(\w+)\s+(\w+)$/){
    ($temp, $mode, $fan) = ($1, $2, $3);
  }else{
    ($temp, $mode, $fan) = (-1, "", "");
  }

  if(0 <= $temp and $temp <= $TARGET_TEMP and $mode =~ /cool/i and $fan =~ /high/i){
    print "  skipping setting fan/temp/mode\n";
  }else{
    run "timeout 60 ge-ac @GE_AC_COLD_ARGS";
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
