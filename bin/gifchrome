#!/usr/bin/perl
use strict;
use warnings;
use Cwd qw(abs_path);

sub getHtml($@);
sub getGifDuration($);

my $DEFAULT_AUTOSCROLL_DELAY_MILLIS_MIN = 1000;
my $DEFAULT_AUTOSCROLL_DELAY_MILLIS_MAX = 10000;

my $tmpHtmlFile = "/tmp/gifchrome.html";

my $usage = "Usage:
  $0 -h|--help
    show this message

  $0 [OPTS] [GIF_FILE GIF_FILE ..]
    generate an HTML file with <img> tags for the indicated images
    \"src\" is \"file://<ABS_PATH>\", where ABS_PATH is the absolute filepath
    open it with `chromium --incognito`
    if no images are passed in, all *.gif and *.GIF images in the CWD are used

  OPTS:
    --dir=PREFIX
      make \"src\" in img tags \"file://<PREFIX>/<GIF_FILE>\"
      where GIF_FILE is the absolute or relative path given

    -n|--nobrowser
      dont launch chromium, print HTML to console

    --max-width=PX
      set CSS property of <img> tag \"max-width:<PX>px\"
    --min-width=PX
      set CSS property of <img> tag \"min-width:<PX>px\"
    --max-height=PX
      set CSS property of <img> tag \"max-height:<PX>px\"
    --min-height=PX
      set CSS property of <img> tag \"min-height:<PX>px\"

    -a|--autoscroll
      enable autoscroll-on-click
        -click on any image to start autoscrolling, starting with that image
          -click again on any image disable
        -each image is displayed for twice the duration of the animation
          -if img is not an animated gif or the duration cannot be read, duration is 1000ms
          -duration cannot exceed min/max
            see: --autoscroll-delay-millis-min / --autoscroll-delay-millis-max
        -then the next image is scrolled into view
        -autoscroll is disabled when no next image is available

    --autoscroll-delay-millis-min=DELAY_MILLIS
      when autoscroll is enabled:
        show each image for at least <DELAY_MILLIS> millis, regardless of animation duration
        default is ${DEFAULT_AUTOSCROLL_DELAY_MILLIS_MIN}ms

    --autoscroll-delay-millis-max=DELAY_MILLIS
      when autoscroll is enabled:
        show each image for at most <DELAY_MILLIS> millis, regardless of animation duration
        default is ${DEFAULT_AUTOSCROLL_DELAY_MILLIS_MIN}ms

    --clickbox
      include a textbox at the bottom
      when an image is clicked, put the image's filename in it
";

sub main(@){
  my $prefixDir = undef;
  my $noBrowser = 0;
  my $config = {
    singleColButton => 1,
    maxWidth => undef,
    minWidth => undef,
    maxHeight => undef,
    minHeight => undef,
    autoScroll => 0,
    autoScrollDelayMillisMin => $DEFAULT_AUTOSCROLL_DELAY_MILLIS_MIN,
    autoScrollDelayMillisMax => $DEFAULT_AUTOSCROLL_DELAY_MILLIS_MAX,
    includeClickBox => 0,
  };
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^--dir=(.+)$/){
      $prefixDir = $1;
    }elsif($arg =~ /^(-n|--nobrowser)$/){
      $noBrowser = 1;
    }elsif($arg =~ /^--max-width=(\d+)$/){
      $$config{maxWidth} = $1;
    }elsif($arg =~ /^--min-width=(\d+)$/){
      $$config{minWidth} = $1;
    }elsif($arg =~ /^--max-height=(\d+)$/){
      $$config{maxHeight} = $1;
    }elsif($arg =~ /^--min-height=(\d+)$/){
      $$config{minHeight} = $1;
    }elsif($arg =~ /^(-a|--autoscroll)$/){
      $$config{autoScroll} = 1;
    }elsif($arg =~ /^--autoscroll-delay-millis-min=(.+)$/){
      $$config{autoScrollDelayMillisMin} = $1;
    }elsif($arg =~ /^--autoscroll-delay-millis-max=(.+)$/){
      $$config{autoScrollDelayMillisMax} = $1;
    }elsif($arg =~ /^(--clickbox)$/){
      $$config{includeClickBox} = 1;
    }else{
      die $usage;
    }
  }

  if($$config{autoScrollDelayMillisMin} !~ /^(\d+)$/){
    die "ERROR: invalid min autoscroll delay millis: $$config{autoScrollDelayMillisMin}\n";
  }
  if($$config{autoScrollDelayMillisMax} !~ /^(\d+)$/){
    die "ERROR: invalid max autoscroll delay millis: $$config{autoScrollDelayMillisMax}\n";
  }

  my @gifs = @_;
  @gifs = (glob("*.gif"), glob "*.GIF") if @gifs == 0;

  for my $gif(@gifs){
    die "$usage\nFile not found: $gif\n" if not -f $gif;
  }

  my @imgPaths;
  if(defined $prefixDir){
    $prefixDir =~ s/\/$//;
    @imgPaths = map {"$prefixDir/$_"} @gifs;
  }else{
    @imgPaths = map {abs_path($_)} @gifs;
  }

  my $html = getHtml $config, @imgPaths;

  if($noBrowser){
    print $html;
  }else{
    open FH, "> $tmpHtmlFile";
    print FH $html;
    close FH;

    system "chromium", "--incognito", $tmpHtmlFile;
    system "rm", $tmpHtmlFile;
  }
}

sub getHtml($@){
  my ($config, @imgAbsPaths) = @_;

  my $html = '';
  $html .= "<html>";
  $html .= "<head>";
  $html .= "
    <style>
      .container-single-column>* {
        display:block;
      }
    </style>
    <script>
    var scrollEnabled = false;
    var singleColEnabled = false;
    var nextImgID = null;
    function addText(event) {
      var targ = event.target || event.srcElement;
      document.getElementById(\"clicked\").value += targ.src + \"\\n\";
    }
    function toggleSingleColumn(){
      singleColEnabled = !singleColEnabled;
      c = document.getElementById('container');
      if(singleColEnabled){
        c.classList.add('container-single-column');
      }else{
        c.classList.remove('container-single-column');
      }
    }
    function toggleScroll(imgID){
      scrollEnabled = !scrollEnabled;
      if(scrollEnabled){
        nextImgID = imgID;
        scrollNextImg();
      }else{
        nextImgID = null;
      }
    }
    function scrollNextImg() {
      if(scrollEnabled){
        var imgElem = document.getElementById('img'+nextImgID);
        if(imgElem == null){
          scrollEnabled = false;
          nextImgID = null;
        }else{
          imgElem.scrollIntoView({behavior: \"smooth\", block: \"start\", inline: \"nearest\"});
          nextImgID++;
          var durS = imgElem.getAttribute('data-duration-seconds');
          var delayMillis = durS * 2.0 * 1000.0;
          if(delayMillis < $$config{autoScrollDelayMillisMin}){
            delayMillis = $$config{autoScrollDelayMillisMin};
          }
          if(delayMillis > $$config{autoScrollDelayMillisMax}){
            delayMillis = $$config{autoScrollDelayMillisMax};
          }
          setTimeout('scrollNextImg()', delayMillis);
        }
      }
    }
    </script>
  ";
  $html .= "<body>";
  $html .= ""
           . "<button"
           . " onclick='toggleSingleColumn()'"
           . " style='height: 100px; width:100%'"
           . ">"
           . "SINGLE COL"
           . "</button>\n" if $$config{singleColButton};
  $html .= "<div id='container'>";
  my $css = "";
  $css .= "max-width: $$config{maxWidth}px; " if defined $$config{maxWidth};
  $css .= "min-width: $$config{minWidth}px; " if defined $$config{minWidth};
  $css .= "max-height: $$config{maxHeight}px; " if defined $$config{maxHeight};
  $css .= "min-height: $$config{minHeight}px; " if defined $$config{minHeight};
  for(my $imgID=0; $imgID<@imgAbsPaths; $imgID++){
    my $img = $imgAbsPaths[$imgID];
    my $durS;
    if($$config{autoScroll}){
      $durS = getGifDuration $img;
      if(not defined $durS){
        $durS = 1.0;
      }
    }else{
      $durS = "";
    }

    my $fileUrl = "file://$img";
    my $onclick = "";
    $onclick .= "addText(event); " if $$config{includeClickBox};
    $onclick .= "toggleScroll($imgID); " if $$config{autoScroll};
    $html .= "<img id=\"img$imgID\" data-duration-seconds=\"$durS\" style=\"$css\" src=\"$fileUrl\" onclick=\"$onclick\"/>\n";
  }
  if($$config{includeClickBox}){
    $html .= "<textarea id=\"clicked\" rows=\"5\" cols=\"80\"></textarea>\n";
  }
  $html .= "</body>";
  $html .= "</head>";
  return $html;
}

sub getGifDuration($){
  my ($img) = @_;
  my @cmd = ("exiftool", "-Duration", $img);
  open my $cmdFH, "-|", @cmd or die "ERROR: could not run @cmd\n$!\n";
  my $out = join '', <$cmdFH>;
  close $cmdFH;

  if($out =~ /^Duration\s*:\s*(\d+|\d*\.\d+)\s*s$/m){
    my $durS = $1;
    return $durS;
  }else{
    return undef;
  }
}

&main(@ARGV);
