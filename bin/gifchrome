#!/usr/bin/perl
use strict;
use warnings;
use Cwd qw(abs_path);

sub getHtml($$@);

my $autoScrollPx = 25;
my $autoScrollMillis = 25;

my $tmpHtmlFile = "/tmp/gifchrome.html";

my $usage = "Usage:
  $0 -h|--help
    show this message

  $0 [OPTS] [GIF_FILE GIF_FILE ..]
    generate an HTML file with <img> tags for the indicated images
    \"src\" is \"file://<ABS_PATH>\", where ABS_PATH is the absolute filepath
    open it with `chromium --incognito`
    if no images are passed in, all *.gif and *.GIF images in the CWD are used

  OPTS:
    --dir=PREFIX
      make \"src\" in img tags \"file://<PREFIX>/<GIF_FILE>\"
      where GIF_FILE is the absolute or relative path given

    -n|--nobrowser
      dont launch chromium, print HTML to console

    -a|--autoscroll
      scroll page slowly when you click on an image, click again to toggle

    --clickbox
      include a textbox at the bottom
      when an image is clicked, put the image's filename in it
";

sub main(@){
  my $prefixDir = undef;
  my $noBrowser = 0;
  my $autoScroll = 0;
  my $includeClickBox = 0;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^--dir=(.+)$/){
      $prefixDir = $1;
    }elsif($arg =~ /^(-n|--nobrowser)$/){
      $noBrowser = 1;
    }elsif($arg =~ /^(-a|--autoscroll)$/){
      $autoScroll = 1;
    }elsif($arg =~ /^(--clickbox)$/){
      $includeClickBox = 1;
    }else{
      die $usage;
    }
  }

  my @gifs = @_;
  @gifs = (glob("*.gif"), glob "*.GIF") if @gifs == 0;

  for my $gif(@gifs){
    die "$usage\nFile not found: $gif\n" if not -f $gif;
  }

  my @imgPaths;
  if(defined $prefixDir){
    $prefixDir =~ s/\/$//;
    @imgPaths = map {"$prefixDir/$_"} @gifs;
  }else{
    @imgPaths = map {abs_path($_)} @gifs;
  }

  my $html = getHtml $autoScroll, $includeClickBox, @imgPaths;

  if($noBrowser){
    print $html;
  }else{
    open FH, "> $tmpHtmlFile";
    print FH $html;
    close FH;

    system "chromium", "--incognito", $tmpHtmlFile;
    system "rm", $tmpHtmlFile;
  }
}

sub getHtml($$@){
  my ($autoScroll, $includeClickBox, @imgAbsPaths) = @_;

  my $html = '';
  $html .= "<html>";
  $html .= "<head>";
  $html .= "
    <script>
    var scrollEnabled = false;
    function addText(event) {
      var targ = event.target || event.srcElement;
      document.getElementById(\"clicked\").value += targ.src + \"\\n\";
    }
    function toggleScroll(){
      scrollEnabled = !scrollEnabled;
      if(scrollEnabled){
        pageScroll();
      }
    }
    function pageScroll() {
      if(scrollEnabled){
        window.scrollBy({
          behavior: 'smooth',
          top: $autoScrollPx
        });
        scrolldelay = setTimeout('pageScroll()',$autoScrollMillis);
      }else{
        window.scrollBy({
          behavior: 'instant',
          top: 0
        });
      }
    }
    </script>
  ";
  my $onclick = "";
  $onclick .= "addText(event); " if $includeClickBox;
  $onclick .= "toggleScroll(); " if $autoScroll;
  $html .= "<body>";
  for my $img(@imgAbsPaths){
    my $fileUrl = "file://$img";
    $html .= "<img src=\"$fileUrl\" onclick=\"$onclick\"/>\n";
  }
  if($includeClickBox){
    $html .= "<textarea id=\"clicked\" rows=\"5\" cols=\"80\"></textarea>\n";
  }
  $html .= "</body>";
  $html .= "</head>";
  return $html;
}

&main(@ARGV);
