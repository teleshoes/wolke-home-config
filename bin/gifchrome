#!/usr/bin/perl
use strict;
use warnings;
use Cwd qw(abs_path);

sub getHtml($@);

my $DEFAULT_AUTOSCROLL_DELAY_MILLIS = 1000;
my $DEFAULT_AUTOSCROLL_BY_PX = 800;

my $tmpHtmlFile = "/tmp/gifchrome.html";

my $usage = "Usage:
  $0 -h|--help
    show this message

  $0 [OPTS] [GIF_FILE GIF_FILE ..]
    generate an HTML file with <img> tags for the indicated images
    \"src\" is \"file://<ABS_PATH>\", where ABS_PATH is the absolute filepath
    open it with `chromium --incognito`
    if no images are passed in, all *.gif and *.GIF images in the CWD are used

  OPTS:
    --dir=PREFIX
      make \"src\" in img tags \"file://<PREFIX>/<GIF_FILE>\"
      where GIF_FILE is the absolute or relative path given

    -n|--nobrowser
      dont launch chromium, print HTML to console

    --max-width=PX
      set CSS property of <img> tag \"max-width:<PX>px\"
    --min-width=PX
      set CSS property of <img> tag \"min-width:<PX>px\"
    --max-height=PX
      set CSS property of <img> tag \"max-height:<PX>px\"
    --min-height=PX
      set CSS property of <img> tag \"min-height:<PX>px\"

    -a|--autoscroll
      scroll page slowly when you click on an image, click again to toggle

    --autoscroll-delay=DELAY_MILLIS
      if --autoscroll is given, scroll page every <DELAY_MILLIS> millis
      (default is $DEFAULT_AUTOSCROLL_DELAY_MILLIS)

    --autoscroll-px=PX
      if --autoscroll is given, scroll page by <PX> px
      (default is $DEFAULT_AUTOSCROLL_BY_PX)

    --clickbox
      include a textbox at the bottom
      when an image is clicked, put the image's filename in it
";

sub main(@){
  my $prefixDir = undef;
  my $noBrowser = 0;
  my $config = {
    singleColButton => 1,
    maxWidth => undef,
    minWidth => undef,
    maxHeight => undef,
    minHeight => undef,
    autoScroll => 0,
    autoScrollDelayMillis => $DEFAULT_AUTOSCROLL_DELAY_MILLIS,
    autoScrollByPx => $DEFAULT_AUTOSCROLL_BY_PX,
    includeClickBox => 0,
  };
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^--dir=(.+)$/){
      $prefixDir = $1;
    }elsif($arg =~ /^(-n|--nobrowser)$/){
      $noBrowser = 1;
    }elsif($arg =~ /^--max-width=(\d+)$/){
      $$config{maxWidth} = $1;
    }elsif($arg =~ /^--min-width=(\d+)$/){
      $$config{minWidth} = $1;
    }elsif($arg =~ /^--max-height=(\d+)$/){
      $$config{maxHeight} = $1;
    }elsif($arg =~ /^--min-height=(\d+)$/){
      $$config{minHeight} = $1;
    }elsif($arg =~ /^(-a|--autoscroll)$/){
      $$config{autoScroll} = 1;
    }elsif($arg =~ /^--autoscroll-delay=(\d+)$/){
      die "invalid autoscroll delay: $1\n" if $1 <= 0;
      $$config{autoScrollDelayMillis} = $1;
    }elsif($arg =~ /^--autoscroll-px=(\d+)$/){
      die "invalid autoscroll scroll-by px: $1\n" if $1 <= 0;
      $$config{autoScrollByPx} = $1;
    }elsif($arg =~ /^(--clickbox)$/){
      $$config{includeClickBox} = 1;
    }else{
      die $usage;
    }
  }

  my @gifs = @_;
  @gifs = (glob("*.gif"), glob "*.GIF") if @gifs == 0;

  for my $gif(@gifs){
    die "$usage\nFile not found: $gif\n" if not -f $gif;
  }

  my @imgPaths;
  if(defined $prefixDir){
    $prefixDir =~ s/\/$//;
    @imgPaths = map {"$prefixDir/$_"} @gifs;
  }else{
    @imgPaths = map {abs_path($_)} @gifs;
  }

  my $html = getHtml $config, @imgPaths;

  if($noBrowser){
    print $html;
  }else{
    open FH, "> $tmpHtmlFile";
    print FH $html;
    close FH;

    system "chromium", "--incognito", $tmpHtmlFile;
    system "rm", $tmpHtmlFile;
  }
}

sub getHtml($@){
  my ($config, @imgAbsPaths) = @_;

  my $html = '';
  $html .= "<html>";
  $html .= "<head>";
  $html .= "
    <style>
      .container-single-column>* {
        display:block;
      }
    </style>
    <script>
    var scrollEnabled = false;
    var singleColEnabled = false;
    function addText(event) {
      var targ = event.target || event.srcElement;
      document.getElementById(\"clicked\").value += targ.src + \"\\n\";
    }
    function toggleSingleColumn(){
      singleColEnabled = !singleColEnabled;
      c = document.getElementById('container');
      if(singleColEnabled){
        c.classList.add('container-single-column');
      }else{
        c.classList.remove('container-single-column');
      }
    }
    function toggleScroll(){
      scrollEnabled = !scrollEnabled;
      if(scrollEnabled){
        pageScroll();
      }
    }
    function pageScroll() {
      if(scrollEnabled){
        window.scrollBy({
          behavior: 'smooth',
          top: $$config{autoScrollByPx}
        });
        scrolldelay = setTimeout(
          'pageScroll()',
          $$config{autoScrollDelayMillis});
      }else{
        window.scrollBy({
          behavior: 'instant',
          top: 0
        });
      }
    }
    </script>
  ";
  my $onclick = "";
  $onclick .= "addText(event); " if $$config{includeClickBox};
  $onclick .= "toggleScroll(); " if $$config{autoScroll};
  $html .= "<body>";
  $html .= ""
           . "<button"
           . " onclick='toggleSingleColumn()'"
           . " style='height: 100px; width:100%'"
           . ">"
           . "SINGLE COL"
           . "</button>\n" if $$config{singleColButton};
  $html .= "<div id='container'>";
  my $css = "";
  $css .= "max-width: $$config{maxWidth}px; " if defined $$config{maxWidth};
  $css .= "min-width: $$config{minWidth}px; " if defined $$config{minWidth};
  $css .= "max-height: $$config{maxHeight}px; " if defined $$config{maxHeight};
  $css .= "min-height: $$config{minHeight}px; " if defined $$config{minHeight};
  for my $img(@imgAbsPaths){
    my $fileUrl = "file://$img";
    $html .= "<img style=\"$css\" src=\"$fileUrl\" onclick=\"$onclick\"/>\n";
  }
  if($$config{includeClickBox}){
    $html .= "<textarea id=\"clicked\" rows=\"5\" cols=\"80\"></textarea>\n";
  }
  $html .= "</body>";
  $html .= "</head>";
  return $html;
}

&main(@ARGV);
