#!/usr/bin/perl
use strict;
use warnings;
use JSON qw(from_json);
use Time::HiRes qw(time);

my $OK_EXTS_REGEX = join "|", qw(
  avi m2ts mts mov mkv mpeg mpg mp4
);

my $BACKUP_DIR = "$ENV{HOME}/.cache/subtitle-tool";

sub getSRTFile($);
sub editSRT($);
sub ensureSRT($$);
sub removeSRT($);
sub selectSubTrackID($$);
sub getTracks($);
sub readFile($);
sub writeFile($$);
sub nowMillis();
sub run(@);

my $USAGE = "Usage:
  $0 [OPTS] --extract VIDEO_FILE [VIDEO_FILE ..]
    -if SRT_FILE exists, do nothing
    -if VIDEO_FILE is an MKV file:
      -extract SRT track using `mkvextract`, and write to SRT_FILE
    -otherwise, fail with an error

  $0 [OPTS] --edit VIDEO_FILE [VIDEO_FILE ..]
  $0 [OPTS] --oneline VIDEO_FILE [VIDEO_FILE ..]
    -edit the existing SRT_FILE, ensuring that each subtitle is on one line
      -replace all newlines with a single space char
    -if the SRT_FILE is changed, move original SRT_FILE to BACKUP_SRT_FILE

  $0 [OPTS] VIDEO_FILE [VIDEO_FILE ..]
    same as: $0 --extract VIDEO_FILE && $0 --edit VIDEO_FILE

  SRT_FILE
    same as VIDEO_FILE, with '.<EXT>' suffix replaced with '.srt', case-insensitive

  BACKUP_SRT_FILE
    backup file for SRT_FILE: $BACKUP_DIR/subtitle-fix-<SRT_FILE_BOING>-<MILLIS>
      SRT_FILE_BOING
        SRT_FILE, with all chars except alphanumeric and underscore with '%'
      MILLIS
        time since epoch, in millis

  OPTS
    --replace
      when extracting subtitles, first move the existing SRT_FILE to BACKUP_SRT_FILE

    --eng
      choose the first english lang subtitle track
      (this is the default)
      same as: --priority=ENG1,1

    --alt
      choose the second english lang subtitle track
      same as: --priority=ENG2,ENG1,1

    --priority=SUB_CSV
      choose the first subtitle track that matches the first SUB

    SUB_CSV
      comma-separated list of <SUB>s
      e.g.:   'ENG1,ENG2,1'   'SPA2'
    SUB
      a specifier to match subtitle tracks
      <LANG><LANG_IDX>
        a language, and the index, starting with 1, of only subtitle tracks with <LANG>
      <LANG>
        same as: <LANG>1
      <IDX>
        the index, starting with 1, of all subtitle tracks
";

my $CMD_EXTRACT_AND_EDIT = "extract-and-edit";
my $CMD_EXTRACT = "extract";
my $CMD_EDIT = "edit";

sub main(@){
  my $cmd = $CMD_EXTRACT_AND_EDIT;
  my @videoFiles;
  my $subArgCSV = "ENG1,1";
  my $replace = 0;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-e|--extract)$/){
      $cmd = $CMD_EXTRACT;
    }elsif($arg =~ /^(--edit|--oneline)$/){
      $cmd = $CMD_EDIT;
    }elsif($arg =~ /^(-r|--replace)$/){
      $replace = 1;
    }elsif($arg =~ /^--eng$/){
      $subArgCSV = "ENG1,1";
    }elsif($arg =~ /^--alt$/){
      $subArgCSV = "ENG2,ENG1,1";
    }elsif($arg =~ /^--priority=(.+)$/){
      $subArgCSV = $1;
    }elsif(-f $arg and $arg =~ /\.($OK_EXTS_REGEX)$/i){
      push @videoFiles, $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  die "$USAGE\nERROR: no VIDEO_FILE give\n" if @videoFiles == 0;

  for my $videoFile(@videoFiles){
    if($cmd =~ /^($CMD_EXTRACT|$CMD_EXTRACT_AND_EDIT)$/){
      if($replace){
        removeSRT($videoFile);
      }
      ensureSRT($videoFile, $subArgCSV);
    }
    if($cmd =~ /^($CMD_EDIT|$CMD_EXTRACT_AND_EDIT)$/){
      editSRT($videoFile);
    }
  }
}

sub getSRTFile($){
  my ($videoFile) = @_;
  my $srtFile = $videoFile;
  if($srtFile !~ s/\.($OK_EXTS_REGEX)/.srt/i){
    die "ERROR: malformed VIDEO_FILE $videoFile, <EXT> must be one of: $OK_EXTS_REGEX\n";
  }
  return $srtFile;
}

sub editSRT($){
  my ($videoFile) = @_;
  my $srtFile = getSRTFile($videoFile);
  die "ERROR: SRT_FILE does not exist\n" if not -f $srtFile;

  my $contents = readFile($srtFile);
  my @lines = split /(?<=\n)/, $contents;

  my @prefixLines;
  my @subs;
  my $curSub = undef;
  for(my $i=0; $i<@lines; $i++){
    my $line = $lines[$i];
    my $nextLine = $i+1<@lines ? $lines[$i+1] : "";
    if($line =~ /^[[:^print:]]*\d+[\r\n]*$/ and $nextLine =~ /^\d+:\d+:\d+,\d+ --> \d+:\d+:\d+,\d+[\r\n]*$/){
      $curSub = {
        numLine => $line,
        timestampLine => $nextLine,
        subLines => [],
      };
      $i++;
      push @subs, $curSub;
    }elsif(defined $curSub){
      push @{$$curSub{subLines}}, $line;
    }else{
      push @prefixLines, $line;
    }
  }

  my $newContents = join '', @prefixLines;
  for my $sub(@subs){
    $newContents .= $$sub{numLine};
    $newContents .= $$sub{timestampLine};
    my @subLines = @{$$sub{subLines}};

    for(my $i=0; $i<@subLines; $i++){
      my $line = $subLines[$i];
      #remove newlines from all except last sub line and the empty line
      if($i < @subLines - 2){
        $line =~ s/[\r\n]+/ /g;
      }
      $newContents .= $line;
    }
  }

  if($contents ne $newContents){
    removeSRT($videoFile);
    writeFile($srtFile, $newContents);
  }
}

sub ensureSRT($$){
  my ($videoFile, $subArgCSV) = @_;
  my $srtFile = getSRTFile($videoFile);
  if(-e $srtFile){
    return;
  }
  my $mkvFile = $videoFile;
  if($mkvFile !~ /\.mkv/i){
    die "ERROR: malformed MKV_FILE $mkvFile, must end in .mkv\n";
  }

  my $trackID = selectSubTrackID($mkvFile, $subArgCSV);
  die "ERROR: could not find subtitle tracks in $mkvFile\n" if not defined $trackID;

  my @cmd = ("mkvextract", $mkvFile, "tracks", "$trackID:$srtFile");
  run @cmd;
}

sub removeSRT($){
  my ($videoFile) = @_;
  my $srtFile = getSRTFile($videoFile);
  if(-f $srtFile){
    my $boingSrtFile = $srtFile;
    $boingSrtFile =~ s/[^a-zA-Z0-9_]/%/g;
    if(not -d $BACKUP_DIR){
      run "mkdir", "-p", $BACKUP_DIR;
    }
    my $tmpFile = "$BACKUP_DIR/subtitle-fix-$boingSrtFile-" . nowMillis();
    my @cmd = ("mv", $srtFile, $tmpFile);
    run @cmd;
  }
}


sub selectSubTrackID($$){
  my ($mkvFile, $subArgCSV) = @_;
  my @tracks = getTracks($mkvFile);
  for my $subArg(split(/,/, $subArgCSV)){
    my ($lang, $langIdx, $idx);
    if($subArg =~ /^\s*([a-z]+)(\d+)\s*$/i){
      ($lang, $langIdx, $idx) = ($1, $2, undef);
    }elsif($subArg =~ /^\s*([a-z]+)\s*$/i){
      ($lang, $langIdx, $idx) = ($1, 1, undef);
    }elsif($subArg =~ /^\s*(\d+)\s*$/){
      ($lang, $langIdx, $idx) = (undef, undef, $1);
    }else{
      die "ERROR: malformed SUB_CSV $subArgCSV\n";
    }

    my $curLangIdx = 0;
    my $curSubIdx = 0;
    for my $track(@tracks){
      next if $$track{type} ne "subtitles";
      next if $$track{codec} !~ /SRT/i;

      my $subLang = $$track{properties}{language};
      $subLang = "" if not defined $subLang;
      if(defined $lang and lc $lang eq lc $subLang){
        $curLangIdx++;
        if($curLangIdx == $langIdx){
          return $$track{id};
        }
      }
      $curSubIdx++;
      if(defined $idx and $curSubIdx == $idx){
        return $$track{id};
      }
    }
  }

  return undef;
}

sub getTracks($){
  my ($mkvFile) = @_;
  my @cmd = ("mkvmerge", "-J", $mkvFile);
  open CMD, "-|", @cmd or die "ERROR: could not run @cmd\n";
  my $json = join '', <CMD>;
  close CMD;

  my $obj = from_json($json);
  my @tracks = @{$$obj{tracks}};
  die "ERROR: could not fetch tracks with @cmd\n" if @tracks == 0;
  return @tracks;
}

sub readFile($){
  open FH, "< $_[0]" or die "ERROR: could not read $_[0]\n$!\n";
  my $out = join '', <FH>;
  close FH;
  return $out;
}

sub writeFile($$){
  open FH, "> $_[0]" or die "ERROR: could not write $_[0]\n$!\n";
  print FH $_[1];
  close FH;
}

sub nowMillis(){
  return int(time * 1000.0 + 0.5);
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
