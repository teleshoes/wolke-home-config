#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use XML::LibXML;
use Date::Parse qw(str2time);

sub parseTcxFile($);
sub formatWorkout($);

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC TCX_FILE [TCX_FILE ..]
    print info about each TCX_FILE
";

sub main(@){
  my @tcxFiles;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif(-f $arg){
      push @tcxFiles, $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if(@tcxFiles == 0){
    die "ERROR: missing TCX_FILE\n";
  }

  for my $file(@tcxFiles){
    my $workout = parseTcxFile($file);
    print formatWorkout($workout);
  }
}

sub parseTcxFile($){
  my ($filename) = @_;

  my $workout = {
    hrByEpoch  => {},
    startEpoch => undef,
  };

  my $dom = XML::LibXML->load_xml(location => $filename);
  $dom->getDocumentElement->setNamespace(
    "http://www.garmin.com/xmlschemas/TrainingCenterDatabase/v2",
    "tcd");

  for my $act($dom->findnodes("/tcd:TrainingCenterDatabase/tcd:Activities/tcd:Activity")){
    for my $lap($act->findnodes("tcd:Lap")){
      if(defined $$workout{startEpoch}){
        die "ERROR: mutiple Lap elements found in $filename\n";
      }
      my $lapStart = $lap->getAttribute("StartTime");
      $$workout{startEpoch} = str2time($lapStart);
      for my $tp($lap->findnodes("tcd:Track/tcd:Trackpoint")){
        my $t = $tp->findvalue("./tcd:Time");
        my $hr = $tp->findvalue("./tcd:HeartRateBpm/tcd:Value");
        my $epoch = str2time($t);
        $$workout{hrByEpoch}{$epoch} = $hr;
      }
    }
  }

  return $workout;
}

sub formatWorkout($){
  my ($workout) = @_;

  my @epochs = sort keys %{$$workout{hrByEpoch}};
  return $$workout{startEpoch} . ": " . $$workout{hrByEpoch}{$epochs[0]} . " - " . $$workout{hrByEpoch}{$epochs[-1]} . "\n";
}

&main(@ARGV);
