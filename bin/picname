#!/usr/bin/perl
use strict;
use warnings;

my @EXTS_PICTURES = qw(
  jpg jpeg png gif mpo
);
my @EXTS_VIDEOS = qw(
  avi m2ts mts mov mkv mpeg mpg mp4
  flv mpv wmv webm
);

my @EXTS = (@EXTS_PICTURES, @EXTS_VIDEOS);
my $OK_EXTS_VIDEOS = "(?:" . (join "|", @EXTS_VIDEOS) . ")";
my $OK_EXTS = "(?:" . (join "|", @EXTS) . ")";

my $USAGE = "Usage:
  $0 -h|--help
    show this message

  $0 [OPTS] FILE [FILE FILE ...]
    rename, touch, or print info about files, using exif data, mtime, or filename
      -default action is to rename
      -default timestamp mode is to use exif for JPEG, and mtime otherwise
    FILE must end in '.' followed by one of: @EXTS
      (see --no-check-ext)

  OPTS
    --rename       {default}
      rename files using mv
      new name is: YYYYMMDD_HHMMSS_<FILE>
        -if FILE already starts with YYYYMMDD_HHMMSS, YYYYMMDD_HHMMSS is removed from FILE
        -if FILE is YYYYMMDD_INDEX.ext, YYYYMMDD is removed from FILE
    --touch
      use touch to set the mtime
      (uses the same method to get timestamp as --rename)
    --print
      do not perform any action, instead just print information

    --exif-mtime   {default}
      use exif datetime if available, and fallback to mtime if not
    --exif
      use (only) exif to get timestamp
      -if file is a JPEG, attempt to read 'Date and Time' from `exif` tool (fast)
      -if file is not a JPEG or `exif` fails, fallback to `exiftool`
        -use CreateDate field if available or DateTimeOriginal if not available
    --mtime
      use (only) mtime to get timestamp
    --filename
      use picname file name to get timestamp
      fails if file is not named in the expected format

    --quiet
      do not print 'skipping rename' or 'skipping touch' messages

    --check-ext
      fail if FILE does not end in '.' followed by one of: @EXTS
      (this is the default)
    -a | --no-check-ext | --allow-ext
      do NOT fail if FILE does not end in '.' followed by one of: @EXTS
      any ext (or no ext) is permitted
      (files ending in .jpg or .jpeg still use exif mode by default)

    --tag=TAG
      insert \"_<TAG>\" after YYYYMMDD_HHMMSS when renaming
        -TAG can contain only lowercase letters, numbers, and underscores
        -TAG is ignored if filename is already named:
          TAG.ext
          or YYYYMMDD_HHMMSS_TAG.ext
          or YYYYMMDD_HHMMSS_TAG_INDEX.ext
      e.g.:
        $0 --tag=screenshot 20150601_001.png
          mv 20150601_001.png 20150601_194358_screenshot_001.png
        $0 --tag=screenshot 20150601_194358_screenshot_001.png
          skipping rename 20150601_194358_screenshot_001.png

    -n | -s | --no-act | --simulate
      print the commands, but dont actually run 'touch' or 'mv'
      commands are prefixed by '#' to make it clear they werent run

    --shift-hours=HOURS
      add HOURS*60*60 seconds to the timestamp, regardless of method chosen
      HOURS must be an integer (positive, negative, or zero)
      e.g.: --shift-hours=-4  subtracts 4*60*60 seconds

    --shift-seconds=SECONDS
      add SECONDS seconds to the timestamp, regardless of method chosen
      SECONDS must be an integer (positive, negative, or zero)
      e.g.: --shift-seconds=3600  adds 3600 seconds

    --subtract-duration
      for each FILE:
        -determine if file is a video by file extension:
          @EXTS_VIDEOS
        -if file is a video:
          -calculate the VIDEO_DURATION of video files in seconds
          -subtract the duration to the timestamp for that video,
            as in --shift-seonds=-VIDEO_DURATION
        -otherwise, do nothing
      NOTE: can be combined with --shift-seconds or --shift-hours
            effect is cumulative
";

sub handleFile($$$$$$$$$);
sub exifEpoch($);
sub exifCmdGetDateAsEpoch($);
sub exiftoolCmdDateFieldAsEpoch($$);
sub getDuration($);
sub mtimeEpoch($);
sub filenameEpoch($);
sub run(@);

my $PREFIX_REGEX = "IMG_|VID_";

my $ACTION_RENAME = "rename";
my $ACTION_TOUCH = "touch";
my $ACTION_PRINT = "print";

my $MODE_EXIF_MTIME = "exif-mtime";
my $MODE_EXIF = "exif";
my $MODE_MTIME = "mtime";
my $MODE_FILENAME = "filename";

sub main(@){
  my $action = $ACTION_RENAME;
  my $mode = $MODE_EXIF_MTIME;
  my $simulate = 0;
  my $shiftSeconds = 0;
  my $subtractDuration = 0;
  my $quiet = 0;
  my $checkExt = 1;
  my $tag = undef;
  my @files;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--rename)$/ and @_ > 0){
      $action = $ACTION_RENAME;
    }elsif($arg =~ /^(--touch)$/ and @_ > 0){
      $action = $ACTION_TOUCH;
    }elsif($arg =~ /^(--print)$/ and @_ > 0){
      $action = $ACTION_PRINT;
    }elsif($arg =~ /^(--exif-mtime)$/ and @_ > 0){
      $mode = $MODE_EXIF_MTIME;
    }elsif($arg =~ /^(--exif)$/){
      $mode = $MODE_EXIF;
    }elsif($arg =~ /^(--mtime)$/){
      $mode = $MODE_MTIME;
    }elsif($arg =~ /^(--filename)$/){
      $mode = $MODE_FILENAME;
    }elsif($arg =~ /^(--quiet)$/){
      $quiet = 1;
    }elsif($arg =~ /^(--check-ext)$/){
      $checkExt = 1;
    }elsif($arg =~ /^(-a|--no-check-ext|--allow-ext)$/){
      $checkExt = 0;
    }elsif($arg =~ /^--tag=(.+)$/){
      $tag = $1;
      die "$USAGE\nERROR: invalid tag $tag\n" if $tag !~ /^[a-z0-9_]+$/;
    }elsif($arg =~ /^(-n|-s|--no-act|--simulate)$/){
      $simulate = 1;
    }elsif($arg =~ /^--shift-hours=(-?\d+)$/){
      $shiftSeconds = $1 * 60 * 60;
    }elsif($arg =~ /^--shift-seconds=(-?\d+)$/){
      $shiftSeconds = $1;
    }elsif($arg =~ /^(--subtract-duration)$/){
      $subtractDuration = 1;
    }elsif(-f $arg){
      push @files, $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }
  die "$USAGE\nERROR: no files given\n" if @files == 0;
  my @errors;
  for my $file(@files){
    if(-f $file){
      my $isOk = handleFile $action, $mode, $shiftSeconds, $subtractDuration, $tag, $simulate, $quiet, $checkExt, $file;
      push @errors, $file if not $isOk;
    }
  }
  if(@errors > 0){
    my $errorsFmt = join "", map {" $_\n"} @errors;
    die "\n\nERROR HANDLING FILES:\n$errorsFmt";
  }
}

sub handleFile($$$$$$$$$){
  my ($action, $mode, $shiftSeconds, $subtractDuration, $tag, $simulate, $quiet, $checkExt, $file) = @_;

  my ($dir, $fileName, $ext);
  $fileName = $file;

  if($fileName =~ /^(.*\/)([^\/]+)$/){
    ($dir, $fileName) = ($1, $2);
  }else{
    $dir = "";
  }
  if($fileName =~ /^(.*)(\.\w+)$/){
    ($fileName, $ext) = ($1, $2);
  }else{
    $ext = "";
  }

  if($fileName =~ /\//){
    die "ERROR: could not parse directory of file: $file\n";
  }
  if($checkExt and $ext !~ /^\.($OK_EXTS)$/i){
    die "ERROR: invalid ext for $file (use -a to allow any): @EXTS\n";
  }

  my $epoch;
  if($mode eq $MODE_EXIF_MTIME){
    $epoch = exifEpoch $file;
    if(not defined $epoch){
      $epoch = mtimeEpoch $file;
    }
  }elsif($mode eq $MODE_EXIF){
    $epoch = exifEpoch $file;
  }elsif($mode eq $MODE_MTIME){
    $epoch = mtimeEpoch $file;
  }elsif($mode eq $MODE_FILENAME){
    $epoch = filenameEpoch $file;
  }else{
    die "unknown mode: $mode\n";
  }

  if(not defined $epoch){
    print "cannot process $file\n";
    return 0;
  }

  if($subtractDuration and $ext =~ /^\.($OK_EXTS_VIDEOS)$/i){
    my $duration = getDuration($file);
    if(defined $duration){
      $epoch -= int($duration + 0.5);
    }else{
      print STDERR "WARNING: could not calculate duration for $file\n";
    }
  }

  $epoch += $shiftSeconds;

  my $dateFmt = `date --date \@$epoch +'%Y%m%d_%H%M%S'`;
  chomp $dateFmt;

  my ($ymd, $hms);
  if($dateFmt =~ /^(\d\d\d\d\d\d\d\d)_(\d\d\d\d\d\d)$/){
    ($ymd, $hms) = ($1, $2);
  }else{
    die "ERROR: malformed output of `date` \"$dateFmt\"\n";
  }

  my ($namePrefix, $nameSuffix);

  #extract prefix+suffix from file
  if($fileName =~ /^($PREFIX_REGEX)(.*)$/){
    $namePrefix = $1;
    $nameSuffix = $2;
  }else{
    $namePrefix = "";
    $nameSuffix = $fileName;
  }

  #trim date/time or date from suffix
  if($nameSuffix =~ /^(\d\d\d\d\d\d\d\d_\d\d\d\d\d\d)(_.*)?$/){
    #remove *any* datetime from any file suffix like ########_###### or ########_######_*
    #e.g.: orig file:  20181101_024332.jpg
    #      with:       suffix=20181101_024332, exif_datetime=20181031_224332
    #      would be:   20181031_224332_20181101_024332.jpg
    #      trimmed to: 20181031_224332.jpg

    my ($removedDtm, $trimmedNameSuffix) = ($1, $2);
    $trimmedNameSuffix = "" if not defined $trimmedNameSuffix;

    if($removedDtm ne "${ymd}_${hms}"){
      if($action ne $ACTION_TOUCH){
        print STDERR "WARNING: replacing date prefix $removedDtm with ${ymd}_${hms} for $file\n";
      }
    }

    my $oldFileNameEpoch = filenameEpoch $file;
    my $diff = $epoch - $oldFileNameEpoch;
    $diff = 0-$diff if $diff < 0;
    if($diff > 60*60*24){
      print STDERR ""
        . "WARNING: old date prefix disagrees by more than a day"
        . " for $file ($oldFileNameEpoch vs $epoch)\n";
    }

    $nameSuffix = $trimmedNameSuffix;
  }elsif($nameSuffix =~ /^${ymd}(_.*)?/){
    #remove *exactly matching* YYYYMMDD from file suffix, where YYYYMMDD is the target date
    #e.g.: orig file:  20180501_001.png
    #      with:       suffix=20180501_001, exif_datetime=20180501_134155
    #      would be:   20180501_134155_20180501_001.jpg
    #      trimmed to: 20180501_134155_001.jpg

    my ($trimmedNameSuffix) = ($1);
    $trimmedNameSuffix = "" if not defined $trimmedNameSuffix;

    $nameSuffix = $trimmedNameSuffix;
  }

  $namePrefix =~ s/^_+//;
  $namePrefix =~ s/_+$//;
  $nameSuffix =~ s/^_+//;
  $nameSuffix =~ s/_+$//;

  if(defined $tag and $nameSuffix !~ /^${tag}(_.*)?$/){
    #prepend <TAG> to file suffix, unless file suffix is already <TAG> or <TAG>_<SUFFIX>
    if(length $nameSuffix > 0){
      $nameSuffix = "${tag}_${nameSuffix}";
    }else{
      $nameSuffix = "${$tag}";
    }
  }

  $namePrefix = "${namePrefix}_" if length $namePrefix > 0;
  $nameSuffix = "_${nameSuffix}" if length $nameSuffix > 0;

  my $newFile;
  my $count = 0;
  while(1){
    my $countFmt = $count == 0 ? "" : "_$count";
    $newFile = "${dir}${namePrefix}${dateFmt}${nameSuffix}${countFmt}${ext}";

    if(not -e $newFile or $file eq $newFile){
      #ensure file does not exist, unless its unchanged from original file
      last;
    }else{
      #otherwise, append an incremental index until file does not exist
      $count++;
    }
  }

  if($action eq $ACTION_RENAME){
    if($file eq $newFile){
      print "skipping rename $file\n" unless $quiet;
      return 1;
    }else{
      my @mvCmd = ("mv", "--no-clobber", $file, $newFile);
      if($simulate){
        print "#@mvCmd\n";
      }else{
        run @mvCmd;
        return $? == 0 ? 1 : 0;
      }
    }
  }elsif($action eq $ACTION_TOUCH){
    my $epochMtime = mtimeEpoch $file;
    if($epoch == $epochMtime){
      print "skipping touch $file\n" unless $quiet;
    }else{
      my @touchCmd = ("touch", $file, "--date", "\@$epoch");
      if($simulate){
        print "#@touchCmd\n";
      }else{
        run @touchCmd;
        return $? == 0 ? 1 : 0;
      }
    }
  }elsif($action eq $ACTION_PRINT){
    my $epochMtime = mtimeEpoch $file;
    my $isSameFilename = $file eq $newFile;
    my $isSameMtime = $epoch == $epochMtime;
    printf "%d %s (%s) %s %s\n",
      $epoch,
      $file,
      $newFile,
      $isSameFilename ? "(same file)" : "(diff file)",
      $isSameMtime ? "(same mtime)" : "(diff mtime)",
      ;
    return 1;
  }else{
    die "unknown action: $action\n";
  }
}

sub exifEpoch($){
  my ($file) = @_;

  my $isJpeg;
  if($file =~ /\.(jpg|jpeg)$/i){
    $isJpeg = 1;
  }

  my $epoch = undef;
  if($isJpeg){
    $epoch = exifCmdGetDateAsEpoch($file) if not defined $epoch;
  }
  $epoch = exiftoolCmdDateFieldAsEpoch($file, "CreateDate") if not defined $epoch;
  $epoch = exiftoolCmdDateFieldAsEpoch($file, "DateTimeOriginal") if not defined $epoch;
  if(not defined $epoch){
    die "ERROR: could not get EXIF date/time with exif/exiftool for $file\n";
  }
  return $epoch;
}

sub exifCmdGetDateAsEpoch($){
  my ($file) = @_;
  my @cmd = ("exif", "-m", $file);
  open EXIF, "-|", @cmd or die "ERROR: could not run @cmd\n$!\n";
  my @lines = <EXIF>;
  close EXIF;

  if($? != 0){
    print STDERR "WARNING: error running '@cmd'\n";
  }

  my @dates;
  for my $line(@lines){
    if($line =~ /^Date and Time[^\t]*\t(.+)$/){
      my $dt = $1;
      if($dt =~ /^(\d\d\d\d):(\d\d):(\d\d) (\d\d):(\d\d):(\d\d)$/){
        my ($y, $m, $d, $hr, $min, $sec) = ($1, $2, $3, $4, $5, $6);
        my $s = `date --date '$y-$m-$d $hr:$min:$sec' +%s`;
        chomp $s;
        if($s !~ /^\d+$/){
          print STDERR "WARNING: malformed date output $s\n";
          return undef;
        }
        push @dates, $s;
      }else{
        print STDERR "WARNING: malformed date line: $line\n";
        return undef;
      }
    }
  }

  if(@dates == 0){
    print STDERR "WARNING: no date/time found for '@cmd'\n";
    return undef;
  }

  my $minDate = $dates[0];
  for my $d(@dates){
    if($d != $minDate){
      print STDERR "WARNING: mismatched date tags for $file: $d != $minDate\n";
      if($d < $minDate){
        $minDate = $d;
      }
    }
  }
  return $minDate;
}

sub exiftoolCmdDateFieldAsEpoch($$){
  my ($file, $exifField) = @_;
  my @exifToolCmd = ("exiftool",
    "-ignoreMinorErrors",
    "-p", "\$$exifField",
    "-d", "%s",
    $file,
  );
  open EXIF, "-|", @exifToolCmd
    or die "ERROR: could not run @exifToolCmd\n$!\n";
  my $out = <EXIF>;
  close EXIF;
  die "ERROR: command '@exifToolCmd' failed\n" if $? != 0;
  chomp $out;

  if($out =~ /^(\d+)$/){
    return $1;
  }elsif($out eq ""){
    return undef;
  }else{
    die "ERROR: malformed output '$out' for '@exifToolCmd'\n";
  }
}

sub getDuration($){
  my ($file) = @_;
  open my $cmdH, "-|", "duration", "-s", "-n", $file;
  my $out = join "", <$cmdH>;
  close $cmdH;
  if($out =~ /^(\d+|\d*\.\d+)$/){
    return $1;
  }else{
    return undef;
  }
}

sub mtimeEpoch($){
  my ($file) = @_;
  my $mtimeEpoch = (stat $file)[9];
  return $mtimeEpoch;
}

sub filenameEpoch($){
  my ($file) = @_;
  my $filename = $file;
  $filename =~ s/.*\///;
  if($filename =~ /^(?:$PREFIX_REGEX)?(\d\d\d\d)(\d\d)(\d\d)_(\d\d)(\d\d)(\d\d)[_\.]/){
    my ($y, $m, $d, $hr, $min, $sec) = ($1, $2, $3, $4, $5, $6);
    my $dateFmt = "$y$m$d $hr:$min:$sec";
    my $epoch = `date +%s --date='$dateFmt'`;
    chomp $epoch;
    if($epoch =~ /^\d+$/){
      return $epoch;
    }else{
      print STDERR "ERROR: could not read datetime fmt '$dateFmt' using `date`\n";
    }
  }else{
    print STDERR "ERROR: filename must start with YYYYMMDD_HHMMSS, then . or _\n";
  }
  return undef;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
