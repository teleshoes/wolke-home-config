#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $EXEC = basename $0;

my @EXTS = qw(png jpg jpeg);
my $OK_EXTS = join "|", @EXTS;

my $usage = "Usage:
  use scrot-bag to take a screenshot and store it in a directory,
  not overwriting existing screenshots

  $EXEC -h | --help
    show this message

  $EXEC DIR
    -create DIR
    -get today's date, formatted YYYYMMDD
    -find all files in DIR named: YYYYMMDD_NUM.EXT
      -EXT is $OK_EXTS
      -YYYYMMDD is today's date
      -NUM is a string of digits
    -find the largest NUM among these files, and add 1 to it for NEXTNUM
    -use 001 if no such files exist
    -run `scrot YYYYMMDD_NEXTNUM.png`

  $EXEC
    same as `$EXEC \$HOME`
";

sub main(@){
  my $dir;
  if(@_ == 1 and $_[0] =~ /^(-h|--help)$/){
    print $usage;
    exit 0;
  }elsif(@_ == 1){
    ($dir) = @_;
  }elsif(@_ == 0){
    $dir = $ENV{HOME};
  }else{
    die "$usage\nERROR: unknown args @_\n";
  }

  system "mkdir", $dir if not -d $dir;

  my $date = `date +%Y%m%d`;
  chomp $date;

  my @files = listDirFiles($dir);
  my $maxNum = 0;
  for my $file(@files){
    if($file =~ /^(?:.*\/)?${date}_(\d+)\.(?:$OK_EXTS)$/i){
      my $num = 0 + $1;
      if($num > $maxNum){
        $maxNum = $num;
      }
    }
  }

  my $nextNum = sprintf "%03d", $maxNum + 1;

  my $file = "${dir}/${date}_${nextNum}.png";

  if(-e $file){
    die "ERROR: $file already exists\n";
  }

  print "Taking screenshot and putting it in $file\n";
  system "scrot $file";
}

sub listDirFiles($){
  my ($dir) = @_;
  opendir(my $dh, $dir) or die "ERROR: could not read dir $dir\n$!\n";
  my @files = readdir($dh);
  closedir($dh);
  $dir =~ s/\/?$//;
  @files = map {"$dir/$_"} @files;
  @files = grep {-f $_} @files;
  return @files;
}

&main(@ARGV);
