#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use Cwd qw(abs_path);

my $EXEC = basename $0;

my @EXTS = qw(png jpg jpeg);
my $OK_EXTS = join "|", @EXTS;

my $usage = "Usage:
  use scrot-bag to take a screenshot and store it in a directory,
  not overwriting existing screenshots

  $EXEC -h | --help
    show this message

  $EXEC DIR
    run: `scrot <DIR>/<YYYYMMDD>_<NEXTNUM>.png`
      DIR
        directory passed in; must already exist
        converted to an absolute path by Cwd::abs_path
      YYYYMMDD
        today's date, formatted +%Y%m%d
      NEXTNUM
        find all files in DIR named: <YYYYMMDD>_<NUM>.<EXT>
        extract the largest NUM, and add 1 to it
        format as at least 3 digits with leading 0s i.e.: %03d
      NUM
        any sequence of digits (non-negative integer)
      EXT
        one of: $OK_EXTS

  $EXEC
    same as `$EXEC \$HOME`
";

sub main(@){
  my $dir = undef;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif(-d $arg){
      die "ERROR: multiple dirs given \"$arg\" and \"$dir\"\n" if defined $dir;
      $dir = $arg;
    }else{
      die "$usage\nERROR: unknown arg $arg\n";
    }
  }

  $dir = $ENV{HOME} if not defined $dir;

  $dir = abs_path($dir);

  $dir =~ s/\/+$//;

  if(not -d $dir){
    die "ERROR: $dir is not a dir\n";
  }

  my $date = `date +%Y%m%d`;
  chomp $date;

  my @files = listDirFiles($dir);
  my $maxNum = 0;
  for my $file(@files){
    if($file =~ /^(?:.*\/)?${date}_(\d+)\.(?:$OK_EXTS)$/i){
      my $num = 0 + $1;
      if($num > $maxNum){
        $maxNum = $num;
      }
    }
  }

  my $nextNum = sprintf "%03d", $maxNum + 1;

  my $file = "${dir}/${date}_${nextNum}.png";

  if(-e $file){
    die "ERROR: $file already exists\n";
  }

  print "Taking screenshot and putting it in $file\n";
  system "scrot $file";
}

sub listDirFiles($){
  my ($dir) = @_;
  opendir(my $dh, $dir) or die "ERROR: could not read dir $dir\n$!\n";
  my @files = readdir($dh);
  closedir($dh);
  $dir =~ s/\/?$//;
  @files = map {"$dir/$_"} @files;
  @files = grep {-f $_} @files;
  return @files;
}

&main(@ARGV);
