#!/usr/bin/perl
use strict;
use warnings;

my $hostsFile = "/etc/hosts";
my $sshConfigFile = "$ENV{HOME}/.ssh/config";
my $defaultUser = "ewolk";
my $marker = "###escribe-hosts###";

sub dig($$);
sub getHosts();
sub getSSHConfig();

my $nameserver = '10.128.1.10';

my $cacertsHosts = [qw(
  ehr-01.dev.int.aws.lillegroup.com
  ehr-01.stage.int.aws.lillegroup.com
)];

my $intAws = "int.aws.lillegroup.com";

my $hosts = [
  "ehr-01.dev.$intAws"       => ["ehr-01.dev", "ehr.dev", "ehr.dev.$intAws"],
  "ehr-01.stage.$intAws"     => ["ehr-01.stage", "ehr.stage", "ehr.stage.$intAws"],
  "ehr-01.prod.$intAws"      => ["ehr-01.prod", "ehr.prod", "ehr.prod.$intAws"],

  "ehr.dev.escribe.com"      => [],
  "ehr.stage.escribe.com"    => [],
  "ehr.prod.escribe.com"     => [],

  "ehr-aux-01.stage.$intAws" => ["ehr-aux-01.stage", "ehr-aux.stage"],
  "ehr-aux-01.prod.$intAws"  => ["ehr-aux-01.prod", "ehr-aux.prod"],

  "ehr-db-01.dev.$intAws"    => ["ehr-db-01.dev", "ehr-db.dev", "ehr-db.dev.$intAws"],
  "ehr-db-01.stage.$intAws"  => ["ehr-db-01.stage", "ehr-db.stage", "ehr-db.stage.$intAws"],
  "ehr-db-01.prod.$intAws"   => ["ehr-db-01.prod", "ehr-db.prod", "ehr-db.prod.$intAws"],

  "ehr-edi-01.dev.$intAws"   => ["ehr-edi-01.dev", "ehr-edi.dev", "ehr-edi.dev.$intAws",
                                 "edi-01.dev", "edi.dev", "edi.dev.$intAws"],
  "ehr-edi-01.stage.$intAws" => ["ehr-edi-01.stage", "ehr-edi.stage", "ehr-edi.stage.$intAws",
                                 "edi-01.stage", "edi.stage", "edi.stage.$intAws"],
  "ehr-edi-01.prod.$intAws"  => ["ehr-edi-01.prod", "ehr-edi.prod", "ehr-edi.prod.$intAws",
                                 "edi-01.prod", "edi.prod", "edi.prod.$intAws"],

  "ehr-proxy.dev.$intAws"    => ["ehr-proxy.dev"],
  "passwd.$intAws"           => ["passwd"],
  "jenkins.$intAws"          => ["jenkins"],
  "trac.$intAws"             => ["trac"],
  "trac-db.$intAws"          => ["trac-db"],
  "mvn.$intAws"              => ["mvn"],

  "mon.$intAws"              => ["mon"],
  "vm.lillegroup.com"        => ["vm.lillegroup.com"],
  "www-01.prod.$intAws"      => ["www-01.prod"],
  "trac.escribe.com"         => ["trac"],
  "proxy.escribe.com"        => ["proxy"],
  "web.escribe.com"          => ["web"],
  "tx.escribe.com"           => ["tx"],
  "monitor.escribe.com"      => ["monitor"],
  "escribehost.com"          => [],
];
my $fixedHosts = [
  'dev1'                => '172.18.10.91',
  'dev2'                => '172.18.10.92',
  'web.escribehost.com' => '10.1.14.20',
  'escribehost.com'     => '10.1.14.20',
];
my $sshHostIdentityFiles = {
   "www-01.prod" => "~/.ssh/lillegroup_rsa",
};

sub main(@){
  die "Cannot run $0 as root\n" if `whoami` eq "root\n";
  die "Usage: $0\n" if @_ != 0;

  my %hostIps;
  my @hostOrder;

  for(my $i=0; $i < @$hosts; $i+=2){
    my $host = $$hosts[$i];
    my @syns = @{$$hosts[$i+1]};
    my $ip = dig $host, $nameserver;
    die "Unknown server: $host\n" if not defined $ip;
    for my $hostname($host, @syns){
      $hostIps{$hostname} = $ip;
      push @hostOrder, $hostname;
    }
  }

  for(my $i=0; $i < @$fixedHosts; $i+=2){
    my $hostname = $$fixedHosts[$i];
    my $ip = $$fixedHosts[$i+1];
    $hostIps{$hostname} = $ip;
    push @hostOrder, $hostname;
  }

  my @hostLines = getHosts();
  my @sshConfigLines = getSSHConfig();
  push @sshConfigLines, "$marker\n";
  for my $host(@hostOrder){
    my $ip = $hostIps{$host};
    push @hostLines, "$ip\t$host $marker\n";
    push @sshConfigLines, "Host $host\n";
    push @sshConfigLines, "    User $defaultUser\n";
    if(defined $$sshHostIdentityFiles{$host}){
      push @sshConfigLines, "    IdentityFile $$sshHostIdentityFiles{$host}\n";
    }
  }
  push @sshConfigLines, "$marker\n";

  print "\n\nhosts:\n";
  open FH, "| sudo tee $hostsFile" or die "Could not write to $hostsFile\n";
  print FH @hostLines;
  close FH;

  print "\n\nssh config:\n";
  open FH, "| tee $sshConfigFile" or die "Could not write to $hostsFile\n";
  print FH @sshConfigLines;
  close FH;

  for my $host(@$cacertsHosts){
    print "\n\n\nrunning: keytool-cacerts $host\n";
    system "keytool-cacerts", $host;
  }
}

sub dig($$){
  my ($host, $nameserver) = @_;
  my $section = undef;
  for my $line(`dig \@$nameserver $host`){
    if($line =~ /^;; (\w+) SECTION:$/){
      $section = $1;
    }
    if(defined $section and $section eq "ANSWER"){
      if($line =~ /(\d+\.\d+\.\d+\.\d+)/){
        return $1;
      }
    }
  }
  return undef;
}

sub getHosts(){
  open FH, "< $hostsFile" or die "Could not read $hostsFile\n";
  my @lines = <FH>;
  close FH;

  #remove lines with comment #escribe-host at the end
  for my $line(@lines){
    $line = '' if $line =~ /$marker\s*$/;
  }
  return @lines;
}

sub getSSHConfig(){
  return () if not -e $sshConfigFile;
  open FH, "< $sshConfigFile" or die "Could not read $sshConfigFile\n";
  my @lines = <FH>;
  close FH;

  my $remove = 0;
  my @okLines;
  for my $line(@lines){
    if($line =~ /^\s*$marker\s*$/){
      $remove = not $remove;
    }else{
      push @okLines, $line unless $remove;
    }
  }
  return @okLines;
}

&main(@ARGV);
