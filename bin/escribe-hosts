#!/usr/bin/perl
use strict;
use warnings;

my $hostsFile = "/etc/hosts";
my $sshConfigFile = "$ENV{HOME}/.ssh/config";
my $defaultUser = "ewolk";
my $COMMENT_MARKER = "###escribe-hosts###";

sub updateHostsFile($);
sub updateSSHConfigFile();
sub dig($$);
sub readFileLines($);
sub canPing($);

my $nameserver = '10.128.1.10';

my $cacertsHosts = [qw(
  ehr-01.dev.int.aws.lillegroup.com
  ehr-01.stage.int.aws.lillegroup.com
)];

my $intAws = "int.aws.lillegroup.com";

my $lookupHosts = [
  "ehr-01.dev.$intAws"       => ["ehr-01.dev", "ehr.dev", "ehr.dev.$intAws"],
  "ehr-01.stage.$intAws"     => ["ehr-01.stage", "ehr.stage", "ehr.stage.$intAws"],
  "ehr-01.prod.$intAws"      => ["ehr-01.prod", "ehr.prod", "ehr.prod.$intAws"],

  "ehr-01.testdeploy.$intAws" => ["ehr-01.testdeploy", "ehr.testdeploy", "ehr.testdeploy.$intAws"],

  "ehr-01.test.$intAws"      => ["ehr-01.test", "ehr.test", "ehr.test.$intAws"],
  "ehr-01.demo.$intAws"      => ["ehr-01.demo", "ehr.demo", "ehr.demo.$intAws"],

  "ehr.dev.escribe.com"      => [],
  "ehr.stage.escribe.com"    => [],
  "ehr.prod.escribe.com"     => [],
  "cypress.$intAws"          => ["cypress"],

  "ehr-aux-01.stage.$intAws" => ["ehr-aux-01.stage", "ehr-aux.stage", "ehr-aux.stage.$intAws"],
  "ehr-aux-01.prod.$intAws"  => ["ehr-aux-01.prod", "ehr-aux.prod"],

  "ehr-db.dev.$intAws"       => ["ehr-db.dev"],
  "ehr-db.stage.$intAws"     => ["ehr-db.stage"],
  "ehr-db.prod.$intAws"      => ["ehr-db.prod"],

  "ehr-db.test.$intAws"      => ["ehr-db.test"],
  "ehr-db.demo.$intAws"      => ["ehr-db.demo"],

  "ehr-edi-01.dev.$intAws"   => ["ehr-edi-01.dev", "ehr-edi.dev", "ehr-edi.dev.$intAws",
                                 "edi-01.dev", "edi.dev", "edi.dev.$intAws"],
  "ehr-edi-01.stage.$intAws" => ["ehr-edi-01.stage", "ehr-edi.stage", "ehr-edi.stage.$intAws",
                                 "edi-01.stage", "edi.stage", "edi.stage.$intAws"],
  "ehr-edi-01.prod.$intAws"  => ["ehr-edi-01.prod", "ehr-edi.prod", "ehr-edi.prod.$intAws",
                                 "edi-01.prod", "edi.prod", "edi.prod.$intAws"],

  "ehr-proxy.dev.$intAws"    => ["ehr-proxy.dev"],
  "passwd.$intAws"           => ["passwd"],
  "jenkins.$intAws"          => ["jenkins"],
  "systems-jenkins.$intAws"  => ["systems-jenkins"],
  "trac.$intAws"             => ["trac"],
  "trac-db.$intAws"          => ["trac-db"],
  "mvn.$intAws"              => ["mvn"],

  "grafana.$intAws"          => ["grafana"],
  "mon.$intAws"              => ["mon"],
  "www-01.prod.$intAws"      => ["www-01.prod"],
  "www-02.prod.$intAws"      => ["www-02.prod"],
  "trac.escribe.com"         => ["trac"],
  "proxy.escribe.com"        => ["proxy"],
  "web.escribe.com"          => ["web"],
  "tx.escribe.com"           => ["tx"],
  "escribehost.com"          => [],
];
my @lookupHostNames = map {$$lookupHosts[$_]} grep {$_%2==0} (0..@$lookupHosts-1);
my %lookupHostSyns = @$lookupHosts;

my $fixedHosts = [
  'dev1'                     => '172.18.10.91',
  'dev2'                     => '172.18.10.92',
  'web.escribehost.com'      => '10.1.14.20',
  'escribehost.com'          => '10.1.14.20',
];
my @fixedHostNames = map {$$fixedHosts[$_]} grep {$_%2==0} (0..@$fixedHosts-1);
my %fixedHostIps = @$fixedHosts;

my $sshHostIdentityFiles = {
   "www-01.prod" => "~/.ssh/lillegroup_rsa",
};

#lookupHost, lookupSyns, lookupHost, lookupSyns, ..., fixedHost, fixedHost
my @allHostNames = map {($_, @{$lookupHostSyns{$_}})} @lookupHostNames;
@allHostNames = (@allHostNames, @fixedHostNames);

my $usage = "Usage:
  $0 -h | --help
    show this message

  $0 [--cacerts]
    -dig IPs for indicated <LOOKUP_HOST>s using DNS nameserver $nameserver
    -update /etc/hosts:
       -fetched IPs for the indicated <LOOKUP_HOST>s and <SYNONYMS>
       -hardcoded IPs for the indicated <FIXED_HOSTS>
    -update SSH config file:
       -with User $defaultUser for all <LOOKUP_HOST>s and <FIXED_HOST>s
       -with IdentityFile <IDENTIFY_FILE> for indicated <SSH_HOST>s
    -if --cacerts: update cacerts for the indicated <CACERT_HOST>s

      LOOKUP_HOST => SYNONYMS (for DNS lookup)
        " . join("\n        ", map {"$_ => @{$lookupHostSyns{$_}}"}
                                 @lookupHostNames) . "

      FIXED_HOST => FIXED_IP (hardcoded)
        " . join("\n        ", map {"$_ => $fixedHostIps{$_}"}
                                 @fixedHostNames) . "

      SSH_HOST => IDENTITY_FILE
        " . join("\n        ", map {"$_ => $$sshHostIdentityFiles{$_}"}
                                 sort keys %$sshHostIdentityFiles) . "

      CACERT_HOST
        " . join("\n        ", @$cacertsHosts) . "
";

sub main(@){
  die "Cannot run $0 as root\n" if `whoami` eq "root\n";
  my $cacertsUpdate = 0;
  while(@_ > 0 and $_[0] =~ /-/){
    my $opt = shift @_;
    if($opt =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($opt =~ /^(--cacerts)$/){
      $cacertsUpdate = 1;
    }else{
      die "$usage\nUNKNOWN OPT: $opt\n";
    }
  }
  die "$usage\nUNKNOWN ARGS: @_\n" if @_ != 0;

  if(not canPing $nameserver){
    die "ERROR: cant ping nameserver $nameserver\n";
  }

  STDOUT->autoflush(1);

  print "digging " . @lookupHostNames . " hosts using $nameserver: ";
  my $lookupHostIps = {};
  for my $host(@lookupHostNames){
    print ".";
    my $ip = dig $host, $nameserver;
    $$lookupHostIps{$host} = $ip;
    print STDERR "\nWARNING: unknown server: $host\n" if not defined $ip;
  }
  print "\n";

  updateHostsFile($lookupHostIps);

  updateSSHConfigFile();

  if($cacertsUpdate){
    for my $host(@$cacertsHosts){
      print "\n\n\nrunning: keytool-cacerts $host\n";
      system "keytool-cacerts", $host;
    }
  }

  my @unknownHosts = grep {not defined $$lookupHostIps{$_}} @lookupHostNames;
  if(@unknownHosts > 0){
    print "\n\n\nWARNING: the following hosts were not found\n";
    print "$_\n" foreach @unknownHosts;
  }
}

sub updateHostsFile($){
  my ($lookupHostIps) = @_;

  my $allHostIps = {};
  $allHostIps = {%$allHostIps, %$lookupHostIps};
  $allHostIps = {%$allHostIps, %fixedHostIps};
  for my $host(@lookupHostNames){
    for my $syn(@{$lookupHostSyns{$host}}){
      $$allHostIps{$syn} = $$lookupHostIps{$host};
    }
  }


  my @hostLines = readFileLines($hostsFile);
  #remove lines with <COMMENT_MARKER> at the end
  @hostLines = grep {$_ !~ /$COMMENT_MARKER\s*$/} @hostLines;

  #add lines for each host, ending with #<COMMENT_MARKER>
  for my $host(@allHostNames){
    push @hostLines, "$$allHostIps{$host}\t$host $COMMENT_MARKER\n";
  }
  print "\n\nhosts:\n";
  open FH, "| sudo tee $hostsFile" or die "Could not write to $hostsFile\n";
  print FH @hostLines;
  close FH;
}

sub updateSSHConfigFile(){
  my $sshConfig = join '', readFileLines($sshConfigFile);
  #remove everything between <COMMENT_MARKER> and <COMMENT_MARKER>
  $sshConfig =~ s/$COMMENT_MARKER.*$COMMENT_MARKER\n//s;

  $sshConfig .= "$COMMENT_MARKER\n";
  for my $host(@allHostNames){
    $sshConfig .= ""
      . "Host $host\n"
      . "    User $defaultUser\n"
    ;
    if(defined $$sshHostIdentityFiles{$host}){
      $sshConfig .= "    IdentityFile $$sshHostIdentityFiles{$host}\n"
    }
  }
  $sshConfig .= "$COMMENT_MARKER\n";

  print "\n\nssh config:\n";
  open FH, "| tee $sshConfigFile" or die "Could not write to $hostsFile\n";
  print FH $sshConfig;
  close FH;
}

sub dig($$){
  my ($host, $nameserver) = @_;
  my $section = undef;
  for my $line(`dig \@$nameserver $host`){
    if($line =~ /^;; (\w+) SECTION:$/){
      $section = $1;
    }
    if(defined $section and $section eq "ANSWER"){
      if($line =~ /(\d+\.\d+\.\d+\.\d+)/){
        return $1;
      }
    }
  }
  return undef;
}

sub readFileLines($){
  my ($file) = @_;
  open FH, "< $file" or die "Could not read $file\n$!\n";
  my @lines = <FH>;
  close FH;
  return @lines;
}

sub canPing($){
  my ($host) = @_;
  system "ping $host -c 1 -w 1 >/dev/null 2>/dev/null";
  if($? == 0){
    return 1;
  }else{
    return 0;
  }
}

&main(@ARGV);
