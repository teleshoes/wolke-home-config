#!/usr/bin/perl
use strict;
use warnings;
use Date::Parse qw(str2time);
use Date::Format qw(time2str);

sub getWorkoutDirs();
sub parseWorkoutDirFileInfo($);
sub getSessionsFromFileInfo(@);
sub cleanupSession($);
sub formatSession($);
sub uniq(@);

my $WORKOUTS_BASE_DIR = "$ENV{HOME}/DCIM/xhealth";
my $KILOMETERS_PER_MILE = 1.609344;

my $USAGE = "Usage:
  $0 -h|--help
    show this message

  $0 [OPTS]
    parse and format workout pic info in:
      $WORKOUTS_BASE_DIR/YYYY-MM-DD_NAME/

  OPTS
    -s | --segments
      include speed segment info

    --date=YYYY-MM-DD
    YYYY-MM-DD
      only include workouts on the indicated date

    --type=TYPE
    run
    bike
    weight
      only include workouts of the indicated type
";

sub main(@){
  my $opts = {
    includeSegments => 0,
    dateFilter => undef,
    typeFilter => undef,
  };
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-s|--segments)$/){
      $$opts{includeSegments} = 1;
    }elsif($arg =~ /^(?:--date=)?(\d\d\d\d-\d\d-\d\d)$/){
      $$opts{dateFilter} = $1;
    }elsif($arg =~ /^--type=(.+)$/){
      $$opts{typeFilter} = $1;
    }elsif($arg =~ /^(run|bike|weight)$/){
      $$opts{typeFilter} = $1;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  my @sessions;
  for my $dir(getWorkoutDirs()){
    my @fileInfoEntries = parseWorkoutDirFileInfo($dir);

    my @dirSessions = getSessionsFromFileInfo(@fileInfoEntries);
    for my $s(@dirSessions){
      cleanupSession($s);
      push @sessions, $s;
    }
  }

  for my $session(@sessions){
    if(defined $$opts{dateFilter} and $$session{date} ne $$opts{dateFilter}){
      next;
    }
    if(defined $$opts{typeFilter} and $$session{type} ne $$opts{typeFilter}){
      next;
    }

    print formatSession($session) . "\n";

    if($$opts{includeSegments}){
      my @segments = @{$$session{segments}};
      if(@segments > 0){
        my @segmentsFmt = map {
          sprintf "%.2fs:%.2fm", $$_{duration}, $$_{distance} * 1000.0
        } @segments;
        my $id = $$session{startTime};

        my $maxHR = defined $$session{maxHR} ? $$session{maxHR} : "???";
        printf "%s.epoch = %.3f\n",         $id, $$session{startTime};
        printf "%s.distance = %.2f\n",      $id, $$session{maxKM} * 1000.0;
        printf "%s.duration = %.2f\n",      $id, $$session{maxDurS};
        printf "%s.max_heart_rate = %3s\n", $id, $maxHR;
        printf "%s.intervals = %s\n",       $id, join(",", @segmentsFmt);
      }
    }
  }
}

sub getWorkoutDirs(){
  return glob "$WORKOUTS_BASE_DIR/????-??-??_*/";
}

sub parseWorkoutDirFileInfo($){
  my ($dir) = @_;

  if($dir !~ /^(?:.*\/)?(\d\d\d\d-\d\d-\d\d)_(\w+)(?:_([^\/]*))?\/?$/){
    die "ERROR: malformed dir $dir\n";
  }
  my ($date, $location, $prevInfo) = ($1, $2, $3);
  my @files = glob "$dir/*.*";

  my @fileInfoEntries;
  for my $file(@files){
    next if $file =~ /^(?:.*\/)?VID_/;
    next if $file =~ /^(?:.*\/)?\d\d\d\d\d\d\d\d_\d\d\d\d\d\d\.(jpg|png)$/;

    if($file !~ /^(?:.*\/)?(\d\d\d\d)(\d\d)(\d\d)_(\d\d)(\d\d)(\d\d)_(.+)\.(jpg|png)$/){
      print STDERR "WARNING: malformed file $file\n";
      next;
    }
    my ($y, $m, $d, $hr, $min, $sec, $infoStr, $ext) = ($1, $2, $3, $4, $5, $6, $7, $8);
    my @infoVals = split /-/, $infoStr;

    my $fileTime = str2time("$y-$m-$d $hr:$min:$sec");

    my $info = {};
    push @fileInfoEntries, $info;

    $$info{location} = $location;
    $$info{fileTime} = $fileTime;
    $$info{file} = $file;
    $$info{infoStr} = $infoStr;

    for my $infoSeg(@infoVals){
      if($infoSeg =~ /^(_+)([a-zA-Z_]+)$/){
        #ignore placeholders that start with _ and do not contain digits
      }elsif($infoSeg =~ /^(run|bike|weight)$/){
        $$info{type} = $1;
      }elsif($infoSeg =~ /^(reminder_[a-zA-Z0-9_\-\.]+)$/){
        $$info{reminder} = $1;
      }elsif($infoSeg =~ /^(\d+)m(\d+)s$/){
        $$info{time} = $1*60 + $2;
      }elsif($infoSeg =~ /^(\d+)s$/){
        $$info{time} = $1;
      }elsif($infoSeg =~ /^(\d+)min$/){
        $$info{min} = $1;
      }elsif($infoSeg =~ /^(\d+|\d*\.\d+)km$/){
        $$info{km} = $1;
      }elsif($infoSeg =~ /^(\d+)cmi$/){
        #centimiles to km
        $$info{km} = $1/100.0*$KILOMETERS_PER_MILE;
      }elsif($infoSeg =~ /^(\d+|\d*\.\d+)(mph|hr|hrmax|v|rpm|rpmavg|cal|lbs|offset)$/){
        my ($val, $field) = ($1, $2);
        $$info{$field} = $val;
      }else{
        die "malformed info segment $infoSeg\n";
      }
    }

    if(defined $$info{reminder} and not defined $$info{type}){
      $$info{type} = "reminder";
    }

    if(defined $$info{km} and defined $$info{time} and $$info{time} > 0){
      $$info{realMPH} = ($$info{km}/$KILOMETERS_PER_MILE) / ($$info{time}/60.0/60.0);
    }

    if(defined $$info{rpmavg} and not defined $$info{rpm}){
      $$info{rpm} = $$info{rpmavg};
    }

    if(defined $$info{min} and defined $$info{mph} and not defined $$info{time} and not defined $$info{km}){
      #3min warmup at 2mph, 'min' min workout at 'mph' mph, 3min cooldown at 1.5mph
      $$info{time} = 3*60 + $$info{min}*60 + 3*60;
      $$info{km} = $KILOMETERS_PER_MILE * ( (2*(3/60)) + ($$info{mph}*($$info{min}/60)) + (1.5*(3/60)) );
      $$info{warmCool} = 1;
    }

    if(defined $$info{time}){
      my $offset = defined $$info{offset} ? $$info{offset} : 0;
      $$info{timeFmt} = sprintf "%02d:%02d", int($$info{time}/60), int($$info{time})%60;
      $$info{startTime} = $$info{fileTime} - $$info{time} - $offset;
    }else{
      $$info{timeFmt} = undef;
      $$info{startTime} = undef;
    }
  }

  return sort {$$a{fileTime} <=> $$b{fileTime}} @fileInfoEntries;
}

sub getSessionsFromFileInfo(@){
  my @fileInfoEntries = @_;

  my @sessions;
  for my $info(@fileInfoEntries){
    my $startTime = $$info{startTime};

    my $curSession = undef;
    if(not defined $curSession and defined $startTime){
      #for sessions with time, take the closest by start time
      my $closestStartTimeDiff = undef;
      for my $session(@sessions){
        if(defined $$session{startTime} and $$session{type} eq $$info{type}){
          my $startTimeDiff = $$session{startTime} - $startTime;
          $startTimeDiff = 0 - $startTimeDiff if $startTimeDiff < 0;
          if(not defined $closestStartTimeDiff or $startTimeDiff < $closestStartTimeDiff){
            $curSession = $session;
            $closestStartTimeDiff = $startTimeDiff;
          }
        }
      }
    }
    if(not defined $curSession){
      my $closestFileTimeDiff = undef;
      for my $session(@sessions){
        my $fileTimeDiff = $$session{maxFileTime} - $$info{fileTime};
        $fileTimeDiff = 0 - $fileTimeDiff if $fileTimeDiff < 0;
        if(not defined $closestFileTimeDiff or $fileTimeDiff < $closestFileTimeDiff){
          $curSession = $session;
          $closestFileTimeDiff = $fileTimeDiff;
        }
      }
    }

    my $isNewSession = 0;
    if(not defined $curSession){
      $isNewSession = 1;
    }elsif($$info{type} eq "reminder"){
      my $reminder = $$info{reminder};
      if($reminder =~ /^reminder_(leg_press|toe_press)\w+$/){
        if($$curSession{type} eq "reminder"){
          my $prevRem = ${$$curSession{reminders}}[-1];
          if($prevRem eq $reminder){
            $isNewSession = 0; #same reminder, duplicate
          }else{
            $isNewSession = 1; #new exercise
          }
        }else{
          $isNewSession = 1;
        }
      }else{
        $isNewSession = 0;
      }
    }elsif($$info{type} ne $$curSession{type}){
      #sessions are of different non-reminder types
      $isNewSession = 1;
    }else{
      if(not defined $$info{time}){
        $isNewSession = 0;
      }else{
        my $elapsedRealTimeS = int(($$info{fileTime} - $$curSession{maxFileTime})/1000.0 + 0.5);
        my $elapsedMachineTimeS = $$info{time} - $$curSession{maxDurS};
        if($elapsedMachineTimeS < 0){
          #timer is smaller => machine stopped and restarted
          $isNewSession = 1;
        }elsif($elapsedMachineTimeS < ($elapsedRealTimeS - ($$info{time}/2))){
          #timer is larger, but should be MUCH larger because a lot of real time passed
          $isNewSession = 1;
        }
      }
    }

    if($isNewSession){
      $curSession = {
        type        => $$info{type},
        startTime   => undef,
        minFileTime => $$info{fileTime},
        maxFileTime => $$info{fileTime},
        maxDurS     => 0,
        maxHR       => undef,
        maxDistance => undef,
        lastRPM     => undef,
        weight      => undef,
        segments    => [],
        reminders   => [],
      };
      push @sessions, $curSession;
    }

    if(defined $startTime){
      if(not defined $$curSession{startTime} or $startTime < $$curSession{startTime}){
        $$curSession{startTime} = $startTime;
      }
    }

    if(defined $$info{km} and defined $$info{time} and defined $$info{mph}){
      my $curMPH = sprintf "%.1f", $$info{mph};
      my $segments = $$curSession{segments};
      my $curSegment = @$segments > 0 ? $$segments[-1] : undef;

      if(defined $curSegment){
        #set end of previous segment even if beginning new segment
        $$curSegment{endInfo} = $info;
      }

      if(not defined $curSegment or $$curSegment{mph} != $curMPH){
        $curSegment = {
          mph       => $curMPH,
          startInfo => $info,
          endInfo   => $info,
          distance  => undef,
          duration  => undef,
        };
        push @$segments, $curSegment;
      }
    }

    if($$info{type} eq "reminder"){
      my $reminder = $$info{reminder};
      if($reminder =~ /^reminder_(\d+)hr$/){
        my $hr = $1;
        if(not defined $$curSession{maxHR} or $hr > $$curSession{maxHR}){
          $$curSession{maxHR} = $hr;
        }
      }else{
        push @{$$curSession{reminders}}, $$info{reminder};
      }
    }elsif($$info{type} =~ /^(weight)$/){
      $$curSession{lbs} = $$info{lbs};
    }elsif($$info{type} =~ /^(bike|run)$/){
      if($$info{fileTime} < $$curSession{minFileTime}){
        $$curSession{minFileTime} = $$info{fileTime};
      }
      if($$info{fileTime} > $$curSession{maxFileTime}){
        $$curSession{maxFileTime} = $$info{fileTime};
      }
      if(defined $$info{time}){
        if(not defined $$curSession{maxDurS} or $$curSession{maxDurS} < $$info{time}){
          $$curSession{maxDurS} = $$info{time};
        }
      }
      if(defined $$info{offset}){
        if(not defined $$curSession{maxOffset} or $$curSession{maxOffset} < $$info{offset}){
          $$curSession{maxOffset} = $$info{offset};
        }
      }
      if(defined $$info{km}){
        if(not defined $$curSession{maxKM} or $$curSession{maxKM} < $$info{km}){
          $$curSession{maxKM} = $$info{km};
        }
      }
      if(defined $$info{warmCool}){
        $$curSession{warmCool} = $$info{warmCool};
      }
      if(defined $$info{hr}){
        if(not defined $$curSession{maxHR} or $$curSession{maxHR} < $$info{hr}){
          $$curSession{maxHR} = $$info{hr};
        }
      }
      if(defined $$info{hrmax}){
        if(not defined $$curSession{maxHR} or $$curSession{maxHR} < $$info{hrmax}){
          $$curSession{maxHR} = $$info{hrmax};
        }
      }
      if(defined $$info{v}){
        if(not defined $$curSession{maxLevel} or $$curSession{maxLevel} < $$info{v}){
          $$curSession{maxLevel} = $$info{v};
        }
      }
      if(defined $$info{rpm}){
        $$curSession{lastRPM} = $$info{rpm};
      }
    }
  }

  for my $session(@sessions){
    $$session{date} = time2str("%Y-%m-%d", $$session{minFileTime});
  }

  for my $session(@sessions){
    my @segments = @{$$session{segments}};
    for(my $i=0; $i<@segments; $i++){
      my $segment = $segments[$i];
      my $startInfo = $$segment{startInfo};
      my $endInfo = $$segment{endInfo};

      my $duration = $$endInfo{time} - $$startInfo{time};
      my $distance = $$endInfo{km} - $$startInfo{km};

      my $isFirstSegment = $i == 0 ? 1 : 0;
      if($isFirstSegment){
        $duration += $$startInfo{time}; #first pic may not be 00:00
      }

      $$segment{duration} = $duration;
      $$segment{distance} = $distance;
    }
  }

  return @sessions;
}

sub cleanupSession($){
  my ($s) = @_;

  my @rem = @{$$s{reminders}};
  @rem = uniq @rem;
  $$s{reminders} = [@rem];

  if($$s{type} eq "reminder" and @rem >= 1 and $rem[0] =~ /^reminder_(leg|toe)_press_(\d+)$/){
    $$s{type} = "press";
    $$s{pressType} = $1;
    $$s{pressWeight} = $2;
    $$s{reminders} = [];
    @rem = shift @rem;
    $$s{reminders} = [@rem];
  }


  my $dtm = $$s{maxFileTime};
  if(defined $$s{maxDurS}){
    $dtm -= $$s{maxDurS};
  }
  if(defined $$s{maxOffset}){
    $dtm -= $$s{maxOffset};
  }
  $$s{dtm} = $dtm;

  my $allowAutoWarmCool = 1;

  #skip auto-trimming warm/cool segments for 5k+ runs
  $allowAutoWarmCool = 0 if defined $$s{maxKM} and $$s{maxKM} >= 4.9;

  #skip auto-trimming warm/cool segments for runs under 12min
  $allowAutoWarmCool = 0 if defined $$s{maxDurS} and $$s{maxDurS} < 12*60;

  #auto-trim warm/cool segments
  if($allowAutoWarmCool and $$s{type} eq "run" and defined $$s{maxDurS}){
    $$s{warmCool} = 1;
  }


  #apply warm/cool transformations (remove warmup+cooldown, add reminder)
  if($$s{warmCool}){
    #remove warmup
    $$s{maxDurS} -= 3*60;
    $$s{maxKM} -= $KILOMETERS_PER_MILE * (2.0*(3/60.0));
    #remove cooldown
    $$s{maxDurS} -= 3*60;
    $$s{maxKM} -= $KILOMETERS_PER_MILE * (1.5*(3/60.0));
    push @{$$s{reminders}}, "+warm3m+cool3m"
  }
}

sub formatSession($){
  my ($s) = shift;
  my $dtmFmt = time2str("%Y-%m-%d %H:%M:%S", $$s{dtm});

  my $type = $$s{type};
  my $durFmt = "00:00";
  if(defined $$s{maxDurS}){
    $durFmt = sprintf "%d:%02d", int($$s{maxDurS}/60), $$s{maxDurS} % 60;
  }
  my $mph = 0;
  if(defined $$s{maxDurS} and defined $$s{maxKM} and $$s{maxDurS} > 0){
    $mph = ($$s{maxKM}/$KILOMETERS_PER_MILE) / ($$s{maxDurS}/60.0/60.0);
  }

  my $maxMI = defined $$s{maxKM} ? $$s{maxKM}/$KILOMETERS_PER_MILE : 0;
  my $maxHR = defined $$s{maxHR} ? $$s{maxHR} : "???";

  my $fmt;
  if($type eq "weight"){
    $fmt = "$$s{lbs}lbs";
  }elsif($type =~ /^(press)$/){
    $fmt = "$$s{pressType}$$s{pressWeight}lbs";
  }elsif($type eq "bike"){
    my $rpm = defined $$s{lastRPM} ? $$s{lastRPM} : "???";
    my $lvl = defined $$s{maxLevel} ? $$s{maxLevel} : "??";
    $fmt = sprintf("%5s  %5.2fmi %3sbpm %4.1fmph %3srpm  %2slvl",
      $durFmt,
      $maxMI,
      $maxHR,
      $mph,
      $rpm,
      $lvl,
    );
  }elsif($type eq "run"){
    $fmt = sprintf("%5s  %5.2fmi %3sbpm %.1fmph",
      $durFmt,
      $maxMI,
      $maxHR,
      $mph,
    );
  }else{
    $fmt = "";
  }
  my @rem = uniq @{$$s{reminders}};
  @rem = map {s/^reminder_//; $_} @rem;
  my $remFmt = "";
  if(@rem > 0){
    $remFmt = "  (@rem)";
  }

  return sprintf "%s %-6s %s%s",
    $dtmFmt, $$s{type}, $fmt, $remFmt;
}

sub uniq(@){
  my (@arr) = @_;
  my %seen;
  my @newArr;
  for my $elem(@arr){
    push @newArr, $elem if not $seen{$elem};
    $seen{$elem} = 1;
  }
  return @newArr;
}

&main(@ARGV);
