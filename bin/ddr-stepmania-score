#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub getScoreDataByXmlFile($);
sub formatScoreData($);
sub getSongAbsDir($);
sub getSMFileFromSongDir($);
sub parseXmlFile($);
sub parseSMFile($);
sub parseNoteSetStats($);
sub gameDiffKey($$);
sub getFCValue($);
sub getScoreDDRA($$);
sub getOldestXmlFileAfterMtime($);
sub assertMtimesCloseEnough($$$$);

sub getSongNameWithZeniusTool($);

sub readFile($);
sub listDirFiles($);
sub mtime($);
sub chunkArr($@);

my $XML_FILE_PATTERN = "$ENV{HOME}/.stepmania-*/Save/Upload/*.xml";
my $SONGS_PARENT_DIR = "$ENV{HOME}/.stepmania-5.0";

my $ZENIUS_TOOL = "zenius-ddrsonglist";

my $DDRNAME_TOOL = "ddrname";
my @DDRNAME_OPTS = qw(--no-check-orientation --no-banner-flash);
my $DDRNAME_MACHINE = "sm53pdp";
my $DDRNAME_STAGE = "0none";
my $DDRNAME_BAR = "bar";

my $XML_MTIME_DIFF_WARNING_SECONDS = 90; #warn if image file and XML file mtimes diff is greater
my $XML_MTIME_DIFF_ERROR_SECONDS = 300; #FAIL if image file and XML file mtimes diff is greater

#song-name-id overrides to use instead of zenius
my $SONG_NAME_IDS_BY_SONG_DIR_SUFFIX = {
  #zenius-generated songs with song-dirs that dont match DDR
  "DDR_16_A/Cirno's Perfect Math Class"                    => "cirno-no-perfect-sansuu-kyoushitsu",
  "DDR_16_A/Night of knights"                              => "night-of-nights",
  "DDR_10_Supernova2/SUNKiSS DROP~Alison Side~"            => "sunkiss-drop",

  #custom/stepmania songs with too-long/unclear/duplicate auto-generated song-name-ids
  "ani/Zetsubou Billy [Death Note 2nd Ending] [neothe0ne]" => "zetsubou-billy-death-note",
  "ani/The WORLD - Death Note 1st OP [Asahina-san]"        => "the-world-death-note",
  "otakus_dream_pad_mix_02/(Huzure) Hacking to the Gate (Steins Gate)",
      => "hacking-to-the-gate-huzure",
  "vid/Super Mario Bros"                                   => "super-mario-bros-behemoth",
};
my $FMT_HARDCODED_SONG_NAME_IDS = join "\n        ", map {join " ", @$_}
  chunkArr(3, sort values %$SONG_NAME_IDS_BY_SONG_DIR_SUFFIX);

my $SM_DDRA_DIFF_ARR = [
  ["Beginner"  => "beginner"],
  ["Easy"      => "basic"],
  ["Medium"    => "difficult"],
  ["Hard"      => "expert"],
  ["Expert"    => "challenge"],
  ["Challenge" => "challenge"],
];
my $DDRA_DIFFICULTIES_BY_STEPMANIA_DIFFICULTY = {map {$$_[0] => $$_[1]} @$SM_DDRA_DIFF_ARR};
my @STEPMANIA_DIFFICULTIES = map {$$_[0]} @$SM_DDRA_DIFF_ARR;

my $SM_DDRA_GAMES_ARR = [
  ["dance-single" => "singles"],
  ["dance-double" => "doubles"],
  ["dance-solo"   => undef],
  ["pump-single"  => undef],
  ["pump-double"  => undef],
];
my $DDRA_GAMES_BY_STEPMANIA_GAME = {map {$$_[0] => $$_[1]} @$SM_DDRA_GAMES_ARR};
my @STEPMANIA_GAMES = map {$$_[0]} @$SM_DDRA_GAMES_ARR;

my $EXEC = basename $0;

my $usage = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC --scoredata XML_SCORE_FILE
    -parse stepmania XML file for: SONG_DIR TIMING_DATA PASS_FAIL SM_GAME SM_DIFF
      -SONG_DIR is relative to the dir the stepmania exec is in, and always starts with Songs/
      -TIMING_DATA includes values like: FLAWLESS PERFECT GREAT GOOD OK
      -PASS_FAIL is 'failed' if grade is failed, and 'passed' otherwise
      -SM_GAME is 'StepsType' att of <Steps> in the XML and contains a value like 'dance-single'
      -SM_DIFF is 'Difficulty' att of <Steps> in the XML and contains a value like 'Hard'
    -parse SM_GAME into DDRA_GAME and SM_DIFF into DDRA_DIFFICULTY using hardcoded transforms
    -locate the stepmania *.sm or *.ssc SIMFILE in $SONGS_PARENT_DIR/<SONG_DIR>/*.*
    -parse SONG_NAME out of SONG_DIR, using $ZENIUS_TOOL
      -extract BASENAME_SONG_DIR (the last element of the SONG_DIR path, not including group name)
      -run `$ZENIUS_TOOL --extract-name-id BASENAME_SONG_DIR`
      -override this value for certain hardcoded SONG_DIR values
      -the following SONG_NAME values have hardcoded SONG_DIR values:
        $FMT_HARDCODED_SONG_NAME_IDS
    -calculate the DDR-A style score
      -parse the SIMFILE:
        -obtain DDRA-style DDR_HOLD_ROW_COUNTS (single and multi-arrow holds)
          -stepmania counts multi-arrow holds as multiple holds, DDR as single holds
        -obtain DDRA-style DDR_TAP_ROW_COUNTS (number of rows with taps)
          -same as TIMING_DATA; both DDR and stepmania count multi-arrow taps as single taps
      -calculate the ratio of successful holds:
        -get OK/LETGO/MISSED holds from TIMING_DATA
        -calculate the SM_HOLD_SUCCESS_RATIO: OK-holds divided by (LETGO-holds + MISSED-holds)
      -estimate the number of successful holds there would have been in DDR:
        -multiply SM_HOLD_SUCCESS_RATIO by DDR_HOLD_ROW_COUNTS to obtain ADJUSTED_OK_HOLD_COUNT
      -calculate the DDRA_SCORE as follows:
        -STEP = 1000000 / (DDR_TAP_ROW_COUNTS + DDR_HOLD_ROW_COUNTS)
        -DDRA_UNROUNDED_SCORE = 0
            + ADJUSTED_OK_HOLD_COUNT * STEP
            + FLAWLESS               * (STEP)
            + PERFECT                * (STEP-10)
            + GREAT                  * (STEP*0.6 - 10)
            + GOOD                   * (STEP*0.2 - 10)
        -DDRA_SCORE = floor(DDRA_UNROUNDED_SCORE/10)*10
      -calculate FC value:
        -'nofc' if PASS_FAIL is 'failed', or any hit mines, or any LETGO/MISSED holds
        -'nofc' if any BAD/MISS taps
        -'gfc'  if any GOOD taps       (only FLAWLESS/PERFECT/GREAT/GOOD taps, no BAD/MISS)
        -'fc'   if any GREAT taps      (only FLAWLESS/PERFECT/GREAT taps, no BAD/MISS/GOOD)
        -'pfc'  if any PERFECT taps    (only FLAWLESS/PERFECT taps, no BAD/MISS/GOOD/GREAT)
        -'mfc'  if any FLAWLESS taps   (only FLAWLESS taps, no BAD/MISS/GOOD/GREAT/PERFECT)
      -obtain SCORE_DATA, consisting of the following items:
        -XML_FILE
        -SONG_DIR
        -SONG_NAME
        -PASS_FAIL
        -DDRA_SCORE
        -FC
        -DDRA_GAME
        -DDRA_DIFFICULTY
        -SM_FEET
        -SM_RADAR
      -print each SCORE_DATA item, one per line
        -formatted: 'ITEM_NAME:   ITEM_VALUE'
        -align ITEM_VALUE whitespace

  $EXEC --closest-scoredata FILE
    -fetch CLOSEST_XML_SCORE_FILE
      -take all XML score file matching $XML_FILE_PATTERN
      -ignore any XML files with mtime after FILE mtime
      -of the remaining, select the oldest file as XML_SCORE_FILE
    -obtain SCORE_DATA, as in `$EXEC --scoredata CLOSEST_XML_FILE`
    -print SCORE_DATA, as in `$EXEC --scoredata CLOSEST_XML_FILE`

  $EXEC --ddrname IMG_SCORE_FILE [IMG_SCORE_FILE ..]
    -for each IMG_SCORE_FILE:
      -obtain SCORE_DATA, as in `$EXEC --closest-scoredata IMG_SCORE_FILE`
      -make system call:
          $DDRNAME_TOOL \\
            IMG_SCORE_FILE \\
            @DDRNAME_OPTS \\
            $DDRNAME_MACHINE \\
            $DDRNAME_STAGE \\
            SONG_NAME \\
            PASS_FAIL \\
            DDRA_SCORE \\
            FC \\
            DDRA_GAME \\
            DDRA_DIFFICULTY \\
          ;
";

sub main(@){
  if(@_ == 0 or (@_ == 1 and $_[0] =~ /^(-h|--help)$/)){
    print $usage;
    exit 0;
  }elsif(@_ == 2 and $_[0] =~ /^(--scoredata)$/i){
    my ($cmd, $xmlFile) = @_;
    my $sd = getScoreDataByXmlFile($xmlFile);
    print formatScoreData($sd);
  }elsif(@_ == 2 and $_[0] =~ /^(--closest-scoredata)$/i){
    my ($cmd, $file) = @_;
    my $closestXmlFile = getOldestXmlFileAfterMtime mtime($file);

    assertMtimesCloseEnough($file, $closestXmlFile,
      $XML_MTIME_DIFF_WARNING_SECONDS, $XML_MTIME_DIFF_ERROR_SECONDS);

    my $sd = getScoreDataByXmlFile($closestXmlFile);
    print formatScoreData($sd);
  }elsif(@_ >= 2 and $_[0] =~ /^(--ddrname)$/i){
    my ($cmd, @imgFiles) = @_;
    my $scoreData = {};
    for my $imgFile(@imgFiles){
      my $closestXmlFile = getOldestXmlFileAfterMtime mtime($imgFile);
      assertMtimesCloseEnough($imgFile, $closestXmlFile,
        $XML_MTIME_DIFF_WARNING_SECONDS, $XML_MTIME_DIFF_ERROR_SECONDS);
      $$scoreData{$imgFile} = getScoreDataByXmlFile($closestXmlFile);
    }

    for my $imgFile(@imgFiles){
      my $sd = $$scoreData{$imgFile};
      my @cmd = ($DDRNAME_TOOL,
        $imgFile,
        @DDRNAME_OPTS,
        $DDRNAME_MACHINE,
        $DDRNAME_STAGE,
        $DDRNAME_BAR,
        $$sd{songName},
        $$sd{passFail},
        $$sd{ddraScore},
        $$sd{fc},
        $$sd{ddraGame},
        $$sd{ddraDifficulty},
      );
      print "========================================================\n";
      print "@cmd\n";
      system @cmd;
      print "========================================================\n";
    }
  }else{
    die $usage;
  }
}

sub getScoreDataByXmlFile($){
  my ($xmlFile) = @_;

  my $xmlInfo = parseXmlFile $xmlFile;
  my $songDir = getSongAbsDir $$xmlInfo{songDir};
  my $gameDiffKey = gameDiffKey($$xmlInfo{game}, $$xmlInfo{difficulty});
  my $smFile = getSMFileFromSongDir $songDir;
  my $songInfo = parseSMFile $smFile;
  my $songStats = $$songInfo{$gameDiffKey}{stats};
  die "ERROR: $gameDiffKey is not defined in $songDir/$smFile\n" if not defined $songStats;

  my $songName = getSongNameWithZeniusTool $songDir;
  my $ddraScore = getScoreDDRA($xmlInfo, $songStats);
  my $fc = getFCValue($xmlInfo);

  return {
    xmlFile        => $xmlFile,
    songDir        => $songDir,
    songName       => $songName,
    passFail       => $$xmlInfo{passFail},
    gameDiffKey    => $gameDiffKey,
    ddraScore      => $ddraScore,
    fc             => $fc,
    percentScore   => $$xmlInfo{percentScore},
    ddraGame       => $$DDRA_GAMES_BY_STEPMANIA_GAME{$$xmlInfo{game}},
    ddraDifficulty => $$DDRA_DIFFICULTIES_BY_STEPMANIA_DIFFICULTY{$$xmlInfo{difficulty}},
    smFeet         => $$songInfo{$gameDiffKey}{feet},
    smRadar        => $$songInfo{$gameDiffKey}{radar},
  };
}

sub formatScoreData($){
  my ($sd) = @_;
  return ""
    . "XML_FILE:        $$sd{xmlFile}\n"
    . "SONG_DIR:        $$sd{songDir}\n"
    . "SONG_NAME:       $$sd{songName}\n"
    . "PASS_FAIL:       $$sd{passFail}\n"
    . "DDRA_SCORE:      $$sd{ddraScore}\n"
    . "FC:              $$sd{fc}\n"
    . "DDRA_GAME:       $$sd{ddraGame}\n"
    . "DDRA_DIFFICULTY: $$sd{ddraDifficulty}\n"
    . "SM_FEET:         $$sd{smFeet}\n"
    . "SM_RADAR:        $$sd{smRadar}\n"
    ;
}

sub getSongAbsDir($){
  my ($songRelDir) = @_;
  my $songDir = "$SONGS_PARENT_DIR/$songRelDir";
  $songDir =~ s/\/$//;
  if(not -d $songDir){
    die "ERROR: \"$songDir\" is not a dir\n";
  }
  return $songDir;
}

sub getSMFileFromSongDir($){
  my ($songDir) = @_;
  my @smFiles = grep {$_ =~ /\.(ssc|sm)$/} listDirFiles $songDir;
  die "ERROR: could not find SSC or SM file in $songDir\n" if @smFiles == 0;
  return $smFiles[0];
}

sub parseXmlFile($){
  my ($xmlFile) = @_;
  my $xml = readFile $xmlFile;

  my $num = '(?:\d+|\d*\.\d+)';
  my $int = '\d+';

  my $info = {};
  $$info{songDir} = $1        if $xml =~ /<Song Dir=['"]([^'"]+)['"]\/>/;
  $$info{difficulty} = $1     if $xml =~ /<Steps.* Difficulty=['"]([^'"]+).*\/>/;
  $$info{game} = $1           if $xml =~ /<Steps.* StepsType=['"]([^'"]+).*\/>/;
  $$info{mods} = $1           if $xml =~ /<Modifiers>(.*)<\/Modifiers>/;

  $$info{grade} = $1          if $xml =~ /<Grade>(.*)<\/Grade>/;
  $$info{percentScore} = $1   if $xml =~ /<PercentDP>($num)<\/PercentDP>/;
  $$info{surviveSeconds} = $1 if $xml =~ /<SurviveSeconds>($num)<\/SurviveSeconds>/;
  $$info{maxCombo} = $1       if $xml =~ /<MaxCombo>($int)<\/MaxCombo>/;

  $$info{mineOk} = $1         if $xml =~ /<AvoidMine>($int)<\/AvoidMine>/;
  $$info{mineHit} = $1        if $xml =~ /<HitMine>($int)<\/HitMine>/;

  $$info{holdOk} = $1         if $xml =~ /<Held>($int)<\/Held>/;
  $$info{holdLetGo} = $1      if $xml =~ /<LetGo>($int)<\/LetGo>/;
  $$info{holdMissed} = $1     if $xml =~ /<MissedHold>($int)<\/MissedHold>/;

  $$info{flawless} = $1       if $xml =~ /<W1>($int)<\/W1>/;
  $$info{perfect} = $1        if $xml =~ /<W2>($int)<\/W2>/;
  $$info{great} = $1          if $xml =~ /<W3>($int)<\/W3>/;
  $$info{good} = $1           if $xml =~ /<W4>($int)<\/W4>/;
  $$info{bad} = $1            if $xml =~ /<W5>($int)<\/W5>/;
  $$info{miss} = $1           if $xml =~ /<Miss>($int)<\/Miss>/;

  for my $key(sort keys %$info){
    my ($val) = $$info{$key};
    $val =~ s/&lt;/</g;
    $val =~ s/&apos;/'/g;
    $val =~ s/&amp;/&/g;
    $$info{$key} = $val;
  }

  my @requiredAtts = qw(
    songDir difficulty game grade
    holdOk holdLetGo holdMissed
    flawless perfect great good bad miss
  );

  for my $att(@requiredAtts){
    die "ERROR: could not parse '$att' in $xmlFile\n" if not defined $$info{$att};
  }

  $$info{passFail} = $$info{grade} =~ /failed/i ? "failed" : "passed";

  return $info;
}

sub parseSMFile($){
  my ($smFile) = @_;
  my $sm = readFile $smFile;
  my $noteSets = {};

  my $reGames = join "|", @STEPMANIA_GAMES;
  my $reDiffs = join "|", @STEPMANIA_DIFFICULTIES;

  #SM files
  while($sm =~ /
    \#NOTES                  \s*:\s*
    (?<game>$reGames)        \s*:\s*
    [^:]*?                   \s*:\s*
    (?<difficulty>$reDiffs)  \s*:\s*
    (?<feet>\d+)             \s*:\s*
    (?<radar>[^;]*?)         \s*:\s*
    (?<notes>[^;]+?)         ;
  /gsxi){
    my $gameDiffKey = gameDiffKey($+{game}, $+{difficulty});
    $$noteSets{$gameDiffKey} = {%+};
  }

  #SSC files
  my @sscNoteSets = $sm =~ /\#NOTEDATA\s*:.*?#NOTES\s*:[^;]*;/gsi;
  for my $noteSet(@sscNoteSets){
    my $info = {};
    $$info{game} = $1       if $noteSet =~ /\#STEPSTYPE    \s*:\s*   ($reGames)   \s*;\s*/sxi;
    $$info{difficulty} = $1 if $noteSet =~ /\#DIFFICULTY   \s*:\s*   ($reDiffs)   \s*;\s*/sxi;
    $$info{feet} = $1       if $noteSet =~ /\#METER        \s*:\s*   (\d+)        \s*;\s*/sxi;
    $$info{radar} = $1      if $noteSet =~ /\#RADARVALUES  \s*:\s*   ([^;]+)      \s*;\s*/sxi;
    $$info{notes} = $1      if $noteSet =~ /\#NOTES        \s*:\s*   ([^;]+)      \s*;\s*/sxi;

    die "ERROR: missing STEPSTYPE from $smFile $noteSet\n" if not defined $$info{game};
    die "ERROR: missing DIFFICULTY from $smFile\n" if not defined $$info{difficulty};
    die "ERROR: missing NOTES from $smFile\n" if not defined $$info{notes};

    my $gameDiffKey = gameDiffKey($$info{game}, $$info{difficulty});
    $$noteSets{$gameDiffKey} = $info;
  }

  my $gameDiffKeyCount = keys %$noteSets;
  if($gameDiffKeyCount == 0){
    die "ERROR: could not parse any note sets in $smFile\n";
  }

  for my $game(@STEPMANIA_GAMES){
    for my $diff(@STEPMANIA_DIFFICULTIES){
      my $gameDiffKey = gameDiffKey($game, $diff);
      if(defined $$noteSets{$gameDiffKey}){
        my $stats = parseNoteSetStats $$noteSets{$gameDiffKey}{notes};
        $$noteSets{$gameDiffKey}{stats} = $stats;
      }
    }
  }

  return $noteSets;
}

sub parseNoteSetStats($){
  my ($noteSetText) = @_;
  my @lines = split /[\r\n]+/, $noteSetText;
  my @noteRows;
  for my $line(@lines){
    my $row = $line;

    $row =~ s/\/\/.*//;                   #remove comments
    $row =~ s/F/0/g;                      #remove fakes
    next if $row =~ /^0+$/;               #ignore empty rows
    next if $row !~ /^[01234MLFAKNH]+$/;  #ignore rows that do not contain only note-chars

    push @noteRows, $row;
  }

  my $stats = {
    totalNoteRows  => 0,
    holdRows       => 0,
    jumpRows       => 0,
    taps           => 0,
    holds          => 0,
    rolls          => 0,
    mines          => 0,
  };
  for my $row(@noteRows){
    if($row =~ /[124]/){
      $$stats{totalNoteRows}++;
    }
    if($row =~ /[2]/){
      $$stats{holdRows}++;
    }
    if($row =~ /[124].*[124]/){
      $$stats{jumpRows}++;
    }
    $$stats{taps}  += $row =~ /1/;
    $$stats{holds} += $row =~ /2/;
    $$stats{rolls} += $row =~ /4/;
    $$stats{mines} += $row =~ /M/;
  }
  return $stats;
}

sub gameDiffKey($$){
  my ($game, $diff) = @_;
  return lc "$game-$diff";
}

sub getFCValue($){
  my ($xmlInfo) = @_;

  my $fc;

  if($$xmlInfo{passFail} =~ /failed/i){
    $fc = undef;
  }if($$xmlInfo{holdLetGo} > 0 or $$xmlInfo{holdMissed} > 0){
    $fc = undef;
  }elsif($$xmlInfo{mineHit} > 0){
    $fc = undef;
  }elsif($$xmlInfo{miss} > 0 or $$xmlInfo{bad} > 0){
    $fc = undef;
  }elsif($$xmlInfo{good} > 0){
    $fc = "gfc";
  }elsif($$xmlInfo{great} > 0){
    $fc = "fc";
  }elsif($$xmlInfo{perfect} > 0){
    $fc = "pfc";
  }elsif($$xmlInfo{flawless} > 0){
    $fc = "mfc";
  }else{
    $fc = undef;
  }

  $fc = "nofc" if not defined $fc;

  return $fc;
}

sub getScoreDDRA($$){
  my ($xmlInfo, $songStats) = @_;
  my $step = 1000000 / ($$songStats{totalNoteRows}+$$songStats{holdRows});

  my $xmlHoldsOk = $$xmlInfo{holdOk};
  my $xmlHoldsTotal = ($$xmlInfo{holdOk} + $$xmlInfo{holdLetGo} + $$xmlInfo{holdMissed});
  my $xmlHoldsRatio = $xmlHoldsTotal == 0 ? 0 : $xmlHoldsOk / $xmlHoldsTotal;

  my $adjustedHolds = int($$songStats{holdRows} * $xmlHoldsRatio);

  my $score = 0;
  $score += $adjustedHolds      * $step;
  $score += $$xmlInfo{flawless} * $step;
  $score += $$xmlInfo{perfect}  * ($step-10);
  $score += $$xmlInfo{great}    * (($step*0.6)-10);
  $score += $$xmlInfo{good}     * (($step*0.2)-10);
  $score = int($score/10) * 10;
  return $score;
}

sub getOldestXmlFileAfterMtime($){
  my ($targetMtime) = @_;

  my @xmlFiles = glob $XML_FILE_PATTERN;
  my %mtimes = map {$_ => mtime $_} @xmlFiles;
  @xmlFiles = sort {$mtimes{$a} <=> $mtimes{$b}} @xmlFiles;

  my $closestXmlFile;
  for my $xmlFile(@xmlFiles){
    my $xmlMtime = $mtimes{$xmlFile};
    if($targetMtime < $xmlMtime){
      last;
    }
    $closestXmlFile = $xmlFile;
  }

  return $closestXmlFile;
}

sub assertMtimesCloseEnough($$$$){
  my ($file1, $file2, $warningMtimeDiffS, $errorMtimeDiffS) = @_;
  my $mtime1 = mtime $file1;
  my $mtime2 = mtime $file2;

  my $mtimeDiff = $mtime1 - $mtime2;
  $mtimeDiff = 0-$mtimeDiff if $mtimeDiff < 0;

  if($mtimeDiff > $errorMtimeDiffS){
    die sprintf "\n\nERROR: mtime is more than %ds (%ds) away\n  %s\n  vs\n  %s\n",
      $errorMtimeDiffS, $mtimeDiff, $file1, $file2;
  }elsif($mtimeDiff > $warningMtimeDiffS){
    printf "\n\nWARNING: mtime is more than %ds (%ds) away\n  %s\n  vs\n  %s\n",
      $warningMtimeDiffS, $mtimeDiff, $file1, $file2;
  }
}

sub getSongNameWithZeniusTool($){
  my ($songDir) = @_;
  for my $songDirSuffix(sort keys %$SONG_NAME_IDS_BY_SONG_DIR_SUFFIX){
    if($songDir =~ /(^|\/)\Q$songDirSuffix\E\/*$/){
      return $$SONG_NAME_IDS_BY_SONG_DIR_SUFFIX{$songDirSuffix};
    }
  }
  my $subDir = $songDir;
  $subDir =~ s/\/+$//g;
  $subDir =~ s/.*\///;
  open CMD, "-|", $ZENIUS_TOOL, "--extract-name-id", $subDir;
  my $songName = join '', <CMD>;
  close CMD;
  chomp $songName;
  if($songName =~ /^[a-z0-9\-]+$/){
    return $songName;
  }else{
    die "ERROR: could not extract song name from song dir: $songDir\n";
  }
}

sub readFile($){
  my ($file) = @_;
  open FH, "< $file" or die "ERROR: could not read $file\n";
  my $contents = join '', <FH>;
  close FH;
  return $contents;
}

sub listDirFiles($){
  my ($dir) = @_;
  opendir(my $dh, $dir) or die "ERROR: could not read dir $dir\n$!\n";
  my @files = readdir($dh);
  closedir($dh);
  $dir =~ s/\/?$//;
  @files = map {"$dir/$_"} @files;
  @files = grep {-f $_} @files;
  return @files;
}

sub mtime($){
  my @stat = stat $_[0];
  return $stat[9];
}

sub chunkArr($@){
  my ($chunkSize, @arr) = @_;
  my @chunkArr;
  my $chunk = undef;
  for my $elem(@arr){
    if(defined $chunk and @$chunk >= $chunkSize){
      $chunk = undef;
    }
    if(not defined $chunk){
      $chunk = [];
      push @chunkArr, $chunk;
    }
    push @$chunk, $elem;
  }
  return @chunkArr;
}

&main(@ARGV);
