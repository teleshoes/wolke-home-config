#!/usr/bin/perl
use strict;
use warnings;

my $DATE_FORMAT_SPEC_DEFAULT = "%Y-%m-%dT%H:%M:%S";
my $DATE_FORMAT_SPEC_MILLIS = "%Y-%m-%dT%H:%M:%S.%3N";
my $DATE_FORMAT_SPEC_ISO_8601_SECONDS = "%Y-%m-%dT%H:%M:%S%:z";

my $FILE_MODE_ALWAYS = 'always';
my $FILE_MODE_NEVER = 'never';
my $FILE_MODE_MULTI = 'multi';

my $FILE_DISPLAY_POS_PREFIX = 'prefix';
my $FILE_DISPLAY_POS_SUFFIX = 'suffix';

my $usage = "Usage:
  $0 -h|--help
    print this message

  $0 [OPTS] [FILE FILE ..]
    print file modification time in seconds since epoch UTC, one per line
    if more than one file is specified, prefix each line with 'FILE',
      followed by whitespace (one or more ' ' chars, to align the mtime)

  OPTS
    -f | --format
      format mtime in local time zone
        if --millis is also given:
          same as: --format=$DATE_FORMAT_SPEC_MILLIS
        otherwise:
          same as: --format=$DATE_FORMAT_SPEC_DEFAULT
    --format=DATE_FORMAT
      format mtime using `date +DATE_FORMAT`

    -I | --iso | --iso-8601 | -Iseconds | --iso-8601=seconds
      same as: --format=$DATE_FORMAT_SPEC_ISO_8601_SECONDS
      NOTE: --millis has no effect with -I
    +DATE_FORMAT
      same as: --format=DATE_FORMAT

    -u | --utc
      pass '--utc' to `date` when using DATE_FORMAT
      (no effect without one of the --format args, epoch is always UTC)

    -m | --ms | --millis | --milliseconds
      use milliseconds from Time::HiRes->stat
      if --format, but no DATE_FORMAT given, use format: $DATE_FORMAT_SPEC_MILLIS
      e.g.: 1666124897.300  or  2022-10-18_16-28-17.300

    --file | -p | --prefix
      always include 'FILE' in output
    --no-file | -n | --no-prefix
      never include 'FILE' in output
    --file-multi
      only include 'FILE' in output if more than one file is given
      (this is the default)

    -s | --suffix
      print mtime before the 'FILE', with a single space between
      implies --file, but --file-multi can override this
";

sub main(@){
  my $useDateFormat = 0;
  my $useDateUTC = 0;
  my $dateFormatSpec = undef;
  my $useMillis = 0;
  my $fileMode = $FILE_MODE_MULTI;
  my $fileDisplayPos = $FILE_DISPLAY_POS_PREFIX;
  my @files;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^(-f|--format)$/){
      $useDateFormat = 1;
    }elsif($arg =~ /^--format=(.+)$/){
      $useDateFormat = 1;
      $dateFormatSpec = $1;
    }elsif($arg =~ /^(-I|--iso|--iso-8601|-Iseconds|--iso-8601=seconds)$/){
      $useDateFormat = 1;
      $dateFormatSpec = $DATE_FORMAT_SPEC_ISO_8601_SECONDS;
    }elsif($arg =~ /^\+(.+)$/){
      $useDateFormat = 1;
      $dateFormatSpec = $1;
    }elsif($arg =~ /^(-u|--utc)$/){
      $useDateUTC = 1;
    }elsif($arg =~ /^(-m|--ms|--millis|--milliseconds)$/){
      $useMillis = 1;
    }elsif($arg =~ /^(--file|-p|--prefix)$/){
      $fileMode = $FILE_MODE_ALWAYS;
    }elsif($arg =~ /^(--no-file|-n|--no-prefix)$/){
      $fileMode = $FILE_MODE_NEVER;
    }elsif($arg =~ /^(--file-multi)$/){
      $fileMode = $FILE_MODE_MULTI;
    }elsif($arg =~ /^(-s|--suffix)$/){
      $fileDisplayPos = $FILE_DISPLAY_POS_SUFFIX;
      $fileMode = $FILE_MODE_ALWAYS;
    }elsif(-l $arg or -e $arg){
      push @files, $arg;
    }else{
      die "$usage\nERROR: unknown arg $arg\n";
    }
  }

  my $maxFileLen = 0;
  for my $file(@files){
    my $len = length $file;
    $maxFileLen = $len if $len > $maxFileLen;
  }

  my $displayFile = 0;
  $displayFile = @files > 1 ? 1 : 0 if $fileMode eq $FILE_MODE_MULTI;
  $displayFile = 1 if $fileMode eq $FILE_MODE_ALWAYS;
  $displayFile = 0 if $fileMode eq $FILE_MODE_NEVER;

  for my $file(@files){
    my @stat;
    if($useMillis){
      require Time::HiRes;
      @stat = Time::HiRes::lstat $file;
    }else{
      @stat = lstat $file;
    }

    my $mtime = $stat[9];
    my $mtimeFmt;
    if($useDateFormat){
      if(not defined $dateFormatSpec){
        $dateFormatSpec = $useMillis ? $DATE_FORMAT_SPEC_MILLIS : $DATE_FORMAT_SPEC_DEFAULT;
      }
      my @dateArgs = $useDateUTC ? ("--utc") : ();
      $mtimeFmt = `date @dateArgs --date \@$mtime +$dateFormatSpec`;
      chomp $mtimeFmt;
    }elsif($useMillis){
      $mtimeFmt = sprintf "%.3f", int($mtime * 1000.0) / 1000.0;
    }else{
      $mtimeFmt = int($mtime);
    }

    if($displayFile){
      if($fileDisplayPos eq $FILE_DISPLAY_POS_PREFIX){
        my $len = $maxFileLen + 2;
        printf "\%-${len}s \%s\n", $file, $mtimeFmt;
      }elsif($fileDisplayPos eq $FILE_DISPLAY_POS_SUFFIX){
        printf "%s %s\n", $mtimeFmt, $file;
      }else{
        die "ERROR: unknown file display pos $fileDisplayPos\n";
      }
    }else{
      print "$mtimeFmt\n";
    }
  }
}

&main(@ARGV);
