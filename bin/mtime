#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $DATETIME_FORMAT_EPOCH = "%s";
my $DATETIME_FORMAT_PRETTY = "%Y-%m-%dT%H:%M:%S";
my $DATETIME_FORMAT_ISO = "%Y-%m-%dT%H:%M:%S%:z";
my $DATETIME_FORMAT_FILENAME = "%Y%m%d_%H%M%S";

my $FILE_WHEN_ALWAYS = 'always';
my $FILE_WHEN_NEVER = 'never';
my $FILE_WHEN_MULTI = 'multi';

my $FILEPOS_PREFIX = 'prefix';
my $FILEPOS_SUFFIX = 'suffix';

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    print this message

  $EXEC [OPTS] [FILE FILE ..]
    print file modification time in seconds since epoch UTC, one per line
      if more than one file is specified, prefix each line with 'FILE',
        followed by whitespace (one or more ' ' chars, to align the mtime)

  OPTS
    --format=DATE_FORMAT
    +DATE_FORMAT
      format mtime in local time zone using `date +DATE_FORMAT`
        -if DATE_FORMAT contains %N or %<WIDTH>N (e.g. '%N' or '%5N'):
          -use Time::HiRes::lstat to get file mtime to millisecond precision
        -default is: --format=$DATETIME_FORMAT_EPOCH

    -f | --format
      same as: --format=$DATETIME_FORMAT_PRETTY
    -I | --iso | --iso-8601 | -Iseconds | --iso-8601=seconds
      same as: --format=$DATETIME_FORMAT_ISO
    -w | --format-file
      same as: --format=$DATETIME_FORMAT_FILENAME

    -m | --millis
    --fracs | --frac-s
      modify DATE_FORMAT after --format args
        -replace all instances of '%s' with '%s.%3N'
        -replace all instances of '%S' with '%S.%3N'
      e.g.:
        '$EXEC -m FILE'     =>   1666124897.300
        '$EXEC -m -f FILE'  =>   2022-10-18T16:28:17.300

    -u | --utc
      pass '--utc' to `date` when using DATE_FORMAT
      (no effect without one of the --format args, epoch is always UTC)

    --file=FILE_WHEN
      control printing filename in output
        FILE_WHEN
          always
            always include 'FILE' in output
          never
            never include 'FILE' in output
          multi
            only include 'FILE' in output if more than one file is given
            (this is the default)

    --filepos=FILE_POS
      control format of filename when printing 'FILE'
      FILE_POS
        prefix
          print 'FILE' before mtime, padding with whitespace to align mtime column
        suffix
          print mtime before the 'FILE', with a single space between

    -p | --prefix
      same as: --file=always --file-pos=prefix
    -n | --no-file
      same as: --file=never
    -s | --suffix
      same as: --file=always --file-pos=suffix
";

sub main(@){
  my $useDateUTC = 0;
  my $dateFormatSpec = $DATETIME_FORMAT_EPOCH;
  my $appendMillisFormat = 0;
  my $fileWhen = $FILE_WHEN_MULTI;
  my $filePos = $FILEPOS_PREFIX;
  my @files;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^--format=(.+)$/){
      $dateFormatSpec = $1;
    }elsif($arg =~ /^\+(.+)$/){
      $dateFormatSpec = $1;
    }elsif($arg =~ /^(-f|--format)$/){
      $dateFormatSpec = $DATETIME_FORMAT_PRETTY;
    }elsif($arg =~ /^(-I|--iso|--iso-8601|-Iseconds|--iso-8601=seconds)$/){
      $dateFormatSpec = $DATETIME_FORMAT_ISO;
    }elsif($arg =~ /^(-w|--format-file)$/){
      $dateFormatSpec = $DATETIME_FORMAT_FILENAME;
    }elsif($arg =~ /^(-m|--millis|--fracs|--frac-s)$/){
      $appendMillisFormat = 1;
    }elsif($arg =~ /^(-u|--utc)$/){
      $useDateUTC = 1;
    }elsif($arg =~ /^(--file=$FILE_WHEN_ALWAYS)$/){
      $fileWhen = $FILE_WHEN_ALWAYS;
    }elsif($arg =~ /^(--file=$FILE_WHEN_NEVER)$/){
      $fileWhen = $FILE_WHEN_NEVER;
    }elsif($arg =~ /^(--file=$FILE_WHEN_MULTI)$/){
      $fileWhen = $FILE_WHEN_MULTI;
    }elsif($arg =~ /^(--filepos=$FILEPOS_PREFIX)$/){
      $filePos = $FILEPOS_PREFIX;
    }elsif($arg =~ /^(--filepos=$FILEPOS_SUFFIX)$/){
      $filePos = $FILEPOS_SUFFIX;
    }elsif($arg =~ /^(-p|--prefix)$/){
      $fileWhen = $FILE_WHEN_ALWAYS;
      $filePos = $FILEPOS_PREFIX;
    }elsif($arg =~ /^(-n|--no-file)$/){
      $fileWhen = $FILE_WHEN_NEVER;
    }elsif($arg =~ /^(-s|--suffix)$/){
      $fileWhen = $FILE_WHEN_ALWAYS;
      $filePos = $FILEPOS_SUFFIX;
    }elsif(-l $arg or -e $arg){
      push @files, $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($appendMillisFormat){
    $dateFormatSpec =~ s/%s/%s.%3N/g;
    $dateFormatSpec =~ s/%S/%S.%3N/g;
  }

  my $formatContainsNano = $dateFormatSpec =~ /%\d*N/ ? 1 : 0;

  my $maxFileLen = 0;
  for my $file(@files){
    my $len = length $file;
    $maxFileLen = $len if $len > $maxFileLen;
  }

  my $displayFile = 0;
  $displayFile = @files > 1 ? 1 : 0 if $fileWhen eq $FILE_WHEN_MULTI;
  $displayFile = 1 if $fileWhen eq $FILE_WHEN_ALWAYS;
  $displayFile = 0 if $fileWhen eq $FILE_WHEN_NEVER;

  for my $file(@files){
    my @stat;
    if($formatContainsNano){
      require Time::HiRes;
      @stat = Time::HiRes::lstat $file;
    }else{
      @stat = lstat $file;
    }

    my $mtime = $stat[9];

    my $mtimeFmt;
    #skip calling `date` for simple epoch formats
    if($dateFormatSpec eq "%s"){
      $mtimeFmt = int($mtime);
    }elsif($dateFormatSpec eq "%s.%3N"){
      $mtimeFmt = sprintf "%.3f", int($mtime * 1000.0) / 1000.0;
    }else{
      my @dateArgs = $useDateUTC ? ("--utc") : ();
      $mtimeFmt = `date @dateArgs --date \@$mtime +$dateFormatSpec`;
      chomp $mtimeFmt;
    }

    if($displayFile){
      if($filePos eq $FILEPOS_PREFIX){
        my $len = $maxFileLen + 2;
        printf "\%-${len}s \%s\n", $file, $mtimeFmt;
      }elsif($filePos eq $FILEPOS_SUFFIX){
        printf "%s %s\n", $mtimeFmt, $file;
      }else{
        die "ERROR: unknown file display pos $filePos\n";
      }
    }else{
      print "$mtimeFmt\n";
    }
  }
}

&main(@ARGV);
