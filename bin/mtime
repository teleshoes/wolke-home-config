#!/usr/bin/perl
use strict;
use warnings;

my $usage = "Usage:
  $0 -h|--help
    print this message

  $0 [OPTS] [FILE FILE ..]
    print file modification time in seconds since epoch UTC, one per line
    if more than one file is specified, prefix each line with 'FILE',
      followed by whitespace to align the mtime

  OPTS
    -f  format mtime YYYY-mm-dd_HH_MM_SS in local time
";

sub main(@){
  my $format = 0;
  while(@_ > 0 and $_[0] =~ /^-/ and not -e $_[0]){
    my $opt = shift;
    if($opt =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($opt =~ /^(-f)$/){
      $format = 1;
    }else{
      die $usage;
    }
  }

  my $maxFileLen = 0;
  for my $file(@_){
    my $len = length $file;
    $maxFileLen = $len if $len > $maxFileLen;
  }

  for my $file(@_){
    print STDERR "$file does not exist\n" if not -e $file;
    my @stat = stat $file;
    my $mtime = $stat[9];
    if($format){
      $mtime = `date --date \@$mtime +%Y-%m-%d_%H-%M-%S`;
      chomp $mtime;
    }
    if(@_ > 1){
      my $len = $maxFileLen + 2;
      printf "\%-${len}s \%s\n", $file, $mtime;
    }else{
      print "$mtime\n";
    }
  }
}

&main(@ARGV);
