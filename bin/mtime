#!/usr/bin/perl
use strict;
use warnings;

my $usage = "Usage:
  $0 -h|--help
    print this message

  $0 [OPTS] [FILE FILE ..]
    print file modification time in seconds since epoch UTC, one per line
    if more than one file is specified, prefix each line with 'FILE',
      followed by whitespace to align the mtime

  OPTS
    -f
      format mtime YYYY-mm-dd_HH_MM_SS in local time
    -m | --ms | --millis | --milliseconds
      use milliseconds from Time::HiRes->stat
      if -f, use format: HH_MM_SS.FFF
    -p | --prefix
      always prefix line with 'FILE' and whitespace
    -n | --no-prefix
      never prefix line with 'FILE' and whitespace
";

sub main(@){
  my $format = 0;
  my $useMillis = 0;
  my $alwaysPrefix = 0;
  my $neverPrefix = 0;
  while(@_ > 0 and $_[0] =~ /^-/ and not -e $_[0]){
    my $opt = shift;
    if($opt =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($opt =~ /^(-f)$/){
      $format = 1;
    }elsif($opt =~ /^(-m|--ms|--millis|--milliseconds)$/){
      $useMillis = 1;
    }elsif($opt =~ /^(-p|--prefix)$/){
      $alwaysPrefix = 1;
    }elsif($opt =~ /^(-n|--no-prefix)$/){
      $neverPrefix = 1;
    }else{
      die $usage;
    }
  }

  my $maxFileLen = 0;
  for my $file(@_){
    my $len = length $file;
    $maxFileLen = $len if $len > $maxFileLen;
  }

  my $usePrefix;
  if($alwaysPrefix){
    $usePrefix = 1;
  }elsif($neverPrefix){
    $usePrefix = 0;
  }elsif(@_ > 1){
    $usePrefix = 1;
  }else{
    $usePrefix = 0;
  }

  for my $file(@_){
    print STDERR "$file does not exist\n" if not -e $file;
    my @stat;
    if($useMillis){
      require Time::HiRes;
      @stat = Time::HiRes::stat $file;
    }else{
      @stat = stat $file;
    }

    my $mtime = $stat[9];
    my $mtimeFmt;
    if($format){
      $mtimeFmt = `date --date \@$mtime +%Y-%m-%d_%H-%M-%S`;
      chomp $mtimeFmt;
      if($useMillis){
        my $fracS = $mtime - int($mtime);
        $mtimeFmt .= sprintf ".%03d", $fracS * 1000.0;
      }
    }elsif($useMillis){
      $mtimeFmt = int($mtime * 1000.0);
    }else{
      $mtimeFmt = int($mtime);
    }

    if($usePrefix){
      my $len = $maxFileLen + 2;
      printf "\%-${len}s \%s\n", $file, $mtimeFmt;
    }else{
      print "$mtimeFmt\n";
    }
  }
}

&main(@ARGV);
