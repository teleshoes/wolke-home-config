#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub getResconfigCmd();

my $RESCONFIG_FILE = "$ENV{HOME}/resconfig-screen";

my $EXEC = basename $0;

my $usage = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC
  $EXEC guess | --guess
    -read 'screen_off_type=TYPE' from file:
      $RESCONFIG_FILE
    -if TYPE = dpms:
      same as: $EXEC --dpms
    -if TYPE = cec:
      same as: $EXEC --cec
    -if TYPE = gpio:
      same as: $EXEC --gpio
    -if file is missing, screen_off is missing, or TYPE is unknown:
      same as: $EXEC --dpms

  $EXEC dpms | --dpms
    run: xset dpms force off

  $EXEC cec | --cec
    run: cec-cmd --tv off

  $EXEC gpio | --gpio
    run: tv-gpio off
";

my $COMMAND_GUESS = "guess";
my $COMMAND_DPMS = "dpms";
my $COMMAND_CEC = "cec";
my $COMMAND_GPIO = "gpio";

sub main(@){
  my $cmd = $COMMAND_GUESS;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^(guess|--guess)$/){
      $cmd = $COMMAND_GUESS;
    }elsif($arg =~ /^(dpms|--dpms)$/){
      $cmd = $COMMAND_DPMS;
    }elsif($arg =~ /^(cec|--cec)$/){
      $cmd = $COMMAND_CEC;
    }elsif($arg =~ /^(gpio|--gpio)$/){
      $cmd = $COMMAND_GPIO;
    }else{
      die "$usage\nERROR: unknown arg $arg\n";
    }
  }

  if(not defined $ENV{DISPLAY} or $ENV{DISPLAY} eq ""){
    $ENV{DISPLAY} = ":0";
  }

  if($cmd eq $COMMAND_GUESS){
    $cmd = getResconfigCmd();
    if(not defined $cmd){
      $cmd = $COMMAND_DPMS;
    }
  }

  if($cmd eq $COMMAND_DPMS){
    exec "xset dpms force off";
  }elsif($cmd eq $COMMAND_CEC){
    exec "cec-cmd --tv off";
  }elsif($cmd eq $COMMAND_GPIO){
    exec "tv-gpio off";
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub getResconfigCmd(){
  my $type = "";
  if(-f $RESCONFIG_FILE){
    open my $fh, "< $RESCONFIG_FILE" or die "ERROR: could not read $RESCONFIG_FILE\n$!\n";
    my $contents = join '', <$fh>;
    close $fh;
    if($contents =~ /^\s*screen_off_type\s*=\s*(\w+)\s*$/m){
      $type = $1;
    }
  }

  if($type =~ /^$COMMAND_DPMS$/i){
    return $COMMAND_DPMS;
  }elsif($type =~ /^$COMMAND_CEC$/i){
    return $COMMAND_CEC;
  }elsif($type =~ /^$COMMAND_GPIO$/i){
    return $COMMAND_GPIO;
  }else{
    return undef;
  }
}

&main(@ARGV);
