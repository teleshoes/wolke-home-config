#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $EXEC_RES = basename $0;

my $usage = "Usage:
  $EXEC_RES --help
    show this message

  $EXEC_RES [OPTS]
  $EXEC_RES [OPTS] -g|--get
    print \"<WIDTH>x<HEIGHT>\" of the <FIRST_MONITOR>

  $EXEC_RES [OPTS] -s GEOMETRY
  $EXEC_RES [OPTS] --set GEOMETRY
  $EXEC_RES [OPTS] --set=GEOMETRY
    set xrandr mode by calling:
      xrandr --output <FIRST_MONITOR> --mode GEOMETRY

  $EXEC_RES [OPTS] --clone
    position the first monitor the same as the last monitor
      (swap order if --alt)
      xrandr --output <FIRST_MONITOR> --same-as <LAST_MONITOR>

  GEOMETRY
    <WIDTH>x<HEIGHT>

  FIRST_MONITOR
    the first monitor returned by `xrandr --list-monitors`
  LAST_MONITOR
    the last monitor returned by `xrandr --list-monitors`

  OPTS:
    --alt | --hdmi
      swap first/last monitors for --get --set and --clone
        set <FIRST_MONITOR> to the last monitor in `xrandr --listmonitors`
        set <LAST_MONITOR> to the first monitor in `xrandr --listmonitors`
    -w | --width | WIDTH | width
      print just <WIDTH> in --get
    -h | --height | HEIGHT | height
      print just <HEIGHT> in --get
    --percent=PERCENT
      print modified <WIDTH> and <HEIGHT> in --get
      multiply the <WIDTH> and <HEIGHT> by PERCENT,
        divide by 100,
        and round to the nearest pixel
      <PERCENT> can be any non-negative rational number
      e.g.: `$EXEC_RES -p 50` prints \"960x540\" on 1920x1080 screen
";

sub getMonitors();

my $CMD_GET = "get";
my $CMD_SET = "set";
my $CMD_CLONE = "clone";

sub main(@){
  my $cmd = $CMD_GET;
  my $setMode = undef;
  my $altMonitor = 0;
  my $showWidth = 1;
  my $showHeight = 1;
  my $percent = 100;
  while(@_ > 0){
    my $opt = shift;
    if($opt =~ /^(--help)$/){
      print $usage;
      exit 0;
    }elsif($opt =~ /^(-g|--get)$/){
      $cmd = $CMD_GET;
    }elsif($opt =~ /^--set=(\d+x\d+)$/){
      $cmd = $CMD_SET;
      $setMode = $1;
    }elsif($opt =~ /^(-s|--set)$/ and @_ > 0 and $_[0] =~ /^(\d+x\d+)$/){
      $cmd = $CMD_SET;
      $setMode = $1;
      shift;
    }elsif($opt =~ /^(--clone)$/){
      $cmd = $CMD_CLONE;
    }elsif($opt =~ /^(--alt|--hdmi)$/){
      $altMonitor = 1;
    }elsif($opt =~ /^(-w|--width|WIDTH|width)$/){
      $showWidth = 1;
      $showHeight = 0;
    }elsif($opt =~ /^(-h|--height|HEIGHT|height)$/){
      $showWidth = 0;
      $showHeight = 1;
    }elsif($opt =~ /^--percent=(\d+(?:\.\d+)?)$/){
      $percent = $1;
    }else{
      die "$usage\nERROR: unknown opt $opt\n";
    }
  }

  my @monitors = getMonitors();
  my $primaryMonitor = $altMonitor ? $monitors[-1] : $monitors[0];
  my $secondaryMonitor = $altMonitor ? $monitors[0] : $monitors[-1];

  if($cmd eq $CMD_GET){
    my ($width, $height) = ($$primaryMonitor{width}, $$primaryMonitor{height});
    $width = int($width * $percent / 100 + 0.5);
    $height = int($height * $percent / 100 + 0.5);

    my $fmt = "";
    $fmt .= $width if $showWidth;
    $fmt .= "x" if $showWidth and $showHeight;
    $fmt .= $height  if $showHeight;

    print "$fmt\n";
  }elsif($cmd eq $CMD_SET){
    my $name = $$primaryMonitor{name};
    my @cmd = ("xrandr", "--output", $name, "--mode", $setMode);
    print "@cmd\n";
    system @cmd;
  }elsif($cmd eq $CMD_CLONE){
    my @cmd = ("xrandr",
      "--output", $$primaryMonitor{name},
      "--same-as", $$secondaryMonitor{name},
    );
    print "@cmd\n";
    system @cmd;
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub getMonitors(){
  my @xrandrOutput = `xrandr --listmonitors`;
  die "Error running xrandr\n" if $? != 0;

  my @monitors;
  for my $line(@xrandrOutput){
    if($line =~ /^\s*(\d+): [+*]*([a-zA-Z0-9_\-]+)\s+(\d+)\/\d+x(\d+)\/\d+\+\d+\+\d+\s+/){
      my ($num, $name, $width, $height) = ($1, $2, $3, $4);
      push @monitors, {name => $name, width => $width, height => $height};
    }
  }
  die "ERROR: could not parse xrandr output\n" if @monitors == 0;
  return @monitors;
}

&main(@ARGV);
