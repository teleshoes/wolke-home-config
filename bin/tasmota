#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
my $FORKMGR_LIB = eval {require Parallel::ForkManager};

my $WIFI_SSID = "FlipsAFad";
my $INITIAL_DEVICE_IP = "192.168.4.1";
my $DEFAULT_DEVICE = "outlet1";

my @DEVICES_ARR = (
  init    => "$INITIAL_DEVICE_IP",
  bulb1   => "192.168.11.93",
  bulb2   => "192.168.11.94",
  bulb3   => "192.168.11.95",
  outlet1 => "192.168.11.96",
  outlet2 => "192.168.11.97",
  outlet3 => "192.168.11.98",
);
my @DEVICE_NAMES = map {$DEVICES_ARR[$_]}
                   grep {$_ % 2 == 0} (0..$#DEVICES_ARR);
my %DEVICES = @DEVICES_ARR;

my @DEVICE_ALIASES_ARR = (
  "all"   => [qw(bulb1 bulb2 bulb3 outlet1 outlet2 outlet3)],

  "b"     => [qw(bulb1 bulb2 bulb3)],
  "bulbs" => [qw(bulb1 bulb2 bulb3)],

  "b1"    => [qw(bulb1)],
  "b2"    => [qw(bulb2)],
  "b3"    => [qw(bulb3)],
  "o1"    => [qw(outlet1)],
  "o2"    => [qw(outlet2)],
  "o3"    => [qw(outlet3)],
);
my @DEVICE_ALIAS_NAMES = map {$DEVICE_ALIASES_ARR[$_]}
                         grep {$_ % 2 == 0} (0..$#DEVICE_ALIASES_ARR);
my %DEVICE_ALIASES = @DEVICE_ALIASES_ARR;

my @COMMAND_ALIASES_ARR = (
  on      => 'POWER ON',
  off     => 'POWER OFF',
  toggle  => 'POWER TOGGLE',

  default => 'COLOR 99999999ff',
  warm    => 'COLOR 00000000ff',
  cold    => 'COLOR ffffffffff',
  bright  => 'COLOR ffffffffff',

  dim     => 'COLOR 0000000044',

  red     => 'COLOR ff00000000',
  green   => 'COLOR 00ff000000',
  blue    => 'COLOR 0000ff0000',

  cyan    => 'COLOR 00ffff0000',
  magenta => 'COLOR ff00ff0000',
  yellow  => 'COLOR ffff000000',

  purple  => 'COLOR 8000800000',
  brown   => 'COLOR 6543210000',
);
my @COMMAND_ALIAS_NAMES = map {$COMMAND_ALIASES_ARR[$_]}
                          grep {$_ % 2 == 0} (0..$#COMMAND_ALIASES_ARR);
my %COMMAND_ALIASES = @COMMAND_ALIASES_ARR;


my $DEVICES_REGEX = join "|", sort keys %DEVICES;

my $DEVICE_ALIASES_REGEX = join "|", sort keys %DEVICE_ALIASES;
my $DEVICE_ALIAS_FMT = join "",
                       map {"      $_ = @{$DEVICE_ALIASES{$_}}\n"}
                       @DEVICE_ALIAS_NAMES;


my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC -s | --setup-wifi
    assuming connection to newly flashed/reset tasmota device,
    send command to $INITIAL_DEVICE_IP to set:
    SSID1=$WIFI_SSID
    Password1=`winfo --key $WIFI_SSID`
      to set up a new device:
        -connect to SSID (looks like: tasmota_FFFFFF-9999)
        -run `$EXEC --mac init` (and note the MAC)
        -run `$EXEC --setup-wifi`
        -add a static DHCP entry for MAC and add to device list
        -restart router

  $EXEC [DEVICE DEVICE ..] COMMAND
    use curl to send <COMMAND> to each named <DEVICE>
    <DEVICE> defaults to '$DEFAULT_DEVICE' if none are given
    available <DEVICE> arguments:
      $DEVICE_ALIASES_REGEX|$DEVICES_REGEX

  $EXEC --toggle-all | -t [DEVICE DEVICE ..]
  $EXEC [DEVICE DEVICE ..]
    either power on all devices OR power off all <DEVICE>s
    does NOT send 'power toggle' command
      -send <COMMAND> 'POWER' to all <DEVICE>s
      -if response is 'OFF' for all: send <COMMAND> 'POWER ON' to all <DEVICE>s
      -otherwise: send <COMMAND> 'POWER OFF' to all <DEVICE>s

  $EXEC --mac [DEVICE DEVICE ..]
    send 'Status 0' command and parse the output to get the MAC address

  DEVICE = DEVICE_NAME | DEVICE_ALIAS
    name of device, or named alias for a list of devices

    DEVICE_NAME
      $DEVICES_REGEX

    DEVICE_ALIAS" . "\n$DEVICE_ALIAS_FMT\n" . "
";

sub runCommandOnDevices($@);
sub cmd($$);

my $MODE_COMMAND = "command";
my $MODE_TOGGLE_ALL = "toggle-all";
my $MODE_MAC = "mac";
my $MODE_INIT_DEVICE = "init-device";

sub main(@){
  my $mode = $MODE_TOGGLE_ALL;
  my (@devices, $cmd);
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-s|--setup-wifi)$/){
      $mode = $MODE_INIT_DEVICE;
    }elsif($arg =~ /^($DEVICES_REGEX)$/){
      push @devices, $arg;
    }elsif($arg =~ /^($DEVICE_ALIASES_REGEX)$/){
      @devices = (@devices, @{$DEVICE_ALIASES{$arg}});
    }elsif($arg =~ /^(--toggle-all|-t|)$/){
      $mode = $MODE_TOGGLE_ALL;
      $cmd = undef;
    }elsif($arg =~ /^(--mac)$/){
      $mode = $MODE_MAC;
      $cmd = undef;
    }elsif(defined $COMMAND_ALIASES{lc $arg}){
      $mode = $MODE_COMMAND;
      $cmd = $COMMAND_ALIASES{lc $arg};
    }elsif(not defined $cmd and @_ == 0){
      $mode = $MODE_COMMAND;
      $cmd = $arg;
    }else{
      die $USAGE;
    }
  }

  push @devices, $DEFAULT_DEVICE if @devices == 0;

  if($mode eq $MODE_COMMAND){
    my $outputByDevice = runCommandOnDevices($cmd, @devices);
    print "$_ - $$outputByDevice{$_}\n" foreach @devices;
  }elsif($mode eq $MODE_TOGGLE_ALL){
    my $outputByDevice = runCommandOnDevices("POWER", @devices);
    print "$_ - $$outputByDevice{$_}\n" foreach @devices;
    my $allOff = 1;
    print "=>\n";
    for my $device(@devices){
      my $output = $$outputByDevice{$device};
      $output = "" if not defined $output;
      if($output !~ /{"POWER":"OFF"}/){
        $allOff = 0;
        last;
      }
    }
    my $cmd = $allOff ? "POWER ON" : "POWER OFF";
    $outputByDevice = runCommandOnDevices($cmd, @devices);
    print "$_ - $$outputByDevice{$_}\n" foreach @devices;
  }elsif($mode eq $MODE_MAC){
    my $outputByDevice = runCommandOnDevices("STATUS 0", @devices);
    for my $device(@devices){
      my $out = $$outputByDevice{$device};
      my $mac = $1 if $out =~ /"Mac":"([^"]+)"/;
      $mac = "?" if not defined $mac;
      print "$device - $mac\n";
    }
  }elsif($mode eq $MODE_INIT_DEVICE){
    my $key = `winfo --key $WIFI_SSID`;
    chomp $key;
    cmd $INITIAL_DEVICE_IP, "BACKLOG SSID1 $WIFI_SSID ; Password1 $key";
  }else{
    die "ERROR: unknown mode \"$mode\"\n";
  }
}

sub runCommandOnDevices($@){
  my ($cmd, @devices) = @_;
  my $outputByDevice = {};
  if($FORKMGR_LIB){
    my $tmpDir = "/tmp/$EXEC-perl-storable";
    $ENV{TMPDIR} = $tmpDir;
    system "mkdir", "-p", "/tmp/$EXEC-perl-storable";

    my $pm = Parallel::ForkManager->new(10, $tmpDir);
    $pm->set_waitpid_blocking_sleep(0);  #enable true blocking calls

    for my $device(@devices){
      if(defined $$outputByDevice{$device}){
        die "ERROR: duplicate device $device (not allowed in parallel)\n";
      }
      $$outputByDevice{$device} = "UNKNOWN";
    }

    $pm->run_on_finish(
      sub {
        my ($pid, $exit_code, $ident, $exit_signal, $core_dump, $data_structure_reference) = @_;
        $$outputByDevice{$ident} = $$data_structure_reference;
      }
    );

    for my $device(@devices){
      my $pid = $pm->start($device);
      next if $pid;

      my $out = cmd $DEVICES{$device}, $cmd;
      $pm->finish(0, \$out);
    }
    $pm->wait_all_children();
  }else{
    for my $device(@devices){
      my $out = cmd $DEVICES{$device}, $cmd;
      $$outputByDevice{$device} = $out;
    }
  }
  return $outputByDevice;
}

sub cmd($$){
  my ($deviceIP, $cmd) = @_;
  $cmd =~ s/ /%20/g;
  $cmd =~ s/;/%3B/g;
  $cmd =~ s/'/'\\''/g;
  my $out = `curl -s 'http://$deviceIP/cm?cmnd=$cmd'`;
  return $out;
}

&main(@ARGV);
