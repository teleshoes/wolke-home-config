#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $EXEC = basename $0;

my @PLAYLIST_ARGS = ('--output', '%(autonumber)s-%(title)s.%(ext)s');

my @SUBTITLE_ARGS = qw(--write-subs --write-auto-subs);

my $DEFAULT_RES = 1080;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC [OPTS] YTDLP_ARGS
    -append to YTDLP_ARGS based on OPTS
    -run 'yt-dlp YTDLP_ARGS'

  OPTS
    --res=RES
      prepend to YTDLP_ARGS: \"-f bv*[height=RES]+ba\"
      (default is --res=1080)
    --no-res
      do NOT prepend \"-f\" args to YTDLP_ARGS

    -x | --extract-audio
      prepend to YTDLP_ARGS: \"-x\"
        implies: --no-res
        implies: --no-subs

    -p | --pl | --playlist
      prepend to YTDLP_ARGS: \"@PLAYLIST_ARGS\"

    --subs | --write-subs
      prepend to YTDLP_ARGS: \"@SUBTITLE_ARGS\"
      (this is the default)
    --no-subs
      do NOT prepend \"--write-subs\" to YTDLP_ARGS
";


sub main(@){
  my $res = $DEFAULT_RES;
  my $isAudioOnly = 0;
  my $isPlaylist = 0;
  my $writeSubs = 1;
  my @ytdlpArgs;
  for my $arg(@_){
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^--res=(.+)$/){
      $res = $1;
      die "$USAGE\nERROR: --res=RES must be a number\n" if $res !~ /^\d+$/;
    }elsif($arg =~ /^(--no-res)$/){
      $res = undef;
    }elsif($arg =~ /^(-x|--extract-audio)$/){
      $isAudioOnly = 1;
    }elsif($arg =~ /^(-p|--pl|--playlist)$/){
      $isPlaylist = 1;
    }elsif($arg =~ /^(--subs|--write-subs)$/){
      $writeSubs = 1;
    }elsif($arg =~ /^(--no-subs)$/){
      $writeSubs = 0;
    }else{
      push @ytdlpArgs, $arg;
    }
  }

  @ytdlpArgs = ("-f", "bv*[height=$res]+ba", @ytdlpArgs) if defined $res and not $isAudioOnly;
  @ytdlpArgs = ("-x", @ytdlpArgs) if $isAudioOnly;
  @ytdlpArgs = (@PLAYLIST_ARGS, @ytdlpArgs) if $isPlaylist;
  @ytdlpArgs = (@SUBTITLE_ARGS, @ytdlpArgs) if $writeSubs and not $isAudioOnly;

  exec "yt-dlp", @ytdlpArgs;
}

&main(@ARGV);
