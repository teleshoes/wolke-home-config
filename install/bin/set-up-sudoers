#!/usr/bin/perl
use strict;
use warnings;
use lib `dirname $0 | tr -d '\n'`;
use ScriptScript;

sub getDefaultsSudoersConfForUser($);
sub getNoPasswdSudoersConfForUser($@);

my $SUDOERS_CONF_DIR = "/etc/sudoers.d";

sub main(@) {
  getRootSu @_;

  my $user = getUsername;

  my @sudoersConfFiles = globAll "$SUDOERS_CONF_DIR/*";
  if(@sudoersConfFiles > 0){
    run "rm", "-f", @sudoersConfFiles;
  }

  my $defaultsConfFile = "$SUDOERS_CONF_DIR/$user-defaults";
  my $defaultsConfContents = getDefaultsSudoersConfForUser $user;
  writeFile $defaultsConfFile, $defaultsConfContents;

  my $nopasswdEntriesDir = getInstallPath "sudo-nopasswd";
  my %entriesByName = readConfDir $nopasswdEntriesDir;
  for my $name(sort keys %entriesByName){
    my @entries = @{$entriesByName{$name}};
    my $nopasswdConfFile = "$SUDOERS_CONF_DIR/$user-nopasswd-$name";
    my $nopasswdConfContents = getNoPasswdSudoersConfForUser($user, @entries);

    writeFile $nopasswdConfFile, $nopasswdConfContents;
  }

  run "chmod 440 /etc/sudoers.d/*";
}

sub getDefaultsSudoersConfForUser($){
  my ($user) = @_;
  join "\n"
    , "Defaults\tenv_keep += HOME"
    , "Defaults\tenv_keep += SSH_AUTH_SOCK"
    , "$user\tALL=(ALL) ALL"
    , ""
}

sub getNoPasswdSudoersConfForUser($@){
  my ($user, @conf) = @_;
  join "\n", ((map {"$user\tALL=(ALL) NOPASSWD: $_"} @conf), "")
}

&main(@ARGV);
