#!/usr/bin/perl
use strict;
use warnings;
use lib `dirname $0 | tr -d '\n'`;
use ScriptScript;

my $POLICY_XML = "/etc/ImageMagick-6/policy.xml";

sub main(@) {
  getRoot();

  editFileLines($POLICY_XML, sub {
    my $line = shift;

    #comment-out ghostscript security vulnerability workaround (fixed in gs 9.24)
    $line =~ s/
      ^ (\s+)
      (?:<!-- \s*)?
      (<policy
        \s+ domain="coder"
        \s+ rights="none"
        \s+ pattern="(?:PS|PS2|PS3|EPS|PDF|XPS)"
        \s*
      \/>)
      \s* (?:--> \s*)?
      \s*$
    /$1<!-- $2 -->/x;

    #fix 'convert-im6.q16: cache resources exhausted `FILENAME' @ error/cache.c/OpenPixelCache/4095.
    $line =~ s/(<policy domain="resource" name="disk" )value="[^"]*"/$1value="16GiB"/;

    #standardize newlines
    $line =~ s/([\r\n]*)$/\n/;
    return $line;
  });
}

&main(@ARGV);
