#!/usr/bin/perl
use strict;
use warnings;
use lib `dirname $0 | tr -d '\n'`;
use ScriptScript;
use File::Basename qw(basename);

sub handleGitInstallMpyCrossPico($);
sub handleGitInstallMpyCrossLatest($);
sub handleGitInstallMicroPython($);

my $EXEC = basename $0;
my $GIT_URL_MICROPYTHON = "https://github.com/teleshoes/micropython";
my $GIT_URL_PICO_LCD = "https://github.com/teleshoes/pico-lcd";

my $DIR_FIRMWARE_IMG = "/media/stuff/Images/micropython/RPI_PICO_W";
my $LATEST_FIRMWARE_SYMLINK = "$DIR_FIRMWARE_IMG/latest.uf2";

my $MPY_6_3_COMMIT = "bdbc869f9ea200c0d28b2bc7bfb60acd9d884e1b";

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC -m | --mpycross | --mpy-cross | mpy-cross
    -build mpy-cross-6.2 from latest commit before micropython 6.3
      -install to /usr/local/bin/mpy-cross-6.2
    -build mpy-cross from latest micropython
      -install to /usr/local/bin/mpy-cross

  $EXEC -b | uf2 | firmware | build | --uf2 | --firmware | --build
    -build micropython RPI_PICO_W firmware
      from $GIT_URL_MICROPYTHON
    -copy built image to $DIR_FIRMWARE_IMG
    -create/replace symlink $DIR_FIRMWARE_IMG/latest.uf2

  $EXEC
  $EXEC -p | msg | lcd-msg | pico-lcd-msg | --msg | --lcd-msg | --pico-lcd-msg
    -install pico-lcd-msg tool
      from $GIT_URL_PICO_LCD
";

my $MODE_INSTALL_MPY_CROSS = "install-mpy-cross";
my $MODE_BUILD_FIRMWARE = "build-firmware";
my $MODE_LCD_MSG = "lcd-msg";

sub main(@) {
  my $mode = $MODE_LCD_MSG;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-m((--)?(mpy-cross|mpycross)))$/){
      $mode = $MODE_INSTALL_MPY_CROSS;
    }elsif($arg =~ /^(-b|((--)?(uf2|firmware|build)))$/){
      $mode = $MODE_BUILD_FIRMWARE;
    }elsif($arg =~ /^(-p|((--)?(msg|lcd-msg|pico-lcd-msg)))$/){
      $mode = $MODE_LCD_MSG;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($mode eq $MODE_INSTALL_MPY_CROSS){
    installFromGit $GIT_URL_MICROPYTHON, \&handleGitInstallMpyCrossPico;
    installFromGit $GIT_URL_MICROPYTHON, \&handleGitInstallMpyCrossLatest;
  }elsif($mode eq $MODE_BUILD_FIRMWARE){
    installFromGit $GIT_URL_MICROPYTHON, \&handleGitInstallMicroPython;
  }elsif($mode eq $MODE_LCD_MSG){
    #copy pico-lcd-msg tool out of pico-lcd
    installFromGit $GIT_URL_PICO_LCD, sub{
      my ($dir) = @_;
      runSudo "cd '$dir' && ./install-pico-lcd-msg.sh";
    };
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub handleGitInstallMpyCrossPico($){
  my ($dir) = @_;
  #last mpy 6.2 commit
  runUser "git", "-C", $dir, "checkout", "$MPY_6_3_COMMIT^";

  runUser "make", "-C", "$dir/mpy-cross", "clean";
  runUser "make", "-C", "$dir/mpy-cross", "CC=gcc-14", "-j", "1";

  if(not -e "$dir/mpy-cross/build/mpy-cross"){
    die "ERROR: missing mpy-cross\n";
  }

  runSudo "cp", "-a", "$dir/mpy-cross/build/mpy-cross", "/usr/local/bin/mpy-cross-6.2";

  runUser "git", "-C", $dir, "checkout", "master";
}

sub handleGitInstallMpyCrossLatest($){
  my ($dir) = @_;
  runUser "git", "-C", $dir, "checkout", "master";

  runUser "make", "-C", "$dir/mpy-cross", "clean";
  runUser "make", "-C", "$dir/mpy-cross", "-j", "1";

  if(not -e "$dir/mpy-cross/build/mpy-cross"){
    die "ERROR: missing mpy-cross\n";
  }

  runSudo "cp", "-a", "$dir/mpy-cross/build/mpy-cross", "/usr/local/bin/mpy-cross";
}

sub handleGitInstallMicroPython($){
  my ($dir) = @_;
  runUser "git", "-C", $dir, "submodule", "init";
  runUser "git", "-C", $dir, "submodule", "update";

  if(-e "$dir/firmware.uf2"){
    runUser "rm", "$dir/firmware.uf2";
  }

  if(-e "$dir/firmware.uf2"){
    die "ERROR: RPI_PICO_W firmware exists\n";
  }

  runUser "cd '$dir' && ./build-pico-w.sh";

  if(not -e "$dir/firmware.uf2"){
    die "ERROR: RPI_PICO_W firmware does not exist\n";
  }
  my $gitCommit = procUserChomp "git", "-C", $dir, "show", "-s", "--format=%h";
  my $gitEpoch = procUserChomp "git", "-C", $dir, "show", "-s", "--format=%at";
  my $gitDtm = procUserChomp "date", "--date=\@$gitEpoch", "+%Y-%m-%d_%H%M%S";
  my $nowMillis = nowMillis();

  my $fileName = "firmware_rpi_pico_w_${gitDtm}_${gitCommit}_${nowMillis}.uf2";
  mkdirRunUser $DIR_FIRMWARE_IMG;
  runUser "cp", "$dir/firmware.uf2", "$DIR_FIRMWARE_IMG/$fileName";

  symlinkFile $fileName, $LATEST_FIRMWARE_SYMLINK;
}

&main(@ARGV);
