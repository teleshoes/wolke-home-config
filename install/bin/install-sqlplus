#!/usr/bin/perl
use strict;
use warnings;
use lib `dirname $0 | tr -d '\n'`;
use ScriptScript;

sub getDebAlien($$);
sub getPkg($$$);

my $VERSION = '19.9.0.0.0';
my $MAJ_MIN_VERSION = $VERSION;
$MAJ_MIN_VERSION =~ s/^(\d+\.\d+).*/$1/;

my $PKG_DIR = "/media/stuff/Images/sqlplus";
my $LD_CONF = "/etc/ld.so.conf.d/oracle.conf";
my $LIB_DIR = "/usr/lib/oracle/$MAJ_MIN_VERSION/client64/lib";
my $GLOGIN_FILE = "$LIB_DIR/glogin.sql";

my $GLOGIN_CONF = "--created by $0
set pagesize 50000
set linesize 200
set timing on

column CUSTOM_PROMPT_COL new_value CUSTOM_PROMPT
set termout off
select global_name || '> ' as CUSTOM_PROMPT_COL from global_name;
set termout on

set sqlprompt \"&CUSTOM_PROMPT\"
";


sub main(@) {
  getRoot @_;
  print "\n\ninstalling sqlplus deps\n";
  runAptGet "install", "libaio1", "libncurses-dev", "libreadline-dev";

  print "\n\ninstalling gqlplus\n";
  runScript "install-src", "gqlplus";

  my $basicDeb = getDebAlien "basic", $VERSION;
  my $sqlplusDeb = getDebAlien "sqlplus", $VERSION;

  print "\n\nremoving and re-installing oracle deb pkgs\n";
  tryrunAptGet "remove", "oracle-instantclient.*";
  run "dpkg", "-i", $basicDeb;
  run "dpkg", "-i", $sqlplusDeb;

  print "\n\noverwriting $GLOGIN_FILE\n";
  writeFile $GLOGIN_FILE, $GLOGIN_CONF;

  print "\n\noverwriting $LD_CONF and running ldconfig\n";
  writeFile $LD_CONF, "$LIB_DIR/\n";
  run "ldconfig";

  print "\n\nupdating sqlplus => sqlplus64 symlink\n";
  my $sqlplus = "/usr/bin/sqlplus";
  my $sqlplus64 = "/usr/bin/sqlplus64";

  if(-e $sqlplus64){
    if(not -e $sqlplus or -l $sqlplus){
      symlinkFile $sqlplus64, $sqlplus;
    }
  }
}

sub getDebAlien($$){
  my ($name, $version) = @_;
  my $rpm = globOne "$PKG_DIR/oracle-*-$name*$version-*.rpm";
  die "Missing shitty oracle instantclient garbage $name v$version\n" if not defined $rpm;

  my $deb = globOne "$PKG_DIR/oracle-*-$name*$version-*.deb";
  if(not defined $deb){
    print "making alien deb for $rpm\n";
    chdir $PKG_DIR;
    $ENV{PWD} = $PKG_DIR;

    run "alien", "--to-deb", $rpm;

    $deb = globOne "$PKG_DIR/oracle-*-$name*$version-*.deb";

    run "chown", getUsername() . ".", $deb;
  }

  die "Could not make $deb\n" if not defined $deb;

  return $deb;
}

&main(@ARGV);
