#!/usr/bin/perl
use strict;
use warnings;
use List::Util qw(max);

sub parseFanTempRange($);
sub getTargetFanLevelFromTempRanges($$@);
sub formatFanInfo($);
sub parseLevelOrSyn($);
sub parseConfigFile($);
sub readTemp();
sub readFan();
sub readFanLevelStateFile();
sub writeFanLevelStateFile($);
sub setLevel($);

my $fanDev = "/proc/acpi/ibm/fan";

my $configFile = "$ENV{HOME}/.config/fanspeed.conf";
my $stateFile = "/tmp/fanspeed-level";

my $MONITOR_INTERVAL_MILLIS = 10000;

my $usage = "Usage:
  $0 -h
  $0 --help
    show this message

  $0
  $0 --get
     print fan information from $fanDev
     formatted as: \"SPEED,LEVEL\"
     where SPEED is an integer and LEVEL is one of the allowed values for <LEVEL>

  $0 --temp
     use `acpi -V` to get first CPU temperature in degrees celsius,
       and print as an integer

  $0 LEVEL_OR_SYN
  $0 --set=LEVEL_OR_SYN
     calculate <LEVEL> from <LEVEL_OR_SYN>
     set fan level to <LEVEL> using $fanDev
     write <LEVEL> (not synonym) to $stateFile

  $0 --reapply
     set fan level to the <LEVEL> in $stateFile if it exists
  $0 --clear
     remove $stateFile (further calls to --reapply without --set will do nothing)

  $0 --monitor [OPTS] FAN_TEMP_RANGE [FAN_TEMP_RANGE]
    -checks the temperature every ${MONITOR_INTERVAL_MILLIS}ms:
      -if the current temp AND fan level matches one of the <FAN_TEMP_RANGE>s:
        do nothing
      -if the current temp matches at least none of the <FAN_TEMP_RANGE>s:
        apply the highest fan level among those <FAN_TEMP_RANGE>s
      -otherwise:
        apply the highest fan level of any <FAN_TEMP_RANGE>

    <OPTS>
       -o | --once
         run exactly once instead of re-running every ${MONITOR_INTERVAL_MILLIS}ms
       -v | --verbose
         print status even when no level change occurs

    <FAN_TEMP_RANGE> = <FAN_LEVEL>:<LOW_TEMP>-<HIGH_TEMP>
      a sequence of 3 non-negative integers, separated with non-alphanumeric characters
      e.g.:   3:60-80    or   7-80,100
      NOTE:
        <FAN_LEVEL> must be greater than 0
        <FAN_TEMP_RANGE>s can overlap temperatures
          (and should, to avoid constant changing)

     e.g.: $0 --monitor 1:0-60 2:60-70 3:65-80 7:80-100
        temp  fan  => action
        78    3    => do nothing
        72    2    => 3
        66    2    => do nothing
        66    3    => do nothing
        64    3    => 2
        106   6    => 7

  <LEVEL_OR_SYN> = <LEVEL> | <SYNONYM>
    argument for $fanDev or a synonym, one of either <LEVEL> or <SYNONYM>

  <LEVEL> = 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | auto | disengaged | full-speed
    valid arguments for $fanDev (0-7, \"auto\", or \"disengaged\"/\"full-speed\")
      NOTE:
        0 means FULLY DISABLED and is quite dangerous
        disengaged and full-speed do the same thing, and are faster than 7
        auto varies based on CPU load and is done by the kernel module or firmware

  <SYNONYM>
    synonyms are defined in $configFile
      -one per line: \"SYN_NAME=SYN_VALUE\"
      -NAME is one or more letters or numbers
      -VALUE is one of the arguments above for $fanDev
    e.g.:  default=5
           fast=disengaged
           medium=4
           slow=3
           morning=5
           evening=1
";

my @okLevels = qw(0 1 2 3 4 5 6 7 auto disengaged full-speed);
my $okLevelsRe = "(?:" . join("|", @okLevels) . ")";

sub main(@){
  if(@_ == 1 and $_[0] =~ /^(-h|--help)$/){
    print $usage;
    exit 0;
  }elsif(@_ == 0 or (@_ == 1 and $_[0] =~ /^(--get)$/)){
    print formatFanInfo(readFan());
  }elsif(@_ == 0 or (@_ == 1 and $_[0] =~ /^(--temp)$/)){
    my $temp = readTemp();
    printf "%d\n", $temp;
  }elsif(@_ == 1 and $_[0] =~ /^(--reapply)$/){
    my $stateLevel = readFanLevelStateFile();
    my $fanInfo = readFan();
    my $curLevel = $$fanInfo{level};
    if(defined $stateLevel and $stateLevel ne $curLevel){
      my $now = `date`;
      chomp $now;
      print "$now  FAN LEVEL CHANGED: REAPPLYING $curLevel => $stateLevel\n";
      setLevel $stateLevel;
    }
  }elsif(@_ == 1 and $_[0] =~ /^(--clear)$/){
    writeFanLevelStateFile undef;
  }elsif(@_ >= 1 and $_[0] =~ /^(--monitor)$/){
    my ($monitorCmd, @args) = @_;
    my $once = 0;
    my $verbose = 0;
    while(@args > 0 and $args[0] =~ /^-/){
      my $opt = shift @args;
      if($opt =~ /^(-o|--once)$/){
        $once = 1;
      }elsif($opt =~ /^(-v|--verbose)$/){
        $verbose = 1;
      }else{
        die "ERROR: invalid --monitor OPT: $opt\n";
      }
    }
    my @fanTempRangeFmts = @args;
    die "ERROR: no FAN_TEMP_RANGE given\n" if @fanTempRangeFmts == 0;

    my @fanTempRanges = map {parseFanTempRange $_} @fanTempRangeFmts;
    while(1){
      my $curLevel = ${readFan()}{level};
      my $curTemp = readTemp();
      my $newLevel = getTargetFanLevelFromTempRanges($curLevel, $curTemp, @fanTempRanges);
      my $changed = 0;
      if(defined $newLevel and $newLevel != $curLevel){
        $changed = 1;
        writeFanLevelStateFile($newLevel);
      }
      if($changed or $verbose){
        my $date = `date`;
        chomp $date;
        $newLevel = $curLevel if not $changed;
        my $changeFmt = $changed ? "" : " (NO CHANGE)";
        print "$date   $curLevel => $newLevel  (${curTemp}C)$changeFmt\n";
      }

      if($once){
        last;
      }else{
        sleep $MONITOR_INTERVAL_MILLIS/1000.0;
      }
    }
  }elsif(@_ == 1){
    my $levelOrSyn = $_[0];
    $levelOrSyn = $1 if $_[0] =~ /^--set=(.+)$/;
    my $level = parseLevelOrSyn $levelOrSyn;
    if(not defined $level or $level !~ /^$okLevelsRe$/){
      die "$usage\ninvalid level: $levelOrSyn\n";
    }

    my $fanInfo = readFan();
    my $curLevel = $$fanInfo{level};
    if($curLevel eq $level){
      print "fan already set to $level\n";
    }else{
      print "$curLevel => $level\n";
      writeFanLevelStateFile($level);
      setLevel $level;
    }
  }else{
    die $usage;
  }
}

sub parseFanTempRange($){
  my ($fanTempRangeFmt) = @_;
  if($fanTempRangeFmt =~ /^(\d+)\W+(\d+)\W+(\d+)$/){
    my ($fanLevel, $lowTemp, $highTemp) = ($1, $2, $3);
    die "ERROR: low temp > high temp \"$fanTempRangeFmt\"\n" if $lowTemp > $highTemp;
    die "ERROR: fan level must be > 0 in FAN_TEMP_RANGE\n" if $fanLevel <= 0;
    return {level => $1, lowTemp => $lowTemp, highTemp => $highTemp};
  }else{
    die "ERROR: malformed fan temp range: $fanTempRangeFmt\n";
  }
}

sub getTargetFanLevelFromTempRanges($$@){
  my ($curLevel, $curTemp, @fanTempRanges) = @_;
  my @levelsForMatchingTemps;
  my $matchingTempAndLevel = 0;
  for my $fanTempRange(@fanTempRanges){
    my ($level, $low, $high) =
      ($$fanTempRange{level}, $$fanTempRange{lowTemp}, $$fanTempRange{highTemp});
    if($low <= $curTemp && $curTemp <= $high){
      if($curLevel == $level){
        $matchingTempAndLevel = 1;
        return undef; #no action
      }else{
        push @levelsForMatchingTemps, $level;
      }
    }
  }
  if($matchingTempAndLevel){
    return undef;
  }elsif(@levelsForMatchingTemps > 0){
    return max(@levelsForMatchingTemps);
  }else{
    return max(map {$$_{level}} @fanTempRanges);
  }
}

sub formatFanInfo($){
  my ($fanInfo) = @_;
  return "$$fanInfo{speed},$$fanInfo{level}\n";
}

sub parseLevelOrSyn($){
  my ($levelOrSyn) = @_;
  if($levelOrSyn =~ /^$okLevelsRe$/){
    return $levelOrSyn;
  }else{
    my $confSynonyms = parseConfigFile $configFile;
    if(defined $$confSynonyms{$levelOrSyn}){
      return $$confSynonyms{$levelOrSyn};
    }else{
      return undef;
    }
  }
}

sub parseConfigFile($){
  my ($configFile) = @_;
  my $conf = {};
  if(-f $configFile){
    open FH, "< $configFile" or die "could not read config file: $configFile\n";
    my @lines = <FH>;
    close FH;
    for my $line(@lines){
      next if $line =~ /^\s*(#|$)/;
      if($line =~ /^\s*(\w+)\s*=\s*($okLevelsRe)\s*$/){
        my ($key, $val) = ($1, $2);
        $$conf{$key} = $val;
      }else{
        die "malformed config file line: $line";
      }
    }
  }
  return $conf;
}

sub readTemp(){
  my $acpi = `acpi -V`;
  if($acpi =~ /, (-?\d+|-?\d*\.\d+) degrees C/){
    return $1;
  }else{
    return undef;
  }
}

sub readFan(){
  my $devInfo = `cat $fanDev`;
  my ($speed, $level);
  if($devInfo =~ /^speed:\s*(\d+)$/m){
    $speed = $1;
  }else{
    die "ERROR: could not parse info from $fanDev:\n$devInfo\n";
  }
  if($devInfo =~ /^level:\s*(\w+)$/m){
    $level = $1;
  }else{
    die "ERROR: could not parse info from $fanDev:\n$devInfo\n";
  }
  return {speed => $speed, level => $level};
}

sub readFanLevelStateFile(){
  my $level;
  if(-f $stateFile){
    $level = `cat $stateFile`;
    chomp $level;
  }
  if(defined $level and $level =~ /^$okLevelsRe$/){
    return $level;
  }else{
    return undef;
  }
}
sub writeFanLevelStateFile($){
  my ($level) = @_;
  if(defined $level){
    open FH, "> $stateFile" or die "Could not write to $stateFile\n$!\n";
    print FH "$_[0]\n";
    close FH;
  }else{
    system "rm", "-f", $stateFile;
  }
}

sub setLevel($){
  open FH, "> $fanDev" or die "Could not open $fanDev for writing\n$!\n";
  print FH "level $_[0]\n";
  close FH;
}

&main(@ARGV);
