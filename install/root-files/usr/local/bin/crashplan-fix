#!/usr/bin/perl
use strict;
use warnings;

my $LIBUAW_SRC = "/media/stuff/Images/installers/crashplan-missing-libs/libuaw.so";
my $LIBUAW_DEST = "/usr/local/crashplan/nlib/libuaw.so";

my @INIT_FILES = grep {-e $_ or -l $_} (
  glob("/etc/init.d/*crashplan*"),
  glob("/etc/rc*.d/*crashplan*"),
  glob("/etc/init.d/*code42*"),
  glob("/etc/rc*.d/*code42*"),
  glob("/etc/systemd/system/crashplan.service"),
  glob("/etc/systemd/system/multi-user.target.wants/crashplan.service"),
  glob("/etc/systemd/system/multi-user.target.wants/code42.service"),
  glob("/usr/local/crashplan/etc/crashplan.service"),
  glob("/usr/lib/systemd/system/crashplan.service"),
  glob("/usr/lib/systemd/system/code42.service"),
);

my $USAGE = "Usage:
  $0 -h | --help
    show this message

  $0
    -remove init and service files for crashplan
    -replace broken libuaw.so
";

sub nowMillis();
sub md5sum($);
sub run(@);
sub getRoot(@);

sub main(@){
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }
  getRoot();

  print "\n\ndeleting init files\n";
  if(@INIT_FILES > 0){
    run "rm", @INIT_FILES;
  }else{
    print "no init files to delete\n";
  }


  print "\n\nchecking uaw installed\n";
  my $srcMd5 = md5sum($LIBUAW_SRC);
  my $destMd5 = md5sum($LIBUAW_DEST);
  my $libuawBak = "$LIBUAW_DEST.bak." . nowMillis();

  if(defined $srcMd5 and defined $destMd5 and $srcMd5 ne $destMd5){
    print "libuaw mismatch, backing up current to $libuawBak\n";
    run "mv", $LIBUAW_DEST, $libuawBak;
  }

  if(not -e $LIBUAW_DEST){
    print "copying libuaw.so (installer is stupid)\n";
    run "cp", "-ar", $LIBUAW_SRC, $LIBUAW_DEST;
    run "chown", "root:root", $LIBUAW_DEST;
  }else{
    print "libuaw.so is already in place\n";
  }
}

sub nowMillis(){
  return int(time * 1000.0 + 0.5);
}

sub md5sum($){
  my ($file) = @_;
  return undef if not -f $file;
  open my $fh, "-|", "md5sum", $file
    or die "ERROR: could not run md5sum\n";
  my $out = join '', <$fh>;
  close $fh;
  if($out =~ /^([0-9a-f]{32})\s+($file)$/){
    return $1;
  }else{
    return undef;
  }
}

sub run(@){
  print "@_\n";
  system @_;
  if($? != 0){
    die "ERROR: command '@_' failed\n";
  }
}

sub getRoot(@){
  if(`whoami` ne "root\n"){
    print "rerunning as root\n";
    exec "sudo", $0, @_;
  }
}

&main(@ARGV);
