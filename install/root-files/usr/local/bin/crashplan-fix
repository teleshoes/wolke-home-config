#!/usr/bin/perl
use strict;
use warnings;

my @INIT_FILE_PATTERNS = (
  "/etc/init.d/*crashplan*",
  "/etc/rc*.d/*crashplan*",
  "/etc/init.d/*code42*",
  "/etc/rc*.d/*code42*",
  "/etc/systemd/system/crashplan.service",
  "/etc/systemd/system/multi-user.target.wants/crashplan.service",
  "/etc/systemd/system/multi-user.target.wants/code42.service",
  "/usr/local/crashplan/etc/crashplan.service",
  "/usr/lib/systemd/system/crashplan.service",
  "/usr/lib/systemd/system/code42.service",
);

my $USAGE = "Usage:
  $0 -h | --help
    show this message

  $0
    -stop systemd services
    -remove init and service files for crashplan

    NOTE: pre-installed libuaw.so works now
";

sub nowMillis();
sub md5sum($);
sub run(@);
sub getRoot(@);

sub main(@){
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }
  getRoot();

  my @initFiles = grep {-e $_ or -l $_} (map {glob($_)} @INIT_FILE_PATTERNS);

  print "\n\nstop+disable systemd services\n";
  run "systemctl --no-pager stop crashplan.service 2>&1; echo ok";
  run "systemctl --no-pager disable crashplan.service 2>&1; echo ok";
  run "systemctl --no-pager stop code42.service 2>&1; echo ok";
  run "systemctl --no-pager disable code42.service 2>&1; echo ok";

  print "\n\ndeleting init files\n";
  if(@initFiles > 0){
    run "rm", @initFiles;
    run "systemctl", "daemon-reload";
  }else{
    print "no init files to delete\n";
  }
}

sub nowMillis(){
  return int(time * 1000.0 + 0.5);
}

sub md5sum($){
  my ($file) = @_;
  return undef if not -f $file;
  open my $fh, "-|", "md5sum", $file
    or die "ERROR: could not run md5sum\n";
  my $out = join '', <$fh>;
  close $fh;
  if($out =~ /^([0-9a-f]{32})\s+($file)$/){
    return $1;
  }else{
    return undef;
  }
}

sub run(@){
  print "@_\n";
  system @_;
  if($? != 0){
    die "ERROR: command '@_' failed\n";
  }
}

sub getRoot(@){
  if(`whoami` ne "root\n"){
    print "rerunning as root\n";
    exec "sudo", $0, @_;
  }
}

&main(@ARGV);
