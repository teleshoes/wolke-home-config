#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub startVPN($$);
sub stopVPN($$);
sub isRunning($);
sub getOpenvpnProcPattern($);
sub ensureRoot();
sub getYubiExec();
sub getPasswordFile($$$);
sub symlinkPem($);
sub readSecrets($);
sub decrypt($);
sub run(@);

my $DIR_OPENVPN = "$ENV{HOME}/openvpn";

my $GPG_SYM_EXEC = "$ENV{HOME}/bin/gpg-sym";

my $STATUS_FILE = "$DIR_OPENVPN/status";

my $SECRETS_FILE = "$ENV{HOME}/.secrets";
my @SECRET_KEYS = qw(user password yubi);
my @REQUIRED_KEYS = qw(user);
my %ENCRYPTED_SECRET_KEYS = map {$_ => 1 } qw(password);

my @YUBI_EXEC_NAMES = qw(yubi yubi-nfc);

my @CONF_NAMES =
  sort
  grep {defined $_ and /^\w+$/}
  map {/^(?:.*\/)?(.+)\.conf$/ ? $1 : undef}
  grep {-f $_}
  glob "$DIR_OPENVPN/*.conf";
my $CONF_NAME_REGEX = join "|", @CONF_NAMES;

my $DEFAULT_CONF_NAME = @CONF_NAMES > 0 ? $CONF_NAMES[0] : undef;
my $DEFAULT_CONF_NAME_FMT = defined $DEFAULT_CONF_NAME ? $DEFAULT_CONF_NAME : "<none>";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS] [CONF_NAME] --on | on
    start openvpn using --config CONF_NAME.conf (default is $DEFAULT_CONF_NAME_FMT)
    also run `sshc --close-all`

  $EXEC [OPTS] [CONF_NAME] --off | off
    kill openvpn with '--config CONF_NAME'
    also run `sshc --close-all`

  $EXEC [OPTS] [CONF_NAME] --running | running
    check for openvpn with '--config CONF_NAME'
    exits with 0 for running and 1 for not running

  $EXEC [OPTS] [CONF_NAME] --toggle | toggle
    check '$EXEC running CONF_NAME' and then run:
      '$EXEC off CONF_NAME' or '$EXEC on CONF_NAME'

  CONF_NAME
    VPN config name in $DIR_OPENVPN/<CONF_NAME>.conf
    optional, defaults to: $DEFAULT_CONF_NAME_FMT
    one of:
      @CONF_NAMES

  OPTS
    -g | --foreground | --nodaemon | --no-daemon
      do not pass --daemon to openvpn
";

my $CMD_ON = "on";
my $CMD_OFF = "off";
my $CMD_RUNNING = "running";
my $CMD_TOGGLE = "toggle";

sub main(@){
  my $cmd = undef;
  my $confName = $DEFAULT_CONF_NAME;
  my $opts = {
    daemon => 1,
  };
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      die $USAGE;
    }elsif($arg =~ /^(--on|on)$/){
      $cmd = $CMD_ON;
    }elsif($arg =~ /^(--off|off)$/){
      $cmd = $CMD_OFF;
    }elsif($arg =~ /^(--running|running)$/){
      $cmd = $CMD_RUNNING;
    }elsif($arg =~ /^(--toggle|toggle)$/){
      $cmd = $CMD_TOGGLE;
    }elsif($arg =~ /^(-g|--foreground|--nodaemon|--no-daemon)$/){
      $$opts{daemon} = 0;
    }elsif($arg =~ /^($CONF_NAME_REGEX)(?:\.conf)?$/){
      $confName = $1;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  die "$USAGE\nERROR: missing command\n" if not defined $cmd;
  die "$USAGE\nERROR: missing CONF_NAME\n" if not defined $confName or $confName !~ /^\w+$/;
  die "ERROR: $DIR_OPENVPN/$confName.conf not found\n" if not -f "$DIR_OPENVPN/$confName.conf";

  ensureRoot();

  if($cmd eq $CMD_ON){
    startVPN($confName, $opts);
  }elsif($cmd eq $CMD_RUNNING){
    exit (isRunning($confName) ? 0 : 1);
  }elsif($cmd eq $CMD_OFF){
    stopVPN($confName, $opts);
  }elsif($cmd eq $CMD_TOGGLE){
    isRunning($confName) ? stopVPN($confName, $opts) : startVPN($confName, $opts);
  }else{
    die "$USAGE\nERROR: unknown command $cmd\n";
  }
}

sub startVPN($$){
  my ($confName, $opts) = @_;

  print "starting\n";

  my $cfg = readSecrets($confName);

  my $user = $$cfg{user};
  my $password = $$cfg{password};
  $password = '' if not defined $password;
  my $yubi = $$cfg{yubi};
  $yubi = 0 if not defined $yubi;

  symlinkPem($confName);

  run "modprobe", "tun";
  run "rm", "-f", $STATUS_FILE;
  run "openvpn",
    ($$opts{daemon} ? ("--daemon") : ()),
    "--log", "$DIR_OPENVPN/$confName.log",
    "--auth-user-pass", getPasswordFile($user, $password, $yubi),
    "--config", "$DIR_OPENVPN/$confName.conf",
    "--status", $STATUS_FILE,
    "--reneg-sec", 0,
    "1";
  run "sshc", "--close-all";
}

sub stopVPN($$){
  my ($confName, $opts) = @_;

  print "stopping\n";
  run "pkill", "-f", getOpenvpnProcPattern($confName);
  run "sshc", "--close-all";
}

sub isRunning($){
  my ($confName) = @_;

  my $procPattern = getOpenvpnProcPattern($confName);
  run "pgrep -f '$procPattern' >/dev/null 2>/dev/null";
  my $isRunning = $? == 0 ? 1 : 0;

  printf "is-running: %s\n", ($isRunning ? "yes" : "no");
  return $isRunning;
}

sub getOpenvpnProcPattern($){
  my ($confName) = @_;
  return "^openvpn.*--config $DIR_OPENVPN/$confName\\.conf ";
}

sub ensureRoot(){
  if(`whoami` ne "root\n"){
    print "rerunning as root\n";
    exec "sudo", $0, @ARGV;
  }
}

sub getYubiExec(){
  my $exec = "";
  for my $exec(@YUBI_EXEC_NAMES){
    my $which = `which '$exec' 2>/dev/null`;
    chomp $which;
    if($which =~ /$exec/){
      return $which;
    }
  }
  return undef;
}

sub getPasswordFile($$$){
  my ($user, $password, $yubi) = @_;

  if($yubi){
    my $yubiExec = getYubiExec;
    my $yubiPass = `$yubiExec`;
    chomp $yubiPass;
    $password .= $yubiPass;
  }

  my $file = "/tmp/openvpn-auth-" . time;
  open FH, "> $file" or die "Cant write to $file\n";
  print FH "$user\n";
  print FH "$password\n";
  close FH;
  return $file;
}

sub symlinkPem($){
  my ($confName) = @_;
  my $destPemFile = "$DIR_OPENVPN/$confName.pem";

  my $hostname = `hostname`;
  chomp $hostname;

  run "rm", "-f", $destPemFile if -l $destPemFile;
  if(-e $destPemFile){
    die "ERROR: $destPemFile already exists and was not a removable symlink\n";
  }

  my $hostPem = "$confName.pem.$hostname";
  my $defaultPem = "$confName.pem.default";

  if(-e "$DIR_OPENVPN/$hostPem"){
    run "ln", "-s", $hostPem, $destPemFile;
  }elsif(-e "$DIR_OPENVPN/$defaultPem"){
    run "ln", "-s", $defaultPem, $destPemFile;
  }

  die "ERROR: failed to select PEM for $confName\n" if not -e $destPemFile;
}

sub readSecrets($){
  my ($confName) = @_;
  my @lines = `cat $SECRETS_FILE 2>/dev/null`;
  my $cfg = {};
  my $okSecretKeys = join "|", @SECRET_KEYS;
  for my $line(@lines){
    if($line =~ /^vpn\.$confName\.($okSecretKeys)\s*=\s*(.+)$/){
      my ($key, $val) = ($1, $2, $3);
      $val = decrypt $val if defined $ENCRYPTED_SECRET_KEYS{$key};

      $$cfg{$key} = $val;
    }
  }
  for my $key(@REQUIRED_KEYS){
    die "ERROR: missing conf vpn.$confName.$key\n" if not defined $$cfg{$key};
  }
  return $cfg;
}

sub decrypt($){
  my ($s) = @_;
  my $value = `$GPG_SYM_EXEC '$s' 2>/dev/null`;
  die "ERROR: $GPG_SYM_EXEC failed\n" if $? != 0;
  chomp $value;
  return $value;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
