#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub ensureRoot();
sub getYubiExec();
sub getPasswordFile($$$);
sub symlinkPem($);
sub readSecrets();
sub decrypt($);
sub isRunning($);
sub run(@);

my $DIR_OPENVPN = "$ENV{HOME}/openvpn";

my $GPG_SYM_EXEC = "$ENV{HOME}/bin/gpg-sym";

my $STATUS_FILE = "status";
my $DEFAULT_CONF_NAME = "lg";

my $SECRETS_FILE = "$ENV{HOME}/.secrets";
my @SECRET_KEYS = qw(user password yubi);
my %ENCRYPTED_SECRET_KEYS = map {$_ => 1 } qw(password);

my @CONF_NAMES =
  grep {defined $_ and /^\w+$/}
  map {/^(?:.*\/)?(.+)\.conf$/ ? $1 : undef}
  grep {-f $_}
  glob "$DIR_OPENVPN/*.conf";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC [OPTS] [CONF_NAME] --on | on
    start openvpn using --config CONF_NAME.conf (default is $DEFAULT_CONF_NAME)
    also run `sshc --close-all`

  $EXEC [OPTS] [CONF_NAME] --off | off
    kill openvpn with '--config CONF_NAME'
    also run `sshc --close-all`

  $EXEC [OPTS] [CONF_NAME] --running | running
    check for openvpn with '--config CONF_NAME'
    exits with 0 for running and 1 for not running

  $EXEC [OPTS] [CONF_NAME] --toggle | toggle
    check '$EXEC running CONF_NAME' and then run:
      '$EXEC off CONF_NAME' or '$EXEC on CONF_NAME'

  CONF_NAME
    VPN config name in $DIR_OPENVPN/<CONF_NAME>.conf
    optional, defaults to: $DEFAULT_CONF_NAME
    one of:
      @CONF_NAMES

  OPTS
    -g | --foreground | --nodaemon | --no-daemon
      do not pass --daemon to openvpn
";

my $CMD_ON = "on";
my $CMD_OFF = "off";
my $CMD_RUNNING = "running";
my $CMD_TOGGLE = "toggle";

sub main(@){
  my $cmd = undef;
  my $name = undef;
  my $opts = {
    daemon => 1,
  };
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      die $USAGE;
    }elsif($arg =~ /^(--on|on)$/){
      $cmd = $CMD_ON;
    }elsif($arg =~ /^(--off|off)$/){
      $cmd = $CMD_OFF;
    }elsif($arg =~ /^(--running|running)$/){
      $cmd = $CMD_RUNNING;
    }elsif($arg =~ /^(--toggle|toggle)$/){
      $cmd = $CMD_TOGGLE;
    }elsif($arg =~ /^(-g|--foreground|--nodaemon|--no-daemon)$/){
      $$opts{daemon} = 0;
    }elsif(not defined $name){
      $name = $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  die "$USAGE\nERROR: missing command\n" if not defined $cmd;

  ensureRoot();
  chdir $DIR_OPENVPN or die "couldnt set cwd to $DIR_OPENVPN\n";

  $name = $DEFAULT_CONF_NAME if not defined $name;
  $name =~ s/\.conf$//;
  die "$DIR_OPENVPN/$name.conf not found\n" if not -f "$name.conf";

  my $procPattern = "^openvpn.*--config $name\\.conf ";

  if($cmd eq $CMD_ON){
    print "starting, maybe...\n";

    my $cfg = ${readSecrets()}{$name};
    die "no 'vpn.$name' config found in $SECRETS_FILE\n" if not defined $cfg;

    my $user = $$cfg{user};
    die "missing 'user' from vpn.$name.* in $SECRETS_FILE\n" if not defined $user;
    my $password = $$cfg{password};
    $password = '' if not defined $password;
    my $yubi = $$cfg{yubi};
    $yubi = 0 if not defined $yubi;

    symlinkPem($name);

    run "modprobe", "tun";
    run "rm", "-f", $STATUS_FILE;
    run "openvpn",
      ($$opts{daemon} ? ("--daemon") : ()),
      "--log", "$name.log",
      "--auth-user-pass", getPasswordFile($user, $password, $yubi),
      "--config", "$name.conf",
      "--status", $STATUS_FILE,
      "--reneg-sec", 0,
      "1";
    run "sshc", "--close-all";
  }elsif($cmd eq $CMD_RUNNING){
    exit (isRunning $procPattern ? 0 : 1);
  }elsif($cmd eq $CMD_OFF){
    print "stopping, maybe...\n";
    run "pkill", "-f", $procPattern;
    run "sshc", "--close-all";
  }elsif($cmd eq $CMD_TOGGLE){
    my @cmd;
    push @cmd, $0;
    push @cmd, isRunning $procPattern ? 'off' : 'on';
    push @cmd, $name if defined $name;
    exec @cmd;
  }else{
    die "$USAGE\nERROR: unknown command $cmd\n";
  }
}

sub ensureRoot(){
  if(`whoami` ne "root\n"){
    print "rerunning as root\n";
    exec "sudo", $0, @ARGV;
  }
}

sub getYubiExec(){
  my $exec = "";
  $exec = `which yubi     2>/dev/null` if $exec =~ /^\s*$/;
  $exec = `which yubi-nfc 2>/dev/null` if $exec =~ /^\s*$/;
  $exec = undef if $exec =~ /^\s*$/;
  return $exec;
}

sub getPasswordFile($$$){
  my ($user, $password, $yubi) = @_;

  if($yubi){
    my $yubiExec = getYubiExec;
    my $yubiPass = `$yubiExec`;
    chomp $yubiPass;
    $password .= $yubiPass;
  }

  my $file = "/tmp/openvpn-auth-" . time;
  open FH, "> $file" or die "Cant write to $file\n";
  print FH "$user\n";
  print FH "$password\n";
  close FH;
  return $file;
}

sub symlinkPem($){
  my $name = shift;
  my $pem = "$name.pem";

  my $hostname = `hostname`;
  chomp $hostname;

  my $hostPem = "$pem.$hostname";
  my $defaultPem = "$pem.default";

  run "rm", "-f", $pem if -l $pem;
  die "$pem exists and is not a symlink" if -e $pem;

  run "ln", "-s", $hostPem, $pem if not -e $pem and -e $hostPem;
  run "ln", "-s", $defaultPem, $pem if not -e $pem and -e $defaultPem;

  die "could not select $pem\n" if not -e $pem;
}

sub readSecrets(){
  my @lines = `cat $SECRETS_FILE 2>/dev/null`;
  my $cfg = {};
  my $okSecretKeys = join "|", @SECRET_KEYS;
  for my $line(@lines){
    if($line =~ /^vpn\.(\w+)\.($okSecretKeys)\s*=\s*(.+)$/){
      my ($confName, $key, $val) = ($1, $2, $3);
      $val = decrypt $val if defined $ENCRYPTED_SECRET_KEYS{$key};

      $$cfg{$confName} = {} if not defined $$cfg{$confName};
      $$cfg{$confName}{$key} = $val;
    }
  }
  return $cfg;
}

sub decrypt($){
  my ($s) = @_;
  my $value = `$GPG_SYM_EXEC '$s' 2>/dev/null`;
  die "ERROR: $GPG_SYM_EXEC failed\n" if $? != 0;
  chomp $value;
  return $value;
}

sub isRunning($){
  my $procPattern = shift;
  run "pgrep -f '$procPattern' >/dev/null 2>/dev/null";
  return $? == 0;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
