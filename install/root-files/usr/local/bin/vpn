#!/usr/bin/perl
use strict;
use warnings;

my $dir = "$ENV{HOME}/openvpn";

my $GPG_SYM_EXEC = "$ENV{HOME}/bin/gpg-sym";

my $status = "status";
my $defaultName = "lg";

my $okCmds = join "|", qw(on off running toggle);
my $okNoDaemonOpts = join "|", qw(-g --foreground --nodaemon --no-daemon);

my $secretsFile = "$ENV{HOME}/.secrets";
my @secretKeys = qw(user password yubi);
my %encryptedSecretKeys = map {$_ => 1 } qw(password);

sub ensureRoot();
sub getPasswordFile($$$);
sub getYubiExec();
sub symlinkPem($);
sub readSecrets();
sub decrypt($);
sub isRunning($);
sub run(@);

my $usage = "Usage:
  $0 [OPTS] on [CONF_NAME]
    start openvpn using --config CONF_NAME.conf (default is $defaultName)
    also run `sshc --close-all`
  $0 [OPTS] off [CONF_NAME]
    kill openvpn with '--config CONF_NAME'
    also run `sshc --close-all`
  $0 [OPTS] running CONF_NAME
    check for openvpn with '--config CONF_NAME'
    exits with 0 for running and 1 for not running
  $0 [OPTS] toggle [CONF_NAME]
    check '$0 running CONF_NAME' and then run:
      '$0 off CONF_NAME' or '$0 on CONF_NAME'

  CONF_NAME
    optional, defaults to $defaultName
    there should be a file named $dir/CONF_NAME.conf

  OPTS
    $okNoDaemonOpts
      do not pass --daemon to openvpn
";

sub main(@){
  ensureRoot();

  my $noDaemon = 0;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $opt = shift;
    if($opt =~ /^(-h|--help)$/){
      die $usage;
    }elsif($opt =~ /^($okNoDaemonOpts)$/){
      $noDaemon = 1;
    }
  }
  my $cmd = shift;
  my $name = shift;

  die $usage if @_ != 0 or not defined $cmd or $cmd !~ /^($okCmds)$/;
  chdir $dir or die "couldnt set cwd to $dir\n";

  $name = $defaultName if not defined $name;
  $name =~ s/\.conf$//;
  die "$dir/$name.conf not found\n" if not -f "$name.conf";

  my $procPattern = "^openvpn.*--config $name\\.conf ";

  if($cmd eq 'on'){
    print "starting, maybe...\n";

    my $cfg = ${readSecrets()}{$name};
    die "no 'vpn.$name' config found in $secretsFile\n" if not defined $cfg;

    my $user = $$cfg{user};
    die "missing 'user' from vpn.$name.* in $secretsFile\n" if not defined $user;
    my $password = $$cfg{password};
    $password = '' if not defined $password;
    my $yubi = $$cfg{yubi};
    $yubi = 0 if not defined $yubi;

    symlinkPem($name);

    run "modprobe", "tun";
    run "rm", "-f", "status";
    run "openvpn",
      ($noDaemon ? () : ("--daemon")),
      "--log", "$name.log",
      "--auth-user-pass", getPasswordFile($user, $password, $yubi),
      "--config", "$name.conf",
      "--status", $status,
      "--reneg-sec", 0,
      "1";
    run "sshc", "--close-all";
  }elsif($cmd eq 'running'){
    exit (isRunning $procPattern ? 0 : 1);
  }elsif($cmd eq 'off'){
    print "stopping, maybe...\n";
    run "pkill", "-f", $procPattern;
    run "sshc", "--close-all";
  }elsif($cmd eq 'toggle'){
    my @cmd;
    push @cmd, $0;
    push @cmd, isRunning $procPattern ? 'off' : 'on';
    push @cmd, $name if defined $name;
    exec @cmd;
  }
}

sub ensureRoot(){
  if(`whoami` ne "root\n"){
    print "rerunning as root\n";
    exec "sudo", $0, @ARGV;
  }
}

sub getYubiExec(){
  my $exec = "";
  $exec = `which yubi     2>/dev/null` if $exec =~ /^\s*$/;
  $exec = `which yubi-nfc 2>/dev/null` if $exec =~ /^\s*$/;
  $exec = undef if $exec =~ /^\s*$/;
  return $exec;
}

sub getPasswordFile($$$){
  my ($user, $password, $yubi) = @_;

  if($yubi){
    my $yubiExec = getYubiExec;
    my $yubiPass = `$yubiExec`;
    chomp $yubiPass;
    $password .= $yubiPass;
  }

  my $file = "/tmp/openvpn-auth-" . time;
  open FH, "> $file" or die "Cant write to $file\n";
  print FH "$user\n";
  print FH "$password\n";
  close FH;
  return $file;
}

sub symlinkPem($){
  my $name = shift;
  my $pem = "$name.pem";

  my $hostname = `hostname`;
  chomp $hostname;

  my $hostPem = "$pem.$hostname";
  my $defaultPem = "$pem.default";

  run "rm", "-f", $pem if -l $pem;
  die "$pem exists and is not a symlink" if -e $pem;

  run "ln", "-s", $hostPem, $pem if not -e $pem and -e $hostPem;
  run "ln", "-s", $defaultPem, $pem if not -e $pem and -e $defaultPem;

  die "could not select $pem\n" if not -e $pem;
}

sub readSecrets(){
  my @lines = `cat $secretsFile 2>/dev/null`;
  my $cfg = {};
  my $okSecretKeys = join "|", @secretKeys;
  for my $line(@lines){
    if($line =~ /^vpn\.(\w+)\.($okSecretKeys)\s*=\s*(.+)$/){
      my ($confName, $key, $val) = ($1, $2, $3);
      $val = decrypt $val if defined $encryptedSecretKeys{$key};

      $$cfg{$confName} = {} if not defined $$cfg{$confName};
      $$cfg{$confName}{$key} = $val;
    }
  }
  return $cfg;
}

sub decrypt($){
  my ($s) = @_;
  my $value = `$GPG_SYM_EXEC '$s' 2>/dev/null`;
  die "ERROR: $GPG_SYM_EXEC failed\n" if $? != 0;
  chomp $value;
  return $value;
}

sub isRunning($){
  my $procPattern = shift;
  run "pgrep -f '$procPattern' >/dev/null 2>/dev/null";
  return $? == 0;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
