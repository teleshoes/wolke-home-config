#!/usr/bin/perl
use strict;
use warnings;

my $GST_LAUNCH_EXEC = "gst-launch-1.0";

my $MAX_VOL_PCT = 200;

my $usage = "Usage:
  $0 -h | --help
    show this message

  $0 [OPTS] WAV_FILE
    play WAV_FILE using $GST_LAUNCH_EXEC and pulse

  WAV_FILE
    an audio wav file

  OPTS
    --volume=VOLUME_PERCENT
      set the volume using 'volume' pipeline (divide VOLUME_PERCENT by 100)
      VOLUME_PERCENT
        integer between 0 and $MAX_VOL_PCT, possibly ending in '%'
      e.g.: --volume=50, --volume=150

    --loop
      repeatedly call $GST_LAUNCH_EXEC forever
";

sub main(@){
  my $wavFile;
  my $volumePercent;
  my $loop = 0;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^--volume=(\d+)%?$/){
      $volumePercent = $1;
      die "$usage\nERROR: invalid volume $volumePercent\n" if $volumePercent > $MAX_VOL_PCT;
    }elsif($arg =~ /^(--loop)$/){
      $loop = 1;
    }elsif(-f $arg){
      $wavFile = $arg;
    }else{
      die "$usage\nERROR: unknown arg $arg\n";
    }
  }

  die "$usage\nERROR: missing WAV file\n" if not defined $wavFile;

  my @volPipeline;
  if(defined $volumePercent){
    @volPipeline = ("!", "volume", sprintf("volume=%.2f", $volumePercent/100.0));
  }

  my @cmd = (
    "gst-launch-1.0", "filesrc", "location=$wavFile",
    "!", "wavparse",
    "!", "audioconvert",
    @volPipeline,
    "!", "audioresample",
    "!", "pulsesink",
  );

  while(1){
    print "@cmd\n";
    system @cmd;
    my $exitCode = $?;
    if($exitCode != 0){
      print "ERROR: @cmd FAILED\n";
      exit $exitCode;
    }
    last if not $loop;
  }
}

&main(@ARGV);
