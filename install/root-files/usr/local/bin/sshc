#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $EXEC = basename $0;
my $CONTROL_PATH_DIR = "$ENV{HOME}/.ssh/control-paths";

my $usage = "Usage:
  Create an SSH master session before running SSH inside it, reusing it for future runs.

  $EXEC -h | --help
    show this message

  $EXEC [SSH_OPTS] DEST [COMMAND]
    -ensure SSH master session, like: `$EXEC --open <DESTINATION>`
    -run: `ssh <SSH_OPTS> -S <CONTROL_PATH> <DEST> <COMMAND>`

  $EXEC --open DEST
    -if CONTROL_PATH does not exist:
      -run: ssh -n -N -F -o ControlMaster=yes -o ControlPath=<CONTROL_PATH> <DEST>

  $EXEC --close DEST
    -if CONTROL_PATH exists and is active (using `ssh -O check`):
      -run: ssh -O exit -o ControlPath=<CONTROL_PATH> <DEST>
    -if CONTROL_PATH exists and is NOT active:
      -run: rm <CONTROL_PATH>

  $EXEC --close-all
    -for all <CONTROL_PATH>s in $CONTROL_PATH_DIR/*
      -run: ssh -O exit -o ControlPath=<CONTROL_PATH> <DEST>

  $EXEC --rsync RSYNC_ARGS
    -find the first arg in <RSYNC_ARGS> that contains a ':'
    -treat everything before the first colon in that arg as <REMOTE>
    -use: CONTROL_PATH=$CONTROL_PATH_DIR/<REMOTE>
    -ensure SSH master session, like: `$EXEC --open <REMOTE>`
    -run: `rsync -e \"ssh -o ControlPath=CONTROL_PATH\" <RSYNC_ARGS>

  DEST
    SSH destination (e.g.: HOSTNAME or USER\@HOSTNAME)
  CONTROL_PATH
    $CONTROL_PATH_DIR/<DEST>
  SSH_OPTS
    any args that you can pass to SSH:
         [-46AaCfGgKkMNnqsTtVvXxYy] [-B bind_interface] [-b bind_address]
         [-c cipher_spec] [-D [bind_address:]port] [-E log_file]
         [-e escape_char] [-F configfile] [-I pkcs11] [-i identity_file]
         [-J destination] [-L address] [-l login_name] [-m mac_spec]
         [-O ctl_cmd] [-o option] [-p port] [-Q query_option] [-R address]
         [-S ctl_path] [-W host:port] [-w local_tun[:remote_tun]]
  COMMAND
    any command that you can pass to SSH
";

my $SSH_OPTS_ARGS_REGEX = "-[BbcDEeFIiJLlmOopQRSWw]";
my $SSH_OPTS_NOARGS_REGEX = "-([46AaCfGgKkMNnqsTtVvXxYy])+";

sub ensureSSHOpen($);
sub ensureSSHClosed($);
sub runSSH($$$);

sub main(@){
  if(@_ == 1 and $_[0] =~ /^(-h|--help)$/){
    print $usage;
  }elsif(@_ == 2 and $_[0] =~ /^(--open)$/){
    my $dest = $_[1];
    ensureSSHOpen($dest);
  }elsif(@_ == 2 and $_[0] =~ /^(--close)$/){
    my $dest = $_[1];
    ensureSSHClosed($dest);
  }elsif(@_ == 1 and $_[0] =~ /^(--close-all)$/){
    my @controlPaths = glob "$CONTROL_PATH_DIR/*";
    my @dests = map {$_ =~ s/^.*\///; $_} @controlPaths;
    ensureSSHClosed($_) foreach @dests;
  }elsif(@_ > 0 and $_[0] =~ /^(--rsync)$/){
    shift @_;

    my @rsyncArgs = @_;
    my $remote;
    for my $arg(@rsyncArgs){
      if($arg =~ /^([^:]+):/){
        $remote = $1;
        last;
      }
    }
    if(not defined $remote){
      die "$usage\nERROR: could not find rsync remote src/dest in \"@rsyncArgs\"\n";
    }
    my $controlPath = "$CONTROL_PATH_DIR/$remote";

    ensureSSHOpen($remote);
    system "rsync", "-e", "ssh -o ControlPath=$controlPath", @_;
  }elsif(@_ > 0){
    my $sshOpts = [];
    while(@_ > 0 and $_[0] =~ /^($SSH_OPTS_ARGS_REGEX|$SSH_OPTS_NOARGS_REGEX)$/){
      my $opt = shift @_;
      push @$sshOpts, $opt;
      if($opt =~ /^$SSH_OPTS_ARGS_REGEX$/){
        die "$usage\nERROR: missing arg to $opt\n" if @_ == 0;
        push @$sshOpts, shift @_;
      }
    }
    die "$usage\nERROR: missing DESTINATION\n" if @_ == 0;

    my $dest = shift @_;
    my $command = [@_];

    ensureSSHOpen($dest);
    runSSH($dest, $sshOpts, $command);
  }
}

sub ensureSSHOpen($){
  my ($dest) = @_;
  my $controlPath = "$CONTROL_PATH_DIR/$dest";
  system "mkdir", "-p", $CONTROL_PATH_DIR if not -d $CONTROL_PATH_DIR;
  if(not -e $controlPath){
    my @cmd = ("ssh",
      "-n", "-N", "-f",
      "-o", "ControlMaster=yes",
      "-o", "ControlPath=$controlPath",
      $dest,
    );
    system @cmd;
    if(not -e $controlPath){
      die "ERROR: @cmd failed\n";
    }
  }
}

sub ensureSSHClosed($){
  my ($dest) = @_;
  my $controlPath = "$CONTROL_PATH_DIR/$dest";
  if(-e $controlPath){
    system "ssh -o ControlPath=$controlPath -O check thisArgIsIgnoredSSHBug";
    if($? != 0){
      print "WARNING: control path \"$controlPath\" was not active, removing file\n";
      system "rm", $controlPath;
    }else{
      my @cmd = ("ssh",
        "-O", "exit",
        "-o", "ControlPath=$controlPath",
        $dest,
      );
      system @cmd;
    }

    if(-f $controlPath){
      die "ERROR: control path \"$controlPath\" still exists\n";
    }
  }
}

sub runSSH($$$){
  my ($dest, $sshOpts, $command) = @_;
  my $controlPath = "$CONTROL_PATH_DIR/$dest";
  my @cmd = ("ssh", @$sshOpts, "-S", $controlPath, $dest, @$command);
  system @cmd;
}

&main(@ARGV);
